#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если НЕ Параметры.Отбор.Свойство("Недействителен") Тогда
		Параметры.Отбор.Вставить("Недействителен", Ложь);
	КонецЕсли;
	
	Если НЕ Параметры.Отбор.Свойство("Служебный") Тогда
		Параметры.Отбор.Вставить("Служебный", Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
		
		ПараметрИзменен = Ложь;
		
		Если Не Параметры.Свойство("Отбор") Тогда
			Параметры.Вставить("Отбор", Новый Структура("Недействителен", Ложь));
			ПараметрИзменен = Истина;
		ИначеЕсли Не Параметры.Отбор.Свойство("Недействителен") Тогда
			Параметры.Отбор.Вставить("Недействителен", Ложь);
			ПараметрИзменен = Истина;
		КонецЕсли;
		
		Если ПараметрИзменен Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаСписка";
		КонецЕсли;
		
	КонецЕсли;
	
	СтандартныеПодсистемыПереопределяемый.ОбработкаПолученияФормыПользователя(ВидФормы, Параметры, ВыбраннаяФорма, 
		ДополнительнаяИнформация, СтандартнаяОбработка);
	
КонецПроцедуры
// Функция получает физическое лицо пользователя
//
// Параметры:
//  Касса - СправочникСсылка.Пользователи - Ссылка на пользователя
//
// Возвращаемое значение:
//	Структура - ФизическоеЛицо пользователя
//
Функция ПолучитьРеквизитыПользователя(Пользователь) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Пользователи.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Пользователи.ТекущееПодразделение КАК Подразделение
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Ссылка = &Пользователь
	|");
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ФизическоеЛицо = Выборка.ФизическоеЛицо;
		Подразделение = Выборка.Подразделение;
	Иначе
		ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
		Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("ФизическоеЛицо, Подразделение",
		ФизическоеЛицо,
		Подразделение
	);
	
	Возврат СтруктураРеквизитов;

КонецФункции // ПолучитьРеквизитыПользователя()

#КонецЕсли