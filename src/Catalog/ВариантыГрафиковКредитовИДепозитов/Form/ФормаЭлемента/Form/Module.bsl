
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Владелец") Тогда
		Объект.Владелец = Параметры.Владелец;
	КонецЕсли;
	
	СсылкаКопирования = Параметры.ЗначениеКопирования;
	Если НЕ СсылкаКопирования.Пустая() Тогда
		
		ПрочитатьНаборСервер("ГрафикТраншей", СсылкаКопирования);
		ПрочитатьНаборСервер("ГрафикОплат", СсылкаКопирования);
		ПрочитатьНаборСервер("ГрафикНачислений", СсылкаКопирования);
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Объект.Утвержден = ЭтоПервый();
		Объект.Используется = Объект.Утвержден;
		ДоговорПриИзмененииСервер();
		
	КонецЕсли;
	
	Элементы.Используется.ТолькоПросмотр = Объект.Используется;
	Элементы.Утвержден.ТолькоПросмотр = Объект.Утвержден;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьСвязанныеДанные();
	ДоговорПриИзмененииСервер();
	УстановитьДоступностьПоДоговору();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ЗаписатьАвтораИзменений();
	
	ОбновитьДаты();
	
	//обновим итоги
	Объект.СуммаТраншей = ГрафикТраншей.Итог("Сумма");
	ОбновитьИтогиОплат();
	ОбновитьИтогиНачислений();
	
	Если НЕ СуммыГрафиковСходятся() Тогда
			
		Ответ = Вопрос(
					НСтр("ru='В текущем варианте графика различаются итоговые суммы.
						|Игнорировать?'"
					),
					РежимДиалогаВопрос.ДаНет);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Отказ = Истина;
		КонецЕсли;
				
	КонецЕсли;//суммы итоговые суммы графика не сходятся
	
	Если НЕ СоответствуетЛимитуДоговора() Тогда
		
		Текст = НСтр("ru='Сумма траншей превосходит заданный лимит по договору!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);		
		Предупреждение(Текст);
		Отказ  = Истина;
		
	КонецЕсли;// есть ограничение по лимиту
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Справочники.ВариантыГрафиковКредитовИДепозитов.ПересчитатьСроки(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьНаборСервер("ГрафикТраншей");
	ЗаписатьНаборСервер("ГрафикОплат");
	ЗаписатьНаборСервер("ГрафикНачислений");
	ОбновитьСвязанныеДанные();
	ОбновитьФлагиАктуальныхГрафиков();
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененыСвязанныеДанныеПоКредитуДепозиту", ПараметрыЗаписи, Объект.Ссылка);
	ОповеститьОбИзменении(Объект.Ссылка);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	ДоговорПриИзмененииСервер();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗагрузитьГрафик(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
			
		Ответ = Вопрос(
		НСтр("ru='Перед загрузкой необходимо записать объект.
			|Записать и продолжить?'"
		),
		РежимДиалогаВопрос.ДаНет);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		
		Записать();
		
	КонецЕсли;
	
	ТипГрафика = СтрЗаменить(Команда.Имя,"ЗагрузитьГрафик_","");
	ПараметрыФормы = Новый Структура("ВариантГрафика, ТипГрафика", Объект.Ссылка, ТипГрафика);
	ОткрытьФормуМодально("Справочник.ВариантыГрафиковКредитовИДепозитов.Форма.ФормаЗагрузки", ПараметрыФормы);
	
	ОбновитьСвязанныеДанные(ТипГрафика);
	Модифицированность = Истина;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ

&НаКлиенте
Процедура ТаблицаГрафикаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	УказаннаяСумма = Элемент.ТекущиеДанные.Проценты + Элемент.ТекущиеДанные.Комиссия;
	Если Элемент.Имя = "Оплаты" Тогда
		УказаннаяСумма = УказаннаяСумма + Элемент.ТекущиеДанные.Сумма;
	КонецЕсли;
	Если УказаннаяСумма = 0 И (НЕ НоваяСтрока ИЛИ НЕ ОтменаРедактирования)  Тогда
		Текст  = НСтр("ru='В строке должна быть указана хотя бы одна из сумм!'");
		Если НЕ НоваяСтрока И ОтменаРедактирования Тогда
			Текст  = НСтр("ru='Строки с нулевыми суммами не могут хранится в базе данных!
			|Удалите всю строку целиком.'");
		КонецЕсли;
		Предупреждение(Текст);		
		Отказ = Истина;		
	КонецЕсли;
	
КонецПроцедуры

// ТРАНШИ
&НаКлиенте
Процедура ТраншиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Объект.СуммаТраншей = ГрафикТраншей.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура ТраншиПослеУдаления(Элемент)
	
	Объект.СуммаТраншей = ГрафикТраншей.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыПослеУдаления(Элемент)
	
	ОбновитьИтогиОплат();
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьИтогиОплат();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПослеУдаления(Элемент)
	
	ОбновитьИтогиНачислений();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьИтогиНачислений();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаКлиенте
Процедура ОбновитьДаты()
	
	//обновим дату первого транша
	Если ГрафикТраншей.Количество() > 0 Тогда
		ГрафикТраншей.Сортировать("Период");
		Объект.ДатаПервогоТранша = ГрафикТраншей[0].Период;
	Иначе
		Объект.ДатаПервогоТранша = Неопределено;
	КонецЕсли;
	
	//обновим дату последнего платежа
	Если ГрафикОплат.Количество() > 0 Тогда
		ГрафикОплат.Сортировать("Период");
		Объект.ДатаПоследнегоПлатежа = ГрафикОплат[ГрафикОплат.Количество()-1].Период;
	Иначе
		Объект.ДатаПоследнегоПлатежа = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтогиНачислений()
	
	НачисленияПроцентов = ГрафикНачислений.Итог("Проценты");
	НачисленияКомиссии = ГрафикНачислений.Итог("Комиссия");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтогиОплат()
	
	СуммаОплаты = ГрафикОплат.Итог("Сумма");
	Объект.СуммаПроцентов = ГрафикОплат.Итог("Проценты");
	Объект.СуммаКомиссии = ГрафикОплат.Итог("Комиссия");
	ОбновитьДаты();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ДоговорПриИзмененииСервер()
	
	РеквизитыДоговора = Справочники.ДоговорыКредитовИДепозитов.РеквизитыДоговора(Объект.Владелец);
	ВалютаДоговора = РеквизитыДоговора.ВалютаВзаиморасчетов;
	ХарактерДоговора = РеквизитыДоговора.ХарактерДоговора;
	
	ПрописатьВалюту(ВалютаДоговора);	
	УстановитьДоступностьПоДоговору();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПоДоговору()
	
	ДоговорЗакрыт = Объект.Владелец.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Закрыт;
	Элементы.НадписьДоговорЗакрыт.Видимость = ДоговорЗакрыт;
	
	ДоступностьЭлементов = Объект.Владелец.Статус <> Перечисления.СтатусыДоговоровКонтрагентов.Закрыт;
	
	// Элементы управления шапки
	МассивДействует = Новый Массив();
	МассивДействует.Добавить("ФормаЗаписать");
	МассивДействует.Добавить("ФормаЗаписатьИЗакрыть");
	МассивДействует.Добавить("Флаги");
	МассивДействует.Добавить("ОписаниеДоговора");
	МассивДействует.Добавить("НаименованиеКод");
	МассивДействует.Добавить("ТраншиКоманднаяПанель");
	МассивДействует.Добавить("ОплатыКоманднаяПанель");
	МассивДействует.Добавить("НачисленияКоманднаяПанель");
	МассивДействует.Добавить("Комментарий");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивДействует, "Доступность", ДоступностьЭлементов);
		
КонецПроцедуры

&НаСервере
Процедура ЗаписатьАвтораИзменений()
	
	Объект.АвторИзменения = ПараметрыСеанса.ТекущийПользователь;
	Объект.ДатаИзменения = ТекущаяДата();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаборСервер(ТипГрафика)
	
	ИмяНабора = "";
	Набор = НаборГрафика(ТипГрафика, ИмяНабора);
	Набор.Заполнить(Новый Структура("ВариантГрафика",Объект.Ссылка));
	Набор.Записать();
	ЗначениеВРеквизитФормы(Набор,ИмяНабора);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяНабораГрафика(ТипГрафика)
	
	Если ТипГрафика = "Транши" Тогда
		ИмяНабора = "ГрафикТраншей";
	ИначеЕсли ТипГрафика = "Оплаты" Тогда
		ИмяНабора = "ГрафикОплат";
	ИначеЕсли ТипГрафика = "Начисления" Тогда
		ИмяНабора = "ГрафикНачислений";
	Иначе
		ИмяНабора = ТипГрафика;
	КонецЕсли;
	
	Возврат ИмяНабора;
	
КонецФункции

&НаСервере
Функция НаборГрафика(ТипГрафика, ИмяНабора = "", СсылкаОбъекта = Неопределено)
	
	Если СсылкаОбъекта = Неопределено Тогда
		СсылкаОбъекта = Объект.Ссылка;		
	КонецЕсли;
	
	ИмяНабора = ИмяНабораГрафика(ТипГрафика);	
	Набор = РеквизитФормыВЗначение(ИмяНабора, Тип("РегистрСведенийНаборЗаписей."+ИмяНабора+"КредитовИДепозитов"));
	Набор.Отбор.ВариантГрафика.Установить(СсылкаОбъекта);
	
	Возврат Набор;
	
КонецФункции

&НаСервере
Процедура ПрописатьВалюту(Валюта)
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		Возврат;
	КонецЕсли;
	
	// подпишем итоговые группы
	ШаблонТекста = НСтр("ru='Всего траншей (%1)'");	
	Элементы.ГруппаТранши.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	ШаблонТекста = НСтр("ru='Всего оплата (%1)'");	
	Элементы.ГруппаОплаты.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	ШаблонТекста = НСтр("ru='Всего начисления (%1)'");	
	Элементы.ГруппаНачисления.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	// подпишем колонки таблицы "Транши"
	ШаблонТекста = НСтр("ru='Сумма (%1)'");	
	Элементы.ТраншиСумма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	// подпишем колонки таблицы "Оплаты"
	ШаблонТекста = НСтр("ru='Сумма основного долга (%1)'");	
	Элементы.ОплатыСумма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	ШаблонТекста = НСтр("ru='Сумма процентов (%1)'");	
	Элементы.ОплатыПроценты.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	ШаблонТекста = НСтр("ru='Сумма комиссии (%1)'");	
	Элементы.ОплатыКомиссия.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	// подпишем колонки таблицы "Начисления"	
	ШаблонТекста = НСтр("ru='Сумма процентов (%1)'");	
	Элементы.НачисленияПроценты.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	ШаблонТекста = НСтр("ru='Сумма комиссии (%1)'");	
	Элементы.НачисленияКомиссия.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
КонецПроцедуры

&НаСервере
Функция СоответствуетЛимитуДоговора()
	
	Результат = Истина;
	
	Набор = НаборГрафика("Транши");
	СуммаТраншей = Набор.Итог("Сумма");
	СуммаЛимита = Объект.Владелец.СуммаЛимита;
	Если СуммаЛимита > 0 Тогда
		Результат = СуммаТраншей <= СуммаЛимита;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция СуммыГрафиковСходятся()
	
	СуммыДолгаРавны	 = Объект.СуммаТраншей = СуммаОплаты;
	ПроцентыРавны	 = Объект.СуммаПроцентов = НачисленияПроцентов;
	КомиссияРавна	 = Объект.СуммаКомиссии = НачисленияКомиссии;

	//проверим суммы основного долга
	Если НЕ СуммыДолгаРавны Тогда
		Текст  = НСтр("ru='Различаются итоговые суммы основного долга на закладке ""Транши"" и ""Оплаты""!'");		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	КонецЕсли;
	
	//проверим суммы процентов		
	Если НЕ ПроцентыРавны Тогда
		Текст  = НСтр("ru='Различаются итоговые суммы процентов на закладке ""Оплаты"" и ""Начисления""!'");		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	КонецЕсли;
	
	//проверим суммы комиссии
	Если НЕ КомиссияРавна Тогда
		Текст  = НСтр("ru='Различаются итоговые суммы комиссии на закладке ""Оплаты"" и ""Начисления""!'");		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);			
	КонецЕсли;
		
	Возврат СуммыДолгаРавны И ПроцентыРавны И КомиссияРавна;
	
КонецФункции

&НаСервере
Процедура ПрочитатьНаборСервер(ТипГрафика, СсылкаОбъекта = Неопределено)
	
	ИмяНабора = "";
	Набор = НаборГрафика(ТипГрафика, ИмяНабора, СсылкаОбъекта);
	Набор.Прочитать();
	ЗначениеВРеквизитФормы(Набор, ИмяНабора);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСвязанныеДанные(ТипГрафика = Неопределено)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
				
		//обновим данные графиков
		Если ТипГрафика = Неопределено Тогда
			ПрочитатьНаборСервер("ГрафикТраншей");
			ПрочитатьНаборСервер("ГрафикОплат");
			ПрочитатьНаборСервер("ГрафикНачислений");
		Иначе
			ПрочитатьНаборСервер(ТипГрафика);
		КонецЕсли;
		
		ИтогиГрафика = Справочники.ВариантыГрафиковКредитовИДепозитов.ИтогиГрафика(Объект.Ссылка);
		ЗаполнитьЗначенияСвойств(Объект,ИтогиГрафика,"ДатаПервогоТранша,ДатаПоследнегоПлатежа,СуммаТраншей,СуммаПроцентов,СуммаКомиссии");
		ЗаполнитьЗначенияСвойств(ЭтаФорма,ИтогиГрафика,"СуммаОплаты,НачисленияПроцентов,НачисленияКомиссии");
		Справочники.ВариантыГрафиковКредитовИДепозитов.ПересчитатьСроки(Объект);
		
	КонецЕсли;//Ссылка существует	
	
КонецПроцедуры

&НаСервере
Функция ЭтоПервый()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВариантыГрафиковКредитовИДепозитов.Ссылка
	|ИЗ
	|	Справочник.ВариантыГрафиковКредитовИДепозитов КАК ВариантыГрафиковКредитовИДепозитов
	|ГДЕ
	|	НЕ ВариантыГрафиковКредитовИДепозитов.ПометкаУдаления
	|	И ВариантыГрафиковКредитовИДепозитов.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец", Объект.Владелец);	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции


&НаСервере
Процедура ОбновитьФлагиАктуальныхГрафиков()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВариантыГрафиковКредитовИДепозитов.Ссылка
	|ИЗ
	|	Справочник.ВариантыГрафиковКредитовИДепозитов КАК ВариантыГрафиковКредитовИДепозитов
	|ГДЕ
	|	ВариантыГрафиковКредитовИДепозитов.Владелец = &Владелец
	|	И ВариантыГрафиковКредитовИДепозитов.Ссылка <> &Ссылка
	|	И (ВариантыГрафиковКредитовИДепозитов.Утвержден
	|			ИЛИ ВариантыГрафиковКредитовИДепозитов.Используется)";
	
	Запрос.УстановитьПараметр("Владелец", Объект.Владелец);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Утвержден", Объект.Утвержден);	
	Выборка = Запрос.Выполнить().Выбрать();
		
	Пока Выборка.Следующий() Цикл
		
		ОбъектГрафика = Выборка.Ссылка.ПолучитьОбъект();
		Если Объект.Утвержден Тогда
			ОбъектГрафика.Утвержден = Ложь;
		КонецЕсли;
		
		Если Объект.Используется Тогда
			ОбъектГрафика.Используется = Ложь;
		КонецЕсли;
		
		ОбъектГрафика.Записать();
		
	КонецЦикла;
	
КонецПроцедуры