#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция НазначениеЗаказа(Заказ, Значение) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Результат = Ложь;

	Запрос = Новый Запрос(
	"
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Т.Ссылка КАК Назначение
	|ИЗ
	|	Справочник.Назначения КАК Т
	|ГДЕ
	|	Т.Заказ = &Заказ
	|");
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 

		Результат = Истина;
		Значение = Выборка.Назначение;

	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция РеквизитыНазначения(Назначение)

	Результат = Новый Структура("Наименование, Заказ", "",Неопределено);

	Запрос = Новый Запрос(
	"
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Т.Наименование КАК Наименование,
	|	Т.Заказ КАК Заказ
	|ИЗ
	|	Справочник.Назначения КАК Т
	|ГДЕ
	|	Т.Ссылка = &Назначение
	|");
	Запрос.УстановитьПараметр("Назначение", Назначение);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 

		ЗаполнитьЗначенияСвойств(Результат, Выборка);

	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура СоздатьОбновитьНазначение(Заказ, Назначение, СинонимДокумента, Номер = Неопределено, Дата = Неопределено) Экспорт
	
	// Назначение - либо ссылка на назначение либо ссылка нового.
	Если НЕ ЗначениеЗаполнено(Назначение) Тогда
		Возврат;
	КонецЕсли;

	// Дата и номер могут быть неопределены-если получается назначение не из объекта - а по ссылке на объект.
	Если Номер = Неопределено Или Дата = Неопределено Тогда

		СтруктураРеквизитов = Новый Структура("Дата, Номер", "Дата", "Номер");
		ЗначениеРеквизитов  = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Заказ, СтруктураРеквизитов);

		НомерДок = ЗначениеРеквизитов.Номер;
		ДатаДок  = ЗначениеРеквизитов.Дата;

	Иначе

		НомерДок = Номер;
		ДатаДок = Дата;

	КонецЕсли;

	ТребуемоеНаименование = НСтр("ru='%СинонимРаспоряжения% %НомерРаспоряжения% от %ДатаРаспоряжения%'");
	ТребуемоеНаименование = СтрЗаменить(ТребуемоеНаименование, "%СинонимРаспоряжения%", СинонимДокумента);
	ТребуемоеНаименование = СтрЗаменить(ТребуемоеНаименование, "%НомерРаспоряжения%", СокрЛП(НомерДок));
	ТребуемоеНаименование = СтрЗаменить(ТребуемоеНаименование, "%ДатаРаспоряжения%", Формат(ДатаДок, "ДФ=dd.MM.yyyy"));
	
	РеквизитыНазначения = РеквизитыНазначения(Назначение);
	
	Если РеквизитыНазначения.Заказ <> Заказ
		Или РеквизитыНазначения.Наименование <> ТребуемоеНаименование Тогда
		
		НазначениеОбъект = Назначение.ПолучитьОбъект();
		Если НазначениеОбъект = Неопределено Тогда 
			
			НазначениеОбъект = Справочники.Назначения.СоздатьЭлемент();
			НазначениеОбъект.УстановитьСсылкуНового(Назначение);
			
		КонецЕсли;
		
		НазначениеОбъект.Заказ        = Заказ;
		НазначениеОбъект.Наименование = ТребуемоеНаименование;
		НазначениеОбъект.Записать();
		
	КонецЕсли;

КонецПроцедуры




#КонецЕсли