#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

//Функция определяет, используются или нет группы доступа номенклатуры.
//
//	Возвращаемое значение:
//		Булево - если ИСТИНА, значит группы доступа используются
//
Функция ИспользуютсяГруппыДоступа() Экспорт
	
	Возврат
		ПолучитьФункциональнуюОпцию("ОграничиватьДоступНаУровнеЗаписей")
		И ПолучитьФункциональнуюОпцию("ИспользоватьГруппыДоступаНоменклатуры");
	
КонецФункции

//Функция определяет, есть ли у текущего пользователя право изменять номенклатуру.
//
//	Параметры:
//		Объект - любая коллекция с полями
//			ГруппаДоступа - СправочникСсылка.ГруппыДоступаНоменклатуры
//			Ссылка - ссылка на справочник, к которому возможно ограничение доступа по группе доступа номенклатуры
//
//	Возвращаемое значение:
//		Булево - если ИСТИНА, значит право изменения есть
//
Функция ЕстьПравоИзменения(Объект) Экспорт
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат Истина;
	ИначеЕсли Не ЗначениеЗаполнено(Объект.ГруппаДоступа) Или Не ИспользуютсяГруппыДоступа() Тогда
		Возврат ПравоДоступа("Изменение", Метаданные.Справочники.Номенклатура);
	КонецЕсли;
	
	МассивГруппДоступа =
		УправлениеДоступом.ГруппыЗначенийДоступаРазрешающиеИзменениеЗначенийДоступа(ТипЗнч(Объект.Ссылка), Истина);
	
	Возврат МассивГруппДоступа.Найти(Объект.ГруппаДоступа) <> Неопределено;
	
КонецФункции

//Функция возвращает группу доступа номенклатуры по умолчанию для текущего пользователя.
//
//	Параметры:
//		Объект - любая коллекция с полями
//			ГруппаДоступа - СправочникСсылка.ГруппыДоступаНоменклатуры
//			Ссылка - ссылка на справочник, к которому возможно ограничение доступа по группе доступа номенклатуры
//
//	Возвращаемое значение:
//		СправочникСсылка.ГруппыДоступаНоменклатуры - группа доступа по умолчанию
//
Функция ПолучитьГруппуДоступаПоУмолчанию(Объект) Экспорт
	
	ГруппаДоступаПоУмолчанию = Справочники.ГруппыДоступаНоменклатуры.ПустаяСсылка();
	
	МассивГруппДоступа =
		УправлениеДоступом.ГруппыЗначенийДоступаРазрешающиеИзменениеЗначенийДоступа(ТипЗнч(Объект.Ссылка), Истина);
	
	Если ЗначениеЗаполнено(Объект.ГруппаДоступа) Тогда
		Если МассивГруппДоступа.Найти(Объект.ГруппаДоступа) <> Неопределено Тогда
			ГруппаДоступаПоУмолчанию = Объект.ГруппаДоступа;
		КонецЕсли;
	ИначеЕсли МассивГруппДоступа.Количество() = 1 Тогда
		ГруппаДоступаПоУмолчанию = МассивГруппДоступа[0];
	КонецЕсли;
	
	Возврат ГруппаДоступаПоУмолчанию;
	
КонецФункции

#КонецЕсли
