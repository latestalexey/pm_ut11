#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий

// Обработчик события ОбработкаПолученияДанныхВыбора
//
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отбор") Тогда
		
		// Заменим платформенный механизм формирования списка выбора,
		// т.к. необходим отбор по дате получения данных об ответственных лицах
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора 		 = Новый СписокЗначений;
		
		ТаблицаОтветственных = ОтветственныеЛицаСервер.ПолучитьТаблицуОтветственныхЛицПоОтбору(Параметры.Отбор, Истина);
		
		Для Каждого ТекСтр Из ТаблицаОтветственных Цикл
			
			СтруктураЭлементаСписка = Новый Структура;
			СтруктураЭлементаСписка.Вставить("Значение", 		ТекСтр.Ссылка);
			СтруктураЭлементаСписка.Вставить("ПометкаУдаления", ТекСтр.ПометкаУдаления);
			СтруктураЭлементаСписка.Вставить("Предупреждение"); // стандартное сообщение
			
			ДанныеВыбора.Добавить(СтруктураЭлементаСписка);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики обновления ИБ

// Обработчик обновления УТ 11.1.0.0 
// Переносит сведения об ответственных лицах организации
// из реквизитов справочника Организации в справочник ОтветственныеЛицаОрганизаций.
//
Процедура ПеренестиДанныеИзСправочникаОрганизации() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Получим сведения об ответственных лицах, которые
	//	- есть в справочнике Организации
	//	- отсутствуют в справочнике ОтветственныеЛицаОрганизаций
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Владелец,
	|	ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель) КАК ОтветственноеЛицо,
	|	Организации.УдалитьТекущийРуководитель КАК ФизическоеЛицо,
	|	Организации.УдалитьТекущаяДолжностьРуководителя КАК Должность,
	|	ЛОЖЬ КАК ПравоПодписиПоДоверенности,
	|	ДАТАВРЕМЯ(1980, 1, 1) КАК ДатаНачала
	|ПОМЕСТИТЬ РеквизитыОрганизаций
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.УдалитьТекущийРуководитель <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер),
	|	Организации.УдалитьТекущийГлавныйБухгалтер,
	|	ВЫБОР
	|		КОГДА Организации.УдалитьТекущийРуководитель <> Организации.УдалитьТекущийГлавныйБухгалтер
	|			ТОГДА ""Главный бухгалтер""
	|		ИНАЧЕ Организации.УдалитьТекущаяДолжностьРуководителя
	|	КОНЕЦ,
	|	ЛОЖЬ,
	|	ДАТАВРЕМЯ(1980, 1, 1)
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.УдалитьТекущийГлавныйБухгалтер <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыОрганизаций.Владелец КАК Владелец,
	|	РеквизитыОрганизаций.ФизическоеЛицо.Наименование КАК Наименование,
	|	РеквизитыОрганизаций.ОтветственноеЛицо,
	|	РеквизитыОрганизаций.ФизическоеЛицо,
	|	РеквизитыОрганизаций.Должность,
	|	РеквизитыОрганизаций.ПравоПодписиПоДоверенности,
	|	РеквизитыОрганизаций.ДатаНачала
	|ИЗ
	|	РеквизитыОрганизаций КАК РеквизитыОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
	|		ПО РеквизитыОрганизаций.Владелец = ОтветственныеЛицаОрганизаций.Владелец
	|			И РеквизитыОрганизаций.ОтветственноеЛицо = ОтветственныеЛицаОрганизаций.ОтветственноеЛицо
	|			И РеквизитыОрганизаций.ФизическоеЛицо = ОтветственныеЛицаОрганизаций.ФизическоеЛицо
	|			И РеквизитыОрганизаций.ПравоПодписиПоДоверенности = ОтветственныеЛицаОрганизаций.ПравоПодписиПоДоверенности
	|			И РеквизитыОрганизаций.ДатаНачала = ОтветственныеЛицаОрганизаций.ДатаНачала
	|ГДЕ
	|	ОтветственныеЛицаОрганизаций.Ссылка ЕСТЬ NULL ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбработанныеОрганизации = Новый Соответствие;
	
	НачатьТранзакцию();
	
	// Запишем данные в справочник ОтветственныеЛицаОрганизаций
	Пока Выборка.Следующий() Цикл
		
		НовыйОтветственный = Справочники.ОтветственныеЛицаОрганизаций.СоздатьЭлемент();
		
		ЗаполнитьЗначенияСвойств(НовыйОтветственный, Выборка);
		НовыйОтветственный.Наименование = ФизическиеЛица.ФамилияИнициалыФизЛица(НовыйОтветственный.ФизическоеЛицо);
		
		НовыйОтветственный.Записать();
		
		ОбработанныеОрганизации.Вставить(Выборка.Владелец);
		
	КонецЦикла;
	
	// Очистим реквизиты ответственных в справочнике Организации.
	Для Каждого КлючИЗначение Из ОбработанныеОрганизации Цикл
		
		Организация = КлючИЗначение.Ключ.ПолучитьОбъект();
		
		Организация.УдалитьТекущийРуководитель 			= Справочники.ФизическиеЛица.ПустаяСсылка();
		Организация.УдалитьТекущийГлавныйБухгалтер 		= Справочники.ФизическиеЛица.ПустаяСсылка();
		Организация.УдалитьТекущаяДолжностьРуководителя = "";
		
		Организация.Записать();
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецЕсли
