
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииЧтенииНаСервере();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВидОтветственногоЛицаПриИзменении(Элемент)
	
	Объект.ПравоПодписиПоДоверенности = Булево(ВидОтветственногоЛица);
	Объект.Наименование 			  = "";
	
	УправлениеЭлементамиФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеПраваПодписиПриИзменении(Элемент)
	
	Объект.Наименование = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	
	Объект.Наименование = "";
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СписокВыбора = Элементы.Наименование.СписокВыбора;
	СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		
		ФИО = ФамилияИнициалыОтветственного(СокрЛП(Объект.ФизическоеЛицо));
		
		Если ВидОтветственногоЛица = 1 Тогда
			
			Если ЗначениеЗаполнено(Объект.ОснованиеПраваПодписи) Тогда
				СписокВыбора.Добавить(ФИО + ", " + Объект.ОснованиеПраваПодписи);
				Если ЗначениеЗаполнено(Объект.Должность) Тогда
					СписокВыбора.Добавить(ФИО + ", " + Объект.Должность + ", " + Объект.ОснованиеПраваПодписи);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		СписокВыбора.Добавить(ФИО);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственноеЛицоПриИзменении(Элемент)
	Массив = УказанныеРанееДолжности(Объект.ОтветственноеЛицо);
	Элементы.Должность.СписокВыбора.ЗагрузитьЗначения(Массив);
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	Если Элементы.Должность.СписокВыбора.Количество() = 0 Тогда
		Массив = УказанныеРанееДолжности(Объект.ОтветственноеЛицо);
		Элементы.Должность.СписокВыбора.ЗагрузитьЗначения(Массив);
	КонецЕсли;
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиФормы(Форма)
	
	Если Форма.ВидОтветственногоЛица = 0 Тогда
		Форма.Элементы.ГруппаОснованиеПраваПодписи.ТекущаяСтраница = Форма.Элементы.СтраницаОснованиеПраваПодписиПоУмолчанию;
	Иначе
		Форма.Элементы.ГруппаОснованиеПраваПодписи.ТекущаяСтраница = Форма.Элементы.СтраницаВводОснованияПраваПодписи;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УказанныеРанееДолжности(ОтвественноеЛицо)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ОтветственныеЛицаОрганизаций.Должность КАК Должность
	|ИЗ
	|	Справочник.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
	|ГДЕ
	|	ОтветственныеЛицаОрганизаций.Должность <> """"
	|	И ОтветственныеЛицаОрганизаций.ОтветственноеЛицо = &ОтветственноеЛицо
	|	И НЕ ОтветственныеЛицаОрганизаций.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Должность";
	
	Запрос.УстановитьПараметр("ОтветственноеЛицо", ОтвественноеЛицо);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Должность");
	
КонецФункции

&НаКлиенте
Функция ФамилияИнициалыОтветственного(Строка) Экспорт

	ФИО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(Строка), " ");
	
	КоличествоПодстрок = ФИО.Количество();
	Фамилия            = ?(КоличествоПодстрок > 0, ФИО[0], "");
	Имя                = ?(КоличествоПодстрок > 1, ФИО[1], "");
	Отчество           = ?(КоличествоПодстрок > 2, ФИО[2], "");
	
	Возврат ?(Не ПустаяСтрока(Фамилия), 
	          Фамилия + ?(Не ПустаяСтрока(Имя), " " + Лев(Имя,1) + "." + ?(Не ПустаяСтрока(Отчество), Лев(Отчество, 1) + ".", ""), ""),
	          "");
	
КонецФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	ВидОтветственногоЛица = Число(Объект.ПравоПодписиПоДоверенности);
	
	УправлениеЭлементамиФормы(ЭтаФорма);
	
КонецПроцедуры
