///////////////////////////////////////////////////////////////
// СЕРВИСНЫЕ МЕТОДЫ

&НаСервере
Функция ПолучитьСписокДрайверов(ТипОборудования)

	СписокДрайверов = Новый СписокЗначений();
	Для каждого ИмяПеречисления Из Метаданные.Перечисления.ОбработчикиДрайверовПодключаемогоОборудования.ЗначенияПеречисления Цикл
		Если ТипОборудования = ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования." + ИмяПеречисления.Комментарий)
		 Или ИмяПеречисления.Комментарий = "" Тогда
			СписокДрайверов.Добавить(ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования." + ИмяПеречисления.Имя));
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокДрайверов;

КонецФункции

////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Загрузка и установка списка доступных обработок
	СписокДрайверов = ПолучитьСписокДрайверов(Объект.ТипОборудования);
	Элементы.ОбработчикДрайвера.СписокВыбора.Очистить();
	Для каждого СтрокаСписка Из СписокДрайверов Цикл
		Элементы.ОбработчикДрайвера.СписокВыбора.Добавить(СтрокаСписка.Значение, СтрокаСписка.Представление);
	КонецЦикла;

	//Перечисление стандартных типов
	Для каждого ИмяПеречисления Из Метаданные.Перечисления.ТипыПодключаемогоОборудования.ЗначенияПеречисления Цикл
		СоответствиеТиповОборудования.Добавить(ИмяПеречисления.Синоним, ИмяПеречисления.Комментарий);
	КонецЦикла;

	// Защита от изменения типа устройства если тип явно задан или экземпляр уже создан
	Элементы.ТипОборудования.ТолькоПросмотр = (Объект.Ссылка <> Справочники.ПодключаемоеОборудование.ПустаяСсылка());

	// Защита от изменения обработчика драйвера уже созданного экземпляра устройства
	Элементы.ОбработчикДрайвера.ТолькоПросмотр = (Объект.Ссылка <> Справочники.ПодключаемоеОборудование.ПустаяСсылка());
	
	Элементы.ФормаНастроить.Доступность = ЗначениеЗаполнено(Объект.Ссылка); 
	
	Если Объект.УстройствоИспользуется = Объект.УстройствоОтключено Тогда
		Объект.УстройствоИспользуется = НЕ Объект.УстройствоОтключено;
		Модифицированность = Ложь;
	КонецЕсли;
	
	// Доступ к узлу есть только для соответствующего оборудования
	Если Объект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККМOffline
		ИЛИ Объект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток Тогда
		Элементы.ПравилоОбмена.Видимость = Истина;
		ПараметрыВыбораПравилаОбмена = ПроцедурыОбменаДаннымиOfflineПереопределяемый.ПолучитьПараметрыВыбораПравилаОбмена(Объект);
		Если ЗначениеЗаполнено(ПараметрыВыбораПравилаОбмена) Тогда
			Элементы.ПравилоОбмена.ПараметрыВыбора = ПараметрыВыбораПравилаОбмена;
		КонецЕсли;
	Иначе
		Элементы.ПравилоОбмена.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Элементы.ФормаНастроить.Доступность = ЗначениеЗаполнено(Объект.Ссылка); 
	
КонецПроцедуры


&НаКлиенте
Процедура ТипОборудованияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если Объект.ТипОборудования <> ВыбранноеЗначение Тогда
		Объект.ТипОборудования = ВыбранноеЗначение;
		Модифицированность = Истина;

		// Загрузка и установка списка доступных обработок
		СписокДрайверов = ПолучитьСписокДрайверов(Объект.ТипОборудования);
		Элементы.ОбработчикДрайвера.СписокВыбора.Очистить();
		Для каждого СтрокаСписка Из СписокДрайверов Цикл
			Элементы.ОбработчикДрайвера.СписокВыбора.Добавить(СтрокаСписка.Значение, СтрокаСписка.Представление);
		КонецЦикла;

		Объект.ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ПустаяСсылка");
		
		// Доступ к узлу есть только для соответствующего оборудования
		Элементы.ПравилоОбмена.Видимость = (Строка(Объект.ТипОборудования) = "ККМ Offline"
			ИЛИ Строка(Объект.ТипОборудования) = "Весы с печатью этикеток");
		
	КонецЕсли;

	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ИмяОбработчикаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если ВыбранноеЗначение <> Объект.ОбработчикДрайвера Тогда
		ОбработатьВыборОбработчика(ВыбранноеЗначение, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборОбработчика(ВыбранныйОбработчик, СтандартнаяОбработка = Истина)

	Объект.Наименование = "'" + Строка(ВыбранныйОбработчик) + "'"
						+ ?(ПустаяСтрока(Строка(Объект.РабочееМесто)),
							"",
							" " + НСтр("ru='на'") + " " + Строка(Объект.РабочееМесто));

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если Не Отказ
	   И Модифицированность Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Настроить(Команда)
                              
	Если Не МенеджерОборудованияКлиент.ДрайверУстановлен(Объект.Ссылка) Тогда
		Результат = Вопрос(НСтр("ru = 'Установить драйвер?'"), РежимДиалогаВопрос.ДаНет);
		Если Результат = КодВозвратаДиалога.Да Тогда
			МенеджерОборудованияКлиент.УстановитьДрайвер(Объект.Ссылка);
		КонецЕсли;
	КонецЕсли;

	ОчиститьСообщения();
	СообщениеОбОшибке = "";
	НастройкиИзменены = Ложь;
	
	Записать();
		
	Результат = МенеджерОборудованияКлиент.ВыполнитьНастройкуОборудования(
	   Объект.Ссылка,
	   НастройкиИзменены,
	   СообщениеОбОшибке);
	   
	Прочитать();
	
	Если Результат И НастройкиИзменены Тогда
		// Настройки были изменены
		ОбновитьПовторноИспользуемыеЗначения();
	ИначеЕсли Не Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
	КонецЕсли;


КонецПроцедуры
