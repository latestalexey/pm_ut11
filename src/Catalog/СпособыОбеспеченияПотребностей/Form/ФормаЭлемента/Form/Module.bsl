////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполним переключатель ТипОбеспечения
	Элементы.ТипОбеспечения.СписокВыбора.Очистить();
	
	Для Каждого Значение Из Метаданные.Перечисления.ТипыОбеспечения.ЗначенияПеречисления Цикл
		Если Элементы.Найти("Страница" + Значение.Имя) <> Неопределено Тогда
			Элементы.ТипОбеспечения.СписокВыбора.Добавить(Перечисления.ТипыОбеспечения[Значение.Имя], Значение.Синоним);
		КонецЕсли;
	КонецЦикла; 
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	Элементы.ПокупкаИсточникОбеспеченияПотребностей.ОграничениеТипа 	= Новый ОписаниеТипов("СправочникСсылка.Партнеры");
	Элементы.ПеремещениеИсточникОбеспеченияПотребностей.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Склады");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ТипОбеспечения", Объект.ТипОбеспечения);
	Оповестить("Запись_СпособОбеспеченияПотребностей", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТипОбеспеченияПриИзменении(Элемент)
	
	ПриИзмененииТипаОбеспечения(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ПриИзмененииТипаОбеспечения(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииТипаОбеспечения(Форма)
	
	Если Форма.Объект.ТипОбеспечения = ПредопределенноеЗначение("Перечисление.ТипыОбеспечения.Покупка") Тогда
		
		Форма.Элементы.СтраницыПараметры.ТекущаяСтраница = Форма.Элементы.СтраницаПокупка;
		
	ИначеЕсли Форма.Объект.ТипОбеспечения = ПредопределенноеЗначение("Перечисление.ТипыОбеспечения.Перемещение") Тогда
		
		Форма.Элементы.СтраницыПараметры.ТекущаяСтраница = Форма.Элементы.СтраницаПеремещение;
		
	ИначеЕсли Форма.Объект.ТипОбеспечения = ПредопределенноеЗначение("Перечисление.ТипыОбеспечения.СборкаРазборка") Тогда
		
		Форма.Элементы.СтраницыПараметры.ТекущаяСтраница = Форма.Элементы.СтраницаСборкаРазборка;
		
	КонецЕсли;
	
КонецПроцедуры
