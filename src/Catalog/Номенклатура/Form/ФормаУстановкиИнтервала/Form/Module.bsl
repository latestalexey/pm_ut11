////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.ТипЗначения = Тип("Строка") Тогда
		
		ЭтаФорма.Заголовок = НСтр("ru = 'Установите значение отбора'");
		Элементы.СтраницыВидовОтборов.ТекущаяСтраница = Элементы.СтраницаВводаСтроки;
		ЗначениеВыбораСтрока = Параметры.ЗначениеОтбора;
		СписокСтрокОтбора    = Параметры.СписокСтрокОтбора;
		
	Иначе
		
		ЭтаФорма.Заголовок = НСтр("ru = 'Установите интервал значений отбора'");
		Элементы.СтраницыВидовОтборов.ТекущаяСтраница = Элементы.СтраницаВводаИнтервала;
		
		Если Параметры.ТипЗначения = Тип("Дата") Тогда
			Элементы.СтраницыТиповИнтервалов.ТекущаяСтраница = Элементы.СтраницаТипаДата;
			ЗначениеДатаОт   = Параметры.ЗначениеОт;
			ЗначениеДатаДо   = Параметры.ЗначениеДо;
			ТипЗначенияДата  = Истина;
		Иначе
			Элементы.СтраницыТиповИнтервалов.ТекущаяСтраница = Элементы.СтраницаТипаЧисло;
			ЗначениеЧислоОт   = Параметры.ЗначениеОт;
			ЗначениеЧислоДо   = Параметры.ЗначениеДо;
		КонецЕсли;
		
	КонецЕсли;
	
	ИмяРеквизита = Параметры.ИмяРеквизита;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ЗначениеВыбораСтрокаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗначениеВыбораСтрока = ВыбратьИзСписка(СписокСтрокОтбора, Элемент);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	НеверныйИнтервал = Ложь;
	
	Если Элементы.СтраницыВидовОтборов.ТекущаяСтраница = Элементы.СтраницаВводаИнтервала Тогда
		
		СтруктураВозврата = Новый Структура("ИнтервалОт, ИнтервалДо");
		
		Если ТипЗначенияДата Тогда
			СтруктураВозврата.ИнтервалОт = ЗначениеДатаОт;
			СтруктураВозврата.ИнтервалДо = ЗначениеДатаДо;
			
			НеверныйИнтервал = (ЗначениеДатаОт > ЗначениеДатаДо);
		Иначе
			СтруктураВозврата.ИнтервалОт = ЗначениеЧислоОт;
			СтруктураВозврата.ИнтервалДо = ЗначениеЧислоДо;
			
			НеверныйИнтервал = (ЗначениеЧислоОт > ЗначениеЧислоДо);
		КонецЕсли;
		
	Иначе
		СтруктураВозврата = Новый Структура("ЗначениеОтбора", ЗначениеВыбораСтрока);
	КонецЕсли;
	
	Если НеверныйИнтервал Тогда
		Предупреждение(НСтр("ru = 'Указан неверный диапазон значений!'"));
		Возврат;
	КонецЕсли;
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	
	Диалог.Период.ДатаНачала    = ЗначениеДатаОт;
	Диалог.Период.ДатаОкончания = ЗначениеДатаДо;
	
	Если Диалог.Редактировать() Тогда
		
		ЗначениеДатаОт = Диалог.Период.ДатаНачала;
		ЗначениеДатаДо = Диалог.Период.ДатаОкончания;
		
	КонецЕсли;
	
КонецПроцедуры