
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ПроверитьДоступностьИспользованияКассовойКниги(Ложь);
		
	КонецЕсли;
	
	ОпределитьРазрешениеПлатежейБезУказанияРаспоряжений();
	
	Элементы.Владелец.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СписокЗначений") Тогда
		
		Объект.ПолучателиПлатежейПриПеремещенииДС.Очистить();
		Для Каждого ПолучательПлатежа Из ВыбранноеЗначение Цикл
			Если ПолучательПлатежа.Значение.Пустая() Тогда
				Продолжить;
			КонецЕсли; 
			СтрокаПолучательПлатежа = Объект.ПолучателиПлатежейПриПеремещенииДС.Добавить();
			СтрокаПолучательПлатежа.ПолучательПлатежа = ПолучательПлатежа.Значение;
		КонецЦикла;
		
		УправлениеЭлементамиФормы();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПроверитьДоступностьИспользованияКассовойКниги(Ложь);

	ОпределитьРазрешениеПлатежейБезУказанияРаспоряжений();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = СформироватьАвтоНаименование();
	КонецЕсли;
	
	Если НЕ РазрешитьПлатежиБезУказанияРаспоряжений Тогда
		Объект.ПолучателиПлатежейПриПеремещенииДС.Очистить();
	ИначеЕсли Объект.ПолучателиПлатежейПриПеремещенииДС.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Не заполнен список касс-получателей для передачи денежных средств без ""распоряжений на перемещение"".'"),
			,
			"СписокКассПолучателейСтрокой",
			,
			Отказ
		);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Объект.ПолучателиПлатежейПриПеремещенииДС.Очистить();
	ОпределитьРазрешениеПлатежейБезУказанияРаспоряжений();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	СформироватьАвтоНаименование();
	ВыбранныйЭлемент = ВыбратьИзСписка(Элементы.Наименование.СписокВыбора, Элемент);
	Если ВыбранныйЭлемент <> Неопределено Тогда
		Объект.Наименование = ВыбранныйЭлемент.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаДенежныхСредствПриИзменении(Элемент)
	
	ПроверитьДоступностьИспользованияКассовойКниги(Истина);
	
	Объект.ПолучателиПлатежейПриПеремещенииДС.Очистить();
	ОпределитьРазрешениеПлатежейБезУказанияРаспоряжений();
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьПлатежиБезУказанияРаспоряженийПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы();

КонецПроцедуры

&НаКлиенте
Процедура СписокКассПолучателейСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Владелец) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Поле ""Организация"" не заполнено'"),
			,
			"Владелец",
			"Объект",
			Отказ
		);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Поле ""Валюта"" не заполнено'"),
			,
			"ВалютаДенежныхСредств",
			"Объект",
			Отказ
		);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Касса", Объект.Ссылка);
		ПараметрыФормы.Вставить("Владелец", Объект.Владелец);
		ПараметрыФормы.Вставить("ВалютаДенежныхСредств", Объект.ВалютаДенежныхСредств);
		ПараметрыФормы.Вставить("СписокПолучателейПлатежей", Новый СписокЗначений);
		Для Каждого СтрокаПолучательПлатежа Из Объект.ПолучателиПлатежейПриПеремещенииДС Цикл
			ПараметрыФормы.СписокПолучателейПлатежей.Добавить(СтрокаПолучательПлатежа.ПолучательПлатежа)
		КонецЦикла;
		
		ОткрытьФормуМодально("Справочник.Кассы.Форма.ФормаПолучателиПлатежейПриПеремещенииДС",
			ПараметрыФормы,
			ЭтаФорма
		);
			
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		Результат = ОткрытьФормуМодально("Справочник.Кассы.Форма.РазблокированиеРеквизитов");
		Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
			ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, Результат);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Управление элементами формы

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	НеИспользоватьПланированиеДенежныхСредств	= ПолучитьФункциональнуюОпцию("НеИспользоватьПланированиеДенежныхСредств");
	ИспользоватьНесколькоКасс					= ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоКасс");
	
	Если НЕ ИспользоватьНесколькоКасс Тогда
		
		Элементы.РазрешитьПлатежиБезУказанияРаспоряжений.Видимость			= Ложь;
		Элементы.ДекорацияРазрешитьПлатежиБезУказанияРаспоряжений.Видимость	= Ложь;
		Элементы.СписокКассПолучателейСтрокой.Видимость						= Ложь;
		
	Иначе
		
		Если РазрешитьПлатежиБезУказанияРаспоряжений
			ИЛИ НеИспользоватьПланированиеДенежныхСредств Тогда
			СписокКассПолучателейСтрокой = ПолучитьСписокКассПолучателей();
			Элементы.СписокКассПолучателейСтрокой.Видимость = Истина;
		Иначе
			Элементы.СписокКассПолучателейСтрокой.Видимость = Ложь;
		КонецЕсли;
		
		Элементы.ДекорацияРазрешитьПлатежиБезУказанияРаспоряжений.Видимость = НеИспользоватьПланированиеДенежныхСредств;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПроверитьДоступностьИспользованияКассовойКниги(СброситьФлагИспользоватьВКассовойКниге)
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Элементы.ИспользоватьВКассовойКниге.Доступность = ВалютаРегламентированногоУчета = Объект.ВалютаДенежныхСредств;
	
	Если СброситьФлагИспользоватьВКассовойКниге Тогда
		Если ВалютаРегламентированногоУчета <> Объект.ВалютаДенежныхСредств Тогда
			Объект.ИспользоватьВКассовойКниге = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Функция СформироватьАвтоНаименование()
	
	Элементы.Наименование.СписокВыбора.Очистить();
	
	СтрокаНаименования = СокрЛП(Объект.Владелец) + " (" + Строка(объект.ВалютаДенежныхСредств) + ")";
	СтрокаНаименования = Лев(СтрокаНаименования, 100);
	
	Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
	
	Возврат СтрокаНаименования;

КонецФункции

&НаСервере
Процедура ОпределитьРазрешениеПлатежейБезУказанияРаспоряжений()
	
	РазрешитьПлатежиБезУказанияРаспоряжений = Объект.ПолучателиПлатежейПриПеремещенииДС.Количество() > 0;
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокКассПолучателей()

	Если НЕ РазрешитьПлатежиБезУказанияРаспоряжений
		ИЛИ Объект.ПолучателиПлатежейПриПеремещенииДС.Количество() = 0 Тогда
		 СписокКасс = НСтр("ru='<Указать кассы>'");
	Иначе
		СписокКасс = "";
		Для Каждого СтрокаПолучательПлатежа Из Объект.ПолучателиПлатежейПриПеремещенииДС Цикл
			СписокКасс = СписокКасс + ?(СписокКасс="","",", ") + СтрокаПолучательПлатежа.ПолучательПлатежа.Наименование;
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат СписокКасс;

КонецФункции
