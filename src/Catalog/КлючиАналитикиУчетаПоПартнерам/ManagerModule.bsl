#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Процедура устанавливает пометку на удаление для найденных элементов справочника.
//
// Параметры:
//	СтруктураПараметров - Структура - Параметры выбора элементов справочника
//	ПометкаУдаления - Булево - Признак установки пометки на удаление
//
Процедура УстановитьПометкуУдаления(СтруктураПараметров, ПометкаУдаления) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаПоПартнерам КАК Таблица
	|ГДЕ
	|	Таблица.ПометкаУдаления <> &ПометкаУдаления
	|");
	Если СтруктураПараметров.Свойство("Партнер") Тогда

		Запрос.УстановитьПараметр("Партнер", СтруктураПараметров.Партнер);
		Запрос.Текст = Запрос.Текст + " И Таблица.Партнер = &Партнер";

	КонецЕсли;
	Если СтруктураПараметров.Свойство("Организация") Тогда

		Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
		Запрос.Текст = Запрос.Текст + " И Таблица.Организация = &Организация";

	КонецЕсли;
	Если СтруктураПараметров.Свойство("Контрагент") Тогда

		Запрос.УстановитьПараметр("Контрагент", СтруктураПараметров.Контрагент);
		Запрос.Текст = Запрос.Текст + " И Таблица.Контрагент = &Контрагент";

	КонецЕсли;
	Запрос.УстановитьПараметр("ПометкаУдаления", ПометкаУдаления);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		Выборка.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(ПометкаУдаления);

	КонецЦикла;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Прочее

Процедура ЗаменитьАналитикуВНабореЗаписей(РезультатЗапроса, СоответствиеАналитик, НаборЗаписей)
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
	
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.Прочитать();
		
		ЗаписыватьНабор = Ложь;
		
		Для Каждого Запись Из НаборЗаписей Цикл
			
			АналитикаУчетаПоПартнерам = СоответствиеАналитик[Запись.АналитикаУчетаПоПартнерам];
			Если ЗначениеЗаполнено(АналитикаУчетаПоПартнерам) Тогда
				Запись.АналитикаУчетаПоПартнерам = АналитикаУчетаПоПартнерам;
				ЗаписыватьНабор = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЗаписыватьНабор Тогда
			НаборЗаписей.Записать(Истина);
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Замена дублей ключей аналитики

Процедура ЗаменитьДублиКлючейАналитики() Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка КАК Ссылка,
	|	ДанныеСправочника.ПометкаУдаления КАК ПометкаУдаления,
	|	Аналитика.КлючАналитики КАК КлючАналитики
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаПоПартнерам КАК ДанныеСправочника
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК ДанныеРегистра
	|	ПО
	|		ДанныеСправочника.Ссылка = ДанныеРегистра.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		ДанныеСправочника.Партнер = Аналитика.Партнер
	|		И ДанныеСправочника.Организация = Аналитика.Организация
	|		И ДанныеСправочника.Контрагент = Аналитика.Контрагент
	|ГДЕ
	|	ДанныеРегистра.КлючАналитики ЕСТЬ NULL
	|");
	
	// Сформируем соответствие ключей аналитики.
	СоответствиеАналитик = Новый Соответствие;
	МассивАналитик = Новый Массив;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
	
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СоответствиеАналитик.Вставить(Выборка.Ссылка, Выборка.КлючАналитики);
			МассивАналитик.Добавить(Выборка.Ссылка);
			
			Если Не Выборка.ПометкаУдаления Тогда
				СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
				Попытка
					СправочникОбъект.УстановитьПометкуУдаления(Истина, Ложь);
				Исключение
				КонецПопытки;
			КонецЕсли;

		КонецЦикла;
	
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПоследовательности.Регистратор КАК Регистратор
		|ИЗ
		|	Последовательность.ПроведениеПоРасчетамСКлиентами КАК ДанныеПоследовательности
		|ГДЕ
		|	ДанныеПоследовательности.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПоследовательности.Регистратор КАК Регистратор
		|ИЗ
		|	Последовательность.ПроведениеПоРасчетамСПоставщиками КАК ДанныеПоследовательности
		|ГДЕ
		|	ДанныеПоследовательности.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваКВыплате КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваБезналичные КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваКПоступлениюБезналичные КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваКСписаниюБезналичные КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.РасчетыПоЭквайрингу КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам В (&МассивАналитик)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|");
		Запрос.УстановитьПараметр("МассивАналитик", МассивАналитик);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		РезультатЗапросаРасчетыСКлиентами = МассивРезультатов[0];
		РезультатЗапросаРасчетыСПоставщиками = МассивРезультатов[1];
		РезультатЗапросаСебестоимость = МассивРезультатов[2];
		РезультатЗапросаВыручка = МассивРезультатов[3];
		РезультатЗапросаПроведениеПоРасчетамСКлиентами = МассивРезультатов[4];
		РезультатЗапросаПроведениеПоРасчетамСПоставщиками = МассивРезультатов[5];
		РезультатЗапросаДенежныеСредстваКВыплате = МассивРезультатов[6];
		РезультатЗапросаДенежныеСредстваБезналичные = МассивРезультатов[7];
		РезультатЗапросаДенежныеСредстваНаличные = МассивРезультатов[8];
		РезультатЗапросаДенежныеСредстваКПоступлению = МассивРезультатов[9];
		РезультатЗапросаДенежныеСредстваКСписанию = МассивРезультатов[10];
		РезультатЗапросаРасчетыПоЭквайрингу = МассивРезультатов[11];
		РезультатЗапросаРасчетыПоДоговорамКредитовИДепозитов = МассивРезультатов[12];
		
		// Заменим дубли аналитики в регистре "Расчеты с клиентами".
		Если Не РезультатЗапросаРасчетыСКлиентами.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.РасчетыСКлиентами.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаРасчетыСКлиентами, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в регистре "Расчеты с поставщиками".
		Если Не РезультатЗапросаРасчетыСПоставщиками.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.РасчетыСПоставщиками.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаРасчетыСПоставщиками, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
	
		// Заменим дубли аналитики в регистре "Себестоимость товаров".
		Если Не РезультатЗапросаСебестоимость.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.СебестоимостьТоваров.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаСебестоимость, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в регистре "Выручка и себестоимость продаж".
		Если Не РезультатЗапросаВыручка.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.ВыручкаИСебестоимостьПродаж.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаВыручка, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в последовательности "Проведение по расчетам с клиентами".
		Если Не РезультатЗапросаПроведениеПоРасчетамСКлиентами.Пустой() Тогда
			НаборЗаписей = Последовательности.ПроведениеПоРасчетамСКлиентами.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаПроведениеПоРасчетамСКлиентами, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в последовательности "Проведение по расчетам с поставщиками".
		Если Не РезультатЗапросаПроведениеПоРасчетамСПоставщиками.Пустой() Тогда
			НаборЗаписей = Последовательности.ПроведениеПоРасчетамСПоставщиками.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаПроведениеПоРасчетамСПоставщиками, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в регистре "Денежные средства к выплате".
		Если Не РезультатЗапросаДенежныеСредстваКВыплате.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.ДенежныеСредстваКВыплате.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаДенежныеСредстваКВыплате, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в регистре "Денежные средства (безналичные)".
		Если Не РезультатЗапросаДенежныеСредстваБезналичные.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.ДенежныеСредстваБезналичные.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаДенежныеСредстваБезналичные, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в регистре "Денежные средства (наличные)".
		Если Не РезультатЗапросаДенежныеСредстваНаличные.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.ДенежныеСредстваНаличные.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаДенежныеСредстваНаличные, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в регистре "Денежные средства (безналичные)".
		Если Не РезультатЗапросаДенежныеСредстваКПоступлению.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.ДенежныеСредстваКПоступлениюБезналичные.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаДенежныеСредстваКПоступлению, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в регистре "Денежные средства (безналичные)".
		Если Не РезультатЗапросаДенежныеСредстваКСписанию.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.ДенежныеСредстваКСписаниюБезналичные.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаДенежныеСредстваКСписанию, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в регистре "Расчеты по эквайрингу".
		Если Не РезультатЗапросаРасчетыПоЭквайрингу.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.РасчетыПоЭквайрингу.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаРасчетыПоЭквайрингу, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
		// Заменим дубли аналитики в регистре "Расчеты по договорам кредитов и депозитов".
		Если Не РезультатЗапросаРасчетыПоДоговорамКредитовИДепозитов.Пустой() Тогда
			НаборЗаписей = РегистрыНакопления.РасчетыПоДоговорамКредитовИДепозитов.СоздатьНаборЗаписей();
			ЗаменитьАналитикуВНабореЗаписей(РезультатЗапросаРасчетыПоДоговорамКредитовИДепозитов, СоответствиеАналитик, НаборЗаписей);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли