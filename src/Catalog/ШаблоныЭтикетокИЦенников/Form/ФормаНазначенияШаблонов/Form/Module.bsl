
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Шаблон = Параметры.Шаблон;
	Назначение = Параметры.Назначение;
	
	Если Параметры.Назначение = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЦенникДляТоваров Тогда
		Элементы.НазначитьДля.СписокВыбора.Добавить("Номенклатура", НСтр("ru = 'Номенклатуры'"));
		Элементы.НазначитьДля.СписокВыбора.Добавить("ВидНоменклатуры", НСтр("ru = 'Вида номенклатуры'"));
		
		Номенклатура = Параметры.ДляЧего;
		ВидНоменклатуры = Параметры.ДляЧего.ВидНоменклатуры;
		
	ИначеЕсли Параметры.Назначение = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляТоваров Тогда
		Элементы.НазначитьДля.СписокВыбора.Добавить("Номенклатура", НСтр("ru = 'Номенклатуры'"));
		Элементы.НазначитьДля.СписокВыбора.Добавить("ВидНоменклатуры", НСтр("ru = 'Вида номенклатуры'"));
		
		Номенклатура = Параметры.ДляЧего;
		ВидНоменклатуры = Параметры.ДляЧего.ВидНоменклатуры;
		
	ИначеЕсли Параметры.Назначение = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляСкладскихЯчеек Тогда
		Элементы.НазначитьДля.СписокВыбора.Добавить("Запрещено", НСтр("ru = 'Не назначается'"));
	ИначеЕсли Параметры.Назначение = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаСерииНоменклатуры Тогда
		Элементы.НазначитьДля.СписокВыбора.Добавить("ВидНоменклатуры", НСтр("ru = 'Вида номенклатуры'"));
	ИначеЕсли Параметры.Назначение = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляДоставки Тогда
		Элементы.НазначитьДля.СписокВыбора.Добавить("Партнер", НСтр("ru = 'Партнера'"));
	КонецЕсли;
	
	НазначитьДля = Элементы.НазначитьДля.СписокВыбора.Получить(0).Значение;
	
	ПриИзмененииНазначения(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура НазначитьДляПриИзменении(Элемент)
	
	ПриИзмененииНазначения(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Назначить(Команда)
	НазначитьНаСервере();
	Закрыть();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура НазначитьНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Назначение = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЦенникДляТоваров Тогда
		
		Если НазначитьДля = "Номенклатура" Тогда
			Объект = Номенклатура.ПолучитьОбъект();
			Объект.ШаблонЦенника = Шаблон;
			Объект.Записать();
		ИначеЕсли НазначитьДля = "ВидНоменклатуры" Тогда
			Объект = ВидНоменклатуры.ПолучитьОбъект();
			Объект.ШаблонЦенника = Шаблон;
			Объект.Записать();
		КонецЕсли;
		
	ИначеЕсли Назначение = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляТоваров Тогда
		
		Если НазначитьДля = "Номенклатура" Тогда
			Объект = Номенклатура.ПолучитьОбъект();
			Объект.ШаблонЭтикетки = Шаблон;
			Объект.Записать();
		ИначеЕсли НазначитьДля = "ВидНоменклатуры" Тогда
			Объект = ВидНоменклатуры.ПолучитьОбъект();
			Объект.ШаблонЭтикетки = Шаблон;
			Объект.Записать();
		КонецЕсли;
		
	ИначеЕсли Назначение = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляСкладскихЯчеек Тогда
		
	ИначеЕсли Назначение = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаСерииНоменклатуры Тогда
		
		Объект = ВидНоменклатуры.ПолучитьОбъект();
		Объект.ШаблонЭтикеткиСерии = Шаблон;
		Объект.Записать();
		
	ИначеЕсли Назначение = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляДоставки Тогда
		
		Объект = Партнер.ПолучитьОбъект();
		Объект.ШаблонЭтикетки = Шаблон;
		Объект.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииНазначения(Форма)

	Если Форма.НазначитьДля = "Номенклатура" Тогда
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаНоменклатура;
	ИначеЕсли Форма.НазначитьДля = "ВидНоменклатуры" Тогда
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаВидНоменклатуры;
	ИначеЕсли Форма.НазначитьДля = "Запрещено" Тогда
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаЗапрещено;
	ИначеЕсли Форма.НазначитьДля = "Партнер" Тогда
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаПартнер;
	КонецЕсли;
	
КонецПроцедуры
