////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьЗаголовки();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступность();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Элементы.Высота.Доступность  			= Не Объект.НеОграниченаПоРазмерам;
	Элементы.Глубина.Доступность 			= Не Объект.НеОграниченаПоРазмерам;
	Элементы.Ширина.Доступность  			= Не Объект.НеОграниченаПоРазмерам;
	Элементы.Объем.Доступность  			= Не Объект.НеОграниченаПоРазмерам;
	
	Элементы.Грузоподъемность.Доступность = Не Объект.НеОграниченаПоГрузоподъемности;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)
	ПересчитатьОбъем();
КонецПроцедуры

&НаКлиенте
Процедура ГлубинаПриИзменении(Элемент)
	ПересчитатьОбъем();
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	ПересчитатьОбъем();
КонецПроцедуры

&НаКлиенте
Процедура НеОграниченаПоРазмерамПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура НеОграниченаПоГрузоподъемностиПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура УстановитьЗаголовки()

	ЕдиницаРазмеров = Строка(Константы.ЕдиницаИзмеренияЛинейныхРазмеров.Получить());
	ЕдиницаИзмеренияОбъема = Строка(Константы.ЕдиницаИзмеренияОбъема.Получить());
	ЕдиницаИзмеренияВеса = Строка(Константы.ЕдиницаИзмеренияВеса.Получить());
		
	Элементы.Высота.Заголовок  			= НСтр("ru='Высота, '") + ЕдиницаРазмеров;
	Элементы.Глубина.Заголовок 			= НСтр("ru='Глубина, '") + ЕдиницаРазмеров;
	Элементы.Ширина.Заголовок  			= НСтр("ru='Ширина, '") + ЕдиницаРазмеров;
	Элементы.Объем.Заголовок  			= НСтр("ru='Объем, '") + ЕдиницаИзмеренияОбъема;
	Элементы.Грузоподъемность.Заголовок = НСтр("ru='Грузоподъемность, '") + ЕдиницаИзмеренияВеса;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	Элементы.Высота.Доступность  			= Не Объект.НеОграниченаПоРазмерам;
	Элементы.Глубина.Доступность 			= Не Объект.НеОграниченаПоРазмерам;
	Элементы.Ширина.Доступность  			= Не Объект.НеОграниченаПоРазмерам;
	Элементы.Объем.Доступность  			= Не Объект.НеОграниченаПоРазмерам;
	
	Элементы.Грузоподъемность.Доступность = Не Объект.НеОграниченаПоГрузоподъемности;
КонецПроцедуры

&НаСервере
Процедура ПересчитатьОбъем()
	
	КоэффициентПересчетаВЕдиницыОбъема = Константы.КоэффициентПересчетаВЕдиницыОбъема.Получить();
	
	Если Не ЗначениеЗаполнено(КоэффициентПересчетаВЕдиницыОбъема) Тогда
		
		ТекстСообщения = НСтр("ru='В настройках программы не установлен коэффициент перерасчета из единиц измерения линейных размеров в единицы измерения объема'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	Иначе
		
		Объект.Объем = Объект.Высота * Объект.Глубина * Объект.Ширина * КоэффициентПересчетаВЕдиницыОбъема;
	
	КонецЕсли;
	
КонецПроцедуры
