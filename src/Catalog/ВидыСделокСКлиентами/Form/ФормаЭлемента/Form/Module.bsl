////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УправлениеДоступностью();
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыИЗадачи") Тогда
		
		Элементы.ТипСделки.СписокВыбора.Добавить(Перечисления.ТипыСделокСКлиентами.ПрочиеНепроцессныеСделки);
		Элементы.ТипСделки.СписокВыбора.Добавить(Перечисления.ТипыСделокСКлиентами.СделкиСРучнымПереходомПоЭтапам);
		Элементы.ТипСделки.КнопкаВыбора = Ложь;
		Элементы.ТипСделки.КнопкаСпискаВыбора = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТипСделкиПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.ТипСделки) Тогда
		СформироватьЭтапыИНаименование();
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ "Этапы сделки по продаже"

&НаКлиенте
Процедура ЭтапыСделкиПоПродажеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	СортироватьЭтапы();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// Команда подсистемы "Запрет редактирования реквизитов"
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		
		Результат = ОткрытьФормуМодально("Справочник.ВидыСделокСКлиентами.Форма.РазблокированиеРеквизитов");
		
		Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
			
			ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, Результат);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура СортироватьЭтапы()

	//получить список этапов, упорядоченный по реквизиту "РеквизитДопУпорядочивания"
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СостоянияПроцессов.Ссылка КАК ЭтапПроцессаПродажи
		|ИЗ
		|	Справочник.СостоянияПроцессов КАК СостоянияПроцессов
		|ГДЕ
		|	СостоянияПроцессов.Ссылка В(&Этапы)
		|
		|УПОРЯДОЧИТЬ ПО
		|	СостоянияПроцессов.РеквизитДопУпорядочивания"
	);
	Запрос.УстановитьПараметр("Этапы",Объект.ЭтапыСделкиПоПродаже.Выгрузить(,"ЭтапПроцессаПродажи"));

	//загрузить упорядоченный список
	Объект.ЭтапыСделкиПоПродаже.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

&НаСервере
Процедура СформироватьЭтапыИНаименование()

	Объект.ЭтапыСделкиПоПродаже.Очистить();

	Если Объект.ТипСделки = Перечисления.ТипыСделокСКлиентами.СделкиСРучнымПереходомПоЭтапам
		ИЛИ Объект.ТипСделки = Перечисления.ТипыСделокСКлиентами.ПрочиеНепроцессныеСделки Тогда
		
		Наименование = "";
		
	Иначе
		
		//для сделок с управлением бизнес-процессом заполнить наименование и набор этапов
		ОписаниеТипа = СделкиСервер.ПолучитьОписаниеТипаСделки(Объект.ТипСделки);
		Объект.Наименование = ОписаниеТипа.Представление;
		ЭтапыПроцесса = БизнесПроцессы[ОписаниеТипа.Имя].СписокЭтапов();
		Для Каждого ЭтапПроцесса Из ЭтапыПроцесса Цикл
			Этап = Объект.ЭтапыСделкиПоПродаже.Добавить();
			Этап.ЭтапПроцессаПродажи = ЭтапПроцесса;
		КонецЦикла;
		
	КонецЕсли;

	УправлениеДоступностью();

КонецПроцедуры

&НаСервере
Процедура УправлениеДоступностью()

	Элементы.ГруппаЭтапы.ТолькоПросмотр = 
		(Объект.ТипСделки <> Перечисления.ТипыСделокСКлиентами.СделкиСРучнымПереходомПоЭтапам);
	Элементы.ГруппаЭтапы.Видимость = 
		(Объект.ТипСделки <> Перечисления.ТипыСделокСКлиентами.ПрочиеНепроцессныеСделки);

КонецПроцедуры