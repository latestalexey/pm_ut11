#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ 

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
		
		
		ДанныеВыбора =  СписокВыбораАвтомобилей(Параметры.СтрокаПоиска)
		
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция СписокВыбораАвтомобилей(СтрокаПоиска)
	ДанныеВыбора = Новый СписокЗначений;
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТранспортныеСредства.Ссылка КАК Значение,
		|	ПРЕДСТАВЛЕНИЕ(ТранспортныеСредства.Ссылка) КАК Представление
		|ИЗ
		|	Справочник.ТранспортныеСредства КАК ТранспортныеСредства
		|ГДЕ
		|	ТранспортныеСредства.Код ПОДОБНО &Текст
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
		Запрос.УстановитьПараметр("Текст", "%" + СокрЛП(СтрокаПоиска) + "%");
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Значение, Выборка.Представление);
		КонецЦикла;
	КонецЕсли;

	Возврат ДанныеВыбора;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы

// Перезаписывает транспортные средства для заполнения реквизита ВместимостьПредставление, используемого в доставке.
//
Процедура ЗаполнитьВместимостьПредставлениеТранспортныхСредств() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТранспортныеСредства.Ссылка
	|ИЗ
	|	Справочник.ТранспортныеСредства КАК ТранспортныеСредства
	|ГДЕ
	|	ТранспортныеСредства.ВместимостьПредставление = """"";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Выборка.Ссылка.ПолучитьОбъект().Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли