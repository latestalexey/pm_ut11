
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Дерево = РеквизитФормыВЗначение("ДеревоРеквизитов");
	
	Объект = Метаданные.НайтиПоПолномуИмени(Параметры.ТипОбъекта);
	
	Заголовок =  НСтр("ru = 'Выбор реквизита'") + " " + ?(Объект.Синоним = "", Объект.Имя, Объект.Синоним);
	
	
	Для Каждого Реквизит Из Объект.СтандартныеРеквизиты Цикл
		Если Реквизит.Имя = "Проведен"
		 Или Реквизит.Имя = "ПометкаУдаления"
		 Или Реквизит.Имя = "Ссылка" Тогда 
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Имя = Реквизит.Имя;
		НоваяСтрока.Представление = Реквизит.Имя;
		НоваяСтрока.Тип = Реквизит.Тип;
	КонецЦикла;
	НоваяСтрока = Дерево.Строки.Добавить();
	НоваяСтрока.Имя = "Представление";
	НоваяСтрока.Представление = "Представление";
	НоваяСтрока.Тип = "Строка";
	
	Для Каждого Реквизит Из Объект.Реквизиты Цикл
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Имя = Реквизит.Имя;
		НоваяСтрока.Представление = Реквизит.Имя;
		НоваяСтрока.Тип = Реквизит.Тип;
		
		Типы = Реквизит.Тип.Типы();
		Если Типы.Количество() > 1 Тогда 
			Продолжить;
		КонецЕсли;
			
		ОбъектРеквизит = Метаданные.НайтиПоТипу(Типы[0]);
		Если ОбъектРеквизит = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Метаданные.Справочники.Содержит(ОбъектРеквизит) 
		 Или Метаданные.Документы.Содержит(ОбъектРеквизит) 
		 Или Метаданные.БизнесПроцессы.Содержит(ОбъектРеквизит) 
		 Или Метаданные.Задачи.Содержит(ОбъектРеквизит) 
		 Или Метаданные.ПланыВидовХарактеристик.Содержит(ОбъектРеквизит) 
		 Или Метаданные.ПланыСчетов.Содержит(ОбъектРеквизит) Тогда 
			
			Для Каждого ДочернийРеквизит Из ОбъектРеквизит.Реквизиты Цикл
				ДочерняяСтрока = НоваяСтрока.Строки.Добавить();
				ДочерняяСтрока.Имя = Реквизит.Имя + "." + ДочернийРеквизит.Имя;
				ДочерняяСтрока.Представление = ДочернийРеквизит.Имя;
				ДочерняяСтрока.Тип = ДочернийРеквизит.Тип;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоРеквизитов");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ "ДЕРЕВО РЕКВИЗИТОВ"

&НаКлиенте
Процедура ДеревоРеквизитовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ВыбратьЗначениеРеквизита();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьЗначениеРеквизита();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ВыбратьЗначениеРеквизита()
	
	ТекущиеДанные = Элементы.ДеревоРеквизитов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	Если ТекущиеДанные.Имя = "" Тогда 
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Имя", ТекущиеДанные.Имя);
	Результат.Вставить("Представление", ТекущиеДанные.Представление);
	
	Закрыть(Результат);
	
КонецПроцедуры