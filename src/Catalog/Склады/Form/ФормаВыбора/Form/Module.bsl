////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Для Каждого Элемент Из Параметры.Отбор Цикл
		СписокОбязательныхОтборов.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	ГруппаСкладов = Неопределено;
	Если Параметры.Свойство("ГруппаСкладов", ГруппаСкладов) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Ссылка", ГруппаСкладов, ВидСравненияКомпоновкиДанных.ВИерархии,,Истина);
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	
	ВыборГруппыСкладов = Неопределено;
	
	Если Не Параметры.Свойство("ВыборГруппыСкладов", ВыборГруппыСкладов) Или
		(Не ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовПродажи") И Параметры.Свойство("ДокументПродажи"))
		Или (Не ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовЗакупки") И Параметры.Свойство("ДокументЗакупки")) Тогда
		
		МассивОтбора = Новый Массив();
		МассивОтбора.Добавить(Перечисления.ВыборГруппыСкладов.Запретить);
		МассивОтбора.Добавить(Перечисления.ВыборГруппыСкладов.РазрешитьВЗаказах);
		МассивОтбора.Добавить(Перечисления.ВыборГруппыСкладов.РазрешитьВЗаказахИНакладных);
		ВыборГруппыСкладов = Новый ФиксированныйМассив(МассивОтбора);
		
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ВыборГруппыСкладов", ВыборГруппыСкладов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Для Каждого ИмяОтбора Из СписокОбязательныхОтборов Цикл
		Для Каждого Элемент Из ЭтаФорма.Список.Отбор.Элементы Цикл
			Если Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяОтбора) Тогда
				Элемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ СПИСОК

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыборГруппыСкладов) И ЗначениеЗаполнено(Значение) И Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		ВыборЗапрещен = ТекущиеДанные.ЭтоГруппа;
		
		Если ВыборЗапрещен Тогда
			Если ТипЗнч(ВыборГруппыСкладов) = Тип("ФиксированныйМассив") Тогда
				Для Каждого ТекЭлемент Из ВыборГруппыСкладов Цикл
					Если ТекЭлемент = ТекущиеДанные.ВыборГруппы Тогда
						ВыборЗапрещен = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Если ВыборГруппыСкладов = ТекущиеДанные.ВыборГруппы Тогда
					ВыборЗапрещен = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ВыборЗапрещен Тогда
			
			СтандартнаяОбработка = Ложь;
			Отказ = Истина;
			ТекстПредупреждения = НСтр("ru='Группу ""%ГруппаСкладов%"" нельзя выбирать в документ.'");
			ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ГруппаСкладов%", ТекущиеДанные.Наименование);
			Предупреждение(ТекстПредупреждения);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

