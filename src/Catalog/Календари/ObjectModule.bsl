#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ 

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроизводственныйКалендарь.Пустая() 
		Или Не ЗначениеЗаполнено(ВидКалендаря) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Ссылка.Пустая() Тогда
		ДополнительныеСвойства.Вставить("СформироватьПоДаннымПроизводственногоКалендаря", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	// Заполняем значениями производственного календаря
	Если ДополнительныеСвойства.Свойство("СформироватьПоДаннымПроизводственногоКалендаря") Тогда
		СформироватьПоДаннымПроизводственногоКалендаря();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Заполняет пользовательских календарь значениями производственного календаря, ссылка на который 
// указана в одноименном поле. Используется в модели сервиса в случае запрета на ручное редактирование словаря
//
Процедура СформироватьПоДаннымПроизводственногоКалендаря() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ДанныеПроизводственногоКалендаря.Год КАК Год
	               |ИЗ
	               |	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	               |ГДЕ
	               |	ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = &ПроизводственныйКалендарь";
	Запрос.УстановитьПараметр("ПроизводственныйКалендарь", ПроизводственныйКалендарь);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Дни = Новый СписокЗначений;
		Дни.ЗагрузитьЗначения(Справочники.Календари.ЗаполнитьПоДаннымПроизводственногоКалендаря(
			ПроизводственныйКалендарь, Выборка.Год, ВидКалендаря));
			
		Справочники.Календари.ЗаписатьДанныеГрафикаВРегистр(Ссылка, Выборка.Год, Дни);
		
	КонецЦикла;
	
	СтандартныеПодсистемыПереопределяемый.УстановитьРучноеРедактированиеКалендаря(Ссылка, Ложь);
	
КонецПроцедуры


#КонецЕсли