///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Заголовок = НСтр("ru = 'Настройка заполнения календаря'");
	
	ПропуститьВыборПроизводственногоКалендаря = Ложь;
	
	ЗаполнитьСписокПроизодственныхКалендарей(ПропуститьВыборПроизводственногоКалендаря);
	
	Если ПропуститьВыборПроизводственногоКалендаря Тогда
		Элементы.ГруппаВыборПроизводственногоКалендаря.Видимость = Ложь;
	КонецЕсли;
	
	ЗаполнитьВидКалендаря();
	
	ЗаполнитьВидимыеСтраницы();
	
	Если ВидимыеСтраницы.Количество() = 0 Тогда
		// Нет отображаемых страниц - форма не открывается
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УстановитьТекущуюСтраницу();
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура СписокПроизводственныхКалендарейПриАктивизацииСтроки(Элемент)
	
	ПроизводственныйКалендарь = Элементы.СписокПроизводственныхКалендарей.ТекущиеДанные.ПроизводственныйКалендарь;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
	
&НаКлиенте
Процедура Далее(Команда)

	Если ЭтоПоследняяСтраница Тогда
		Готово();
	Иначе
		СменитьСтраницу(1);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	СменитьСтраницу(-1);	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьВидимыеСтраницы()
	
	Страницы = Элементы.ГруппаСтраницыФормы.ПодчиненныеЭлементы;
	
	МассивСтраниц = Новый Массив;
	Для ИндексСтраницы = 0 По Страницы.Количество() - 1 Цикл
		Страница = Страницы[ИндексСтраницы];
		Если Страница.Видимость Тогда 
			МассивСтраниц.Добавить(ИндексСтраницы);
		КонецЕсли;
	КонецЦикла;
	
	ВидимыеСтраницы = Новый ФиксированныйМассив(МассивСтраниц);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтраницу()
	
	Если ВидимыеСтраницы.Количество() > 0 Тогда
		Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницыФормы.ПодчиненныеЭлементы.Получить(ВидимыеСтраницы.Получить(0));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПроизодственныхКалендарей(ПропускатьВыборПроизводственногоКалендаря)
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицуИзМассива(
		СписокПроизводственныхКалендарей, 
		Справочники.ПроизводственныеКалендари.СписокПроизводственныхКалендарей(), 
		"ПроизводственныйКалендарь");
	
	Если СписокПроизводственныхКалендарей.Количество() = 1 Тогда
		ПропускатьВыборПроизводственногоКалендаря = Истина;
		ПроизводственныйКалендарь = СписокПроизводственныхКалендарей[0].ПроизводственныйКалендарь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидКалендаря()
	
	ВидКалендаря = Перечисления.ВидыКалендарей.Пятидневка;
	
КонецПроцедуры

&НаСервере
Процедура СменитьСтраницу(Направление)
	
	ИндексТекущейСтраницы 	= ВидимыеСтраницы.Найти(ИндексСтраницы(Элементы.ГруппаСтраницыФормы.ТекущаяСтраница));
	ИндексНовойСтраницы 	= ИндексТекущейСтраницы + Направление;
	
	Если ИндексНовойСтраницы >= 0 И ИндексНовойСтраницы < ВидимыеСтраницы.Количество() Тогда
		Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницыФормы.ПодчиненныеЭлементы.Получить(ВидимыеСтраницы.Получить(ИндексНовойСтраницы));
	КонецЕсли;
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	ИндексТекущейСтраницы = ИндексСтраницы(Элементы.ГруппаСтраницыФормы.ТекущаяСтраница);
	ЭтоПоследняяСтраница = ВидимыеСтраницы.Найти(ИндексТекущейСтраницы) = ВидимыеСтраницы.ВГраница();
	
	Элементы.ФормаДалее.Заголовок = ?(ЭтоПоследняяСтраница, Нстр("ru = 'Готово'"), Нстр("ru = 'Далее >>'"));
	Элементы.ФормаНазад.Видимость = НЕ ВидимыеСтраницы.Найти(ИндексТекущейСтраницы) = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура Готово()
	
	ОповеститьОВыборе(Новый Структура("ПроизводственныйКалендарь, ВидКалендаря", ПроизводственныйКалендарь, ВидКалендаря));
	
КонецПроцедуры

&НаСервере
Функция ИндексСтраницы(Страница)
	
	Возврат Элементы.ГруппаСтраницыФормы.ПодчиненныеЭлементы.Индекс(Страница);
	
КонецФункции
