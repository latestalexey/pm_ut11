////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПартнерыИКонтрагенты.ПартнерФормаЭлементаПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	УпрощенныйВводДоступен = ПартнерыИКонтрагенты.УпрощенныйВводДоступен() ИЛИ ТолькоПросмотр;
	Если Объект.Ссылка.Пустая() Тогда
		ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
		ПриСозданииИЧтенииНаСервере();
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка КАК КонтрагентПартнера,
	|	Контрагенты.ЮрФизЛицо,
	|	Контрагенты.ИНН,
	|	Контрагенты.КПП,
	|	Контрагенты.КодПоОКПО
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Партнер = &Партнер";
	
	Запрос.УстановитьПараметр("Партнер", ТекущийОбъект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
	
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Выборка);
	
	КонецЕсли;
	
	ПриСозданииИЧтенииНаСервере();
	ПартнерыИКонтрагенты.ПартнерФормаЭлементаПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Обработчик механизма "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект)
	
	// Обработчик механизма "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// Обработчик механизма "Контактная информация"
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект, Отказ);
	
	Если ЗначениеЗаполнено(ИНН) Тогда
		ВыборкаКонтрагент = ПартнерыИКонтрагенты.ИННКППУжеИспользуетсяВИнформационнойБазе(ИНН, КПП, КонтрагентПартнера);
		Если ВыборкаКонтрагент <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Данные ИНН и КПП уже указаны для партнера с кодом %1, ответственный - %2.'"),ВыборкаКонтрагент.Код,ВыборкаКонтрагент.ОсновнойМенеджер),
			,
			"Объект.ИНН",
			,
			Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	ПартнерыИКонтрагенты.ПартнерФормаЭлементаПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	ЗаписатьКонтрагентаПартнера();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ИНН) И (ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент"))Тогда
		ИНН = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КПП) И НЕ ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо") ИЛИ НЕ ЗначениеЗаполнено(ИНН) Тогда
		КПП = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодПоОКПО) И (ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент") ИЛИ ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо")) Тогда
		КодПоОКПО = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПартнерыИКонтрагентыКлиент.ПартнерФормаЭлементаПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
	
	УправлениеДоступностьюКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеДоступностьюКлиент();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Перем ТекстСообщения;
	ЭтоЮрЛицо = ПартнерыИКонтрагенты.ЭтоЮрЛицо(ЮрФизЛицо);
	ЭтоНовыйКонтрагент = КонтрагентПартнера.Пустая();
	
	Если Не ЭтоНовыйКонтрагент Тогда
		ЗначенияРеквизитовВИнформационнойБазе = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(КонтрагентПартнера, Новый Структура("ИНН,КПП,КодПоОКПО"));
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ИНН) Тогда 
		Если (ЭтоНовыйКонтрагент ИЛИ ЗначенияРеквизитовВИнформационнойБазе.ИНН <> ИНН)
			И НЕ РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН,ЭтоЮрЛицо,ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			,
			"ИНН",
			,
			Отказ
			);
		КонецЕсли;
	Иначе
		Если НЕ ПартнерыИКонтрагенты.УпрощенныйВводДоступен() И
			(ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указан ИНН'"),
			,
			"ИНН",
			,
			Отказ
			);
		КонецЕсли;
	Конецесли;
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо
		ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		
		Если НЕ ПустаяСтрока(КПП) 
			И (ЭтоНовыйКонтрагент ИЛИ ЗначенияРеквизитовВИнформационнойБазе.КПП <> КПП)
			И НЕ РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(КПП, ТекстСообщения) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			,
			"КПП",
			,
			Отказ);
			
		КонецЕсли;
		
	Конецесли;
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо 
		ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		
		Если НЕ ПустаяСтрока(КодПоОКПО) 
			И (ЭтоНовыйКонтрагент ИЛИ ЗначенияРеквизитовВИнформационнойБазе.КодПоОКПО <> КодПоОКПО)
			И НЕ РегламентированныеДанныеКлиентСервер.КодПоОКПОСоответствуетТребованиям(КодПоОКПО,ЭтоЮрЛицо,ТекстСообщения) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			,
			"КодПоОкпо",
			,
			Отказ);
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонЭтикеткиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура();
	Отбор = Новый Структура();
	Отбор.Вставить("Назначение",ПредопределенноеЗначение("Перечисление.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляДоставки"));
	ПараметрыФормы.Вставить("Отбор",Отбор);
	ШаблонЭтикетки = ОткрытьФормуМодально("Справочник.ШаблоныЭтикетокИЦенников.ФормаВыбора",ПараметрыФормы);
	Если ШаблонЭтикетки=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ШаблонЭтикетки =  ШаблонЭтикетки;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура КаналПервичногоИнтересаПриИзменении(Элемент)
	
	ПартнерыИКонтрагентыКлиент.ПартнерФормаЭлементаКаналПервичногоИнтересаПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры 

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	ПартнерыИКонтрагентыКлиент.ПартнерФормаЭлементаНаименованиеПриИзменении(ЭтаФорма,Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ФлагПризнакПартнераПриИзменении(Элемент)
	
	ПартнерыИКонтрагентыКлиент.ПартнерФормаЭлементаФлагПризнакПартнераПриИзменении(ЭтаФорма, Элемент);
	
	ОбновитьЭлементыДополнительныхРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПартнерыИКонтрагентыКлиент.ПартнерФормаЭлементаКомментарииНачалоВыбора(ЭтаФорма, Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура БизнесРегионПриИзменении(Элемент)
	
	ПартнерыИКонтрагентыКлиент.ПартнерФормаЭлементаБизнесРегионПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЮрФизЛицоПриИзменении(Элемент)
	
	Если ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо") Тогда
		Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.КомпанияЧастноеЛицо.ЧастноеЛицо");
	Иначе
		Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.КомпанияЧастноеЛицо.Компания");
	КонецЕсли;
	
	УправлениеДоступностьюКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	УправлениеДоступностьюКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)
	
	ПартнерыИКонтрагентыКлиент.СокрЮрНаименованиеПриИзменении(Объект.Наименование, Объект.НаименованиеПолное);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ КОНТАКТНАЯ ИНФОРМАЦИЯ

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	
	УправлениеКонтактнойИнформациейКлиент.ПредставлениеПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.ПредставлениеНачалоВыбора(ЭтаФорма, Элемент, Модифицированность, СтандартнаяОбработка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств(Команда)
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКПП(Команда)
	
	ПартнерыИКонтрагентыКлиент.ЗаполнитьКППпоИНН(ИНН, КПП, Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура УправлениеДоступностьюКлиент()
	
	Элементы.КПП.Доступность          =  (ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо") И ЗначениеЗаполнено(ИНН));
	Элементы.КодПоОКПО.Доступность    = (ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо") ИЛИ
	                                     ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель"));
	Элементы.ИНН.Доступность          = (НЕ ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент"));
	Элементы.ЗаполнитьКПП.Доступность = (ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо") И НЕ ПустаяСтрока(ИНН));
	
	Элементы.Пол.Доступность          = (ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо"));
	Элементы.ДатаРождения.Доступность = (ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо"));
	
	Если ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо") Тогда
		Элементы.СтраницыНаименованиеПолноеКомпанияЧастноеЛицо.ТекущаяСтраница = Элементы.СтраницаНаименованиеПолноеЧастноеЛицо;
	Иначе
		Элементы.СтраницыНаименованиеПолноеКомпанияЧастноеЛицо.ТекущаяСтраница = Элементы.СтраницаНаименованиеПолноеКомпания;
	КонецЕсли;
	
	ОтключитьОтметкуНезаполненного();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьКонтрагентаПартнера()
	
	Если КонтрагентПартнера.Пустая() Тогда
		КонтрагентОбъект = Справочники.Контрагенты.СоздатьЭлемент();
	Иначе
		КонтрагентОбъект = КонтрагентПартнера.ПолучитьОбъект();
	КонецЕсли;
	
	КонтрагентОбъект.Наименование = Объект.Наименование;
	КонтрагентОбъект.НаименованиеПолное = Объект.НаименованиеПолное;
	КонтрагентОбъект.ИНН = ИНН;
	КонтрагентОбъект.Партнер = Объект.Ссылка;
	КонтрагентОбъект.ЮрФизЛицо = ЮрФизЛицо;
	КонтрагентОбъект.КодПоОКПО = КодПоОКПО;
	КонтрагентОбъект.КПП = КПП;
	
	КонтрагентОбъект.КонтактнаяИнформация.Очистить();
	Для каждого СтрокаКИПартнера Из Объект.КонтактнаяИнформация Цикл
		
		Если СтрокаКИПартнера.Вид = Справочники.ВидыКонтактнойИнформации.EmailПартнера Тогда
			ВидКиКонтрагента = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента;
		ИначеЕсли СтрокаКИПартнера.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонПартнера Тогда
			ВидКиКонтрагента = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
		ИначеЕсли СтрокаКИПартнера.Вид = Справочники.ВидыКонтактнойИнформации.АдресПартнера Тогда
			ВидКиКонтрагента = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
		Иначе
			Продолжить;
		КонецЕсли;
		
		СтрокаКиКонтрагента = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКиКонтрагента,СтрокаКИПартнера);
		СтрокаКиКонтрагента.Вид = ВидКиКонтрагента;
		
		Если СтрокаКИПартнера.Вид = Справочники.ВидыКонтактнойИнформации.АдресПартнера Тогда
			СтрокаКиКонтрагента = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКиКонтрагента,СтрокаКИПартнера);
			СтрокаКиКонтрагента.Вид = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
		КонецЕсли;
	
	КонецЦикла;
	
	КонтрагентОбъект.Записать();
	
	КонтрагентПартнера = КонтрагентОбъект.Ссылка;

КонецПроцедуры 

&НаСервере
Процедура ПриСозданииИЧтенииНаСервере()
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		Элементы.СтраницыНаименованиеПолноеКомпанияЧастноеЛицо.ТекущаяСтраница = Элементы.СтраницаНаименованиеПолноеЧастноеЛицо;
	Иначе
		Элементы.СтраницыНаименованиеПолноеКомпанияЧастноеЛицо.ТекущаяСтраница = Элементы.СтраницаНаименованиеПолноеКомпания;
	КонецЕсли;
	
КонецПроцедуры







