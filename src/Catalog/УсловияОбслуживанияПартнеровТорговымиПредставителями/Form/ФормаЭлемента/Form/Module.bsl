////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПрочитатьГрафикПосещенияСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПрочитатьГрафикПосещенияСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьГрафикПосещенияСервер(ТекущийОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СоглашениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПродажиКлиент.НачалоВыбораСоглашенияСКлиентом(Элемент,СтандартнаяОбработка,Объект.Владелец,Объект.Соглашение,ТекущаяДата(),,Истина);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ГРАФИК ПОСЕЩЕНИЯ

&НаКлиенте
Процедура ГрафикПосещенияВремяНачалаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ТекущиеДанные = Элементы.ГрафикПосещения.ТекущиеДанные;
	ВыбранноеВремя = РаботаСДиалогамиКлиент.ВыбратьВремя(ЭтаФорма, Элемент, ТекущиеДанные.ВремяНачала);
	
	Если ВыбранноеВремя = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущиеДанные.ВремяНачала = ВыбранноеВремя;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрафикПосещенияВремяОкончанияНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
    ТекущиеДанные = Элементы.ГрафикПосещения.ТекущиеДанные;
	ВыбранноеВремя = РаботаСДиалогамиКлиент.ВыбратьВремя(ЭтаФорма, Элемент, ТекущиеДанные.ВремяОкончания);
	
	Если ВыбранноеВремя = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущиеДанные.ВремяОкончания = ВыбранноеВремя;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура УстановитьОтметкиВсехДнейНедели(Команда)
	
	Для Каждого ТекущаяСтрока Из ГрафикПосещения Цикл
		
		Если Не ТекущаяСтрока.Посещение Тогда
			ТекущаяСтрока.Посещение = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкиВсехДнейНедели(Команда)
	
	Для Каждого ТекущаяСтрока Из ГрафикПосещения Цикл
		
		Если ТекущаяСтрока.Посещение Тогда
			ТекущаяСтрока.Посещение = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВремяВсехДнейНедели(Команда)
	
	Если Элементы.ГрафикПосещения.ВыделенныеСтроки.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='Не выбраны дни недели для указания времени'");
		Предупреждение(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ФормаУказанияВремени = ПолучитьФорму("Справочник.УсловияОбслуживанияПартнеровТорговымиПредставителями.Форма.УказаниеВремени");
	
	КодВозврата = ОткрытьФормуМодально(ФормаУказанияВремени);
	
	Если КодВозврата = КодВозвратаДиалога.ОК Тогда
		Для Каждого ИдентификаторСтроки из Элементы.ГрафикПосещения.ВыделенныеСтроки Цикл
			Элементы.ГрафикПосещения.ДанныеСтроки(ИдентификаторСтроки).ВремяНачала = ФормаУказанияВремени.ВремяНачала;
			Элементы.ГрафикПосещения.ДанныеСтроки(ИдентификаторСтроки).ВремяОкончания = ФормаУказанияВремени.ВремяОкончания;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПрочитатьГрафикПосещенияСервер()
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	
	График = СправочникОбъект.ГрафикПосещения;
	
	ГрафикПосещения.Очистить();
	
	Для Каждого ДеньНедели Из Перечисления.ДниНедели Цикл
		
		НоваяСтрока            = ГрафикПосещения.Добавить();
		НоваяСтрока.ДеньНедели = ДеньНедели;
		СтрокаТЧ               = График.Найти(ДеньНедели, "ДеньНедели");
		
		Если СтрокаТЧ <> Неопределено Тогда
			
			НоваяСтрока.Посещение       = Истина;
			НоваяСтрока.ВремяНачала     = СтрокаТЧ.ВремяНачала;
			НоваяСтрока.ВремяОкончания  = СтрокаТЧ.ВремяОкончания;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьГрафикПосещенияСервер(ТекущийОбъект)
	
	ТекущийОбъект.ГрафикПосещения.Очистить();
	
	Для Каждого СтрокаТЧ Из ГрафикПосещения Цикл
		
		Если СтрокаТЧ.Посещение Тогда
			
			НоваяСтрока                 = ТекущийОбъект.ГрафикПосещения.Добавить();
			НоваяСтрока.ДеньНедели      = СтрокаТЧ.ДеньНедели;
			НоваяСтрока.ВремяНачала     = СтрокаТЧ.ВремяНачала;
			НоваяСтрока.ВремяОкончания  = СтрокаТЧ.ВремяОкончания;
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры