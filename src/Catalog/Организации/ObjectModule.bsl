#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Перем ТекстСообщения;
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если Не ЭтоНовый() Тогда
		ЗначенияКодовВИнформационнойБазе = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, Новый Структура("ИНН,КПП,ОГРН"));
	КонецЕсли;

	Если Не ПустаяСтрока(ИНН) 
		И (ЭтоНовый() Или ЗначенияКодовВИнформационнойБазе.ИНН <> ИНН)
		И Не РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, ПартнерыИКонтрагенты.ЭтоЮрЛицо(ЮрФизЛицо), ТекстСообщения) Тогда

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ИНН",, Отказ);

	Конецесли;
	
	Если ПустаяСтрока(ИНН) И НЕ ПустаяСтрока(КПП) Тогда
		КПП = "";
	КонецЕсли;	

	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		Если Не ПустаяСтрока(КПП) 
			И (ЭтоНовый() Или ЗначенияКодовВИнформационнойБазе.КПП <> КПП)
			И Не РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(КПП, ТекстСообщения) Тогда

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "КПП",, Отказ);

		КонецЕсли;  		
	Конецесли;
	
	Если НЕ ПустаяСтрока(КодПоОКПО) И НЕ РегламентированныеДанныеКлиентСервер.КодПоОКПОСоответствуетТребованиям(КодПоОКПО,ПартнерыИКонтрагенты.ЭтоЮрЛицо(ЮрФизЛицо),ТекстСообщения) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект,"КодПоОКПО",,Отказ);
		
	КонецЕсли;	
		
	Если Не ПустаяСтрока(ОГРН) 
		И (ЭтоНовый() Или ЗначенияКодовВИнформационнойБазе.ОГРН <> ОГРН)
		И Не РегламентированныеДанныеКлиентСервер.ОГРНСоответствуетТребованиям(ОГРН, ПартнерыИКонтрагенты.ЭтоЮрЛицо(ЮрФизЛицо), ТекстСообщения) Тогда

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОГРН",, Отказ);

	Конецесли;

	Если Не ОбособленноеПодразделение Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГоловнаяОрганизация");
	КонецЕсли;
	
	Если Не ЭтоНовый()
		И ОбособленноеПодразделение
		И ЕстьОбособленныеПодразделенияОрганизации() Тогда
		
		ТекстСообщения = "Организация не может быть обособленным подразделением, потому что она уже выбрана в качестве головной для других организаций.";
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОбособленноеПодразделение",, Отказ);
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо
	 ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда

		Если Не ПустаяСтрока(КПП) Тогда
			КПП = "";
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(СвидетельствоДатаВыдачи) Тогда
			СвидетельствоДатаВыдачи = Дата(1,1,1);
		КонецЕсли;
		
		Если Не ПустаяСтрока(СвидетельствоСерияНомер) Тогда
			СвидетельствоСерияНомер = ""
		КонецЕсли;

	КонецЕсли;
	
	// Обработка смены пометки удаления.
	Если Не ЭтоНовый() И Не ЭтоГруппа Тогда

		Если ПометкаУдаления <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления") Тогда
			Справочники.КлючиАналитикиУчетаПоПартнерам.УстановитьПометкуУдаления(Новый Структура("Организация", Ссылка), ПометкаУдаления);
			Справочники.ВидыЗапасов.УстановитьПометкуУдаления(Новый Структура("Организация", Ссылка), ПометкаУдаления);
		КонецЕсли;

	КонецЕсли;
	
	Если ПартнерыИКонтрагенты.ЭтоЮрЛицо(ЮрФизЛицо) = Истина Тогда
		ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	Иначе
		ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		УстановитьПривилегированныйРежим(Истина);
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка <> &Ссылка
		|	И Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)");
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Если Не Запрос.Выполнить().Пустой() Тогда
			Константы.ИспользоватьНесколькоОрганизаций.Установить(Истина);
			Константы.ИспользоватьНесколькоКасс.Установить(Истина);
			Константы.ИспользоватьНесколькоРасчетныхСчетов.Установить(Истина);
			Константы.ИспользоватьНесколькоРасчетныхСчетовКасс.Установить(Истина);
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СинхронизироватьНастройкиОбособленныхПодразделений(Отказ);
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

Процедура СинхронизироватьНастройкиОбособленныхПодразделений(Отказ)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеПодразделенияВыделенныеНаБаланс") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ОбособленноеПодразделение
		|	И Организации.ГоловнаяОрганизация = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Выборка.Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			
		КонецПопытки;
			
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.ИНН = ИНН;
		Объект.ЮрФизЛицо = ЮрФизЛицо;
		
		Попытка
			
			Объект.Записать();
			
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось записать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Выборка.Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			
		КонецПопытки
		
	КонецЦикла;
	
	Если ОбособленноеПодразделение Тогда
		
		// Синхронизация настроек системы налогообложения
		
		НастройкиГоловнойОрганизации = РегистрыСведений.СистемыНалогообложенияОрганизаций.СоздатьНаборЗаписей();
		НастройкиГоловнойОрганизации.Отбор.Организация.Установить(ГоловнаяОрганизация);
		НастройкиГоловнойОрганизации.Прочитать();
		
		НастройкиОбособленногоПодразделения = НастройкиГоловнойОрганизации;
		НастройкиОбособленногоПодразделения.Отбор.Организация.Установить(Ссылка);
		Для Каждого НастройкаОбособленногоПодразделения Из НастройкиОбособленногоПодразделения Цикл
			НастройкаОбособленногоПодразделения.Организация = Ссылка;
		КонецЦикла;
		
		Попытка
			
			НастройкиОбособленногоПодразделения.ДополнительныеСвойства.Вставить("СинхронизацияНастроек", Истина);
			НастройкиОбособленногоПодразделения.Записать();
			
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось изменить настройки учета НДС. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			
			Возврат;
		КонецПопытки;
		
		// Синхронизация настроек ЕНВД
		
		НастройкиГоловнойОрганизации = РегистрыСведений.ПримененияЕНВД.СоздатьНаборЗаписей();
		НастройкиГоловнойОрганизации.Отбор.Организация.Установить(ГоловнаяОрганизация);
		НастройкиГоловнойОрганизации.Прочитать();
		
		НастройкиОбособленногоПодразделения = НастройкиГоловнойОрганизации;
		НастройкиОбособленногоПодразделения.Отбор.Организация.Установить(Ссылка);
		Для Каждого НастройкаОбособленногоПодразделения Из НастройкиОбособленногоПодразделения Цикл
			НастройкаОбособленногоПодразделения.Организация = Ссылка;
		КонецЦикла;
		
		Попытка
			
			НастройкиОбособленногоПодразделения.ДополнительныеСвойства.Вставить("СинхронизацияНастроек", Истина);
			НастройкиОбособленногоПодразделения.Записать();
			
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось изменить настройки применения ЕНВД. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			
			Возврат;
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьОбособленныеПодразделенияОрганизации()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ГоловнаяОрганизация = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецЕсли