&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	Элементы.СостоитИзДругихУпаковокПереключатель.ТолькоПросмотр = Элементы.СостоитИзДругихУпаковок.ТолькоПросмотр;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	ИспользоватьМногооборотнуюТару = ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару");
	Элементы.МногооборотнаяТараВложеннаяУпаковка.Видимость = ИспользоватьМногооборотнуюТару;
	Элементы.ДекорацияПоясняющаяНадпись.Видимость = ИспользоватьМногооборотнуюТару;
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ХарактеристикаМногооборотнаяТара",
		"Видимость",
		ИспользоватьМногооборотнуюТару
	);
	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
	УстановитьОграничениеКоличестваУпаковокМногооборотнойТары(
		Элементы.МинимальноеКоличествоУпаковокМногооборотнойТары,
		Объект.СостоитИзДругихУпаковок,
		Объект.Коэффициент,
		Объект.КоличествоУпаковок);
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ДополнительныеСвойства.Вставить("РазрешенаСменаРодителя");
	
	Если Объект.ПоставляетсяВМногооборотнойТаре 
		И НЕ Объект.СостоитИзДругихУпаковок 
		И Объект.МинимальноеКоличествоУпаковокМногооборотнойТары > Объект.Коэффициент Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Минимальное количество упаковок многооборотной тары не может быть больше количества упаковок номенклатуры.'"),
			,
			"Объект.МинимальноеКоличествоУпаковокМногооборотнойТары",
			,
			Отказ);
			
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)
	ПересчитатьОбъем();
КонецПроцедуры

&НаКлиенте
Процедура ГлубинаПриИзменении(Элемент)
	ПересчитатьОбъем();
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	ПересчитатьОбъем();
КонецПроцедуры

&НаКлиенте
Процедура БезразмернаяПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ТипоразмерПриИзменении(Элемент)
	ТипоразмерПриИзмененииСервер();
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура КоэффициентПриИзменении(Элемент)
	
	Объект.Наименование = ОбновитьНаименование(Объект.ЕдиницаИзмерения, Объект.Коэффициент, ЕдиницаИзмеренияВладельца);
	
	Если Объект.Коэффициент < Объект.МинимальноеКоличествоУпаковокМногооборотнойТары Тогда
		Объект.МинимальноеКоличествоУпаковокМногооборотнойТары = Объект.Коэффициент;
	КонецЕсли;
	
	УстановитьОграничениеКоличестваУпаковокМногооборотнойТары(
		Элементы.МинимальноеКоличествоУпаковокМногооборотнойТары,
		Объект.СостоитИзДругихУпаковок,
		Объект.Коэффициент,
		Объект.КоличествоУпаковок);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	Объект.Наименование = ОбновитьНаименование(Объект.ЕдиницаИзмерения, Объект.Коэффициент, ЕдиницаИзмеренияВладельца);
КонецПроцедуры

&НаКлиенте
Процедура КонечнаяПриИзменении(Элемент)
	Если СостоитИзДругихУпаковок = 0 Тогда
		Объект.СостоитИзДругихУпаковок = Ложь;
	Иначе
		Объект.СостоитИзДругихУпаковок = Истина;
	КонецЕсли;
	
	УстановитьТекущуюСтраницуКоэффициентов(ЭтаФорма)
КонецПроцедуры

&НаКлиенте
Процедура КоличествоУпаковокПриИзменении(Элемент)
	
	Объект.Коэффициент = Объект.КоличествоУпаковок * КоэффициентРодителя;
	
	Объект.Наименование = ОбновитьНаименование(Объект.ЕдиницаИзмерения, Объект.Коэффициент, ЕдиницаИзмеренияВладельца);
	
	Если Объект.КоличествоУпаковок < Объект.МинимальноеКоличествоУпаковокМногооборотнойТары Тогда
		Объект.МинимальноеКоличествоУпаковокМногооборотнойТары = Объект.КоличествоУпаковок;
	КонецЕсли;
	
	УстановитьОграничениеКоличестваУпаковокМногооборотнойТары(
		Элементы.МинимальноеКоличествоУпаковокМногооборотнойТары,
		Объект.СостоитИзДругихУпаковок,
		Объект.Коэффициент,
		Объект.КоличествоУпаковок);
	
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	РодительПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ПоставляетсяВМногооборотнойТареПриИзменении(Элемент)
	
	ОбновитьДоступностьЭлементовМногооборотнойТары(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура НоменклатураМногооборотнаяТараПриИзменении(Элемент)
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", Объект.ХарактеристикаМногооборотнаяТара);

	СтруктураСтроки = Новый Структура;
	СтруктураСтроки.Вставить("Номенклатура", Объект.НоменклатураМногооборотнаяТара);
	СтруктураСтроки.Вставить("Характеристика", Объект.ХарактеристикаМногооборотнаяТара);
	СтруктураСтроки.Вставить("ХарактеристикиИспользуются", ХарактеристикиИспользуются);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, КэшированныеЗначения);

	ЗаполнитьЗначенияСвойств(Объект, СтруктураСтроки);
	
	ХарактеристикиИспользуются = СтруктураСтроки.ХарактеристикиИспользуются;
	Элементы.ХарактеристикаМногооборотнаяТара.Доступность = ХарактеристикиИспользуются;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		Результат = ОткрытьФормуМодально("Справочник.УпаковкиНоменклатуры.Форма.РазблокированиеРеквизитов");
		Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
			
			ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, Результат);
			Элементы.СостоитИзДругихУпаковокПереключатель.ТолькоПросмотр = Элементы.СостоитИзДругихУпаковок.ТолькоПросмотр;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаСервере
Процедура РодительПриИзмененииСервер()
	
	ОбновитьКоэффициентРодителя();	
	
	Объект.Коэффициент = Объект.КоличествоУпаковок * КоэффициентРодителя;
	
	Объект.Наименование = ОбновитьНаименование(Объект.ЕдиницаИзмерения, Объект.Коэффициент, ЕдиницаИзмеренияВладельца );
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Процедура УстановитьДоступность()
	Элементы.Высота.Доступность  			= НЕ Объект.Безразмерная;
	Элементы.Глубина.Доступность 			= НЕ Объект.Безразмерная;
	Элементы.Ширина.Доступность  			= НЕ Объект.Безразмерная;
	Элементы.Объем.Доступность  			= НЕ Объект.Безразмерная;
КонецПроцедуры

&НаСервере
Процедура ПересчитатьОбъем()
	
	КоэффициентПересчетаВЕдиницыОбъема = Константы.КоэффициентПересчетаВЕдиницыОбъема.Получить();
	
	Если НЕ ЗначениеЗаполнено(КоэффициентПересчетаВЕдиницыОбъема) Тогда
		
		ТекстСообщения = НСтр("ru='В настройках программы не установлен коэффициент перерасчета из единиц измерения линейных размеров в единица измерения объема'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	Иначе
		
		Объект.Объем = Объект.Высота * Объект.Глубина * Объект.Ширина * КоэффициентПересчетаВЕдиницыОбъема;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ТипоразмерПриИзмененииСервер()
	ЗаполнитьЗначенияСвойств(Объект,Объект.Типоразмер, "Глубина, Ширина, Высота, Объем, Безразмерная"); 
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбновитьНаименование(ЕдиницаИзмерения, Коэффициент, ЕдиницаИзмеренияВладельца)
	Возврат Справочники.УпаковкиНоменклатуры.СформироватьНаименование(ЕдиницаИзмерения, Коэффициент, ЕдиницаИзмеренияВладельца);	
КонецФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	Элементы.Высота.Доступность  			= НЕ Объект.Безразмерная;
	Элементы.Глубина.Доступность 			= НЕ Объект.Безразмерная;
	Элементы.Ширина.Доступность  			= НЕ Объект.Безразмерная;
	Элементы.Объем.Доступность  			= НЕ Объект.Безразмерная;
	
	Если Объект.СостоитИзДругихУпаковок Тогда
		СостоитИзДругихУпаковок = 1;
	Иначе
		СостоитИзДругихУпаковок = 0;
	КонецЕсли;
	
	ОбновитьКоэффициентРодителя();	
	
	Элементы.ДекорацияПредупреждение.Видимость =  (ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.НаборыУпаковок"));
	
	ЕдиницаИзмеренияВладельца = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ЕдиницаИзмерения");
	
	ЕдиницаИзмеренияЛинейныхРазмеров = Константы.ЕдиницаИзмеренияЛинейныхРазмеров.Получить();
	ЕдиницаИзмененияОбъема 	         = Константы.ЕдиницаИзмеренияОбъема.Получить();
	ЕдиницаИзмеренияВеса 	         = Константы.ЕдиницаИзмеренияВеса.Получить();
	
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		Элементы.Владелец.Заголовок = НСтр("ru='Номенклатура'");
	ИначеЕсли ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.НаборыУпаковок") Тогда
		Элементы.Владелец.Заголовок = НСтр("ru='Набор упаковок'");
	КонецЕсли;
	
	УстановитьТекущуюСтраницуКоэффициентов(ЭтаФорма);
	
	ХарактеристикиИспользуются = Справочники.Номенклатура.ХарактеристикиИспользуются(Объект.НоменклатураМногооборотнаяТара);
	ОбновитьДоступностьЭлементовМногооборотнойТары(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницуКоэффициентов(Форма)
	Если Форма.Объект.СостоитИзДругихУпаковок Тогда
		Форма.Элементы.ГруппаКоэффицентыСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаКоличествоУпаковок;
	Иначе
		Форма.Элементы.ГруппаКоэффицентыСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаКоэффициентЕдиницаИзмерения;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоэффициентРодителя()
	
	Если Объект.СостоитИзДругихУпаковок Тогда
		КоэффициентРодителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Родитель, "Коэффициент");
	Иначе
		КоэффициентРодителя = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьЭлементовМногооборотнойТары(Форма)
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("МинимальноеКоличествоУпаковокМногооборотнойТары");
	МассивЭлементов.Добавить("МногооборотнаяТараВложеннаяУпаковка");
	МассивЭлементов.Добавить("НоменклатураМногооборотнаяТара");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
		Форма.Элементы,
		МассивЭлементов,
		"Доступность",
		Форма.Объект.ПоставляетсяВМногооборотнойТаре
	);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ХарактеристикаМногооборотнаяТара",
		"Доступность",
		Форма.Объект.ПоставляетсяВМногооборотнойТаре И Форма.ХарактеристикиИспользуются
	);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОграничениеКоличестваУпаковокМногооборотнойТары(Поле,
	                                                                СостоитИзДругихУпаковок,
	                                                                Коэффициент,
	                                                                КоличествоУпаковок)
	
	Поле.МаксимальноеЗначение = ?(СостоитИзДругихУпаковок, КоличествоУпаковок, Коэффициент);
	
КонецПроцедуры
