#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ВалютаДенежныхСредств = Константы.ВалютаРегламентированногоУчета.Получить();
	Склад = ЗначениеНастроекПовтИсп.ПолучитьРозничныйСкладПоУмолчанию(Склад);
	Владелец = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Владелец);
	
	Если Не ОбщегоНазначенияУТ.ИспользоватьПодключаемоеОборудование() Тогда
		ИспользоватьБезПодключенияОборудования = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипКассы = Перечисления.ТипыКассККМ.АвтономнаяККМ Тогда
		
		ИспользоватьБезПодключенияОборудования = Ложь;
		ПодключаемоеОборудование               = Неопределено;
		АвтоматическаяИнкассация               = Ложь;
		
	ИначеЕсли ТипКассы = Перечисления.ТипыКассККМ.ККМOffline Тогда
		
		ИспользоватьБезПодключенияОборудования = Ложь;
		АвтоматическаяИнкассация               = Ложь;
		Склад                                  = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ТипКассы = Перечисления.ТипыКассККМ.ККМOffline Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Склад");
	КонецЕсли;
	Если ТипКассы <> Перечисления.ТипыКассККМ.ККМOffline Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПодключаемоеОборудование");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПодключаемоеОборудование) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КассыККМ.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КассыККМ КАК КассыККМ
		|ГДЕ
		|	КассыККМ.Ссылка <> &Ссылка
		|	И КассыККМ.ПодключаемоеОборудование = &ПодключаемоеОборудование");
		
		Запрос.УстановитьПараметр("ПодключаемоеОборудование", ПодключаемоеОборудование);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Если Выборка.Следующий() Тогда
		
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Устройство ""%1"" уже подключено к ККМ ""%2"".'"), ПодключаемоеОборудование, Выборка.Ссылка);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				"ПодключаемоеОборудование",
				,
				Отказ);
		
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецЕсли