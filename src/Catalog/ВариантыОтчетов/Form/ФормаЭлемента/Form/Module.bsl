////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	Если Объект.Ссылка.Пустая() Тогда
		ОшибкаОткрытия = НСтр("ru = 'Новый вариант отчета можно создать только из формы отчета'");
		Возврат;
	КонецЕсли;
	
	ПолныеПраваНаВарианты = ВариантыОтчетов.ПолныеПраваНаВарианты();
	
	Если Объект.ПометкаУдаления Тогда
		Элементы.ВидимостьПоУмолчанию.ТолькоПросмотр = Истина;
		Элементы.ДеревоПодсистем.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если НЕ ПолныеПраваНаВарианты Тогда
		Элементы.ГруппаДоступен.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если НЕ Объект.Пользовательский Тогда
		Элементы.Наименование.ТолькоПросмотр = Истина;
		Элементы.Доступен.ТолькоПросмотр = Истина;
		Элементы.Автор.ТолькоПросмотр = Истина;
		Элементы.Автор.АвтоОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
	ЭтоВнешний = (Объект.ТипОтчета = Перечисления.ТипыОтчетов.Внешний);
	Если ЭтоВнешний Тогда
		Элементы.ВидимостьПоУмолчанию.ТолькоПросмотр = Истина;
		Элементы.ДеревоПодсистем.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Элементы.ВосстановитьИсходныеНастройки.Видимость = (
		ПолныеПраваНаВарианты
		И Объект.ТипОтчета = Перечисления.ТипыОтчетов.Внутренний
		И НЕ Объект.Пользовательский
	);
	
	// Заполнение имени отчета для команды "Просмотр"
	Если Объект.ТипОтчета = Перечисления.ТипыОтчетов.Внутренний Тогда
		ИмяОтчета = Объект.Отчет.Имя;
	ИначеЕсли Объект.ТипОтчета = Перечисления.ТипыОтчетов.Дополнительный Тогда
		ИмяОтчета = Объект.Отчет.ИмяОбъекта;
	Иначе
		ИмяОтчета = Объект.Отчет;
	КонецЕсли;
	
	ПерезаполнитьДерево();
	
	ВариантыОтчетов.ДеревоПодсистемДобавитьУсловноеОформление(ЭтаФорма);
	
	Доступен = ?(Объект.ТолькоДляАвтора, "1", "2");
	
	ВидимостьДоступность(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ПустаяСтрока(ОшибкаОткрытия) Тогда
		Если Объект.Ссылка.Пустая() Тогда
			Отказ = Истина;
		Иначе
			ТолькоПросмотр = Истина;
		КонецЕсли;
		
		Предупреждение(ОшибкаОткрытия);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ВариантыОтчетов.ДеревоПодсистемЗаписать(ЭтаФорма, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПараметрОповещения = Новый Структура("Ссылка, Наименование, Автор, Описание, ВидимостьПоУмолчанию");
	ЗаполнитьЗначенияСвойств(ПараметрОповещения, Объект);
	Оповестить(ВариантыОтчетовКлиентСервер.ИмяСобытияИзменениеВарианта(), ПараметрОповещения, ИмяФормы);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбщегоНазначенияКлиент.ОткрытьФормуРедактированияМногострочногоТекста(
		Объект.Описание,
		Объект.Описание,
		Модифицированность,
		НСтр("ru = 'Описание'")
	);
КонецПроцедуры

&НаКлиенте
Процедура ДоступенПриИзменении(Элемент)
	Объект.ТолькоДляАвтора = (ЭтаФорма.Доступен = "1");
	ВидимостьДоступность(ЭтаФорма, "ТолькоДляАвтора");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ДеревоПодсистем

&НаКлиенте
Процедура ДеревоПодсистемИспользованиеПриИзменении(Элемент)
	ВариантыОтчетовКлиент.ДеревоПодсистемИспользованиеПриИзменении(ЭтаФорма, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодсистемВажностьПриИзменении(Элемент)
	ВариантыОтчетовКлиент.ДеревоПодсистемВажностьПриИзменении(ЭтаФорма, Элемент);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВосстановитьИсходныеНастройки(Команда)
	РезультатРаботыСерверера = ВосстановитьИсходныеНастройкиСервер();
	Элементы.ДеревоПодсистем.Развернуть(ДеревоПодсистем.ПолучитьЭлементы()[0].ПолучитьИдентификатор(), Истина);
	ВариантыОтчетовКлиент.ПоказатьРезультат(РезультатРаботыСерверера);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьДоступность(ЭтаФорма, Изменения = "")
	Если ЭтаФорма.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Объект = ЭтаФорма.Объект;
	Элементы = ЭтаФорма.Элементы;
	
	Если Изменения = "" ИЛИ Изменения = "ТолькоДляАвтора" Тогда
		Если НЕ ЭтаФорма.ЭтоВнешний И НЕ ЭтаФорма.Объект.ПометкаУдаления Тогда
			Элементы.ВидимостьПоУмолчанию.Доступность = НЕ Объект.ТолькоДляАвтора;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПерезаполнитьДерево()
	ДеревоПриемник = ВариантыОтчетов.ДеревоПодсистемСформировать(ЭтаФорма, Объект);
	ЗначениеВРеквизитФормы(ДеревоПриемник, "ДеревоПодсистем");
	Возврат Истина;
КонецФункции

&НаСервере
Функция ВосстановитьИсходныеНастройкиСервер()
	Результат = Новый Структура("Оповестить, Заголовок, Текст, ТекстОшибок, Ссылка", Ложь, "", "", "", "", "");
	
	ДеревоНастроек = ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации();
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(ДеревоНастроек, Объект.Отчет, Объект.КлючВарианта);
	
	Объект.Наименование = ОписаниеВарианта.Наименование;
	Объект.Описание     = ОписаниеВарианта.Описание;
	
	Объект.Размещение.Очистить();
	ВариантыОтчетов.ВосстановитьРазмещениеПредопределенногоВариантаОтчета(Объект, ОписаниеВарианта);
	
	ПерезаполнитьДерево();
	
	Модифицированность = Истина;
	
	Результат.Заголовок = НСтр("ru = 'Восстановлены настройки варианта отчета'");
	Результат.Оповестить = Истина;
	Результат.Текст = Строка(Объект.Ссылка);
	Результат.Ссылка = ПолучитьНавигационнуюСсылку(Объект.Ссылка);
	
	Возврат Результат;
КонецФункции

