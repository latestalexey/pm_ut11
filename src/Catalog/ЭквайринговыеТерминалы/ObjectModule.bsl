#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения <> Неопределено
	   И ДанныеЗаполнения.Свойство("Владелец")
	Тогда
		Организация = Справочники.ДоговорыЭквайринга.ПолучитьРеквизитыДоговораЭквайринга(ДанныеЗаполнения.Владелец).Организация;
	КонецЕсли;
	
	Касса = Справочники.Кассы.ПолучитьКассуПоУмолчанию(Организация);
	
	Если Не ОбщегоНазначенияУТ.ИспользоватьПодключаемоеОборудование() Тогда
		ИспользоватьБезПодключенияОборудования = Истина;
	КонецЕсли;
	
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверитьКассу(Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоРозничнаяТорговля = ТипЗнч(Касса) = Тип("СправочникСсылка.КассыККМ");
	Если РозничнаяТорговля <> ЭтоРозничнаяТорговля Тогда
		РозничнаяТорговля = ЭтоРозничнаяТорговля;
	КонецЕсли;
	
	Если ИспользоватьБезПодключенияОборудования Тогда
		ПодключаемоеОборудование = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Прочее

Процедура ПроверитьКассу(Отказ)
	
	ВалютаДоговора = Справочники.ДоговорыЭквайринга.ПолучитьРеквизитыДоговораЭквайринга(Владелец).Валюта;
	Если ТипЗнч(Касса) = Тип("СправочникСсылка.Кассы") Тогда
		Реквизиты = Справочники.Кассы.ПолучитьРеквизитыКассы(Касса);
	ИначеЕсли ТипЗнч(Касса) = Тип("СправочникСсылка.КассыККМ") Тогда
		Реквизиты = Справочники.КассыККМ.РеквизитыКассыККМ(Касса);
	Иначе
		Реквизиты = Новый Структура("Валюта, Организация");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВалютаДоговора)
	   И ЗначениеЗаполнено(Реквизиты.Валюта)
	   И ВалютаДоговора <> Реквизиты.Валюта Тогда
	
		Текст = НСтр("ru = 'Валюта кассы не соответствует валюте договора эквайринга'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ЭтотОбъект,
			"Касса",
			,
			Отказ
		);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация)
	   И ЗначениеЗаполнено(Реквизиты.Организация)
	   И Организация <> Реквизиты.Организация Тогда
	
		Текст = НСтр("ru = 'Организация кассы не соответствует организации договора эквайринга'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ЭтотОбъект,
			"Касса",
			,
			Отказ
		);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли