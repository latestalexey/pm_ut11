
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Валюта = Справочники.ДоговорыЭквайринга.ПолучитьРеквизитыДоговораЭквайринга(Объект.Владелец).Валюта;
		УправлениеЭлементамиФормы();
	КонецЕсли;
	
	Элементы.ПодключаемоеОборудование.Доступность = Не Объект.ИспользоватьБезПодключенияОборудования;
	
	Если Объект.ИспользоватьБезПодключенияОборудования
		И Не ОбщегоНазначенияУТ.ИспользоватьПодключаемоеОборудование() Тогда
		Элементы.ИспользоватьБезПодключенияОборудования.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Валюта = Справочники.ДоговорыЭквайринга.ПолучитьРеквизитыДоговораЭквайринга(Объект.Владелец).Валюта;
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = СформироватьАвтоНаименование();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	ВладелецПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	СформироватьАвтоНаименование();
	ВыбранныйЭлемент = ВыбратьИзСписка(Элементы.Наименование.СписокВыбора, Элемент);
	Если ВыбранныйЭлемент <> Неопределено Тогда
		Объект.Наименование = ВыбранныйЭлемент.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидКассыПриИзменении(Элемент)
	
	Если ПустаяСтрока(ВидКассы) Тогда
		 ВидКассы = "Касса";
	КонецЕсли;
	
	Если ВидКассы = "КассаККМ" Тогда
		Элементы.Касса.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.КассыККМ");
		Если ТипЗнч(Объект.Касса) <> Тип("СправочникСсылка.КассыККМ") Тогда
			Объект.Касса = Неопределено;
		КонецЕсли;
	Иначе
		Элементы.Касса.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Кассы");
		Если ТипЗнч(Объект.Касса) <> Тип("СправочникСсылка.Кассы") Тогда
			Объект.Касса = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьБезПодключенияОборудованияПриИзменении(Элемент)
	
	Элементы.ПодключаемоеОборудование.Доступность = Не Объект.ИспользоватьБезПодключенияОборудования;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		Результат = ОткрытьФормуМодально("Справочник.ЭквайринговыеТерминалы.Форма.РазблокированиеРеквизитов");
		Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
			ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, Результат);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаСервере
Процедура ВладелецПриИзмененииСервер()
	
	СтруктураПараметров = Справочники.ДоговорыЭквайринга.ПолучитьРеквизитыДоговораЭквайринга(Объект.Владелец);
	Объект.Организация = СтруктураПараметров.Организация;
	Валюта = СтруктураПараметров.Валюта;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Функция СформироватьАвтоНаименование()
	
	Элементы.Наименование.СписокВыбора.Очистить();
	
	СтрокаНаименования = СокрЛП(Объект.Владелец) + " (" + Строка(Валюта) + ")";
	СтрокаНаименования = Лев(СтрокаНаименования, 100);
	
	Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
	
	Возврат СтрокаНаименования;

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Управление элементами формы

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРозничныеПродажи") Тогда
		Элементы.ВидКассы.Видимость = Истина;
		Элементы.Касса.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Иначе
		Элементы.ВидКассы.Видимость = Ложь;
		Элементы.Касса.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	КонецЕсли;
	
	Если ТипЗнч(Объект.Касса) = Тип("СправочникСсылка.КассыККМ") Тогда
		ВидКассы = "КассаККМ";
	Иначе
		ВидКассы = "Касса";
	КонецЕсли;
	
КонецПроцедуры
