////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Содается новый элемент
	Если НЕ ЭтоАдресВременногоХранилища(АдресСхемыКомпоновкиДанных) Тогда
		
		// Копированием
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			
			// При копировании схема компоновки данных получается из копируемого объекта
			АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(Параметры.ЗначениеКопирования.СхемаКомпоновкиДанных.Получить(), УникальныйИдентификатор);
			
		Иначе
			
			// Создается новая схема компоновки данных
			АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(Новый СхемаКомпоновкиДанных, УникальныйИдентификатор);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// Загрузка схемы компоновки данных
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(ТекущийОбъект.СхемаКомпоновкиДанных.Получить(), УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Сохранение схемы компоновки данных
	ТекущийОбъект.СхемаКомпоновкиДанных = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Редактировать(Команда)

	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр("ru = 'Настройки источника данных планирования ""%ИмяИсточникаДанных%""'");
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = СтрЗаменить(ЗаголовокФормыНастройкиСхемыКомпоновкиДанных, "%ИмяИсточникаДанных%", Объект.Наименование);
	
	Результат = ОткрытьФормуМодально("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных",
				Новый Структура("АдресСхемыКомпоновкиДанных, ИсточникШаблонов, Заголовок, УникальныйИдентификатор, ИмяШаблонаСКД, ВозвращатьИмяТекущегоШаблонаСКД",
					АдресСхемыКомпоновкиДанных,
					Объект.Ссылка,
					ЗаголовокФормыНастройкиСхемыКомпоновкиДанных,
					УникальныйИдентификатор,
					Объект.ИмяШаблонаСКД,
					Истина));
					
	Если Результат <> Неопределено Тогда
						
		Объект.ИмяШаблонаСКД = Результат.ИмяТекущегоШаблонаСКД;
						
	КонецЕсли;					
	
КонецПроцедуры
