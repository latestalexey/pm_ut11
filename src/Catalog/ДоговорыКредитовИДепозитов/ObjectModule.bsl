#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация   = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	БанковскийСчет = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(Организация, , БанковскийСчет);
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		ВалютаВзаиморасчетов = БанковскийСчет.ВалютаДенежныхСредств;
	Иначе
		ВалютаВзаиморасчетов = ДоходыИРасходыСервер.ПолучитьВалютуУправленческогоУчета(ВалютаВзаиморасчетов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ФормаОплаты = Перечисления.ФормыОплаты.Наличная Тогда
		БанковскийСчет = Справочники.БанковскиеСчетаОрганизаций.ПустаяСсылка();
	Иначе
		Касса = Справочники.Кассы.ПустаяСсылка();
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Статус = Перечисления.СтатусыДоговоровКонтрагентов.НеСогласован;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНеПроверяемыхРеквизитов = Новый Массив;
	
	Если ФормаОплаты = Перечисления.ФормыОплаты.Наличная Тогда
		МассивНеПроверяемыхРеквизитов.Добавить("БанковскийСчетКонтрагента");
	Иначе
		МассивНеПроверяемыхРеквизитов.Добавить("Касса");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНеПроверяемыхРеквизитов);
	
КонецПроцедуры




#КонецЕсли