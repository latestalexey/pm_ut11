#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

//Возвращает имена блокруемых реквизитов для механизма блокирования реквизитов БСП
//	Возвращаемое значание:
//		Массив - имена блокируемых реквизитов
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт

	Результат = Новый Массив;
	
	Результат.Добавить("Владелец");
	Результат.Добавить("НастройкаАдресногоХранения");

	Возврат Результат;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы

// Обработчик обновления УТ 11.1.0.0
// Если есть рабочие участки, то включается их использование
Процедура ВключитьИспользованиеРабочихУчастков() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РабочиеУчастки.Ссылка) КАК Количество,
	|	РабочиеУчастки.Владелец КАК Склад,
	|	РабочиеУчастки.Помещение КАК Помещение
	|ПОМЕСТИТЬ РабочиеУчастки
	|ИЗ
	|	Справочник.РабочиеУчастки КАК РабочиеУчастки
	|
	|СГРУППИРОВАТЬ ПО
	|	РабочиеУчастки.Владелец,
	|	РабочиеУчастки.Помещение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкладскиеПомещения.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА РабочиеУчастки.Количество ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ИспользованиеСкладскихРабочихУчастков.НеИспользовать)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ИспользованиеСкладскихРабочихУчастков.Использовать)
	|	КОНЕЦ КАК ИспользованиеРабочихУчастков
	|ИЗ
	|	Справочник.СкладскиеПомещения КАК СкладскиеПомещения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеУчастки КАК РабочиеУчастки
	|		ПО СкладскиеПомещения.Ссылка = РабочиеУчастки.Помещение
	|			И СкладскиеПомещения.Владелец = РабочиеУчастки.Склад
	|ГДЕ
	|	ВЫБОР
	|			КОГДА РабочиеУчастки.Количество ЕСТЬ NULL 
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ИспользованиеСкладскихРабочихУчастков.НеИспользовать)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ИспользованиеСкладскихРабочихУчастков.Использовать)
	|		КОНЕЦ <> СкладскиеПомещения.ИспользованиеРабочихУчастков";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СправочникОбъект.ИспользованиеРабочихУчастков = Выборка.ИспользованиеРабочихУчастков;
		
		СправочникОбъект.Записать();
		
	КонецЦикла;

КонецПроцедуры

#КонецЕсли