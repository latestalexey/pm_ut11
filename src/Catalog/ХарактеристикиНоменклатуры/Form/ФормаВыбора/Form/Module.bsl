////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	КодФормы = "Справочник_ХарактеристикиНоменклатуры_ФормаВыбора";
	
	Номенклатура          = Неопределено;
	ВладелецХарактеристик = Неопределено;
	
	Если Параметры.Свойство("ПараметрВладелец", ВладелецХарактеристик)
		И ЗначениеЗаполнено(ВладелецХарактеристик) Тогда
		
		ПодборТоваровСервер.УстановитьОтборПоВладельцуХарактеристик(ЭтаФорма);
		
	ИначеЕсли Параметры.Свойство("Номенклатура", Номенклатура) 
		И ЗначениеЗаполнено(Номенклатура) Тогда
		
		ВладелецХарактеристик = Неопределено;
		
		Если Справочники.Номенклатура.ПроверитьИспользованиеХарактеристикИПолучитьВладельцаДляВыбора(Номенклатура, ВладелецХарактеристик) Тогда
			
			Если ВладелецХарактеристик = Неопределено Тогда
				
				ТекстИсключения = НСтр("ru = 'Для данной номенклатуры характеристики не заданы.'");
				ВызватьИсключение ТекстИсключения;
				
			Иначе
				
				ПодборТоваровСервер.УстановитьОтборПоВладельцуХарактеристик(ЭтаФорма);
				
			КонецЕсли;
			
		Иначе
			
			ТекстИсключения = НСтр("ru = 'Для данной номенклатуры отключено использование характеристик.'");
			ВызватьИсключение ТекстИсключения;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(ВладелецХарактеристик) = Тип("СправочникСсылка.ВидыНоменклатуры") Тогда
		
		ВидНоменклатуры = ВладелецХарактеристик;
		
	ИначеЕсли ТипЗнч(ВладелецХарактеристик) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецХарактеристик, "ВидНоменклатуры");
		
	КонецЕсли;
	
	ПодборТоваровСервер.ЗаполнитьДеревоОтборовХарактеристик(ЭтаФорма);
	
	ЕстьДоступныеОтборы                      = ДеревоОтборов.ПолучитьЭлементы().Количество() > 0;
	Элементы.ГруппаДеревоОтборов.Видимость   = ЕстьДоступныеОтборы;
	ИспользоватьФильтры                      = ЕстьДоступныеОтборы;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ХарактеристикиНоменклатуры" 
		И ЗначениеЗаполнено(Источник) Тогда
		
		Элементы.Список.ТекущаяСтрока = Источник;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ИспользоватьФильтрыПриИзменении(Элемент)
	
	ИспользоватьФильтрыПриИзмененииНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ СПИСОК

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если НЕ Копирование И НЕ Группа Тогда
		
		Отказ = Истина;
		ПараметрыФормы = Новый Структура("Владелец, ВидНоменклатуры, АдресТаблицы");
		
		Если ИспользоватьФильтры
			И ЗначениеЗаполнено(ВидНоменклатуры)
			И ДеревоОтборов.ПолучитьЭлементы().Количество() > 0 Тогда
			
			ПараметрыФормы.АдресТаблицы = АдресТаблицыПараметровДереваОтборовНаСервере();
			ПараметрыФормы.ВидНоменклатуры = ВидНоменклатуры;
			
		КонецЕсли;
		
		ПараметрыФормы.Владелец = ВладелецХарактеристик;
		
	ИначеЕсли Копирование И НЕ Группа Тогда
		
		Отказ = Истина;
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		ПараметрыФормы = Новый Структура("ЗначениеКопирования", ТекущиеДанные.Ссылка);
		
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ХарактеристикиНоменклатуры.Форма.ПомощникНового", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ДЕРЕВО ОТБОРОВ

&НаКлиенте
Процедура ДеревоОтборовОтборПриИзменении(Элемент)
	
	ПодборТоваровКлиент.ДеревоОтборовОтборПриИзменении(ЭтаФорма);
	ДеревоОтборовОтборПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОтборовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПодборТоваровКлиент.ДеревоОтборовВыбор(ЭтаФорма);
	ДеревоОтборовОтборПриИзмененииНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ИспользоватьФильтрыПриИзмененииНаСервере()
	
	ПодборТоваровСервер.ПриИзмененииИспользованияФильтров(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ДеревоОтборовОтборПриИзмененииНаСервере()
	
	ПодборТоваровСервер.ДеревоОтборовОтборПриИзменении(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция АдресТаблицыПараметровДереваОтборовНаСервере()
	
	АдресТаблицы = ПодборТоваровСервер.АдресТаблицыПараметровДереваОтборов(ЭтаФорма);
	Возврат АдресТаблицы;
	
КонецФункции