////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ВидДокумента", ВидДокумента);
	
	// Обработчик механизма "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	
	УправлениеДоступом.ПриСозданииФормыЗначенияДоступа(ЭтаФорма);
	
	УстановитьВидимостьПоВидуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ЗначениеЗаполнено(ВидДокумента)
		И (ЗначениеЗаполнено(Номер)
		Или ЗначениеЗаполнено(Серия))
		И Не ЗначениеЗаполнено(ДатаВыдачи) Тогда
		
		ТекстСообщения = НСтр("ru='Укажите дату выдачи документа.'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"ДатаВыдачи",,Отказ);
		
		Возврат;
		
	КонецЕсли;
	
	Если ПроверятьНаДублиПередЗаписью Тогда
		Отказ = Не ЕстьДубли();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЗначениеЗаполнено(ВидДокумента)
		И (ЗначениеЗаполнено(Номер)
		Или ЗначениеЗаполнено(Серия)) Тогда
		Набор = РегистрыСведений.ДокументыФизическихЛиц.СоздатьНаборЗаписей();
		
		Набор.Отбор.Период.Установить(ДатаВыдачи);
		Набор.Отбор.ФизЛицо.Установить(ТекущийОбъект.Ссылка);
		Набор.Отбор.ВидДокумента.Установить(ВидДокумента);
		
		НоваяСтрока = Набор.Добавить();
		
		НоваяСтрока.Период           = ДатаВыдачи;
		НоваяСтрока.Физлицо          = ТекущийОбъект.Ссылка;
		НоваяСтрока.ВидДокумента     = ВидДокумента;
		НоваяСтрока.Серия            = СокрЛП(Серия);
		НоваяСтрока.Номер            = СокрЛП(Номер);
		НоваяСтрока.ДатаВыдачи       = ДатаВыдачи;
		НоваяСтрока.СрокДействия     = СрокДействия;
		НоваяСтрока.КемВыдан         = КемВыдан;
		НоваяСтрока.КодПодразделения = КодПодразделения;
		
		Попытка
			Набор.Записать();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке().Описание,,,,Отказ)
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ВозращаемаяСсылка = Объект.Ссылка;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
		СтандартнаяОбработка = СтандартнаяОбработкаЗакрытияФормы;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Справочники.ФизическиеЛица.ОбработкаПроверкиЗаполненияНаСервере(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ТАБЛИЦА НАЙДЕНО

&НаКлиенте
Процедура ТаблицаНайденоПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Элементы.ТаблицаРасшифровкаНайдено.ОтборСтрок = Новый ФиксированнаяСтруктура("ФизЛицо",ТекущиеДанные.ФизЛицо); 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНайденоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаОбъекта",Новый Структура("Ключ",Элемент.ТекущиеДанные.ФизЛицо));
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРасшифровкаНайденоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаОбъекта",Новый Структура("Ключ",Элемент.ТекущиеДанные.ФизЛицо));
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаНайдено.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru='Выберите физическое лицо, которое необходимо подставить.'");
		Предупреждение(ТекстПредупреждения);
	Иначе
		СтандартнаяОбработкаЗакрытияФормы = Ложь;
		ВозращаемаяСсылка = ТекущиеДанные.ФизЛицо;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Элементы.КоманднаяПанельФормы.Видимость = Истина;
	Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницыФормы.ПодчиненныеЭлементы.Основная;
	ВозращаемаяСсылка = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура Создать(Команда)
	
	ПроверятьНаДублиПередЗаписью = Ложь;
	СтандартнаяОбработкаЗакрытияФормы = Истина;
	
	Записать(Параметры);
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьВидимостьПоВидуДокумента()
	
	Элементы.ГруппаУдостоверение.Заголовок = Строка(ВидДокумента) + ":";
	
	Если ВидДокумента = Справочники.ВидыДокументовФизическихЛиц.ВодительскоеУдостоверение Тогда
		
		Элементы.Серия.Видимость            = Истина;
		Элементы.Номер.Видимость            = Истина;
		Элементы.КемВыдан.Видимость         = Ложь;
		Элементы.ДатаВыдачи.Видимость       = Истина;
		Элементы.СрокДействия.Видимость     = Истина;
		Элементы.КодПодразделения.Видимость = Ложь;
		Элементы.ЯвляетсяДокументомУдостоверяющимЛичность.Видимость = Ложь;
		
	ИначеЕсли ВидДокумента = Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ Тогда 
		
		Элементы.Серия.Видимость            = Истина;
		Элементы.Номер.Видимость            = Истина;
		Элементы.КемВыдан.Видимость         = Истина;
		Элементы.ДатаВыдачи.Видимость       = Истина;
		Элементы.СрокДействия.Видимость     = Истина;
		Элементы.КодПодразделения.Видимость = Истина;
		Элементы.ЯвляетсяДокументомУдостоверяющимЛичность.Видимость = Истина;
		
	Иначе
		
		Элементы.Серия.Видимость            = Ложь;
		Элементы.Номер.Видимость            = Ложь;
		Элементы.КемВыдан.Видимость         = Ложь;
		Элементы.ДатаВыдачи.Видимость       = Ложь;
		Элементы.СрокДействия.Видимость     = Ложь;
		Элементы.КодПодразделения.Видимость = Ложь;
		Элементы.ЯвляетсяДокументомУдостоверяющимЛичность.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЕстьДубли()
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК ФизЛицо,
	|	ФизическиеЛица.Наименование КАК ЗначениеРеквизита,
	|	&НайденоВРеквизитеНаименование КАК НайденоВРеквизите 
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	(ФизическиеЛица.Наименование ПОДОБНО &ФамилияИмяОтчетство
	|			ИЛИ ФизическиеЛица.Наименование ПОДОБНО &ФамилияИмя
	|			ИЛИ ФизическиеЛица.Наименование ПОДОБНО &Фамилия)";
	
	
	Если ЗначениеЗаполнено(ВидДокумента)
		И (ЗначениеЗаполнено(Серия)
		Или ЗначениеЗаполнено(Номер)) Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыФизическихЛиц.Физлицо,
		|	ДокументыФизическихЛиц.Представление,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыФизическихЛиц.ВидДокумента)
		|ИЗ
		|	РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
		|ГДЕ
		|	ДокументыФизическихЛиц.ВидДокумента = &ВидДокумента
		|	И ДокументыФизическихЛиц.Серия = &Серия
		|	И ДокументыФизическихЛиц.Номер = &Номер";
		Запрос.УстановитьПараметр("ВидДокумента", ВидДокумента);
		Запрос.УстановитьПараметр("Серия", СокрЛП(Серия));
		Запрос.УстановитьПараметр("Номер", СокрЛП(Номер));
		Запрос.УстановитьПараметр("Документ", НСтр("ru='ФИО'"));
		
	КонецЕсли;	
	ТекстЗапроса = ТекстЗапроса + "
	|ИТОГИ ПО
	|	ФизЛицо";
	
	Запрос.Текст = ТекстЗапроса;
	
	ФИО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(Объект.Наименование)," ");
	
	КоличествоПодстрок = ФИО.Количество();
	Фамилия            = ?(КоличествоПодстрок > 0,ФИО[0],"");
	Имя                = ?(КоличествоПодстрок > 1,ФИО[1],"");
	Отчество           = ?(КоличествоПодстрок > 2,ФИО[2],"");
	
	Запрос.УстановитьПараметр("ФамилияИмяОтчетство", СокрЛП(Фамилия + " " + Имя + " " + Отчество) + "%");
	Запрос.УстановитьПараметр("ФамилияИмя", СокрЛП(Фамилия + " " + Имя));
	Запрос.УстановитьПараметр("Фамилия",  СокрЛП(Фамилия));
	Запрос.УстановитьПараметр("НайденоВРеквизитеНаименование", НСтр("ru='ФИО'"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаНайдено.Очистить();
	ТаблицаРасшифровкаНайдено.Очистить();
	
	Если РезультатЗапроса.Пустой() Тогда
	     Возврат Истина;	 
	 КонецЕсли;
	
	ВыборкаФизЛицо  = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаФизЛицо.Следующий() Цикл
		СтрокаТаблицыНайдено = ТаблицаНайдено.Добавить();
		СтрокаТаблицыНайдено.ФизЛицо = ВыборкаФизЛицо.ФизЛицо;
		СтрокаТаблицыНайдено.КоличествоНайденныхАналогов = 0;
		
		ВыборкаНайдено = ВыборкаФизЛицо.Выбрать();
		Пока ВыборкаНайдено.Следующий() Цикл
						
			СтрокаТаблицыРасшифровкаНайдено = ТаблицаРасшифровкаНайдено.Добавить();
			СтрокаТаблицыРасшифровкаНайдено.ФизЛицо = ВыборкаФизЛицо.ФизЛицо;
			СтрокаТаблицыРасшифровкаНайдено.ПредставлениеНайденоПо = ВыборкаНайдено.НайденоВРеквизите;
			СтрокаТаблицыНайдено.КоличествоНайденныхАналогов = СтрокаТаблицыНайдено.КоличествоНайденныхАналогов + 1;
			
		КонецЦикла;
		
		СтрокаТаблицыНайдено.Представление = Строка(ВыборкаФизЛицо.ФизЛицо) + ?(СтрокаТаблицыНайдено.КоличествоНайденныхАналогов > 1, " (Совпадений - " + СтрокаТаблицыНайдено.КоличествоНайденныхАналогов + ")","");
		
				
	КонецЦикла;
	
	Если ТаблицаНайдено.Количество() > 0 Тогда
		ТаблицаНайдено.Сортировать("КоличествоНайденныхАналогов УБЫВ");
	КонецЕсли;
	
	Элементы.КоманднаяПанельФормы.Видимость = Ложь;
	
	Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницыФормы.ПодчиненныеЭлементы.ГруппаДубли;
	
	Возврат Ложь;
КонецФункции

