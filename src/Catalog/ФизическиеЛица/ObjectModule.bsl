
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ 

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		Наименование = СокрЛП(Наименование);
		Пока Найти(Наименование,"  ") > 0 Цикл
			Наименование = СтрЗаменить(Наименование, "  ", " ");
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Перем ТекстСообщения;
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ЭтоНовый() Тогда
		ЗначенияРеквизитовВИнформационнойБазе = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Ссылка, Новый Структура("ИНН"));
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ИНН) Тогда 
		Если (ЭтоНовый() ИЛИ ЗначенияРеквизитовВИнформационнойБазе.ИНН <> ИНН)
			И НЕ РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			"ИНН",
			,
			Отказ
			);
		КонецЕсли;
	Конецесли;
	
	Если Не ЗначениеНастроекПовтИсп.ИспользуютсяГруппыДоступаФизическихЛиц() Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГруппаДоступа");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры
