#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстОплатаОтКлиента = "
	|ВЫБРАТЬ // Получение оплаты от клиента (Дт 76.09 :: Кт 62.01)
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(РасчетыСуммаРегл.СуммаРегл, 0) - ЕСТЬNULL(Расчеты.ПредоплатаРегл, 0) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР КОГДА Операция.Валюта = Константы.ВалютаРегламентированногоУчета ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ)
	|	КОНЕЦ КАК СчетДт,
	|	Операция.КонтрагентКредитор КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	ЕСТЬNULL(Расчеты.Долг, 0) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаКт,
	|	ЕСТЬNULL(Расчеты.ЗаказКлиента.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Расчеты.Валюта КАК ВалютаКт,
	|	Расчеты.ЗаказКлиента.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	ВЫБОР КОГДА Расчеты.ЗаказКлиента ССЫЛКА Справочник.ДоговорыКонтрагентов ТОГДА
	|		Расчеты.ЗаказКлиента
	|	ИНАЧЕ
	|		Расчеты.ЗаказКлиента.Договор
	|	КОНЕЦ СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ЕСТЬNULL(Расчеты.Долг, 0) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта,
	|			СУММА(Расчеты.Долг) КАК Долг,
	|			СУММА(Расчеты.Предоплата) КАК Предоплата,
	|			СУММА(Расчеты.ПредоплатаРегл) КАК ПредоплатаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта
	|		) КАК Расчеты
	|	ПО
	|		ИСТИНА
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта,
	|			СУММА(Расчеты.СуммаРегл) КАК СуммаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта
	|		) КАК РасчетыСуммаРегл
	|	ПО
	|		Расчеты.Контрагент = РасчетыСуммаРегл.Контрагент
	|		И Расчеты.ЗаказКлиента = РасчетыСуммаРегл.ЗаказКлиента
	|		И Расчеты.Валюта = РасчетыСуммаРегл.Валюта
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Константы КАК Константы
	|	ПО
	|		Истина
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И ЕСТЬNULL(Расчеты.Долг, 0) > 0
	|";
	
	ТекстАвансОтКлиента = "
	|ВЫБРАТЬ // Поступление аванса от клиента (Дт 76.09 :: Кт 62.02)
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Расчеты.ПредоплатаРегл, 0) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР КОГДА Операция.Валюта = Константы.ВалютаРегламентированногоУчета ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ)
	|	КОНЕЦ КАК СчетДт,
	|	Операция.КонтрагентКредитор КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	ЕСТЬNULL(Расчеты.Предоплата, 0) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные) КАК ВидСчетаКт,
	|	ЕСТЬNULL(Расчеты.ЗаказКлиента.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Расчеты.Валюта КАК ВалютаКт,
	|	Расчеты.ЗаказКлиента.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	ВЫБОР КОГДА Расчеты.ЗаказКлиента ССЫЛКА Справочник.ДоговорыКонтрагентов ТОГДА
	|		Расчеты.ЗаказКлиента
	|	ИНАЧЕ
	|		Расчеты.ЗаказКлиента.Договор
	|	КОНЕЦ СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ЕСТЬNULL(Расчеты.Предоплата, 0) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта,
	|			СУММА(Расчеты.Долг) КАК Долг,
	|			СУММА(Расчеты.Предоплата) КАК Предоплата,
	|			СУММА(Расчеты.ПредоплатаРегл) КАК ПредоплатаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта
	|		) КАК Расчеты
	|	ПО
	|		ИСТИНА
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Константы КАК Константы
	|	ПО
	|		Истина
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И ЕСТЬNULL(Расчеты.Предоплата, 0) > 0
	|";
	
	ТекстВозвратАвансаКлиенту = "
	|ВЫБРАТЬ // Поступление аванса от клиента (Дт 62.02 :: Кт 76.09)
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Расчеты.ПредоплатаРегл, 0) КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные) КАК ВидСчетаДт,
	|	ЕСТЬNULL(Расчеты.ЗаказКлиента.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	Расчеты.ЗаказКлиента.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	ВЫБОР КОГДА Расчеты.ЗаказКлиента ССЫЛКА Справочник.ДоговорыКонтрагентов ТОГДА
	|		Расчеты.ЗаказКлиента
	|	ИНАЧЕ
	|		Расчеты.ЗаказКлиента.Договор
	|	КОНЕЦ СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	ЕСТЬNULL(Расчеты.Предоплата, 0) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Операция.Валюта КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ВЫБОР КОГДА Операция.Валюта = Константы.ВалютаРегламентированногоУчета ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ)
	|	КОНЕЦ КАК СчетКт,
	|	Операция.КонтрагентКредитор КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ЕСТЬNULL(Расчеты.Предоплата, 0) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта,
	|			СУММА(-Расчеты.Долг) КАК Долг,
	|			СУММА(-Расчеты.Предоплата) КАК Предоплата,
	|			СУММА(-Расчеты.ПредоплатаРегл) КАК ПредоплатаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта
	|		) КАК Расчеты
	|	ПО
	|		ИСТИНА
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Константы КАК Константы
	|	ПО
	|		Истина
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И ЕСТЬNULL(Расчеты.Предоплата, 0) > 0
	|";
	
	ТекстВозвратОплатыКлиенту = "
	|ВЫБРАТЬ // Поступление аванса от клиента (Дт 62.01 :: Кт 76.09)
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(РасчетыСуммаРегл.СуммаРегл, 0) - ЕСТЬNULL(Расчеты.ПредоплатаРегл, 0) КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаДт,
	|	ЕСТЬNULL(Расчеты.ЗаказКлиента.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	Расчеты.ЗаказКлиента.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	ВЫБОР КОГДА Расчеты.ЗаказКлиента ССЫЛКА Справочник.ДоговорыКонтрагентов ТОГДА
	|		Расчеты.ЗаказКлиента
	|	ИНАЧЕ
	|		Расчеты.ЗаказКлиента.Договор
	|	КОНЕЦ СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	ЕСТЬNULL(Расчеты.Долг, 0) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Операция.Валюта КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ВЫБОР КОГДА Операция.Валюта = Константы.ВалютаРегламентированногоУчета ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ)
	|	КОНЕЦ КАК СчетКт,
	|	Операция.КонтрагентКредитор КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ЕСТЬNULL(Расчеты.Долг, 0) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта,
	|			СУММА(-Расчеты.Долг) КАК Долг,
	|			СУММА(-Расчеты.Предоплата) КАК Предоплата,
	|			СУММА(-Расчеты.ПредоплатаРегл) КАК ПредоплатаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта
	|		) КАК Расчеты
	|	ПО
	|		ИСТИНА
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта,
	|			СУММА(-Расчеты.СуммаРегл) КАК СуммаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказКлиента,
	|			Расчеты.Валюта
	|		) КАК РасчетыСуммаРегл
	|	ПО
	|		Расчеты.Контрагент = РасчетыСуммаРегл.Контрагент
	|		И Расчеты.ЗаказКлиента = РасчетыСуммаРегл.ЗаказКлиента
	|		И Расчеты.Валюта = РасчетыСуммаРегл.Валюта
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Константы КАК Константы
	|	ПО
	|		Истина
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И ЕСТЬNULL(Расчеты.Долг, 0) > 0
	|";
	
	ТекстОплатаПоставщику = "
	|ВЫБРАТЬ // Оплата поставщикам через подотчетное лицо (Дт 60.01 :: Кт 76.09)
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(РасчетыСуммаРегл.СуммаРегл, 0) - ЕСТЬNULL(Расчеты.ПредоплатаРегл, 0) КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаДт,
	|	ЕСТЬNULL(Расчеты.ЗаказПоставщику.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	Расчеты.ЗаказПоставщику.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	ВЫБОР КОГДА Расчеты.ЗаказПоставщику ССЫЛКА Справочник.ДоговорыКонтрагентов ТОГДА
	|		Расчеты.ЗаказПоставщику
	|	ИНАЧЕ
	|		Расчеты.ЗаказПоставщику.Договор
	|	КОНЕЦ СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	ЕСТЬNULL(Расчеты.Долг, 0) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Операция.Валюта КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ВЫБОР КОГДА Операция.Валюта = Константы.ВалютаРегламентированногоУчета ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ)
	|	КОНЕЦ КАК СчетКт,
	|	Операция.КонтрагентКредитор КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ЕСТЬNULL(Расчеты.Долг, 0) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказПоставщику,
	|			Расчеты.Валюта,
	|			СУММА(Расчеты.Долг) КАК Долг,
	|			СУММА(Расчеты.Предоплата) КАК Предоплата,
	|			СУММА(Расчеты.ПредоплатаРегл) КАК ПредоплатаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказПоставщику,
	|			Расчеты.Валюта
	|		) КАК Расчеты
	|	ПО
	|		ИСТИНА
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказПоставщику,
	|			Расчеты.Валюта,
	|			СУММА(Расчеты.СуммаРегл) КАК СуммаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказПоставщику,
	|			Расчеты.Валюта
	|		) КАК РасчетыСуммаРегл
	|	ПО
	|		Расчеты.Контрагент = РасчетыСуммаРегл.Контрагент
	|		И Расчеты.ЗаказПоставщику = РасчетыСуммаРегл.ЗаказПоставщику
	|		И Расчеты.Валюта = РасчетыСуммаРегл.Валюта
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Константы КАК Константы
	|	ПО
	|		Истина
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И ЕСТЬNULL(Расчеты.Долг, 0) > 0
	|";
	
	ТекстПредоплатаПоставщику = "
	|ВЫБРАТЬ // Оплата аванса поставщикам через подотчетное лицо (Дт 60.02 :: Кт 76.09)
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Расчеты.ПредоплатаРегл, 0) КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные) КАК ВидСчетаДт,
	|	ЕСТЬNULL(Расчеты.ЗаказПоставщику.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	Расчеты.ЗаказПоставщику.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	ВЫБОР КОГДА Расчеты.ЗаказПоставщику ССЫЛКА Справочник.ДоговорыКонтрагентов ТОГДА
	|		Расчеты.ЗаказПоставщику
	|	ИНАЧЕ
	|		Расчеты.ЗаказПоставщику.Договор
	|	КОНЕЦ СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	ЕСТЬNULL(Расчеты.Предоплата, 0) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Операция.Валюта КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ВЫБОР КОГДА Операция.Валюта = Константы.ВалютаРегламентированногоУчета ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ)
	|	КОНЕЦ КАК СчетКт,
	|	Операция.КонтрагентКредитор КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ЕСТЬNULL(Расчеты.Предоплата, 0) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказПоставщику КАК ЗаказПоставщику,
	|			Расчеты.Валюта КАк Валюта,
	|			СУММА(Расчеты.Предоплата) КАК Предоплата,
	|			СУММА(Расчеты.ПредоплатаРегл) КАК ПредоплатаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказПоставщику,
	|			Расчеты.Валюта
	|		) КАК Расчеты
	|	ПО
	|		ИСТИНА
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Константы КАК Константы
	|	ПО
	|		Истина
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И ЕСТЬNULL(Расчеты.Предоплата, 0) > 0
	|";
	
	ТекстВозвратПредоплатыОтПоставщика = "
	|ВЫБРАТЬ // Оплата аванса поставщикам через подотчетное лицо (Дт 76.09 :: Кт 60.02)
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Расчеты.ПредоплатаРегл, 0) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Операция.Валюта КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР КОГДА Операция.Валюта = Константы.ВалютаРегламентированногоУчета ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ)
	|	КОНЕЦ КАК СчетДт,
	|	Операция.КонтрагентКредитор КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	ЕСТЬNULL(Расчеты.Предоплата, 0) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные) КАК ВидСчетаКт,
	|	ЕСТЬNULL(Расчеты.ЗаказПоставщику.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Расчеты.Валюта КАК ВалютаКт,
	|	Расчеты.ЗаказПоставщику.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	ВЫБОР КОГДА Расчеты.ЗаказПоставщику ССЫЛКА Справочник.ДоговорыКонтрагентов ТОГДА
	|		Расчеты.ЗаказПоставщику
	|	ИНАЧЕ
	|		Расчеты.ЗаказПоставщику.Договор
	|	КОНЕЦ СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ЕСТЬNULL(Расчеты.Предоплата, 0) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказПоставщику КАК ЗаказПоставщику,
	|			Расчеты.Валюта КАк Валюта,
	|			СУММА(-Расчеты.Предоплата) КАК Предоплата,
	|			СУММА(-Расчеты.ПредоплатаРегл) КАК ПредоплатаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказПоставщику,
	|			Расчеты.Валюта
	|		) КАК Расчеты
	|	ПО
	|		ИСТИНА
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Константы КАК Константы
	|	ПО
	|		Истина
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И ЕСТЬNULL(Расчеты.Предоплата, 0) > 0
	|";
	
	ТекстВозвратОплатыОтПоставщика = "
	|ВЫБРАТЬ // Оплата аванса поставщикам через подотчетное лицо (Дт 76.09 :: Кт 60.01)
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(РасчетыСуммаРегл.СуммаРегл, 0) - ЕСТЬNULL(Расчеты.ПредоплатаРегл, 0) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Операция.Валюта КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР КОГДА Операция.Валюта = Константы.ВалютаРегламентированногоУчета ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ)
	|	КОНЕЦ КАК СчетДт,
	|	Операция.КонтрагентКредитор КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	ЕСТЬNULL(Расчеты.Долг, 0) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
	|	ЕСТЬNULL(Расчеты.ЗаказПоставщику.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Расчеты.Валюта КАК ВалютаКт,
	|	Расчеты.ЗаказПоставщику.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	ВЫБОР КОГДА Расчеты.ЗаказПоставщику ССЫЛКА Справочник.ДоговорыКонтрагентов ТОГДА
	|		Расчеты.ЗаказПоставщику
	|	ИНАЧЕ
	|		Расчеты.ЗаказПоставщику.Договор
	|	КОНЕЦ СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ЕСТЬNULL(Расчеты.Долг, 0) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказПоставщику,
	|			Расчеты.Валюта,
	|			СУММА(-Расчеты.Долг) КАК Долг,
	|			СУММА(-Расчеты.Предоплата) КАК Предоплата,
	|			СУММА(-Расчеты.ПредоплатаРегл) КАК ПредоплатаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказПоставщику,
	|			Расчеты.Валюта
	|		) КАК Расчеты
	|	ПО
	|		ИСТИНА
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|			Расчеты.ЗаказПоставщику,
	|			Расчеты.Валюта,
	|			СУММА(-Расчеты.СуммаРегл) КАК СуммаРегл
	|		ИЗ
	|			РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|		ГДЕ
	|			Расчеты.Регистратор = &Ссылка
	|		СГРУППИРОВАТЬ ПО
	|			Расчеты.АналитикаУчетаПоПартнерам.Контрагент,
	|			Расчеты.ЗаказПоставщику,
	|			Расчеты.Валюта
	|		) КАК РасчетыСуммаРегл
	|	ПО
	|		Расчеты.Контрагент = РасчетыСуммаРегл.Контрагент
	|		И Расчеты.ЗаказПоставщику = РасчетыСуммаРегл.ЗаказПоставщику
	|		И Расчеты.Валюта = РасчетыСуммаРегл.Валюта
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Константы КАК Константы
	|	ПО
	|		Истина
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И ЕСТЬNULL(Расчеты.Долг, 0) > 0
	|";
	
	Возврат
	ТекстОплатаОтКлиента
	+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстАвансОтКлиента
	+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстВозвратАвансаКлиенту
	+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстВозвратОплатыКлиенту
	+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстОплатаПоставщику
	+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПредоплатаПоставщику
	+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстВозвратПредоплатыОтПоставщика
	+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстВозвратОплатыОтПоставщика;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.КонтрагентДебитор КАК КонтрагентДебитор,
	|	ДанныеДокумента.КонтрагентКредитор КАК КонтрагентКредитор,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.РасчетыМеждуОрганизациямиДебитор КАК РасчетыМеждуОрганизациямиДебитор,
	|	ДанныеДокумента.РасчетыМеждуОрганизациямиКредитор КАК РасчетыМеждуОрганизациямиКредитор
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК ДанныеДокумента
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	// Формирование ключей аналитики учета по партнерам.
	СформироватьКлючиАналитикиПоДокументу(ДокументСсылка, Реквизиты);
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
		Реквизиты.Валюта, 
		, // ВалютаВзаиморасчетов
		Реквизиты.Период
	);
	
	ТекстЗапроса = ТекстЗапросаТаблицаРасчетыСКлиентами()
		+ ТекстЗапросаТаблицаРасчетыСКлиентамиПоследовательность()
		+ ТекстЗапросаТаблицаРасчетыСПоставщиками()
		+ ТекстЗапросаТаблицаРасчетыСПоставщикамиПоследовательность()
		+ ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл()
		;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Валюта", Реквизиты.Валюта);
	Запрос.УстановитьПараметр("КонтрагентДебитор", Реквизиты.КонтрагентДебитор);
	Запрос.УстановитьПараметр("КонтрагентКредитор", Реквизиты.КонтрагентКредитор);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("РасчетыМеждуОрганизациямиДебитор",Реквизиты.РасчетыМеждуОрганизациямиДебитор);
	Запрос.УстановитьПараметр("РасчетыМеждуОрганизациямиКредитор",Реквизиты.РасчетыМеждуОрганизациямиКредитор);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСКлиентами",                      МассивРезультатов[0].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСКлиентамиПоследовательность",    МассивРезультатов[1].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщиками",                   МассивРезультатов[2].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщикамиПоследовательность", МассивРезультатов[3].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСуммыДокументовВВалютеРегл",             МассивРезультатов[4].Выгрузить());
	
КонецПроцедуры

Процедура СформироватьКлючиАналитикиПоДокументу(ДокументСсылка, Реквизиты)
	
	Запрос = Новый Запрос(" 
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТабличныеЧасти.Организация КАК Организация,
	|	ТабличныеЧасти.Партнер     КАК Партнер,
	|	ТабличныеЧасти.Контрагент  КАК Контрагент
	|ИЗ (
	|	ВЫБРАТЬ // аналитика {Организация, Дебитор-партнер, Дебитор-контрагент}
	|		&Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ ДебиторскаяЗадолженность.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ДебиторскаяЗадолженность
	|	ГДЕ
	|		ДебиторскаяЗадолженность.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Организация, Кредитор-партнер, Кредитор-контрагент}
	|		&Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ КредиторскаяЗадолженность.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК КредиторскаяЗадолженность
	|	ГДЕ
	|		КредиторскаяЗадолженность.Ссылка = &Ссылка
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Дебитор-организация, Кредитор-партнер, Кредитор-контрагент}
	|		&КонтрагентДебитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Дт_Кт.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Дт_Кт
	|	ГДЕ
	|		Дт_Кт.Ссылка=&Ссылка И &РасчетыМеждуОрганизациямиДебитор И &КонтрагентКредитор<>&КонтрагентДебитор
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Кредитор-организация, Дебитор-партнер, Дебитор-контрагент}
	|		&КонтрагентКредитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Кт_Дт.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Кт_Дт
	|	ГДЕ
	|		Кт_Дт.Ссылка=&Ссылка И &РасчетыМеждуОрганизациямиКредитор И &КонтрагентКредитор<>&КонтрагентДебитор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Дебитор-организация, Предприятие-партнер, Организация}
	|		&КонтрагентДебитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&Организация КАК Контрагент
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Дт_Орг
	|	ГДЕ
	|		Дт_Орг.Ссылка=&Ссылка И &РасчетыМеждуОрганизациямиДебитор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Кредитор-организация, Предприятие-партнер, Организация}
	|		&КонтрагентКредитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&Организация КАК Контрагент
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Кт_Орг
	|	ГДЕ
	|		Кт_Орг.Ссылка=&Ссылка И &РасчетыМеждуОрганизациямиКредитор
	|	
	|	) КАК ТабличныеЧасти
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам  КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		ТабличныеЧасти.Организация = РегистрАналитикаУчетаПоПартнерам.Организация
	|		И ТабличныеЧасти.Партнер = РегистрАналитикаУчетаПоПартнерам.Партнер
	|		И ТабличныеЧасти.Контрагент = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.КлючАналитики ЕСТЬ NULL
	|");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("КонтрагентДебитор", Реквизиты.КонтрагентДебитор);
	Запрос.УстановитьПараметр("КонтрагентКредитор", Реквизиты.КонтрагентКредитор);
	Запрос.УстановитьПараметр("РасчетыМеждуОрганизациямиДебитор", Реквизиты.РасчетыМеждуОрганизациямиДебитор);
	Запрос.УстановитьПараметр("РасчетыМеждуОрганизациямиКредитор", Реквизиты.РасчетыМеждуОрганизациямиКредитор);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРасчетыСКлиентами()
	
	ТекстЗапроса =
	"
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Период КАК ДатаРегистратора,
	|	ТабличнаяЧасть.ВидДвижения КАК ВидДвижения,
	|	
	|	// Измерения
	|	РегистрАналитикаУчетаПоПартнерам.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ТабличнаяЧасть.Заказ КАК ЗаказКлиента,
	|	ТабличнаяЧасть.ВалютаВзаиморасчетов КАК Валюта,
	|	
	|	// Ресурсы
	|	// Сумма в валюте взаиморассчетов.
	|	ТабличнаяЧасть.Сумма      КАК Сумма,
	|	ТабличнаяЧасть.КОплате    КАК КОплате,
	|	
	|	// Реквизиты
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Взаимозачет) КАК ФормаОплаты,
	|	ТабличнаяЧасть.СуммаРегл КАК СуммаРегл
	|	
	|ИЗ (
	// расчеты по основной организации
	|	ВЫБРАТЬ // дебиторская задолженность дебитора-клиента перед организацией считается оплаченной
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|		&Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ ДебиторскаяЗадолженность.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент,
	|		ДебиторскаяЗадолженность.Заказ КАК Заказ,
	|		ДебиторскаяЗадолженность.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		ДебиторскаяЗадолженность.СуммаВзаиморасчетов КАК Сумма,
	|		ДебиторскаяЗадолженность.СуммаВзаиморасчетов КАК КОплате,
	|		ВЫРАЗИТЬ(ДебиторскаяЗадолженность.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		ДебиторскаяЗадолженность.НомерСтроки КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ДебиторскаяЗадолженность
	|	ГДЕ
	|		ДебиторскаяЗадолженность.Ссылка = &Ссылка
	|		И ДебиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // кредиторская задолженность организации перед кредитором-клиентом сторнируется
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|		&Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ КредиторскаяЗадолженность.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент,
	|		КредиторскаяЗадолженность.Заказ КАК Заказ,
	|		КредиторскаяЗадолженность.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		(-КредиторскаяЗадолженность.СуммаВзаиморасчетов) КАК Сумма,
	|		(-КредиторскаяЗадолженность.СуммаВзаиморасчетов) КАК КОплате,
	|		- ВЫРАЗИТЬ(КредиторскаяЗадолженность.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		КредиторскаяЗадолженность.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК КредиторскаяЗадолженность
	|	ГДЕ
	|		КредиторскаяЗадолженность.Ссылка = &Ссылка
	|		И КредиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|
	// (РасчетыСКлиентом, Кредитор-Организация)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // перенос дебиторки по клиентам у организации на дебиторку по клиентам у кредитора-организации
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|		&КонтрагентКредитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Кт_Дт.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент,
	|		Неопределено КАК Заказ,
	|		Кт_Дт.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		-СУММА(Кт_Дт.СуммаВзаиморасчетов) КАК Сумма,
	|		-СУММА(Кт_Дт.СуммаВзаиморасчетов) КАК КОплате,
	|		-ВЫРАЗИТЬ(СУММА(Кт_Дт.Сумма) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		МАКСИМУМ(Кт_Дт.НомерСтроки) КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Кт_Дт
	|	ГДЕ
	|		Кт_Дт.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиКредитор И &КонтрагентКредитор<>&КонтрагентДебитор
	|		И Кт_Дт.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	СГРУППИРОВАТЬ ПО
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Кт_Дт.Партнер КОНЕЦ), Кт_Дт.ВалютаВзаиморасчетов
	|
	// (РасчетыСПоставщиком, Дебитор-Организация)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // сторно кредиторки по клиенту у дебитора-организации перед организацией
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|		&КонтрагентДебитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&Организация КАК Контрагент,
	|		Дт_Орг.Заказ КАК Заказ,
	|		Дт_Орг.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		-Дт_Орг.СуммаВзаиморасчетов КАК Сумма,
	|		-Дт_Орг.СуммаВзаиморасчетов КАК КОплате,
	|		- ВЫРАЗИТЬ(Дт_Орг.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		Дт_Орг.НомерСтроки КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Дт_Орг
	|	ГДЕ
	|		Дт_Орг.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиДебитор
	|		И Дт_Орг.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|
	// (РасчетыСПоставщиком, Кредитор-Организация)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // зачет (оплата) дебиторской задолженности организации перед кредитором-организацией
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|		&КонтрагентКредитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&Организация КАК Контрагент,
	|		Кт_Орг.Заказ КАК Заказ,
	|		Кт_Орг.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		Кт_Орг.СуммаВзаиморасчетов КАК Сумма,
	|		Кт_Орг.СуммаВзаиморасчетов КАК КОплате,
	|		ВЫРАЗИТЬ(Кт_Орг.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))  КАК СуммаРегл,
	|		Кт_Орг.НомерСтроки КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Кт_Орг
	|	ГДЕ
	|		Кт_Орг.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиКредитор
	|		И Кт_Орг.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|		&КонтрагентКредитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Кт_Дт.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент,
	|		Неопределено КАК Заказ,
	|		Кт_Дт.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		-СУММА(Кт_Дт.СуммаВзаиморасчетов) КАК Сумма,
	|		-СУММА(Кт_Дт.СуммаВзаиморасчетов) КАК КОплате,
	|		-ВЫРАЗИТЬ(СУММА(Кт_Дт.Сумма) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		МАКСИМУМ(Кт_Дт.НомерСтроки) КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Кт_Дт
	|	ГДЕ
	|		Кт_Дт.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиКредитор И &КонтрагентКредитор<>&КонтрагентДебитор
	|		И Кт_Дт.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	СГРУППИРОВАТЬ ПО
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Кт_Дт.Партнер КОНЕЦ), Кт_Дт.ВалютаВзаиморасчетов
	|
	|	) КАК ТабличнаяЧасть
	|	
	|	// Определим Ключи аналитики
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		ТабличнаяЧасть.Организация = РегистрАналитикаУчетаПоПартнерам.Организация
	|		И ТабличнаяЧасть.Партнер = РегистрАналитикаУчетаПоПартнерам.Партнер
	|		И ТабличнаяЧасть.Контрагент = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ВидДвижения,
	|	ТабличнаяЧасть.НомерСтроки
	|	
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСКлиентамиПоследовательность()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	РегистрАналитикаУчетаПоПартнерам.КлючАналитики	КАК АналитикаУчетаПоПартнерам,
	|	МАКСИМУМ(ДанныеДокумента.НомерСтроки) КАК НомерСтроки
	|ИЗ (
	|	ВЫБРАТЬ // аналитика {организация, дебитор-партнер, дебитор-клиент}
	|		&Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ ДебиторскаяЗадолженность.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент,
	|		ДебиторскаяЗадолженность.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ДебиторскаяЗадолженность
	|	ГДЕ
	|		ДебиторскаяЗадолженность.Ссылка = &Ссылка
	|		И ДебиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {организация, кредитор-партнер, кредитор-клиент}
	|		&Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ КредиторскаяЗадолженность.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент,
	|		КредиторскаяЗадолженность.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК КредиторскаяЗадолженность
	|	ГДЕ
	|		КредиторскаяЗадолженность.Ссылка = &Ссылка
	|		И КредиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // аналитика {кредитор-организация, дебитор-партнер, дебитор-клиент}
	|		&КонтрагентКредитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Кт_Дт.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент,
	|		Кт_Дт.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Кт_Дт
	|	ГДЕ
	|		Кт_Дт.Ссылка=&Ссылка И &РасчетыМеждуОрганизациямиКредитор И &КонтрагентКредитор<>&КонтрагентДебитор
	|		И Кт_Дт.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // аналитика {дебитор-организация, предприятие-партнер, организация}
	|		&КонтрагентДебитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&Организация КАК Контрагент,
	|		Дт_Орг.НомерСтроки КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Дт_Орг
	|	ГДЕ
	|		Дт_Орг.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиДебитор
	|		И Дт_Орг.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // аналитика {кредитор-организация, предприятие-партнер, организация}
	|		&КонтрагентКредитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&Организация КАК Контрагент,
	|		Кт_Орг.НомерСтроки КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Кт_Орг
	|	ГДЕ
	|		Кт_Орг.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиКредитор
	|		И Кт_Орг.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // аналитика {кредитор-организация, дебитор-партнер, дебитор-поставщик}
	|		&КонтрагентКредитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Кт_Дт.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент,
	|		Кт_Дт.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Кт_Дт
	|	ГДЕ
	|		Кт_Дт.Ссылка=&Ссылка И &РасчетыМеждуОрганизациямиКредитор И &КонтрагентКредитор<>&КонтрагентДебитор
	|		И Кт_Дт.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|
	|	) КАК ДанныеДокумента
	|	
	|	// Определим Ключи аналитики
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		ДанныеДокумента.Организация = РегистрАналитикаУчетаПоПартнерам.Организация
	|		И ДанныеДокумента.Партнер = РегистрАналитикаУчетаПоПартнерам.Партнер
	|		И ДанныеДокумента.Контрагент = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|	
	|СГРУППИРОВАТЬ ПО
	|	РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|	
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПоставщиками()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Период КАК ДатаРегистратора,
	|	ТабличнаяЧасть.ВидДвижения КАК ВидДвижения,
	|	
	|	// Измерения
	|	РегистрАналитикаУчетаПоПартнерам.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ТабличнаяЧасть.Заказ КАК ЗаказПоставщику,
	|	ТабличнаяЧасть.ВалютаВзаиморасчетов КАК Валюта,
	|	
	|	// Ресурсы
	|	// Сумма в валюте взаиморассчетов
	|	ТабличнаяЧасть.Сумма КАК Сумма,
	|	ТабличнаяЧасть.КПоступлению КАК КПоступлению,
	|	ТабличнаяЧасть.КОплате КАК КОплате,
	|	
	|	// Реквизиты
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Взаимозачет) КАК ФормаОплаты,
	|	ТабличнаяЧасть.СуммаРегл КАК СуммаРегл
	|	
	|ИЗ
	// расчеты по основной организации
	|	(ВЫБРАТЬ // дебиторская задолженность дебитора-поставщика перед организацией сторнируется
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|		&Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ ДебиторскаяЗадолженность.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент,
	|		ДебиторскаяЗадолженность.Заказ КАК Заказ,
	|		ДебиторскаяЗадолженность.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		(-ДебиторскаяЗадолженность.СуммаВзаиморасчетов) КАК Сумма,
	|		0 КАК КПоступлению,
	|		(-ДебиторскаяЗадолженность.СуммаВзаиморасчетов) КАК КОплате,
	|		-ВЫРАЗИТЬ(ДебиторскаяЗадолженность.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		ДебиторскаяЗадолженность.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ДебиторскаяЗадолженность
	|	ГДЕ
	|		ДебиторскаяЗадолженность.Ссылка = &Ссылка
	|		И ДебиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // кредиторская задолженность организации перед кредитором-поставщиком считается оплаченной
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|		&Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ КредиторскаяЗадолженность.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент,
	|		КредиторскаяЗадолженность.Заказ КАК Заказ,
	|		КредиторскаяЗадолженность.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		КредиторскаяЗадолженность.СуммаВзаиморасчетов КАК Сумма,
	|		0 КАК КПоступлению,
	|		КредиторскаяЗадолженность.СуммаВзаиморасчетов КАК КОплате,
	|		ВЫРАЗИТЬ(КредиторскаяЗадолженность.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		КредиторскаяЗадолженность.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК КредиторскаяЗадолженность
	|	ГДЕ
	|		КредиторскаяЗадолженность.Ссылка = &Ссылка
	|		И КредиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|
	// (РасчетыСКлиентом, Дебитор-Организация)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // зачет (оплата) кредиторской задолженности дебитора-организации перед организацией 
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|		&КонтрагентДебитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&Организация КАК Контрагент,
	|		Дт_Орг.Заказ КАК Заказ,
	|		Дт_Орг.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		Дт_Орг.СуммаВзаиморасчетов КАК Сумма,
	|		0 КАК КПоступлению,
	|		Дт_Орг.СуммаВзаиморасчетов КАК КОплате,
	|		ВЫРАЗИТЬ(Дт_Орг.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		Дт_Орг.НомерСтроки КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Дт_Орг
	|	ГДЕ
	|		Дт_Орг.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиДебитор
	|		И Дт_Орг.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // перенос кредиторки по клиенту у дебитора-организации на кредиторку поставщику перед кредитором-клиентом
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|		&КонтрагентДебитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Дт_Кт.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент,
	|		Неопределено КАК Заказ,
	|		Дт_Кт.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		-СУММА(Дт_Кт.СуммаВзаиморасчетов) КАК Сумма,
	|		0 КАК КПоступлению,
	|		-СУММА(Дт_Кт.СуммаВзаиморасчетов) КАК КОплате,
	|		-ВЫРАЗИТЬ(СУММА(Дт_Кт.Сумма) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		МАКСИМУМ(Дт_Кт.НомерСтроки) КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Дт_Кт
	|	ГДЕ
	|		Дт_Кт.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиДебитор И &КонтрагентКредитор<>&КонтрагентДебитор
	|		И Дт_Кт.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	СГРУППИРОВАТЬ ПО
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Дт_Кт.Партнер КОНЕЦ), Дт_Кт.ВалютаВзаиморасчетов
	|
	// (РасчетыСКлиентом, Кредитор-Организация)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // сторно дебиторки по поставщику у кредитора-организации
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|		&КонтрагентКредитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&Организация КАК Контрагент,
	|		Кт_Орг.Заказ КАК Заказ,
	|		Кт_Орг.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		-Кт_Орг.СуммаВзаиморасчетов КАК Сумма,
	|		0 КАК КПоступлению,
	|		-Кт_Орг.СуммаВзаиморасчетов КАК КОплате,
	|		-ВЫРАЗИТЬ(Кт_Орг.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		Кт_Орг.НомерСтроки КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Кт_Орг
	|	ГДЕ
	|		Кт_Орг.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиКредитор
	|		И Кт_Орг.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|		
	// (РасчетыСПоставщиком, Дебитор-Организация)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // перенос кредиторки по поставщику у дебитора-организации на кредиторку по поставщику перед кредитором-поставщиком
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|		&КонтрагентДебитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Дт_Кт.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент,
	|		Неопределено КАК Заказ,
	|		Дт_Кт.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		-СУММА(Дт_Кт.СуммаВзаиморасчетов) КАК Сумма,
	|		0 КАК КПоступлению,
	|		-СУММА(Дт_Кт.СуммаВзаиморасчетов) КАК КОплате,
	|		-ВЫРАЗИТЬ(СУММА(Дт_Кт.Сумма) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СуммаРегл,
	|		МАКСИМУМ(Дт_Кт.НомерСтроки) КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Дт_Кт
	|	ГДЕ
	|		Дт_Кт.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиДебитор И &КонтрагентКредитор<>&КонтрагентДебитор
	|		И Дт_Кт.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	СГРУППИРОВАТЬ ПО
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Дт_Кт.Партнер КОНЕЦ), Дт_Кт.ВалютаВзаиморасчетов
	|
	|	) КАК ТабличнаяЧасть
	|	
	|	// Определим Ключи аналитики
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		ТабличнаяЧасть.Организация = РегистрАналитикаУчетаПоПартнерам.Организация
	|		И ТабличнаяЧасть.Партнер = РегистрАналитикаУчетаПоПартнерам.Партнер
	|		И ТабличнаяЧасть.Контрагент = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ВидДвижения,
	|	ТабличнаяЧасть.НомерСтроки
	|	
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПоставщикамиПоследовательность()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	РегистрАналитикаУчетаПоПартнерам.КлючАналитики	КАК АналитикаУчетаПоПартнерам,
	|	МАКСИМУМ(ДанныеДокумента.НомерСтроки) КАК НомерСтроки
	|ИЗ (
	|	ВЫБРАТЬ // аналитика {организация, дебитор-партнер, дебитор-поставщик}
	|		&Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ ДебиторскаяЗадолженность.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент,
	|		ДебиторскаяЗадолженность.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ДебиторскаяЗадолженность
	|	ГДЕ
	|		ДебиторскаяЗадолженность.Ссылка = &Ссылка
	|		И ДебиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {организация, кредитор-партнер, кредитор-поставщик}
	|		&Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ КредиторскаяЗадолженность.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент,
	|		КредиторскаяЗадолженность.НомерСтроки КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК КредиторскаяЗадолженность
	|	ГДЕ
	|		КредиторскаяЗадолженность.Ссылка = &Ссылка
	|		И КредиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // аналитика {дебитор-организация, предприятие-партнер, организация}
	|		&КонтрагентДебитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&Организация КАК Контрагент,
	|		Дт_Орг.НомерСтроки КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Дт_Орг
	|	ГДЕ
	|		Дт_Орг.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиДебитор
	|		И Дт_Орг.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // аналитика {дебитор-организация, кредитор-партнер, кредитор-клиент}
	|		&КонтрагентДебитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Дт_Кт.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент,
	|		Дт_Кт.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Дт_Кт
	|	ГДЕ
	|		Дт_Кт.Ссылка=&Ссылка И &РасчетыМеждуОрганизациямиДебитор И &КонтрагентКредитор<>&КонтрагентДебитор
	|		И Дт_Кт.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // аналитика {кредитор-организация, предприятие-партнер, организация}
	|		&КонтрагентКредитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&Организация КАК Контрагент,
	|		Кт_Орг.НомерСтроки КАК НомерСтроки
	|	ИЗ 
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Кт_Орг
	|	ГДЕ
	|		Кт_Орг.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиКредитор
	|		И Кт_Орг.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ // аналитика {дебитор-организация, кредитор-партнер, кредитор-клиент}
	|		&КонтрагентДебитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ Дт_Кт.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент,
	|		Дт_Кт.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Дт_Кт
	|	ГДЕ
	|		Дт_Кт.Ссылка=&Ссылка И &РасчетыМеждуОрганизациямиДебитор И &КонтрагентКредитор<>&КонтрагентДебитор
	|		И Дт_Кт.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|
	|	) КАК ДанныеДокумента
	|	
	|	// Определим Ключи аналитики
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		ДанныеДокумента.Организация = РегистрАналитикаУчетаПоПартнерам.Организация
	|		И ДанныеДокумента.Партнер = РегистрАналитикаУчетаПоПартнерам.Партнер
	|		И ДанныеДокумента.Контрагент = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|	
	|СГРУППИРОВАТЬ ПО
	|	РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|	
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	ТаблицаДокумента.Сумма * &КоэффициентПересчетаВВалютуРегл КАК СуммаБезНДСРегл,
	|	0 КАК СуммаНДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ТипРасчетов
	|
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &Валюта <> &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	ТаблицаДокумента.Сумма * &КоэффициентПересчетаВВалютуРегл КАК СуммаБезНДСРегл,
	|	0 КАК СуммаНДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ТипРасчетов
	|
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &Валюта <> &ВалютаРегламентированногоУчета
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл()

///////////////////////////////////////////////////////////////////////////////
// Печать

Функция ПолучитьДанныеПечати(знач МассивДокументов, знач МассивИменМакетов) Экспорт
	
	Структура = Новый Структура("Данные, Макеты",
		ПолучитьДанныеОбъектаПоМакетам(МассивДокументов, МассивИменМакетов),
		ПолучитьМакетыИОписанияСекций(МассивИменМакетов)
	);
				
	Возврат Структура;
	
КонецФункции

Функция ПолучитьДанныеОбъектаПоМакетам(знач МассивДокументов, знач МассивИменМакетов) Экспорт
	
	ДанныеПоВсемОбъектам = Новый Соответствие;
	
	МассивРезультатов = ПолучитьДанныеВзаимозачетЗадолженности(МассивДокументов);
	
	ВыборкаДокумент = МассивРезультатов[0].Выбрать();
	ВыборкаДебиторскаяЗадолженность = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаКредиторскаяЗадолженность = МассивРезультатов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокумент.Следующий() Цикл
	
		ДанныеОбъекта = Новый Структура;
		ДанныеОбъекта.Вставить("Организация", ВыборкаДокумент.Организация);
		ДанныеОбъекта.Вставить("ДолжностьРуководителя", ВыборкаДокумент.ДолжностьРуководителя);
		ДанныеОбъекта.Вставить("Руководитель", ВыборкаДокумент.Руководитель);
		ДанныеОбъекта.Вставить("КонтрагентДебитор", ВыборкаДокумент.КонтрагентДебитор);
		ДанныеОбъекта.Вставить("КонтрагентКредитор", ВыборкаДокумент.КонтрагентКредитор);
		ДанныеОбъекта.Вставить("Дата", Формат(ВыборкаДокумент.Дата, "ДЛФ=ДД"));
		ДанныеОбъекта.Вставить("АдресОрганизации", ВыборкаДокумент.АдресОрганизации);
		ДанныеОбъекта.Вставить("АдресКонтрагентаДебитора", ВыборкаДокумент.АдресКонтрагентаДебитора);
		ДанныеОбъекта.Вставить("АдресКонтрагентаКредитора", ВыборкаДокумент.АдресКонтрагентаКредитора);
		ДанныеОбъекта.Вставить("Взаимозачет", ВыборкаДокумент.Взаимозачет);
		ДанныеОбъекта.Вставить("Сумма", ФормированиеПечатныхФорм.ФорматСумм(ВыборкаДокумент.СуммаДокумента, ВыборкаДокумент.Валюта));
		
		СуммаПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(ВыборкаДокумент.СуммаДокумента, ВыборкаДокумент.Валюта);
		ДанныеОбъекта.Вставить("СуммаПрописью", СуммаПрописью);

		ДанныеОбъекта.Вставить("ДебиторскаяЗадолженность", Новый Массив);
		ДанныеОбъекта.Вставить("КредиторскаяЗадолженность", Новый Массив);
		
		Если ВыборкаДебиторскаяЗадолженность.НайтиСледующий(ВыборкаДокумент.Ссылка, "Ссылка") Тогда 
			СформироватьТаблицуЗадолженности(ВыборкаДебиторскаяЗадолженность, ВыборкаДокумент, ДанныеОбъекта.ДебиторскаяЗадолженность);
		КонецЕсли;
		Если ВыборкаКредиторскаяЗадолженность.НайтиСледующий(ВыборкаДокумент.Ссылка, "Ссылка") Тогда 
			СформироватьТаблицуЗадолженности(ВыборкаКредиторскаяЗадолженность, ВыборкаДокумент, ДанныеОбъекта.КредиторскаяЗадолженность);
		КонецЕсли;

		ДанныеОбъектаПоМакетам = Новый Структура;
		Для Каждого ИмяМакета Из МассивИменМакетов Цикл
			ДанныеОбъектаПоМакетам.Вставить(ИмяМакета, ДанныеОбъекта);
		КонецЦикла;
		ДанныеПоВсемОбъектам.Вставить(ВыборкаДокумент.Ссылка, ДанныеОбъектаПоМакетам);
	КонецЦикла;
	
	Возврат ДанныеПоВсемОбъектам;
	
КонецФункции

Функция ПолучитьМакетыИОписанияСекций(знач МассивИменМакетов) Экспорт
	
	ОписаниеСекций = Новый Структура;
	ДвоичныеДанныеМакетов = Новый Структура;
	
	Для Каждого ИмяМакета Из МассивИменМакетов Цикл
		
		Макет = Неопределено;
		ОписаниеСекцийМакета = Неопределено;
		
		Если ИмяМакета = "ПФ_DOC_АктВзаимозачета" Тогда
			ОписаниеСекцийМакета = ПолучитьОписаниеОбластейВзаимозачетЗадолженности();
			Макет = УправлениеПечатью.ПолучитьМакет("Документ.ВзаимозачетЗадолженности.ПФ_DOC_АктВзаимозачета");
		ИначеЕсли ИмяМакета = "ПФ_DOC_АктПереуступкиДолга" Тогда
			ОписаниеСекцийМакета = ПолучитьОписаниеОбластейВзаимозачетЗадолженности();
			Макет = УправлениеПечатью.ПолучитьМакет("Документ.ВзаимозачетЗадолженности.ПФ_DOC_АктПереуступкиДолга");
		КонецЕсли;
		
		Если ОписаниеСекцийМакета <> Неопределено И Макет <> Неопределено Тогда
			
			ОписаниеСекций.Вставить(ИмяМакета, ОписаниеСекцийМакета);
			ДвоичныеДанныеМакетов.Вставить(ИмяМакета, Макет);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Структура = Новый Структура("ОписаниеСекций, ДвоичныеДанныеМакетов",
		ОписаниеСекций,
	    ДвоичныеДанныеМакетов
	);
	
	Возврат Структура;
	
КонецФункции

Процедура СформироватьТаблицуЗадолженности(РезультатЗапроса, ВыборкаДокумент, МассивСтрок)
	
	НомерСтроки = 0;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НомерСтроки = НомерСтроки + 1;
		СтрокаТаблицы = Новый Структура;
		СтрокаТаблицы.Вставить("НомерСтроки", НомерСтроки);
		СтрокаТаблицы.Вставить("Сумма", Формат(Выборка.Сумма, "ЧДЦ=2"));
		
		ТекстДокумент = "";
		Если ЗначениеЗаполнено(Выборка.НаименованиеДляПечати) Тогда
			ТекстДокумент = Выборка.НаименованиеДляПечати;
		ИначеЕсли ЗначениеЗаполнено(Выборка.ТипДокумента) Тогда
			ТекстДокумент = ТекстДокумент
				+ ?(Не ПустаяСтрока(ТекстДокумент), ", " + Символы.ПС, "")
				+ ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(Выборка, Строка(Выборка.ТипДокумента))
			;
		КонецЕсли;
		Если ПустаяСтрока(ТекстДокумент) Тогда
			ТекстДокумент = "Без документа";
		КонецЕсли;
	
		СтрокаТаблицы.Вставить("Документ", ТекстДокумент);
		
		МассивСтрок.Добавить(СтрокаТаблицы);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДанныеВзаимозачетЗадолженности(МассивДокументов)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивДокументов, МенеджерВременныхТаблиц);	
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Организация.НаименованиеСокращенное КАК Организация,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ДанныеДокумента.КонтрагентДебитор.НаименованиеПолное КАК КонтрагентДебитор,
	|	ДанныеДокумента.КонтрагентКредитор.НаименованиеПолное КАК КонтрагентКредитор,
	|	ВЫБОР КОГДА ДанныеДокумента.КонтрагентДебитор = ДанныеДокумента.КонтрагентКредитор ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК Взаимозачет,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаДокумента,
	|	АдресОрганизации.Представление КАК АдресОрганизации,
	|	АдресКонтрагентаДебитора.Представление КАК АдресКонтрагентаДебитора,
	|	АдресКонтрагентаКредитора.Представление КАК АдресКонтрагентаКредитора
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК ДанныеДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Организации.КонтактнаяИнформация КАК АдресОрганизации
	|	ПО
	|		ДанныеДокумента.Организация = АдресОрганизации.Ссылка
	|		И (АдресОрганизации.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации))
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Контрагенты.КонтактнаяИнформация КАК АдресКонтрагентаДебитора
	|	ПО
	|		ДанныеДокумента.КонтрагентДебитор = АдресКонтрагентаДебитора.Ссылка
	|		И (АдресКонтрагентаДебитора.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента))
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Контрагенты.КонтактнаяИнформация КАК АдресКонтрагентаКредитора
	|	ПО
	|		ДанныеДокумента.КонтрагентДебитор = АдресКонтрагентаКредитора.Ссылка
	|		И (АдресКонтрагентаКредитора.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента))
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задолженность.Ссылка КАК Ссылка,
	|	Задолженность.ТипРасчетов КАК ТипРасчетов,
	|	ВЫБОР КОГДА Задолженность.Заказ = НЕОПРЕДЕЛЕНО ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ТИПЗНАЧЕНИЯ(Задолженность.Заказ)
	|	КОНЕЦ КАК ТипДокумента,
	|	Задолженность.Заказ.Дата КАК Дата,
	|	Задолженность.Заказ.Номер КАК Номер,
	|	ВЫБОР КОГДА Задолженность.Заказ ССЫЛКА Справочник.ДоговорыКонтрагентов ТОГДА
	|		Задолженность.Заказ.НаименованиеДляПечати
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаименованиеДляПечати,
	|	Задолженность.Сумма КАК Сумма
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Задолженность
	|ГДЕ
	|	Задолженность.Ссылка В (&МассивДокументов)
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задолженность.Ссылка КАК Ссылка,
	|	Задолженность.ТипРасчетов КАК ТипРасчетов,
	|	ВЫБОР КОГДА Задолженность.Заказ = НЕОПРЕДЕЛЕНО ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ТИПЗНАЧЕНИЯ(Задолженность.Заказ)
	|	КОНЕЦ КАК ТипДокумента,
	|	Задолженность.Заказ.Дата КАК Дата,
	|	Задолженность.Заказ.Номер КАК Номер,
	|	ВЫБОР КОГДА Задолженность.Заказ ССЫЛКА Справочник.ДоговорыКонтрагентов ТОГДА
	|		Задолженность.Заказ.НаименованиеДляПечати
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаименованиеДляПечати,
	|	Задолженность.Сумма КАК Сумма
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Задолженность
	|ГДЕ
	|	Задолженность.Ссылка В (&МассивДокументов)
	|
	|ИТОГИ ПО
	|	Ссылка
	|");
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
		
	Возврат МассивРезультатов;

КонецФункции

Функция ПолучитьОписаниеОбластейВзаимозачетЗадолженности()

	Секции = Новый Структура;
	
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "Заголовок", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "ДебиторскаяЗадолженность", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "КредиторскаяЗадолженность", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "ШапкаТаблицы", "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "СтрокаТаблицы", "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "Итог", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "Подвал", "Общая");
	
	Возврат Секции;

КонецФункции

#КонецЕсли