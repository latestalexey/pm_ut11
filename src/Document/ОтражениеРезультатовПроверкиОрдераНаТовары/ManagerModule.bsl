#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	ИменаРеквизитов = "Склад,Помещение";
	
	Возврат ИменаРеквизитов;
КонецФункции

//Возвращает параметры указания серий для товаров, указанных в документе
//
//	Параметры
//			Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий
//	Возвращаемое значение
//			Тип Структура
//				Состав полей задается в функции ОбработкаТабличнойЧастиКлиентСервер.ПараметрыУказанияСерий
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры = 
						ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатурыСклад",Новый Структура("Склад",Объект.Склад));
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.КонтрольОтгрузки);
	
	Если СкладыСервер.ИспользоватьАдресноеХранение(Объект.Склад, Объект.Помещение) Тогда
		ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	КонецЕсли;
	
	ПараметрыУказанияСерий.ЭтоОрдер = Истина;
	ПараметрыУказанияСерий.ИмяПоляПомещение = "Помещение";
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	
	Возврат ПараметрыУказанияСерий;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеШапки.Дата КАК Период,
	|	ДанныеШапки.Склад КАК Склад,
	|	ДанныеШапки.Ордер КАК Ордер,
	|	ДанныеШапки.РаспоряжениеНаИнвентаризацию КАК РаспоряжениеНаИнвентаризацию,
	|	ДанныеШапки.Помещение КАК Помещение,
	|	ДанныеШапки.ЗонаОтгрузки КАК ЗонаОтгрузки
	|ИЗ
	|	Документ.ОтражениеРезультатовПроверкиОрдераНаТовары КАК ДанныеШапки
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();

	Запрос.УстановитьПараметр("Период",          		Реквизиты.Период);
	Запрос.УстановитьПараметр("Склад",           		Реквизиты.Склад);
	Запрос.УстановитьПараметр("Ордер",					Реквизиты.Ордер);
	Запрос.УстановитьПараметр("Помещение",    			Реквизиты.Помещение);
	Запрос.УстановитьПараметр("РаспоряжениеНаИнвентаризацию", Реквизиты.РаспоряжениеНаИнвентаризацию);
 	Запрос.УстановитьПараметр("ЗонаОтгрузки",           Реквизиты.ЗонаОтгрузки);

	Запрос.Текст =
	// 0 ТаблицаТоварыКОтбору
	"ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаТовары.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	&Ордер КАК Распоряжение,
	|	СУММА(ТаблицаТовары.Количество) КАК Отобрано,
	|	СУММА(ТаблицаТовары.КоличествоУпаковок) КАК ОтобраноУпаковок,
	|	0 КАК КОтбору
	|ИЗ
	|	Документ.ОтражениеРезультатовПроверкиОрдераНаТовары.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И (ТаблицаТовары.ВидОперации = Значение(Перечисление.ВидыОперацийОтраженияРезультатовПроверкиОрдераНаТовары.ОтразитьНедостачу)
	|	ИЛИ ТаблицаТовары.ВидОперации = Значение(Перечисление.ВидыОперацийОтраженияРезультатовПроверкиОрдераНаТовары.ОставитьВЗонеОтгрузки))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Упаковка,
	|	ТаблицаТовары.Серия
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	// 1 ТаблицаТоварыКОформлениюИзлишковНедостач
	|////////////////////////////////////////////////////////////////////////////////
	// Недостачи
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаТовары.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Склад КАК Склад,
	|	&Помещение КАК Помещение,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Серия КАК Серия,
	|	&РаспоряжениеНаИнвентаризацию КАК ДокументОснование,
	|	СУММА(ТаблицаТовары.Количество) КАК КОформлениюОрдеров
	|ИЗ
	|	Документ.ОтражениеРезультатовПроверкиОрдераНаТовары.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ВидОперации = Значение(Перечисление.ВидыОперацийОтраженияРезультатовПроверкиОрдераНаТовары.ОтразитьНедостачу)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Излишки
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаТовары.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Склад,
	|	&Помещение,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	&РаспоряжениеНаИнвентаризацию,
	|	СУММА(ТаблицаТовары.Количество)
	|ИЗ
	|	Документ.ОтражениеРезультатовПроверкиОрдераНаТовары.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ВидОперации = Значение(Перечисление.ВидыОперацийОтраженияРезультатовПроверкиОрдераНаТовары.ОтразитьИзлишек)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	// 2 ТаблицаТоварыВСкладскихЯчейках
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Товары.НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Упаковка КАК Упаковка,
	|	&ЗонаОтгрузки КАК Ячейка,
	|	Товары.КоличествоУпаковок КАК ВНаличии
	|ИЗ
	|	Документ.ОтражениеРезультатовПроверкиОрдераНаТовары.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|	И (Товары.ВидОперации = Значение(Перечисление.ВидыОперацийОтраженияРезультатовПроверкиОрдераНаТовары.ОтразитьИзлишек)
	|	ИЛИ Товары.ВидОперации = Значение(Перечисление.ВидыОперацийОтраженияРезультатовПроверкиОрдераНаТовары.ОставитьВЗонеОтгрузки))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 3 ТаблицаДвиженияСерийТоваров
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура КАК Номенклатура,
	|	ТаблицаСерии.Характеристика КАК Характеристика,
	|	ТаблицаСерии.Серия КАК Серия,
	|	ТаблицаСерии.Количество  КАК Количество,
	|	&Склад КАК Склад,
	|	&Помещение КАК Помещение,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.КонтрольОтгрузки) КАК СкладскаяОперация,
	|	&Ордер  КАК Документ,
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ТаблицаСерии.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.ОрдерНаОтражениеИзлишковТоваров.Товары КАК ТаблицаСерии
	|
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Результат = Запрос.ВыполнитьПакет();

	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТоварыКОтбору",                       Результат[0].Выгрузить());
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТоварыКОформлениюИзлишковНедостач",   Результат[1].Выгрузить());
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТоварыВСкладскихЯчейках",             Результат[2].Выгрузить());
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДвиженияСерийТоваров",                Результат[3].Выгрузить());
КонецПроцедуры // ИнициализироватьДанныеДокумента()


#КонецЕсли