#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

//Возвращает распоряжение на инвентаризацию по складу, если есть только одно действующее распоряжение
// 
//	Параметры:
//		Склад - СправочникСсылка.Склады - склад, по которому нужно вернуть распоряжение
//		Распоряжение - если значение заполнено, то проверяется, действует ли это распоряжение, если
//				распоряжение действует, то возвращается оно, если нет - делается попытка найти
//				действующее распоряжение, если находится одно действующее распоряжение, то оно возвращается,
//				если нет - возвращается пустая ссылка
//	Возвращаемое значение:
//		ДокументСсылка.РаспоряжениеНаИнвентаризацию - действующее распоряжение на инвентаризацию
//
Функция РаспоряжениеПоУмолчанию(Склад, Распоряжение = Неопределено) Экспорт 
	
	ТекущаяДата = ТекущаяДата();
	
	Если ЗначениеЗаполнено(Распоряжение) Тогда
		
		РеквизитыРаспоряжения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Распоряжение, "Склад,Статус,ДатаНачала,ДатаОкончания,Проведен");
		
		Если РеквизитыРаспоряжения.Статус = Перечисления.СтатусыРаспоряженийНаПроведениеИнвентаризацийТоваров.ВРаботе
			И РеквизитыРаспоряжения.Склад = Склад
			И РеквизитыРаспоряжения.Проведен
			И РеквизитыРаспоряжения.ДатаНачала <= ТекущаяДата
			И РеквизитыРаспоряжения.ДатаОкончания >= НачалоДня(ТекущаяДата) Тогда
			
			Возврат Распоряжение;
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка КАК Распоряжение
	|ИЗ
	|	Документ.РаспоряжениеНаИнвентаризациюТоваров КАК РаспоряжениеНаИнвентаризациюТоваров
	|ГДЕ
	|	РаспоряжениеНаИнвентаризациюТоваров.Проведен
	|	И РаспоряжениеНаИнвентаризациюТоваров.ДатаНачала <= &ТекущаяДата
	|	И КОНЕЦПЕРИОДА(РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания, ДЕНЬ) >= &ТекущаяДата
	|	И РаспоряжениеНаИнвентаризациюТоваров.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыРаспоряженийНаПроведениеИнвентаризацийТоваров.Закрыт)
	|	И РаспоряжениеНаИнвентаризациюТоваров.Склад = &Склад";
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Распоряжение;
	Иначе
		Возврат Документы.РаспоряжениеНаИнвентаризациюТоваров.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

//Возвращает список организаций, по которым есть проведенные складские акты
//	Параметры:
//		МассивДокументов - Массив - массви сслылок на складские акты
//
//	Возвращаемые значения:
//		СписокЗначений - список организаций
//
Функция СписокОрганизацийИзАктов(МассивДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОрганизаций.Организация
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТоварыОрганизаций.Организация КАК Организация
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|	ГДЕ
	|		ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).ДокументОснование В (&МассивДокументов)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТоварыОрганизаций.Организация
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|	ГДЕ
	|		ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).ДокументОснование В (&МассивДокументов)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТоварыОрганизаций.Организация
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|	ГДЕ
	|		ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).ДокументОснование В (&МассивДокументов)) КАК ТаблицаОрганизаций
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОрганизаций.Организация.Наименование";
	
	Запрос.УстановитьПараметр("МассивДокументов",МассивДокументов);
	
	СписокОрганизаций = Новый СписокЗначений;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СписокОрганизаций.Добавить(Выборка.Организация);
	КонецЦикла;
	
	Возврат СписокОрганизаций;
	
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Печать

// Функция получает данные для формирования печатной формы ИНВ3
//
Функция ПолучитьДанныеДляПечатнойФормыИНВ3(ПараметрыПечати, ДокументОснование) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	ЗапросПредварительныхДанных = Новый Запрос;
	ЗапросПредварительныхДанных.Текст = 
	"ВЫБРАТЬ
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет КАК ПредварительныйРасчет,
	|	РаспоряжениеНаИнвентаризациюТоваров.ИсточникИнформацииОЦенахДляПечати КАК ИсточникИнформацииОЦенахДляПечати
	|ИЗ
	|	Документ.РаспоряжениеНаИнвентаризациюТоваров КАК РаспоряжениеНаИнвентаризациюТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО (РасчетСебестоимостиТоваровОрганизации.Организация = &Организация)
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен)
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(РаспоряжениеНаИнвентаризациюТоваров.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(РаспоряжениеНаИнвентаризациюТоваров.Дата, МЕСЯЦ))
	|ГДЕ
	|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДатыДокументов.Дата КАК Дата
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПересортицаТоваров.Дата КАК Дата
	|	ИЗ
	|		Документ.ПересортицаТоваров КАК ПересортицаТоваров
	|	ГДЕ
	|		ПересортицаТоваров.Проведен = ИСТИНА
	|		И ПересортицаТоваров.Организация = &Организация
	|		И ПересортицаТоваров.ДокументОснование = &ДокументОснование
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОприходованиеИзлишковТоваров.Дата
	|	ИЗ
	|		Документ.ОприходованиеИзлишковТоваров КАК ОприходованиеИзлишковТоваров
	|	ГДЕ
	|		ОприходованиеИзлишковТоваров.Проведен = ИСТИНА
	|		И ОприходованиеИзлишковТоваров.Организация = &Организация
	|		И ОприходованиеИзлишковТоваров.ДокументОснование = &ДокументОснование
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеНедостачТоваров.Дата
	|	ИЗ
	|		Документ.СписаниеНедостачТоваров КАК СписаниеНедостачТоваров
	|	ГДЕ
	|		СписаниеНедостачТоваров.Проведен = ИСТИНА
	|		И СписаниеНедостачТоваров.Организация = &Организация
	|		И СписаниеНедостачТоваров.ДокументОснование = &ДокументОснование
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПорчаТоваров.Дата
	|	ИЗ
	|		Документ.ПорчаТоваров КАК ПорчаТоваров
	|	ГДЕ
	|		ПорчаТоваров.Проведен = ИСТИНА
	|		И ПорчаТоваров.Организация = &Организация
	|		И ПорчаТоваров.ДокументОснование = &ДокументОснование) КАК ДатыДокументов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
		
	ЗапросПредварительныхДанных.УстановитьПараметр("ДокументОснование", ДокументОснование);
	ЗапросПредварительныхДанных.УстановитьПараметр("Организация", ПараметрыПечати.Организация);
	
 	ПредварительныеДанные = ЗапросПредварительныхДанных.ВыполнитьПакет();
	
	ПредварительныеДанныеРезультат = ПредварительныеДанные[0].Выбрать();
	
	Если НЕ ПредварительныеДанныеРезультат.Следующий() Тогда
		Возврат Неопределено
	КонецЕсли;
	
	ПредварительныйРасчет = ПредварительныеДанныеРезультат.ПредварительныйРасчет;
	ИсточникИнформацииОЦенахДляПечати = ПредварительныеДанныеРезультат.ИсточникИнформацииОЦенахДляПечати;
		
	
	СтруктураРеквизитов = Новый Структура("Дата, ВидЦены, Склад");
	СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование,СтруктураРеквизитов);
	
	МаксДатаСкладскихАктовВыборка = ПредварительныеДанные[1].Выбрать();

	Если МаксДатаСкладскихАктовВыборка.Следующий() Тогда
		МаксДатаСкладскихАктов = МаксДатаСкладскихАктовВыборка.Дата;
	Иначе
		МаксДатаСкладскихАктов = СтруктураРеквизитов.Дата;	
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДокументОснование", 	ДокументОснование);
	Запрос.УстановитьПараметр("ДатаОстатков", 		КонецДня(МаксДатаСкладскихАктов));
	Запрос.УстановитьПараметр("Организация", 		ПараметрыПечати.Организация);
	Запрос.УстановитьПараметр("ВидЦены", 			СтруктураРеквизитов.ВидЦены);
	
	УчитыватьСебестоимостьТоваровПоВидамЗапасов = ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоВидамЗапасов");
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов", УчитыватьСебестоимостьТоваровПоВидамЗапасов);
	
	// Ответственные лица в печатной форме
	СтруктураОтветственных = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(ПараметрыПечати.Организация, СтруктураРеквизитов.Дата);
	Запрос.УстановитьПараметр("Руководитель", 		   СтруктураОтветственных.Руководитель);
	Запрос.УстановитьПараметр("ДолжностьРуководителя", СтруктураОтветственных.РуководительДолжность);
	Запрос.УстановитьПараметр("ГлавныйБухгалтер", 	   СтруктураОтветственных.ГлавныйБухгалтер);
	
	Если ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости Тогда
		
		Если ПредварительныйРасчет = Null Тогда
			
			ТекстСообщения = НСтр("ru = 'Не удалось получить цены по себестоимости для документа %Документ%: на %Период% не произведен расчет себестоимости.'");
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОснование.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Период%", Формат(НачалоМесяца(МаксДатаСкладскихАктов),"ДЛФ=DD"));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		Если ПредварительныйРасчет Тогда
			
			ТекстСообщения = НСтр("ru = 'При печати документа %Документ% использовались данные предварительного расчета себестоимости.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОснование.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПересчетТоваровТовары.Номенклатура,
			|	ПересчетТоваровТовары.Характеристика,
			|	&Организация,
			|	ПересчетТоваровТовары.Ссылка.Склад КАК Склад
			|ПОМЕСТИТЬ ТаблицаНоменклатуры
			|ИЗ
			|	Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
			|ГДЕ
			|	ПересчетТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересчетТоваровТовары.Ссылка.Проведен
			|	И ПересчетТоваровТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.Выполнено)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.Номенклатура КАК Номенклатура,
			|	ТоварыОрганизаций.Характеристика КАК Характеристика,
			|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА -ТоварыОрганизаций.Количество
			|		ИНАЧЕ ТоварыОрганизаций.Количество
			|	КОНЕЦ КАК КоличествоОтклонение,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫБОР
			|					КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|						ТОГДА -ТоварыОрганизаций.Количество
			|					ИНАЧЕ ТоварыОрганизаций.Количество
			|				КОНЕЦ
			|			ИНАЧЕ 0
			|	КОНЕЦ КАК КоличествоПоУчету
			|ПОМЕСТИТЬ ТоварыИнвентаризации
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.СписаниеНедостачТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).ДокументОснование = &ДокументОснование
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.Номенклатура,
			|	ТоварыОрганизаций.Характеристика,
			|	ТоварыОрганизаций.ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА -ТоварыОрганизаций.Количество
			|		ИНАЧЕ ТоварыОрганизаций.Количество
			|	КОНЕЦ,
			|	0
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ОприходованиеИзлишковТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).ДокументОснование = &ДокументОснование
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.Номенклатура,
			|	ТоварыОрганизаций.Характеристика,
			|	ТоварыОрганизаций.ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА -ТоварыОрганизаций.Количество
			|		ИНАЧЕ ТоварыОрганизаций.Количество
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫБОР
			|					КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|						ТОГДА -ТоварыОрганизаций.Количество
			|					ИНАЧЕ ТоварыОрганизаций.Количество
			|				КОНЕЦ
			|			ИНАЧЕ 0
			|	КОНЕЦ
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ПересортицаТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).ДокументОснование = &ДокументОснование
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизацийОстатки.Номенклатура,
			|	ТоварыОрганизацийОстатки.Характеристика,
			|	ТоварыОрганизацийОстатки.ВидЗапасов,
			|	0,
			|	ТоварыОрганизацийОстатки.КоличествоОстаток
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций.Остатки(
			|			&ДатаОстатков,
			|			(Номенклатура, Характеристика, Организация, Склад) В
			|				(ВЫБРАТЬ
			|					Таблица.Номенклатура,
			|					Таблица.Характеристика,
			|					Таблица.Организация,
			|					Таблица.Склад
			|				ИЗ
			|					ТаблицаНоменклатуры КАК Таблица)
			|				И ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)) КАК ТоварыОрганизацийОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СправочникКлючейАналитики.Ссылка КАК КлючАналитикиУчетаНоменклатуры,
			|	ТоварыИнвентаризации.Номенклатура,
			|	ТоварыИнвентаризации.Характеристика,
			|	ТоварыИнвентаризации.ВидЗапасов,
			|	&Организация КАК Организация,
			|	СУММА(ТоварыИнвентаризации.КоличествоПоУчету) КАК КоличествоПоУчету,
			|	СУММА(ТоварыИнвентаризации.КоличествоПоУчету + ТоварыИнвентаризации.КоличествоОтклонение) КАК КоличествоФактическое
			|ПОМЕСТИТЬ ТаблицаИнвентаризации
			|ИЗ
			|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СправочникКлючейАналитики
			|		ПО (СправочникКлючейАналитики.Номенклатура = ТоварыИнвентаризации.Номенклатура)
			|			И (СправочникКлючейАналитики.Характеристика = ТоварыИнвентаризации.Характеристика)
			|			И (СправочникКлючейАналитики.Склад = ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).Склад)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТоварыИнвентаризации.Номенклатура,
			|	ТоварыИнвентаризации.Характеристика,
			|	ТоварыИнвентаризации.ВидЗапасов,
			|	СправочникКлючейАналитики.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&Руководитель КАК Руководитель,
			|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
			|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).НаименованиеСокращенное КАК ОрганизацияПредставление,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущийОтветственный КАК Кладовщик,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.Наименование КАК ПодразделениеПредставление,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаНачала КАК ДатаНачала,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаОкончания,
			|	РаспоряжениеНаИнвентаризациюТоваров.Дата КАК ОснованиеДата,
			|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК ОснованиеНомер,
			|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК НомерДокумента,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаДокумента,
			|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка КАК Ссылка,
			|	&ДатаОстатков КАК ДатаСнятияОстатков
			|ИЗ
			|	Документ.РаспоряжениеНаИнвентаризациюТоваров КАК РаспоряжениеНаИнвентаризациюТоваров
			|ГДЕ
			|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка = &ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.Номенклатура,
			|	ТаблицаИнвентаризации.Характеристика.НаименованиеПолное КАК Характеристика,
			|	СУММА(ТаблицаИнвентаризации.КоличествоПоУчету) КАК КоличествоПоУчету,
			|	СУММА(ТаблицаИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое,
			|	ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0) КАК Цена,
			|	СУММА(ТаблицаИнвентаризации.КоличествоПоУчету * ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0)) КАК СуммаПоУчету,
			|	СУММА(ТаблицаИнвентаризации.КоличествоФактическое * ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0)) КАК СуммаФактическая,
			|	ТаблицаИнвентаризации.Номенклатура." + КолонкаКодов + " КАК ТоварКод,
			|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
			|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
			|	ТаблицаИнвентаризации.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	ЛОЖЬ КАК ЭтоВозвратнаяТара
			|ИЗ
			|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров.СрезПоследних(
			|				КОНЕЦПЕРИОДА(&ДатаОстатков, ДЕНЬ),
			|				(АналитикаУчетаНоменклатуры, ВидЗапасов, Организация) В
			|					(ВЫБРАТЬ
			|						Таблица.КлючАналитикиУчетаНоменклатуры,
			|						Таблица.ВидЗапасов,
			|						Таблица.Организация
			|					ИЗ
			|						ТаблицаИнвентаризации КАК Таблица)) КАК ЦеныНоменклатуры
			|		ПО ТаблицаИнвентаризации.КлючАналитикиУчетаНоменклатуры = ЦеныНоменклатуры.АналитикаУчетаНоменклатуры
			|			И (ТаблицаИнвентаризации.ВидЗапасов = ЦеныНоменклатуры.ВидЗапасов И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|				ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов) 
			|			И (ЦеныНоменклатуры.Организация = &Организация)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.Номенклатура,
			|	ТаблицаИнвентаризации.Характеристика.НаименованиеПолное,
			|	ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0),
			|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Код,
			|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Наименование,
			|	ТаблицаИнвентаризации.Номенклатура.НаименованиеПолное
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТоварНаименование";
			
		Иначе
			//Фактический расчет
			Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПересчетТоваровТовары.Номенклатура,
			|	ПересчетТоваровТовары.Характеристика,
			|	&Организация,
			|	ПересчетТоваровТовары.Ссылка.Склад КАК Склад
			|ПОМЕСТИТЬ ТаблицаНоменклатуры
			|ИЗ
			|	Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
			|ГДЕ
			|	ПересчетТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересчетТоваровТовары.Ссылка.Проведен
			|	И ПересчетТоваровТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.Выполнено)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.Номенклатура КАК Номенклатура,
			|	ТоварыОрганизаций.Характеристика КАК Характеристика,
			|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА -ТоварыОрганизаций.Количество
			|		ИНАЧЕ ТоварыОрганизаций.Количество
			|	КОНЕЦ КАК КоличествоОтклонение,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫБОР
			|					КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|						ТОГДА -ТоварыОрганизаций.Количество
			|					ИНАЧЕ ТоварыОрганизаций.Количество
			|				КОНЕЦ
			|			ИНАЧЕ 0
			|	КОНЕЦ КАК КоличествоПоУчету
			|ПОМЕСТИТЬ ТоварыИнвентаризации
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.СписаниеНедостачТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).ДокументОснование = &ДокументОснование
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.Номенклатура,
			|	ТоварыОрганизаций.Характеристика,
			|	ТоварыОрганизаций.ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА -ТоварыОрганизаций.Количество
			|		ИНАЧЕ ТоварыОрганизаций.Количество
			|	КОНЕЦ,
			|	0
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ОприходованиеИзлишковТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).ДокументОснование = &ДокументОснование
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.Номенклатура,
			|	ТоварыОрганизаций.Характеристика,
			|	ТоварыОрганизаций.ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА ТоварыОрганизаций.Количество
			|		ИНАЧЕ -ТоварыОрганизаций.Количество
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫБОР
			|					КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|						ТОГДА -ТоварыОрганизаций.Количество
			|					ИНАЧЕ ТоварыОрганизаций.Количество
			|				КОНЕЦ
			|			ИНАЧЕ 0
			|	КОНЕЦ
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ПересортицаТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).ДокументОснование = &ДокументОснование
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизацийОстатки.Номенклатура,
			|	ТоварыОрганизацийОстатки.Характеристика,
			|	ТоварыОрганизацийОстатки.ВидЗапасов,
			|	0,
			|	ТоварыОрганизацийОстатки.КоличествоОстаток
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций.Остатки(
			|			&ДатаОстатков,
			|			(Номенклатура, Характеристика, Организация, Склад) В
			|				(ВЫБРАТЬ
			|					Таблица.Номенклатура,
			|					Таблица.Характеристика,
			|					Таблица.Организация,
			|					Таблица.Склад
			|				ИЗ
			|					ТаблицаНоменклатуры КАК Таблица)
			|					И ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)) КАК ТоварыОрганизацийОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СправочникКлючейАналитики.Ссылка КАК КлючАналитикиУчетаНоменклатуры,
			|	ТоварыИнвентаризации.Номенклатура,
			|	ТоварыИнвентаризации.Характеристика,
			|	ТоварыИнвентаризации.ВидЗапасов,
			|	&Организация КАК Организация,
			|	СУММА(ТоварыИнвентаризации.КоличествоПоУчету) КАК КоличествоПоУчету,
			|	СУММА(ТоварыИнвентаризации.КоличествоПоУчету + ТоварыИнвентаризации.КоличествоОтклонение) КАК КоличествоФактическое
			|ПОМЕСТИТЬ ТаблицаИнвентаризации
			|ИЗ
			|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СправочникКлючейАналитики
			|		ПО (СправочникКлючейАналитики.Номенклатура = ТоварыИнвентаризации.Номенклатура)
			|			И (СправочникКлючейАналитики.Характеристика = ТоварыИнвентаризации.Характеристика)
			|			И (СправочникКлючейАналитики.Склад = ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).Склад)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТоварыИнвентаризации.Номенклатура,
			|	ТоварыИнвентаризации.Характеристика,
			|	ТоварыИнвентаризации.ВидЗапасов,
			|	СправочникКлючейАналитики.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&Руководитель КАК Руководитель,
			|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
			|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).НаименованиеСокращенное КАК ОрганизацияПредставление,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущийОтветственный КАК Кладовщик,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.Наименование КАК ПодразделениеПредставление,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаНачала КАК ДатаНачала,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаОкончания,
			|	РаспоряжениеНаИнвентаризациюТоваров.Дата КАК ОснованиеДата,
			|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК ОснованиеНомер,
			|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК НомерДокумента,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаДокумента,
			|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка КАК Ссылка,
			|	&ДатаОстатков КАК ДатаСнятияОстатков
			|ИЗ
			|	Документ.РаспоряжениеНаИнвентаризациюТоваров КАК РаспоряжениеНаИнвентаризациюТоваров
			|ГДЕ
			|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка = &ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.Номенклатура,
			|	ТаблицаИнвентаризации.Характеристика.НаименованиеПолное КАК Характеристика,
			|	СУММА(ТаблицаИнвентаризации.КоличествоПоУчету) КАК КоличествоПоУчету,
			|	СУММА(ТаблицаИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СебестоимостьТоваров.КоличествоОстаток, 0) = 0
			|			ТОГДА 0
			|		ИНАЧЕ ЕСТЬNULL(СебестоимостьТоваров.СтоимостьРеглОстаток, 0) / СебестоимостьТоваров.КоличествоОстаток
			|	КОНЕЦ КАК Цена,
			|	СУММА(ТаблицаИнвентаризации.КоличествоПоУчету * ВЫБОР 
			|														КОГДА ЕСТЬNULL(СебестоимостьТоваров.КоличествоОстаток, 0) = 0 
			|															ТОГДА 0 
			|														ИНАЧЕ ЕСТЬNULL(СебестоимостьТоваров.СтоимостьРеглОстаток, 0) / СебестоимостьТоваров.КоличествоОстаток
			|													КОНЕЦ) КАК СуммаПоУчету,
			|	СУММА(ТаблицаИнвентаризации.КоличествоФактическое * ВЫБОР 
			|															КОГДА ЕСТЬNULL(СебестоимостьТоваров.КоличествоОстаток, 0) = 0 
			|																ТОГДА 0 
			|															ИНАЧЕ ЕСТЬNULL(СебестоимостьТоваров.СтоимостьРеглОстаток, 0) / СебестоимостьТоваров.КоличествоОстаток 
			|														КОНЕЦ) КАК СуммаФактическая,
			|	ТаблицаИнвентаризации.Номенклатура." + КолонкаКодов + " КАК ТоварКод,
			|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
			|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
			|	ТаблицаИнвентаризации.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	ЛОЖЬ КАК ЭтоВозвратнаяТара
			|ИЗ	
			|ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров.Остатки(КОНЕЦПЕРИОДА(&ДатаОстатков, МЕСЯЦ), Организация = &Организация) КАК СебестоимостьТоваров
			|		ПО ТаблицаИнвентаризации.КлючАналитикиУчетаНоменклатуры = СебестоимостьТоваров.АналитикаУчетаНоменклатуры
			|			И (ТаблицаИнвентаризации.ВидЗапасов = СебестоимостьТоваров.ВидЗапасов И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|				ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
			|			И (СебестоимостьТоваров.Организация = &Организация)
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.Номенклатура,
			|	ТаблицаИнвентаризации.Характеристика.НаименованиеПолное,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СебестоимостьТоваров.КоличествоОстаток, 0) = 0
			|			ТОГДА 0
			|		ИНАЧЕ ЕСТЬNULL(СебестоимостьТоваров.СтоимостьРеглОстаток, 0) / СебестоимостьТоваров.КоличествоОстаток
			|	КОНЕЦ,
			|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Код,
			|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Наименование,
			|	ТаблицаИнвентаризации.Номенклатура.НаименованиеПолное
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТоварНаименование";
			
		КонецЕсли;
		
		Результаты = Запрос.ВыполнитьПакет();
		РезультатПоШапке = Результаты[3];
		РезультатПоТабличнойЧасти = Результаты[4];
		РезультатКурсВалюты = Неопределено;
		
		
	ИначеЕсли ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен Тогда 
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПересчетТоваровТовары.Номенклатура,
		|	ПересчетТоваровТовары.Характеристика,
		|	&Организация,
		|	ПересчетТоваровТовары.Ссылка.Склад КАК Склад
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
		|ГДЕ
		|	ПересчетТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
		|	И ПересчетТоваровТовары.Ссылка.Проведен
		|	И ПересчетТоваровТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.Выполнено)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыОрганизаций.Номенклатура КАК Номенклатура,
		|	ТоварыОрганизаций.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА -ТоварыОрганизаций.Количество
		|		ИНАЧЕ ТоварыОрганизаций.Количество
		|	КОНЕЦ КАК КоличествоОтклонение,
		|	ВЫБОР
		|		КОГДА ТоварыОрганизаций.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|			ТОГДА ВЫБОР
		|					КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|						ТОГДА -ТоварыОрганизаций.Количество
		|					ИНАЧЕ ТоварыОрганизаций.Количество
		|				КОНЕЦ
		|			ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоПоУчету
		|ПОМЕСТИТЬ ТоварыИнвентаризации
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|ГДЕ
		|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.СписаниеНедостачТоваров
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).ДокументОснование = &ДокументОснование
		|	И ТоварыОрганизаций.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыОрганизаций.Номенклатура,
		|	ТоварыОрганизаций.Характеристика,
		|	ВЫБОР
		|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА -ТоварыОрганизаций.Количество
		|		ИНАЧЕ ТоварыОрганизаций.Количество
		|	КОНЕЦ,
		|	0
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|ГДЕ
		|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ОприходованиеИзлишковТоваров
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).ДокументОснование = &ДокументОснование
		|	И ТоварыОрганизаций.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыОрганизаций.Номенклатура,
		|	ТоварыОрганизаций.Характеристика,
		|	ВЫБОР
		|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА -ТоварыОрганизаций.Количество
		|		ИНАЧЕ ТоварыОрганизаций.Количество
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТоварыОрганизаций.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|			ТОГДА ВЫБОР
		|					КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|						ТОГДА -ТоварыОрганизаций.Количество
		|					ИНАЧЕ ТоварыОрганизаций.Количество
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|ГДЕ
		|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ПересортицаТоваров
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).ДокументОснование = &ДокументОснование
		|	И ТоварыОрганизаций.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыОрганизацийОстатки.Номенклатура,
		|	ТоварыОрганизацийОстатки.Характеристика,
		|	0,
		|	ТоварыОрганизацийОстатки.КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций.Остатки(
		|			&ДатаОстатков,
		|			(Номенклатура, Характеристика, Организация, Склад) В
		|					(ВЫБРАТЬ
		|						Таблица.Номенклатура,
		|						Таблица.Характеристика,
		|						Таблица.Организация,
		|						Таблица.Склад
		|					ИЗ
		|						ТаблицаНоменклатуры КАК Таблица)
		|				И ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)) КАК ТоварыОрганизацийОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыИнвентаризации.Номенклатура,
		|	ТоварыИнвентаризации.Характеристика,
		|	СУММА(ТоварыИнвентаризации.КоличествоПоУчету) КАК КоличествоПоУчету,
		|	СУММА(ТоварыИнвентаризации.КоличествоПоУчету + ТоварыИнвентаризации.КоличествоОтклонение) КАК КоличествоФактическое
		|ПОМЕСТИТЬ ТаблицаИнвентаризации
		|ИЗ
		|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыИнвентаризации.Номенклатура,
		|	ТоварыИнвентаризации.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Руководитель КАК Руководитель,
		|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
		|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).НаименованиеСокращенное КАК ОрганизацияПредставление,
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
		|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущийОтветственный КАК Кладовщик,
		|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
		|	РаспоряжениеНаИнвентаризациюТоваров.Склад.Наименование КАК ПодразделениеПредставление,
		|	РаспоряжениеНаИнвентаризациюТоваров.ДатаНачала КАК ДатаНачала,
		|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаОкончания,
		|	РаспоряжениеНаИнвентаризациюТоваров.Дата КАК ОснованиеДата,
		|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК ОснованиеНомер,
		|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК НомерДокумента,
		|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаДокумента,
		|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка КАК Ссылка,
		|	&ДатаОстатков КАК ДатаСнятияОстатков
		|ИЗ
		|	Документ.РаспоряжениеНаИнвентаризациюТоваров КАК РаспоряжениеНаИнвентаризациюТоваров
		|ГДЕ
		|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИнвентаризации.Номенклатура,
		|	ТаблицаИнвентаризации.Характеристика.НаименованиеПолное КАК Характеристика,
		|	ТаблицаИнвентаризации.КоличествоПоУчету,
		|	ТаблицаИнвентаризации.КоличествоФактическое,
		|	ВЫБОР
		|		КОГДА НЕ ЦеныНоменклатуры.Упаковка.Коэффициент ЕСТЬ NULL 
		|				И ЦеныНоменклатуры.Упаковка.Коэффициент <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / ЦеныНоменклатуры.Упаковка.Коэффициент
		|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ КАК Цена,
		|	ТаблицаИнвентаризации.КоличествоПоУчету * ВЫБОР
		|		КОГДА НЕ ЦеныНоменклатуры.Упаковка.Коэффициент ЕСТЬ NULL 
		|				И ЦеныНоменклатуры.Упаковка.Коэффициент <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / ЦеныНоменклатуры.Упаковка.Коэффициент
		|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ КАК СуммаПоУчету,
		|	ТаблицаИнвентаризации.КоличествоФактическое * ВЫБОР
		|		КОГДА НЕ ЦеныНоменклатуры.Упаковка.Коэффициент ЕСТЬ NULL 
		|				И ЦеныНоменклатуры.Упаковка.Коэффициент <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / ЦеныНоменклатуры.Упаковка.Коэффициент
		|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ КАК СуммаФактическая,
		|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	ТаблицаИнвентаризации.Номенклатура." + КолонкаКодов + " КАК ТоварКод,
		|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
		|	ТаблицаИнвентаризации.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
		|	ЛОЖЬ КАК ЭтоВозвратнаяТара
		|ИЗ
		|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|				КОНЕЦПЕРИОДА(&ДатаОстатков, ДЕНЬ),
		|				(ВидЦены, Номенклатура, Характеристика) В
		|					(ВЫБРАТЬ
		|						&ВидЦены,
		|						Таблица.Номенклатура,
		|						Таблица.Характеристика
		|					ИЗ
		|						ТаблицаИнвентаризации КАК Таблица)) КАК ЦеныНоменклатуры
		|		ПО ТаблицаИнвентаризации.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|			И ТаблицаИнвентаризации.Характеристика = ЦеныНоменклатуры.Характеристика
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТоварНаименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность КАК КоэффициентПересчета
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, Валюта = &ВидЦены) КАК КурсыВалютСрезПоследних";
		
		Результаты = Запрос.ВыполнитьПакет();
		РезультатПоШапке = Результаты[3];
		РезультатПоТабличнойЧасти = Результаты[4];
		РезультатКурсВалюты = Результаты[5];
		
	КонецЕсли;
	
	Возврат Новый Структура(
	"РезультатПоШапке, РезультатПоТабличнойЧасти, РезультатКурсВалюты",
	РезультатПоШапке,
	РезультатПоТабличнойЧасти,
	РезультатКурсВалюты
	);
	
КонецФункции

// Функция получает данные для формирования печатной формы ИНВ19
//
Функция ПолучитьДанныеДляПечатнойФормыИНВ19(ПараметрыПечати, ДокументОснование) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	ЗапросПредварительныхДанных = Новый Запрос;
	ЗапросПредварительныхДанных.Текст = 
	"ВЫБРАТЬ
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет КАК ПредварительныйРасчет,
	|	РаспоряжениеНаИнвентаризациюТоваров.ИсточникИнформацииОЦенахДляПечати КАК ИсточникИнформацииОЦенахДляПечати 
	|ИЗ
	|	Документ.РаспоряжениеНаИнвентаризациюТоваров КАК РаспоряжениеНаИнвентаризациюТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО (РасчетСебестоимостиТоваровОрганизации.Организация = &Организация)
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен)
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(РаспоряжениеНаИнвентаризациюТоваров.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(РаспоряжениеНаИнвентаризациюТоваров.Дата, МЕСЯЦ))
	|ГДЕ
	|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДатыДокументов.Дата КАК Дата
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПересортицаТоваров.Дата КАК Дата
	|	ИЗ
	|		Документ.ПересортицаТоваров КАК ПересортицаТоваров
	|	ГДЕ
	|		ПересортицаТоваров.Проведен = ИСТИНА
	|		И ПересортицаТоваров.Организация = &Организация
	|		И ПересортицаТоваров.ДокументОснование = &ДокументОснование
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОприходованиеИзлишковТоваров.Дата
	|	ИЗ
	|		Документ.ОприходованиеИзлишковТоваров КАК ОприходованиеИзлишковТоваров
	|	ГДЕ
	|		ОприходованиеИзлишковТоваров.Проведен = ИСТИНА
	|		И ОприходованиеИзлишковТоваров.Организация = &Организация
	|		И ОприходованиеИзлишковТоваров.ДокументОснование = &ДокументОснование
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеНедостачТоваров.Дата
	|	ИЗ
	|		Документ.СписаниеНедостачТоваров КАК СписаниеНедостачТоваров
	|	ГДЕ
	|		СписаниеНедостачТоваров.Проведен = ИСТИНА
	|		И СписаниеНедостачТоваров.Организация = &Организация
	|		И СписаниеНедостачТоваров.ДокументОснование = &ДокументОснование) КАК ДатыДокументов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
	ЗапросПредварительныхДанных.УстановитьПараметр("ДокументОснование", ДокументОснование); 
	ЗапросПредварительныхДанных.УстановитьПараметр("Организация", ПараметрыПечати.Организация);
	
	ПредварительныеДанные = ЗапросПредварительныхДанных.ВыполнитьПакет();
	
	ПредварительныеДанныеРезультат = ПредварительныеДанные[0].Выбрать();
	
	Если НЕ ПредварительныеДанныеРезультат.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МаксДатаСкладскихАктовВыборка = ПредварительныеДанные[1].Выбрать();
	МаксДатаСкладскихАктовВыборка.Следующий();
	МаксДатаСкладскихАктов = МаксДатаСкладскихАктовВыборка.Дата;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДокументОснование", 	ДокументОснование);
	Запрос.УстановитьПараметр("ДатаОстатков", 		КонецДня(МаксДатаСкладскихАктов));
	Запрос.УстановитьПараметр("Организация", 		ПараметрыПечати.Организация);
	
	УчитыватьСебестоимостьТоваровПоВидамЗапасов = ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоВидамЗапасов");
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов", УчитыватьСебестоимостьТоваровПоВидамЗапасов);
	
	// Ответственные лица в печатной форме
	СтруктураОтветственных = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(ПараметрыПечати.Организация, ДокументОснование.Дата);
	Запрос.УстановитьПараметр("ГлавныйБухгалтер", СтруктураОтветственных.ГлавныйБухгалтер);
	
	Если ПредварительныеДанныеРезультат.ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости Тогда
		
		Если ПредварительныеДанныеРезультат.ПредварительныйРасчет = Null Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось получить цены по себестоимости для документа %Документ%: на %Период% не произведен расчет себестоимости.'");
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОснование.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Период%", Формат(НачалоМесяца(МаксДатаСкладскихАктов),"ДЛФ=DD"));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		Если ПредварительныеДанныеРезультат.ПредварительныйРасчет Тогда
			
			ТекстСообщения = НСтр("ru = 'При печати документа %Документ% использовались данные предварительного расчета себестоимости.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОснование.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СписаниеНедостачТоваровВидЗапасов.Номенклатура КАК Номенклатура,
			|	СписаниеНедостачТоваровВидЗапасов.Характеристика КАК Характеристика,
			|	СписаниеНедостачТоваровВидЗапасов.ВидЗапасов КАК ВидЗапасов,
			|	СписаниеНедостачТоваровВидЗапасов.Количество КАК КоличествоНедостачи,
			|	0 КАК КоличествоИзлишков,
			|	""1_Списание"" КАК Операция,
			|	СправочникКлючейАналитики.Ссылка КАК КлючАналитикиУчетаНоменклатуры
			|ПОМЕСТИТЬ КоличествоНоменктарурыПоВидамЗапасов
			|ИЗ
			|	Документ.СписаниеНедостачТоваров.ВидыЗапасов КАК СписаниеНедостачТоваровВидЗапасов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СправочникКлючейАналитики
			|		ПО (СправочникКлючейАналитики.Номенклатура = СписаниеНедостачТоваровВидЗапасов.Номенклатура)
			|			И (СправочникКлючейАналитики.Характеристика = СписаниеНедостачТоваровВидЗапасов.Характеристика)
			|			И (СправочникКлючейАналитики.Склад = ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).Склад)
			|ГДЕ
			|	СписаниеНедостачТоваровВидЗапасов.Ссылка.ДокументОснование = &ДокументОснование
			|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Организация = &Организация
			|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОприходованиеИзлишковТоваровТовары.Номенклатура,
			|	ОприходованиеИзлишковТоваровТовары.Характеристика,
			|	ОприходованиеИзлишковТоваровТовары.ВидЗапасов,
			|	0  КАК КоличествоНедостачи,
			|	ОприходованиеИзлишковТоваровТовары.Количество КАК КоличествоИзлишков,
			|	""2_Оприходование"",
			|	СправочникКлючейАналитики.Ссылка
			|ИЗ
			|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СправочникКлючейАналитики
			|		ПО (СправочникКлючейАналитики.Номенклатура = ОприходованиеИзлишковТоваровТовары.Номенклатура)
			|			И (СправочникКлючейАналитики.Характеристика = ОприходованиеИзлишковТоваровТовары.Характеристика)
			|			И (СправочникКлючейАналитики.Склад = ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).Склад)
			|ГДЕ
			|	ОприходованиеИзлишковТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПересортицаТоваровТовары.Номенклатура,
			|	ПересортицаТоваровТовары.Характеристика,
			|	ПересортицаТоваровТовары.ВидЗапасов,
			|	ПересортицаТоваровТовары.Количество КАК КоличествоНедостачи,
			|	0 КАК КоличествоИзлишков,
			|	""3_ПересортицаСписание"",
			|	СправочникКлючейАналитики.Ссылка
			|ИЗ
			|	Документ.ПересортицаТоваров.ВидыЗапасов КАК ПересортицаТоваровТовары 	 
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СправочникКлючейАналитики
			|		ПО (СправочникКлючейАналитики.Номенклатура = ПересортицаТоваровТовары.Номенклатура)
			|			И (СправочникКлючейАналитики.Характеристика = ПересортицаТоваровТовары.Характеристика)
			|			И (СправочникКлючейАналитики.Склад = ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).Склад)
			|ГДЕ
			|	ПересортицаТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПересортицаТоваровТовары.НоменклатураОприходование,
			|	ПересортицаТоваровТовары.ХарактеристикаОприходование,
			|	ПересортицаТоваровТовары.ВидЗапасов,
			|	0 КАК КоличествоНедостачи,
			|	ПересортицаТоваровТовары.Количество КАК КоличествоИзлишков,
			|	""4_ПересортицаОприходование"",
			|	СправочникКлючейАналитики.Ссылка
			|ИЗ
			|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СправочникКлючейАналитики
			|		ПО (СправочникКлючейАналитики.Номенклатура = ПересортицаТоваровТовары.НоменклатураОприходование)
			|			И (СправочникКлючейАналитики.Характеристика = ПересортицаТоваровТовары.ХарактеристикаОприходование)
			|			И (СправочникКлючейАналитики.Склад = ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).Склад)
			|ГДЕ
			|	ПересортицаТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПересортицаТоваровВидыЗапасов.Номенклатура КАК Номенклатура,
			|	ПересортицаТоваровВидыЗапасов.Характеристика КАК Характеристика,
			|	ПересортицаТоваровТовары.НоменклатураОприходование КАК СвязаннаяНоменклатура,
			|	ПересортицаТоваровТовары.ХарактеристикаОприходование КАК СвязаннаяХарактеристика,
			|	0 КАК Количество
			|ПОМЕСТИТЬ СвязанныеТовары
			|ИЗ
			|	Документ.ПересортицаТоваров.ВидыЗапасов КАК ПересортицаТоваровВидыЗапасов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|		ПО ПересортицаТоваровВидыЗапасов.Номенклатура = ПересортицаТоваровТовары.Номенклатура
			|			И ПересортицаТоваровВидыЗапасов.Характеристика = ПересортицаТоваровТовары.Характеристика
			|ГДЕ
			|	ПересортицаТоваровВидыЗапасов.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересортицаТоваровВидыЗапасов.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровВидыЗапасов.Ссылка.Проведен
			|	И ПересортицаТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПересортицаТоваровТовары.НоменклатураОприходование,
			|	ПересортицаТоваровТовары.ХарактеристикаОприходование,
			|	ПересортицаТоваровТовары.Номенклатура,
			|	ПересортицаТоваровТовары.Характеристика,
			|	ПересортицаТоваровТовары.Количество
			|ИЗ
			|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|ГДЕ
			|	ПересортицаТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровТовары.Ссылка.Проведен;
			|
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура,
			|	КоличествоНоменктарурыПоВидамЗапасов.Характеристика КАК Характеристика,
			|	ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0) КАК Стоимость,
			|	КоличествоНоменктарурыПоВидамЗапасов.ВидЗапасов КАК ВидЗапасов,
			|	СУММА(ВЫБОР
			|			КОГДА КоличествоНоменктарурыПоВидамЗапасов.КоличествоНедостачи <> 0
			|				ТОГДА КоличествоНоменктарурыПоВидамЗапасов.КоличествоНедостачи
			|			КОГДА КоличествоНоменктарурыПоВидамЗапасов.КоличествоИзлишков <> 0
			|				ТОГДА КоличествоНоменктарурыПоВидамЗапасов.КоличествоИзлишков
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК Количество,
			|	КоличествоНоменктарурыПоВидамЗапасов.Операция КАК Операция,
			|	КоличествоНоменктарурыПоВидамЗапасов.Характеристика.НаименованиеПолное КАК ХарактеристикаПредставление,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура." + КолонкаКодов + " КАК ТоварКод,
			|	ЛОЖЬ КАК ЭтоВозвратнаяТара
			|ИЗ
			|	КоличествоНоменктарурыПоВидамЗапасов КАК КоличествоНоменктарурыПоВидамЗапасов
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров.СрезПоследних(
			|				КОНЕЦПЕРИОДА(&ДатаОстатков, ДЕНЬ),
			|				(АналитикаУчетаНоменклатуры, Организация, ВидЗапасов) В
			|					(ВЫБРАТЬ
			|						Таблица.КлючАналитикиУчетаНоменклатуры,
			|						&Организация,
			|						Таблица.ВидЗапасов
			|					ИЗ
			|						КоличествоНоменктарурыПоВидамЗапасов КАК Таблица)) КАК ЦеныНоменклатуры
			|		ПО КоличествоНоменктарурыПоВидамЗапасов.КлючАналитикиУчетаНоменклатуры = ЦеныНоменклатуры.АналитикаУчетаНоменклатуры
			|			И (КоличествоНоменктарурыПоВидамЗапасов.ВидЗапасов = ЦеныНоменклатуры.ВидЗапасов И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|				ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
			|			И (ЦеныНоменклатуры.Организация = &Организация)
			|
			|СГРУППИРОВАТЬ ПО
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура,
			|	КоличествоНоменктарурыПоВидамЗапасов.Характеристика,
			|	КоличествоНоменктарурыПоВидамЗапасов.Операция,
			|	КоличествоНоменктарурыПоВидамЗапасов.Характеристика.НаименованиеПолное,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.НаименованиеПолное,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.ЕдиницаИзмерения.Код,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.ЕдиницаИзмерения.Наименование,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура." + КолонкаКодов + ",
			|	ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0),
			|	КоличествоНоменктарурыПоВидамЗапасов.ВидЗапасов,
			| 	ЛОЖЬ
			|
			|УПОРЯДОЧИТЬ ПО 
			|	ТоварНаименование,
			|	ХарактеристикаПредставление
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).НаименованиеСокращенное КАК ОрганизацияПредставление,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущийОтветственный КАК Кладовщик,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.Наименование КАК ПодразделениеПредставление,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаНачала КАК ДатаНачала,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаОкончания,
			|	РаспоряжениеНаИнвентаризациюТоваров.Дата КАК ОснованиеДата,
			|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК ОснованиеНомер,
			|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК НомерДокумента,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаДокумента,
			|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка КАК Ссылка,
			|	&ДатаОстатков КАК ДатаСнятияОстатков
			|ИЗ
			|	Документ.РаспоряжениеНаИнвентаризациюТоваров КАК РаспоряжениеНаИнвентаризациюТоваров
			|ГДЕ
			|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка = &ДокументОснование;
			| 
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|  	СвязанныеТовары.Номенклатура КАК Номенклатура,
			|	СвязанныеТовары.Характеристика КАК Характеристика,
			|	СвязанныеТовары.СвязаннаяНоменклатура КАК СвязаннаяНоменклатура,
			|	СвязанныеТовары.СвязаннаяХарактеристика КАК СвязаннаяХарактеристика,
			|	СУММА(СвязанныеТовары.Количество) КАК Количество
			|ИЗ СвязанныеТовары КАК СвязанныеТовары
			|СГРУППИРОВАТЬ ПО 
			|	СвязанныеТовары.Номенклатура,
			|	СвязанныеТовары.Характеристика,
			|	СвязанныеТовары.СвязаннаяНоменклатура,
			|	СвязанныеТовары.СвязаннаяХарактеристика";
			
		Иначе
			// Фактический расчет
			Запрос.Текст = "ВЫБРАТЬ
			|	СписаниеНедостачТоваровВидЗапасов.Номенклатура КАК Номенклатура,
			|	СписаниеНедостачТоваровВидЗапасов.Характеристика КАК Характеристика,
			|	СписаниеНедостачТоваровВидЗапасов.ВидЗапасов КАК ВидЗапасов,
			|	СписаниеНедостачТоваровВидЗапасов.Количество КАК КоличествоНедостачи,
			|	0 КАК КоличествоИзлишков,
			|	""1_Списание"" КАК Операция,
			|	СправочникКлючейАналитики.Ссылка КАК КлючАналитикиУчетаНоменклатуры
			|ПОМЕСТИТЬ КоличествоНоменктарурыПоВидамЗапасов
			|ИЗ
			|	Документ.СписаниеНедостачТоваров.ВидыЗапасов КАК СписаниеНедостачТоваровВидЗапасов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СправочникКлючейАналитики
			|		ПО (СправочникКлючейАналитики.Номенклатура = СписаниеНедостачТоваровВидЗапасов.Номенклатура)
			|			И (СправочникКлючейАналитики.Характеристика = СписаниеНедостачТоваровВидЗапасов.Характеристика)
			|			И (СправочникКлючейАналитики.Склад = ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).Склад)
			|ГДЕ
			|	СписаниеНедостачТоваровВидЗапасов.Ссылка.ДокументОснование = &ДокументОснование
			|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Организация = &Организация
			|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОприходованиеИзлишковТоваровТовары.Номенклатура,
			|	ОприходованиеИзлишковТоваровТовары.Характеристика,
			|	ОприходованиеИзлишковТоваровТовары.ВидЗапасов,
			|	0,
			|	ОприходованиеИзлишковТоваровТовары.Количество,
			|	""2_Оприходование"",
			|	СправочникКлючейАналитики.Ссылка
			|ИЗ
			|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СправочникКлючейАналитики
			|		ПО (СправочникКлючейАналитики.Номенклатура = ОприходованиеИзлишковТоваровТовары.Номенклатура)
			|			И (СправочникКлючейАналитики.Характеристика = ОприходованиеИзлишковТоваровТовары.Характеристика)
			|			И (СправочникКлючейАналитики.Склад = ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).Склад)
			|ГДЕ
			|	ОприходованиеИзлишковТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПересортицаТоваровТовары.Номенклатура,
			|	ПересортицаТоваровТовары.Характеристика,
			|	ПересортицаТоваровТовары.ВидЗапасов,
			|	ПересортицаТоваровТовары.Количество,
			|	0,
			|	""3_ПересортицаСписание"",
			|	СправочникКлючейАналитики.Ссылка
			|ИЗ
			|	Документ.ПересортицаТоваров.ВидыЗапасов КАК ПересортицаТоваровТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СправочникКлючейАналитики
			|		ПО (СправочникКлючейАналитики.Номенклатура = ПересортицаТоваровТовары.Номенклатура)
			|			И (СправочникКлючейАналитики.Характеристика = ПересортицаТоваровТовары.Характеристика)
			|			И (СправочникКлючейАналитики.Склад = ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).Склад)
			|ГДЕ
			|	ПересортицаТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПересортицаТоваровТовары.НоменклатураОприходование,
			|	ПересортицаТоваровТовары.ХарактеристикаОприходование,
			|	ПересортицаТоваровТовары.ВидЗапасов,
			|	0,
			|	ПересортицаТоваровТовары.Количество,
			|	""4_ПересортицаОприходование"",
			|	СправочникКлючейАналитики.Ссылка
			|ИЗ
			|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СправочникКлючейАналитики
			|		ПО (СправочникКлючейАналитики.Номенклатура = ПересортицаТоваровТовары.НоменклатураОприходование)
			|			И (СправочникКлючейАналитики.Характеристика = ПересортицаТоваровТовары.ХарактеристикаОприходование)
			|			И (СправочникКлючейАналитики.Склад = ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).Склад)
			|ГДЕ
			|	ПересортицаТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПересортицаТоваровВидыЗапасов.Номенклатура КАК Номенклатура,
			|	ПересортицаТоваровВидыЗапасов.Характеристика КАК Характеристика,
			|	ПересортицаТоваровТовары.НоменклатураОприходование КАК СвязаннаяНоменклатура,
			|	ПересортицаТоваровТовары.ХарактеристикаОприходование КАК СвязаннаяХарактеристика,
			|	0 КАК Количество
			|ПОМЕСТИТЬ СвязанныеТовары
			|ИЗ
			|	Документ.ПересортицаТоваров.ВидыЗапасов КАК ПересортицаТоваровВидыЗапасов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|		ПО ПересортицаТоваровВидыЗапасов.Номенклатура = ПересортицаТоваровТовары.Номенклатура
			|			И ПересортицаТоваровВидыЗапасов.Характеристика = ПересортицаТоваровТовары.Характеристика
			|ГДЕ
			|	ПересортицаТоваровВидыЗапасов.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересортицаТоваровВидыЗапасов.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровВидыЗапасов.Ссылка.Проведен
			|	И ПересортицаТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПересортицаТоваровТовары.НоменклатураОприходование,
			|	ПересортицаТоваровТовары.ХарактеристикаОприходование,
			|	ПересортицаТоваровТовары.Номенклатура,
			|	ПересортицаТоваровТовары.Характеристика,
			|	ПересортицаТоваровТовары.Количество
			|ИЗ
			|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|ГДЕ
			|	ПересортицаТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура,
			|	КоличествоНоменктарурыПоВидамЗапасов.Характеристика КАК Характеристика,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СебестоимостьТоваров.КоличествоОстаток, 0) = 0
			|			ТОГДА 0
			|		ИНАЧЕ ЕСТЬNULL(СебестоимостьТоваров.СтоимостьРеглОстаток, 0) / СебестоимостьТоваров.КоличествоОстаток
			|	КОНЕЦ КАК Стоимость,
			|	СебестоимостьТоваров.КоличествоОстаток,
			|	КоличествоНоменктарурыПоВидамЗапасов.ВидЗапасов КАК ВидЗапасов,
			|	СУММА(ВЫБОР
			|			КОГДА КоличествоНоменктарурыПоВидамЗапасов.КоличествоНедостачи <> 0
			|				ТОГДА КоличествоНоменктарурыПоВидамЗапасов.КоличествоНедостачи
			|			КОГДА КоличествоНоменктарурыПоВидамЗапасов.КоличествоИзлишков <> 0
			|				ТОГДА КоличествоНоменктарурыПоВидамЗапасов.КоличествоИзлишков
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК Количество,
			|	КоличествоНоменктарурыПоВидамЗапасов.Операция КАК Операция,
			|	КоличествоНоменктарурыПоВидамЗапасов.Характеристика.НаименованиеПолное КАК ХарактеристикаПредставление,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура." + КолонкаКодов + " КАК ТоварКод,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
			|	ЛОЖЬ КАК ЭтоВозвратнаяТара
			|ИЗ
			|	КоличествоНоменктарурыПоВидамЗапасов КАК КоличествоНоменктарурыПоВидамЗапасов
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров.Остатки(КОНЕЦПЕРИОДА(&ДатаОстатков, МЕСЯЦ), Организация = &Организация) КАК СебестоимостьТоваров
			|		ПО КоличествоНоменктарурыПоВидамЗапасов.КлючАналитикиУчетаНоменклатуры = СебестоимостьТоваров.АналитикаУчетаНоменклатуры
			|			И (КоличествоНоменктарурыПоВидамЗапасов.ВидЗапасов = СебестоимостьТоваров.ВидЗапасов И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|				ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
			|			И (СебестоимостьТоваров.Организация = &Организация)
			|СГРУППИРОВАТЬ ПО
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура,
			|	КоличествоНоменктарурыПоВидамЗапасов.Характеристика,
			|	КоличествоНоменктарурыПоВидамЗапасов.Операция,
			|	КоличествоНоменктарурыПоВидамЗапасов.Характеристика.НаименованиеПолное,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.НаименованиеПолное,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.ЕдиницаИзмерения.Код,
			|	КоличествоНоменктарурыПоВидамЗапасов.Номенклатура.ЕдиницаИзмерения.Наименование,
			|	СебестоимостьТоваров.КоличествоОстаток,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СебестоимостьТоваров.КоличествоОстаток, 0) = 0
			|			ТОГДА 0
			|		ИНАЧЕ ЕСТЬNULL(СебестоимостьТоваров.СтоимостьРеглОстаток, 0) / СебестоимостьТоваров.КоличествоОстаток
			|	КОНЕЦ,
			|	КоличествоНоменктарурыПоВидамЗапасов.ВидЗапасов
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТоварНаименование,
			|	ХарактеристикаПредставление
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).НаименованиеСокращенное КАК ОрганизацияПредставление,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущийОтветственный КАК Кладовщик,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
			|	РаспоряжениеНаИнвентаризациюТоваров.Склад.Наименование КАК ПодразделениеПредставление,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаНачала КАК ДатаНачала,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаОкончания,
			|	РаспоряжениеНаИнвентаризациюТоваров.Дата КАК ОснованиеДата,
			|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК ОснованиеНомер,
			|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК НомерДокумента,
			|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаДокумента,
			|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка КАК Ссылка,
			|	&ДатаОстатков КАК ДатаСнятияОстатков
			|ИЗ
			|	Документ.РаспоряжениеНаИнвентаризациюТоваров КАК РаспоряжениеНаИнвентаризациюТоваров
			|ГДЕ
			|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка = &ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СвязанныеТовары.Номенклатура КАК Номенклатура,
			|	СвязанныеТовары.Характеристика КАК Характеристика,
			|	СвязанныеТовары.СвязаннаяНоменклатура КАК СвязаннаяНоменклатура,
			|	СвязанныеТовары.СвязаннаяХарактеристика КАК СвязаннаяХарактеристика,
			|	СУММА(СвязанныеТовары.Количество) КАК Количество
			|ИЗ
			|	СвязанныеТовары КАК СвязанныеТовары
			|
			|СГРУППИРОВАТЬ ПО
			|	СвязанныеТовары.Номенклатура,
			|	СвязанныеТовары.Характеристика,
			|	СвязанныеТовары.СвязаннаяНоменклатура,
			|	СвязанныеТовары.СвязаннаяХарактеристика";
			
		КонецЕсли;
		
		Результаты = Запрос.ВыполнитьПакет();
		РезультатПоШапке = Результаты[3];
		РезультатПоТабличнойЧасти = Результаты[2];
		РезультатКурсВалюты = Неопределено;
		РезультатСвязанныеТовары = Результаты[4];
		
	ИначеЕсли ПредварительныеДанныеРезультат.ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен Тогда 
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СписаниеНедостачТоваровВидЗапасов.Номенклатура КАК Номенклатура,
		|	СписаниеНедостачТоваровВидЗапасов.Характеристика КАК Характеристика,
		|	СписаниеНедостачТоваровВидЗапасов.Количество КАК КоличествоНедостачи,
		|	0 КАК КоличествоИзлишков,
		|	""1_Списание"" КАК Операция,
		|	NULL КАК СвязаннаяНоменклатура,
		|	NULL КАК СвязаннаяХарактеристика
		|ПОМЕСТИТЬ ВложенныйЗапрос
		|ИЗ
		|	Документ.СписаниеНедостачТоваров.Товары КАК СписаниеНедостачТоваровВидЗапасов
		|ГДЕ
		|	СписаниеНедостачТоваровВидЗапасов.Ссылка.ДокументОснование = &ДокументОснование
		|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Организация = &Организация
		|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОприходованиеИзлишковТоваровТовары.Номенклатура,
		|	ОприходованиеИзлишковТоваровТовары.Характеристика,
		|	0,
		|	ОприходованиеИзлишковТоваровТовары.Количество,
		|	""2_Оприходование"",
		|	NULL,
		|	NULL
		|ИЗ
		|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
		|ГДЕ
		|	ОприходованиеИзлишковТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
		|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
		|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПересортицаТоваровТовары.Номенклатура,
		|	ПересортицаТоваровТовары.Характеристика,
		|	ПересортицаТоваровТовары.Количество,
		|	0,
		|	""3_ПересортицаСписание"",
		|	ПересортицаТоваровТовары.НоменклатураОприходование,
		|	ПересортицаТоваровТовары.ХарактеристикаОприходование
		|ИЗ
		|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
		|ГДЕ
		|	ПересортицаТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
		|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
		|	И ПересортицаТоваровТовары.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПересортицаТоваровТовары.НоменклатураОприходование,
		|	ПересортицаТоваровТовары.ХарактеристикаОприходование,
		|	0,
		|	ПересортицаТоваровТовары.Количество,
		|	""4_ПересортицаОприходование"",
		|	ПересортицаТоваровТовары.Номенклатура,
		|	ПересортицаТоваровТовары.Характеристика
		|ИЗ
		|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
		|ГДЕ
		|	ПересортицаТоваровТовары.Ссылка.ДокументОснование = &ДокументОснование
		|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
		|	И ПересортицаТоваровТовары.Ссылка.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	СУММА(ВложенныйЗапрос.КоличествоНедостачи) КАК КоличествоНедостачи,
		|	СУММА(ВложенныйЗапрос.КоличествоИзлишков) КАК КоличествоИзлишков,
		|	ВложенныйЗапрос.Операция,
		|	ВложенныйЗапрос.СвязаннаяНоменклатура,
		|	ВложенныйЗапрос.СвязаннаяХарактеристика,
		|	ВЫРАЗИТЬ(&ДокументОснование КАК Документ.РаспоряжениеНаИнвентаризациюТоваров).ВидЦены КАК ВидЦен
		|ПОМЕСТИТЬ ТаблицаТовары
		|ИЗ
		|	ВложенныйЗапрос КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Операция,
		|	ВложенныйЗапрос.СвязаннаяНоменклатура,
		|	ВложенныйЗапрос.СвязаннаяХарактеристика,
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|	ТаблицаТовары.Характеристика.НаименованиеПолное КАК ХарактеристикаПредставление,
		|	ТаблицаТовары.КоличествоНедостачи,
		|	ТаблицаТовары.КоличествоИзлишков,
		|	ТаблицаТовары.КоличествоНедостачи * ВЫБОР
		|		КОГДА НЕ ЦеныНоменклатуры.Упаковка.Коэффициент ЕСТЬ NULL 
		|				И ЦеныНоменклатуры.Упаковка.Коэффициент <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / ЦеныНоменклатуры.Упаковка.Коэффициент
		|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ КАК СуммаНедостачи,
		|	ТаблицаТовары.КоличествоИзлишков * ВЫБОР
		|		КОГДА НЕ ЦеныНоменклатуры.Упаковка.Коэффициент ЕСТЬ NULL 
		|				И ЦеныНоменклатуры.Упаковка.Коэффициент <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / ЦеныНоменклатуры.Упаковка.Коэффициент
		|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ КАК СуммаИзлишков,
		|	ТаблицаТовары.Операция,
		|	ТаблицаТовары.СвязаннаяНоменклатура,
		|	ТаблицаТовары.СвязаннаяХарактеристика,
		|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
		|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
		|	ТаблицаТовары.Номенклатура." + КолонкаКодов + " КАК ТоварКод,
		|	ЛОЖЬ КАК ЭтоВозвратнаяТара
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|				КОНЕЦПЕРИОДА(&ДатаОстатков, ДЕНЬ),
		|				(ВидЦены, Номенклатура, Характеристика) В
		|					(ВЫБРАТЬ
		|						Таблица.ВидЦен,
		|						Таблица.Номенклатура,
		|						Таблица.Характеристика
		|					ИЗ
		|						ТаблицаТовары КАК Таблица)) КАК ЦеныНоменклатуры
		|		ПО ТаблицаТовары.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|			И ТаблицаТовары.Характеристика = ЦеныНоменклатуры.Характеристика
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТоварНаименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).НаименованиеСокращенное КАК ОрганизацияПредставление,
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
		|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущийОтветственный КАК Кладовщик,
		|	РаспоряжениеНаИнвентаризациюТоваров.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
		|	РаспоряжениеНаИнвентаризациюТоваров.Склад.Наименование КАК ПодразделениеПредставление,
		|	РаспоряжениеНаИнвентаризациюТоваров.ДатаНачала КАК ДатаНачала,
		|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаОкончания,
		|	РаспоряжениеНаИнвентаризациюТоваров.Дата КАК ОснованиеДата,
		|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК ОснованиеНомер,
		|	РаспоряжениеНаИнвентаризациюТоваров.Номер КАК НомерДокумента,
		|	РаспоряжениеНаИнвентаризациюТоваров.ДатаОкончания КАК ДатаДокумента,
		|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка КАК Ссылка,
		|	&ДатаОстатков КАК ДатаСнятияОстатков
		|ИЗ
		|	Документ.РаспоряжениеНаИнвентаризациюТоваров КАК РаспоряжениеНаИнвентаризациюТоваров
		|ГДЕ
		|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность КАК КоэффициентПересчета
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(
		|			&ДатаОстатков,
		|			Валюта В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					Таблица.ВидЦен.ВалютаЦены
		|				ИЗ
		|					ТаблицаТовары КАК Таблица)) КАК КурсыВалютСрезПоследних";
		
		Результаты = Запрос.ВыполнитьПакет();
		РезультатПоШапке = Результаты[3];
		РезультатПоТабличнойЧасти = Результаты[2];
		РезультатКурсВалюты = Результаты[4];
		РезультатСвязанныеТовары = Неопределено;
		
	КонецЕсли;
	
	Возврат Новый Структура(
	"РезультатПоШапке, РезультатПоТабличнойЧасти, РезультатКурсВалюты, РезультатСвязанныеТовары",
	РезультатПоШапке,
	РезультатПоТабличнойЧасти,
	РезультатКурсВалюты,
	РезультатСвязанныеТовары
	
	);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы

Процедура ЗаполнитьИсточникИнформацииОЦенахДляПечати() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РаспоряжениеНаИнвентаризациюТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РаспоряжениеНаИнвентаризациюТоваров КАК РаспоряжениеНаИнвентаризациюТоваров
	|ГДЕ
	|	РаспоряжениеНаИнвентаризациюТоваров.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли