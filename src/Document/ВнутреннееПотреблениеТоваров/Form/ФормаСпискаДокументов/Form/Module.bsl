////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияУТ.НастроитьПодключаемоеОборудование(ЭтаФорма);
	
	// Обработчик подсистемы "Внешние обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	ЗаполнитьСписокХозяйственныхОпераций();
	
	СписокРаспоряженияНаОформление.ТекстЗапроса = ТекстЗапросаСписокРаспоряженияНаОформление();
	
	ДатаОтгрузки = ТекущаяДата();
	СтруктураБыстрогоОтбора = Неопределено;
	Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	Если СтруктураБыстрогоОтбора <> Неопределено Тогда
		Если СтруктураБыстрогоОтбора.Свойство("Склад", Склад) Тогда
			СкладПриИзмененииСервер();
		КонецЕсли;
		ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(
			СписокДокументы,
			"Статус",
			Статус,
			СтруктураБыстрогоОтбора
		);
		ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(
			СписокДокументы,
			"ХозяйственнаяОперация",
			ХозяйственнаяОперация,
			СтруктураБыстрогоОтбора
		);
		ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(
			СписокРаспоряженияНаОформление,
			"ХозяйственнаяОперация",
			ХозяйственнаяОперация,
			СтруктураБыстрогоОтбора
		);
		Если НЕ СтруктураБыстрогоОтбора.Свойство("ДатаОтгрузки",ДатаОтгрузки) Тогда
			ДатаОтгрузки = ТекущаяДата();
		КонецЕсли;
	КонецЕсли;
	УстановитьОтборПоДатеОформления(СписокРаспоряженияНаОформление, ПолучитьДатуОформления(ДатаОтгрузки));
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаВнутреннееПотребление")
	 И Метаданные.НайтиПоПолномуИмени("Документ.МаршрутныйЛистПроизводства") = Неопределено Тогда
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	УстановитьТекущуюСтраницу();
	ИспользоватьВнутреннееПотреблениеПоНесколькимЗаказам = ПолучитьФункциональнуюОпцию("ИспользоватьВнутреннееПотреблениеПоНесколькимЗаказам");
	Если НЕ ИспользоватьВнутреннееПотреблениеПоНесколькимЗаказам Тогда
		Элементы.СписокРаспоряженияНаОформление.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МенеджерОборудованияКлиент.ПодключитьОборудованиеПриОткрытииФормы(ЭтаФорма, "СканерШтрихкода");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	МенеджерОборудованияКлиент.ОтключитьОборудованиеПриЗакрытииФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			ОбработатьШтрихкоды(МенеджерОборудованияКлиент.ПреобразоватьДанныеСоСканераВСтруктуру(Параметр));
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	Если ИмяСобытия = "Запись_ВнутреннееПотреблениеТоваров"
			ИЛИ ИмяСобытия = "Запись_ЗаказНаВнутреннееПотребление" Тогда
		Элементы.СписокРаспоряженияНаОформление.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если СтруктураБыстрогоОтбора = Неопределено Тогда
		Склад = Настройки.Получить("Склад");
		СкладПриИзмененииСервер();
		ХозяйственнаяОперация = Настройки.Получить("ХозяйственнаяОперация");
		Статус = Настройки.Получить("Статус");
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			СписокДокументы.Отбор,
			"Статус",
			Статус,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ЗначениеЗаполнено(Статус)
		);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			СписокДокументы.Отбор,
			"ХозяйственнаяОперация",
			ХозяйственнаяОперация,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ЗначениеЗаполнено(ХозяйственнаяОперация)
		);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			СписокРаспоряженияНаОформление.Отбор,
			"ХозяйственнаяОперация",
			ХозяйственнаяОперация,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ЗначениеЗаполнено(ХозяйственнаяОперация)
		);
	Иначе
		СтруктураБыстрогоОтбора.Свойство("Склад", Склад);
		СтруктураБыстрогоОтбора.Свойство("ХозяйственнаяОперация", ХозяйственнаяОперация);
		СтруктураБыстрогоОтбора.Свойство("Статус", Статус);
	КонецЕсли;
	Настройки.Удалить("Склад");
	Настройки.Удалить("ХозяйственнаяОперация");
	Настройки.Удалить("Статус");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	СкладПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		СписокДокументы.Отбор,
		"Статус",
		Статус,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Статус)
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтгрузкиПриИзменении(Элемент)
	
	УстановитьОтборПоДатеОформления(СписокРаспоряженияНаОформление, ПолучитьДатуОформления(ДатаОтгрузки));
	
КонецПроцедуры

&НаКлиенте
Процедура ХозяйственнаяОперацияПриИзменении(Элемент)
	
	ХозяйственнаяОперацияПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда 
		Возврат;
	КонецЕсли;

	СтруктураОснование = Новый Структура("ХозяйственнаяОперация,Склад",
		ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию"), Склад
	);
	СтруктураПараметры = Новый Структура("Основание", СтруктураОснование);
	ОткрытьФорму("Документ.ВнутреннееПотреблениеТоваров.ФормаОбъекта", СтруктураПараметры, Элементы.СписокДокументы);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРаспоряженияНаОформлениеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьЗначение(Элемент.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СоздатьПередачуВЭксплуатацию(Команда)
	
	СоздатьВнутреннееПотреблениеТоваровСОперацией(1);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПередачуВПроизводство(Команда)
	
	СоздатьВнутреннееПотреблениеТоваровСОперацией(2);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПередачуНаПрочиеЦели(Команда)
	
	СоздатьВнутреннееПотреблениеТоваровСОперацией(3);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВнутреннееПотреблениеТоваровПоРаспоряжению(Команда)
	
	Если Элементы.СписокРаспоряженияНаОформление.ТекущиеДанные = Неопределено Тогда
		
		ТекстПредупреждения = НСтр("ru = 'Команда не может быть выполнена для указанного объекта!'");
		Предупреждение(ТекстПредупреждения);
		Возврат;
		
	КонецЕсли;
	
	ВыделенныеСтроки = Элементы.СписокРаспоряженияНаОформление.ВыделенныеСтроки;
	
	КоличествоВыделенныхСтрок = ВыделенныеСтроки.Количество();
	
	МассивСсылок = Новый Массив;
	
	Если КоличествоВыделенныхСтрок = 1 ИЛИ НЕ ИспользоватьВнутреннееПотреблениеПоНесколькимЗаказам Тогда
		
		ДанныеСтроки = Элементы.СписокРаспоряженияНаОформление.ТекущиеДанные;
		ТипДокументаОснования = ДанныеСтроки.ДокументТип;
		МассивСсылок.Добавить(ДанныеСтроки.Ссылка);
		
	Иначе
		
		ПредыдущийТипДокументаОснования = Неопределено;
		Для Каждого НомерСтроки Из ВыделенныеСтроки Цикл
			
			ДанныеСтроки = Элементы.СписокРаспоряженияНаОформление.ДанныеСтроки(НомерСтроки);
			
			ТипДокументаОснования = ДанныеСтроки.ДокументТип;
			
			Если ПредыдущийТипДокументаОснования <> Неопределено
				И ТипДокументаОснования <> ПредыдущийТипДокументаОснования Тогда
				
				ТекстПредупреждения = НСтр("ru = 'Оформление одного документа на основании различных типов распоряжений не предусмотрено.'");
				Предупреждение(ТекстПредупреждения);
				Возврат;
				
			КонецЕсли;
			ПредыдущийТипДокументаОснования = ТипДокументаОснования;
			
			МассивСсылок.Добавить(ДанныеСтроки.Ссылка);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Получим данные для заполнения
	ИмяМенеджераДокументаОснования = "ВнутреннееПотреблениеТоваров";
	СписокДокументов = МассивСсылок;
	
	ОчиститьСообщения();
	РеквизитыШапки = Новый Структура;
	Если НЕ ДанныеЗаполненияДокументаДвиженияТоваров(ИмяМенеджераДокументаОснования, СписокДокументов, РеквизитыШапки) Тогда
		Возврат;
	КонецЕсли;
	
	// Откроем форму
	ПараметрыОснования = Новый Структура;
	ПараметрыОснования.Вставить("РеквизитыШапки",    РеквизитыШапки);
	ПараметрыОснования.Вставить("ДокументОснование", МассивСсылок);
	ПараметрыОснования.Вставить("ДатаОтгрузки",      ДатаОтгрузки);
	
	ОткрытьФорму("Документ.ВнутреннееПотреблениеТоваров.Форма.ФормаДокумента", Новый Структура("Основание", ПараметрыОснования));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусКОтгрузке(Команда)
	
	УстановитьСтатус("КОтгрузке", НСтр("ru='К отгрузке'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусОтгружено(Команда)
	
	УстановитьСтатус("Отгружено", НСтр("ru='Отгружено'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРаспоряжение(Команда)

	Если Элементы.СписокРаспоряженияНаОформление.ТекущиеДанные <> Неопределено Тогда
		ОткрытьЗначение(Элементы.СписокРаспоряженияНаОформление.ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Штрихкоды и торговое оборудование

&НаКлиенте
Функция СсылкаНаЭлементСпискаПоШтрихкоду(Штрихкод)
	
	Менеджеры = Новый Массив();
	Менеджеры.Добавить(ПредопределенноеЗначение("Документ.ПеремещениеТоваров.ПустаяСсылка"));
	Менеджеры.Добавить(ПредопределенноеЗначение("Документ.ЗаказНаПеремещение.ПустаяСсылка"));
	Возврат ШтрихкодированиеПечатныхФормКлиент.ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод, Менеджеры);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьШтрихкоды(Данные)
	
	МассивСсылок = СсылкаНаЭлементСпискаПоШтрихкоду(Данные.Штрихкод);
	Если МассивСсылок.Количество() > 0 Тогда
		
		Ссылка = МассивСсылок[0];
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
			Элементы.СписокДокументыПоступления.ТекущаяСтрока = Ссылка;
			Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.СтраницаПеремещенияТоваров;
		ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаказНаПеремещение") Тогда
			Элементы.СписокРаспоряженияНаОформление.ТекущаяСтрока = Ссылка;
			Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.СтраницаРаспоряженияНаОформление;
		КонецЕсли;
		
		ОткрытьЗначение(Ссылка);
		
	Иначе
		ШтрихкодированиеПечатныхФормКлиент.ОбъектНеНайден(Данные.Штрихкод);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ЗаполнитьСписокХозяйственныхОпераций()
	
	СписокХозяйственныхОпераций.Очистить();
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию);
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПередачаВЭксплуатацию);
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПередачаВПроизводство);
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПередачаНаПрочиеЦели);
	
КонецПроцедуры

&НаСервере
Функция ДанныеЗаполненияДокументаДвиженияТоваров(ИмяМенеджераДокументаОснования, Знач МассивСсылок, РеквизитыШапки)
	
	Если ИмяМенеджераДокументаОснования = "ВнутреннееПотреблениеТоваров" Тогда
		Возврат Документы.ЗаказНаВнутреннееПотребление.ДанныеЗаполненияВнутреннегоПотребления(МассивСсылок, РеквизитыШапки);
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьТекущуюСтраницу()
	
	ИмяТекущейСтраницы = "";
	
	Если Параметры.Свойство("ИмяТекущейСтраницы", ИмяТекущейСтраницы) Тогда
		Если ЗначениеЗаполнено(ИмяТекущейСтраницы) Тогда
			ТекущийЭлемент = Элементы[ИмяТекущейСтраницы];
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатус(СтатусСтрока, ПредставлениеСтатуса = Неопределено)
	
	ПредставлениеСтатуса = ?(ПредставлениеСтатуса = Неопределено, СтатусСтрока, ПредставлениеСтатуса);
	ВыделенныеСтроки = РаботаСДиалогамиКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.СписокДокументы);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru='У выделенных в списке документов будет установлен статус ""%ПредставлениеСтатуса%"". Продолжить?'");
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ПредставлениеСтатуса%", ПредставлениеСтатуса);
	Режим = Новый СписокЗначений;
	Режим.Добавить(КодВозвратаДиалога.Да,НСтр("ru='Установить'"));
	Режим.Добавить(КодВозвратаДиалога.Нет,НСтр("ru='Не устанавливать'"));
	Ответ = Вопрос(ТекстВопроса,Режим);
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоОбработанных = ОбщегоНазначенияУТВызовСервера.УстановитьСтатусДокументов(ВыделенныеСтроки, СтатусСтрока);
	ОповеститьПользователяОбУстановкеСтатуса(КоличествоОбработанных, ВыделенныеСтроки.Количество(), ПредставлениеСтатуса);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьПользователяОбУстановкеСтатуса(КоличествоОбработанных, КоличествоВсего, Статус)
	
	Если КоличествоОбработанных > 0 Тогда
		
		Элементы.СписокДокументы.Обновить();
		
		ТекстСообщения = НСтр("ru='Для %КоличествоОбработанных% из %КоличествоВсего% выделенных в списке документов внутренних потреблений товаров установлен статус ""%Статус%""'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КоличествоОбработанных%", КоличествоОбработанных);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КоличествоВсего%",        КоличествоВсего);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Статус%",                 Статус);
		ТекстЗаголовка = НСтр("ru='Статус ""%Статус%"" установлен'");
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%Статус%", Статус);
		ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
		
	Иначе
		
		ТекстСообщения = НСтр("ru='Статус ""%Статус%"" не установлен ни для одного документа внутреннего потребления товаров.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Статус%", Статус);
		ТекстЗаголовка = НСтр("ru='Статус ""%Статус%"" не установлен'");
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%Статус%", Статус);
		ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоДатеОформления(СписокДокументов, ЗначениеПараметра)

	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокДокументов,
		"ДатаОтгрузки",
		ЗначениеПараметра
	);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьДатуОформления(Дата)

	Если Не ЗначениеЗаполнено(Дата) Тогда

		Результат = '00010101';

	ИначеЕсли Дата < НачалоДня(ТекущаяДата()) Тогда

		Дата = КонецДня(ТекущаяДата());
		Результат = Дата + 1;

	Иначе

		Результат = КонецДня(Дата) + 1;

	КонецЕсли;

	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура СоздатьВнутреннееПотреблениеТоваровСОперацией(ХозяйственнаяОперацияИндекс)
	
	СтруктураОснование = Новый Структура("ХозяйственнаяОперация,Склад",
		СписокХозяйственныхОпераций[ХозяйственнаяОперацияИндекс].Значение, Склад);
		
	СтруктураПараметры = Новый Структура("Основание", СтруктураОснование);
	ОткрытьФорму("Документ.ВнутреннееПотреблениеТоваров.ФормаОбъекта", СтруктураПараметры, Элементы.СписокДокументы);
	
КонецПроцедуры

&НаСервере
Процедура СкладПриИзмененииСервер()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		СписокДокументы.Отбор,
		"Склад",
		Склад,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Склад)
	);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		СписокРаспоряженияНаОформление.Отбор,
		"Склад",
		Склад,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Склад)
	);
	
КонецПроцедуры

&НаСервере
Процедура ХозяйственнаяОперацияПриИзмененииСервер()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		СписокДокументы.Отбор,
		"ХозяйственнаяОперация",
		ХозяйственнаяОперация,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(ХозяйственнаяОперация)
	);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		СписокРаспоряженияНаОформление.Отбор,
		"ХозяйственнаяОперация",
		ХозяйственнаяОперация,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(ХозяйственнаяОперация)
	);
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаСписокРаспоряженияНаОформление()

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыВнутр.ЗаказНаВнутреннееПотребление КАК Ссылка,
	|	ЗаказыВнутр.ЗаказНаВнутреннееПотребление.Организация КАК Организация,
	|	ТИПЗНАЧЕНИЯ(ЗаказыВнутр.ЗаказНаВнутреннееПотребление) КАК ДокументТип,
	|	ЗаказыВнутр.Склад КАК Склад,
	|	ЗаказыВнутр.ЗаказНаВнутреннееПотребление.Подразделение КАК Подразделение,
	|	ЗаказыВнутр.ЗаказНаВнутреннееПотребление.Дата КАК Дата,
	|	ЗаказыВнутр.ЗаказНаВнутреннееПотребление.Номер КАК Номер,
	|			ВЫРАЗИТЬ(ЗаказыВнутр.ЗаказНаВнутреннееПотребление КАК Документ.ЗаказНаВнутреннееПотребление).ХозяйственнаяОперация
	|	КАК ХозяйственнаяОперация
	|ИЗ
	|	РегистрНакопления.ЗаказыНаВнутреннееПотребление.Остатки(&ДатаОтгрузки, {(ЗаказНаВнутреннееПотребление) КАК Распоряжение}) КАК ЗаказыВнутр
	|ГДЕ
	|	ЗаказыВнутр.КОформлениюОстаток > 0";
	
    Возврат ТекстЗапроса;
	
КонецФункции
 
