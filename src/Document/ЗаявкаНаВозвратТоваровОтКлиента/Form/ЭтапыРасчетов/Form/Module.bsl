////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры);
	Объект.ЭтапыГрафикаОплаты.Очистить();
	ИдентификаторВызывающейФормы = Параметры.УникальныйИдентификатор;
	ЗаполнитьЭтапыОплатыИзВременногоХранилищаСервер(Параметры.АдресВоВременномХранилище);
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	ТолькоПросмотр = Параметры.ТолькоПросмотр;
	
	Заголовок = ТекстЗаголовкаФормы(Объект.ГрафикОплаты, Объект.ЭтапыГрафикаОплаты.Количество());
	
	Если ТолькоПросмотр Тогда
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЭтапыГрафикаОплаты", "ТолькоПросмотр", Истина);
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЭтапыГрафикаОплатыЗаполнитьЭтапыГрафикаОплаты", "Доступность", Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Модифицированность И Не Готово Тогда
		
		СписокКнопок = Новый СписокЗначений();
		СписокКнопок.Добавить("Закрыть", НСтр("ru = 'Закрыть'"));
		СписокКнопок.Добавить("НеЗакрывать", НСтр("ru = 'Не закрывать'"));
		
		ОтветНаВопрос = Вопрос(НСтр("ru = 'Все измененные данные будут потеряны. Закрыть форму?'"), СписокКнопок);
		
		Если ОтветНаВопрос = "НеЗакрывать" Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНД

&НаКлиенте
Процедура ЗаполнитьЭтапыГрафикаОплаты()
	
	СуммаЭтаповОплаты = Объект.ЭтапыГрафикаОплаты.Итог("СуммаПлатежа");
	
	ГрафикСоглашенияЗаполнен = ПродажиВызовСервера.ГрафикСоглашенияЗаполнен(Объект.Соглашение);
	ГрафикЗаполнен = ЗначениеЗаполнено(Объект.ГрафикОплаты);
	
	Если Объект.СуммаДокумента = 0 Тогда
		
		Если Объект.ЭтапыГрафикаОплаты.Количество() = 0 Тогда
			
			Предупреждение(НСтр("ru='Сумма заказанных строк нулевая. Заполнение этапов расчетов не требуется.'"));
			Возврат;
			
		КонецЕсли;
		
		Объект.ЭтапыГрафикаОплаты.Очистить();
		ЦенообразованиеКлиент.ОповеститьОНевозможностиЗаполненияЭтаповГрафикаОплаты();
		РассчитатьИтоговыеПоказатели(ЭтаФорма);
		Возврат;
		
	КонецЕсли;
	
	ВариантыОтветов = Новый СписокЗначений;
	ВариантыОтветов.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Перезаполнить таблицу'"));
	ВариантыОтветов.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отменить'"));
	
	ЕстьВопросКПользователю = Истина;
	ТекстВопроса = "";
	ПерезаполнитьЭтапы = Ложь;
	РаспределитьСумму = Ложь;
	
	Если Объект.СуммаДокумента = СуммаЭтаповОплаты Тогда
		
		ТекстВопроса = НСтр("ru='Сумма заказанных строк совпадает с суммой этапов расчетов'") + Символы.ПС +
							НСтр("ru='Перезаполнить этапы расчетов %ИсточникЗаполнения%?'");
		
	ИначеЕсли Объект.ЭтапыГрафикаОплаты.Количество() > 0 Тогда
		
		ВариантыОтветов.Вставить(1, КодВозвратаДиалога.Нет, НСтр("ru='Распределить сумму'"));
		
		ТекстВопроса = НСтр("ru='Таблица этапов оплаты заполнена'")+ Символы.ПС +
							НСтр("ru='Перезаполнить этапы расчетов %ИсточникЗаполнения% или распределить сумму по имеющимся этапам?'");
		
	Иначе
		
		ПерезаполнитьЭтапы = Истина;
		ЕстьВопросКПользователю = Ложь;
		
	КонецЕсли;
	
	Если ЕстьВопросКПользователю Тогда
		
		Если ГрафикСоглашенияЗаполнен Тогда
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ИсточникЗаполнения%", НСтр("ru='по соглашению'"));
		ИначеЕсли ГрафикЗаполнен Тогда
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ИсточникЗаполнения%", НСтр("ru='по графику'"));
		Иначе
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ИсточникЗаполнения%", НСтр("ru='по умолчанию'"));
		КонецЕсли;
		
		ОтветНаВопрос = Вопрос(ТекстВопроса, ВариантыОтветов);
		
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			ПерезаполнитьЭтапы = Истина;
		ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
			РаспределитьСумму = Истина;
		Иначе
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПерезаполнитьЭтапы Тогда
		
		Если ГрафикСоглашенияЗаполнен Тогда
			
			ЗаполнитьЭтапыОплатыПоСоглашениюСервер();
			ЦенообразованиеКлиент.ОповеститьОбОкончанииЗаполненияЭтаповГрафикаОплаты();
			
		ИначеЕсли ГрафикЗаполнен Тогда
			
			ЗаполнитьЭтапыОплатыПоГрафикуСервер();
			ЦенообразованиеКлиент.ОповеститьОбОкончанииЗаполненияЭтаповГрафикаОплаты();
			
		Иначе
			
			Если ЖелаемаяДатаОтгрузкиКорректна() Тогда
				
				ЦенообразованиеКлиент.ДобавитьЭтапОплатыПоУмолчанию(
					Объект.ЭтапыГрафикаОплаты,
					Объект.ЖелаемаяДатаОтгрузки,
					Объект.СуммаДокумента,
					ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки")
				);
				ЦенообразованиеКлиент.ОповеститьОбОкончанииЗаполненияЭтаповГрафикаОплаты();
				
			Иначе
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если РаспределитьСумму Тогда
		
		ЦенообразованиеКлиентСервер.РаспределитьСуммуПоЭтапамГрафикаОплаты(Объект.ЭтапыГрафикаОплаты, Объект.СуммаДокумента);
		ЦенообразованиеКлиент.ОповеститьОбОкончанииЗаполненияЭтаповГрафикаОплаты();
		
	КонецЕсли;
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	Заголовок = ТекстЗаголовкаФормы(Объект.ГрафикОплаты, Объект.ЭтапыГрафикаОплаты.Количество())
	
Конецпроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	
	Если ТолькоПросмотр Тогда
		
		Закрыть();
		
	ИначеЕсли ОплатаКорректна() Тогда
		
		Готово = Истина;
		СтруктураОбъекта = Новый Структура();
		СтруктураОбъекта.Вставить("АдресВоВременномХранилище", ПоместитьВоВременноеХранилищеНаСервере());
		
		Закрыть(СтруктураОбъекта);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ЭТАПЫ ГРАФИКА ОПЛАТЫ

&НаКлиенте
Процедура ЭтапыГрафикаОплатыПослеУдаления(Элемент)
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыПроцентПлатежаПриИзменении(Элемент)

	ЦенообразованиеКлиент.ЭтапыГрафикаОплатыПроцентПлатежаПриИзменении(
		Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные,
		Объект.ЭтапыГрафикаОплаты,
		Объект.СуммаДокумента
	);

КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыСуммаПлатежаПриИзменении(Элемент)

	ЦенообразованиеКлиент.ЭтапыГрафикаОплатыСуммаПлатежаПриИзменении(
		Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные,
		Объект.ЭтапыГрафикаОплаты,
		Объект.СуммаДокумента
	);

КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыПриИзменении(Элемент)
	
	Заголовок = ТекстЗаголовкаФормы(Объект.ГрафикОплаты, Объект.ЭтапыГрафикаОплаты.Количество());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// График оплаты

&НаСервере
Процедура ЗаполнитьЭтапыОплатыПоСоглашениюСервер()
	
	ПродажиСервер.ЗаполнитьЭтапыГрафикаОплатыПоСоглашению(Объект, Объект.СуммаДокумента);
	
КонецПроцедуры

&НаКлиенте
Функция ЖелаемаяДатаОтгрузкиКорректна()
	
	ДатаДокумента = НачалоДня(Объект.Дата);
		
	Если ЗначениеЗаполнено(Объект.ЖелаемаяДатаОтгрузки) И Объект.ЖелаемаяДатаОтгрузки < ДатаДокумента Тогда
			
		ТекстВопроса = НСтр("ru='Желаемая дата отгрузки меньше даты документа. Необходимо ввести корректную желаемую дату.'");
		ОтветНаВопрос = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		
		Если ОтветНаВопрос = КодВозвратаДиалога.Отмена Тогда
			Возврат Ложь;
		КонецЕсли;
			
		ЖелаемаяДата  = ДатаДокумента;
		
		Если Не РаботаСДиалогамиКлиент.ВвестиДатуСКонтролемПустогоЗначения(ЖелаемаяДата, НСтр("ru='Введите желаемую дату отгрузки'"), ЧастиДаты.Дата) Тогда
			Возврат Ложь;
		КонецЕсли;
			
		Если ЖелаемаяДата < ДатаДокумента Тогда
			Предупреждение( НСтр("ru='Желаемая дата отгрузки меньше даты документа. Этапы расчетов не могут быть заполнены.'"));
			Возврат Ложь;
		Иначе
			Объект.ЖелаемаяДатаОтгрузки = ЖелаемаяДата;
			Возврат Истина;
		КонецЕсли;
		
	Иначе
		
		Возврат Истина;
		
	КонецЕсли;
		
КонецФункции

&НаСервере
Функция ОплатаКорректна()
	
	Отказ = Ложь;
	ПродажиСервер.ПроверитьКорректностьЭтаповГрафикаОплаты(Объект, Объект.СуммаДокумента, Истина, Отказ);
	Возврат Не Отказ;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЭтапыОплатыПоГрафикуСервер()
	
	ПродажиСервер.ЗаполнитьЭтапыГрафикаОплаты(Объект, Объект.СуммаДокумента);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭтапыОплатыИзВременногоХранилищаСервер(АдресВоВременномХранилище)
	
	ЭтапыОплаты = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	Для Каждого ТекСтрока Из ЭтапыОплаты Цикл
		НоваяСтрока = Объект.ЭтапыГрафикаОплаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Функция ПоместитьВоВременноеХранилищеНаСервере()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.ЭтапыГрафикаОплаты.Выгрузить(), ИдентификаторВызывающейФормы);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтоговыеПоказатели(Форма)
	
	ПредыдущееЗначениеДаты = Дата(1,1,1);
	Форма.НомерСтрокиПолнойОплаты = 0;
	ПроцентПлатежейОбщий = 0;
	
	СоответствиеВариантовОплаты = Новый Соответствие;
	СоответствиеВариантовОплаты.Вставить(ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.АвансДоОбеспечения"),
		Новый Структура("Сумма, Проценты", "СуммаАвансаДоОбеспечения", "ПроцентАвансаДоОбеспечения")
	);
	СоответствиеВариантовОплаты.Вставить(ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки"),
		Новый Структура("Сумма, Проценты", "СуммаПредоплатыДоОтгрузки", "ПроцентПредоплатыДоОтгрузки")
	);
	СоответствиеВариантовОплаты.Вставить(ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.КредитПослеОтгрузки"),
		Новый Структура("Сумма, Проценты", "СуммаКредитаПослеОтгрузки", "ПроцентКредитаПослеОтгрузки")
	);
	
	Для Каждого ТекСтрока Из Форма.Объект.ЭтапыГрафикаОплаты Цикл
		ПроцентПлатежейОбщий = ПроцентПлатежейОбщий + ТекСтрока.ПроцентПлатежа;
		ТекСтрока.ПроцентЗаполненНеВерно = (ПроцентПлатежейОбщий > 100);
		ТекСтрока.ДатаЗаполненаНеВерно = (ПредыдущееЗначениеДаты > ТекСтрока.ДатаПлатежа);
		ПредыдущееЗначениеДаты = ТекСтрока.ДатаПлатежа;
		Если ПроцентПлатежейОбщий = 100 Тогда
			Форма.НомерСтрокиПолнойОплаты = ТекСтрока.НомерСтроки;
		КонецЕсли;
		ИменаЭлементов = СоответствиеВариантовОплаты[ТекСтрока.ВариантОплаты];
		Если ИменаЭлементов <> Неопределено Тогда
			Форма[ИменаЭлементов.Сумма] = Форма[ИменаЭлементов.Сумма] + ТекСтрока.СуммаПлатежа;
			Форма[ИменаЭлементов.Проценты] = Форма[ИменаЭлементов.Проценты] + ТекСтрока.ПроцентПлатежа;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекстЗаголовкаФормы(ГрафикОплаты, КоличествоЭтаповОплаты)
	
	Если КоличествоЭтаповОплаты > 0 Тогда
		Если ЗначениеЗаполнено(ГрафикОплаты) Тогда
			ТекстЗаголовка = НСтр("ru = 'Этапы расчетов (%КоличествоЭтаповОплаты%) - %ГрафикОплаты%'");
		Иначе
			ТекстЗаголовка = НСтр("ru = 'Этапы расчетов (%КоличествоЭтаповОплаты%)'");
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(ГрафикОплаты) Тогда
			ТекстЗаголовка = НСтр("ru = 'Этапы расчетов - %ГрафикОплаты%'");
		Иначе
			ТекстЗаголовка = НСтр("ru = 'Этапы расчетов'");
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%ГрафикОплаты%", ГрафикОплаты);
	ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%КоличествоЭтаповОплаты%", КоличествоЭтаповОплаты);
	
	Возврат ТекстЗаголовка;
	
КонецФункции