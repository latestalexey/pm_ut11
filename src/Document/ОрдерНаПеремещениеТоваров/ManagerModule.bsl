#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	ИменаРеквизитов = "Склад,ПомещениеПолучатель,ПомещениеОтправитель,Статус";
	
	Возврат ИменаРеквизитов;
КонецФункции

//Возвращает параметры указания серий для товаров, указанных в документе
//
//	Параметры
//			Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий
//	Возвращаемое значение
//			Тип Структура
//				Состав полей задается в функции ОбработкаТабличнойЧастиКлиентСервер.ПараметрыУказанияСерий
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры = 
		ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатурыСклад",Новый Структура("Склад", Объект.Склад));
		
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПеремещениеМеждуПомещениями);
	
	Если (СкладыСервер.ИспользоватьАдресноеХранение(Объект.Склад,Объект.ПомещениеПолучатель)
		Или СкладыСервер.ИспользоватьАдресноеХранение(Объект.Склад,Объект.ПомещениеОтправитель))
		И Объект.Статус <> Перечисления.СтатусыОрдеровНаПеремещение.КОтбору Тогда
			ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	КонецЕсли;
	
	Если Объект.Статус = Перечисления.СтатусыОрдеровНаПеремещение.КОтбору Тогда
		
		ПараметрыУказанияСерий.ПланированиеОтгрузки = Истина;
		ПараметрыУказанияСерий.ПланированиеОтбора = Истина;
		ПараметрыУказанияСерий.ПроверкаОтбора = Ложь;
		ПараметрыУказанияСерий.ФактОтбора = Ложь;
		
	ИначеЕсли Объект.Статус = Перечисления.СтатусыОрдеровНаПеремещение.КПроверке Тогда	
		
		ПараметрыУказанияСерий.ПланированиеОтгрузки = Ложь;
		ПараметрыУказанияСерий.ПланированиеОтбора = Ложь;
		ПараметрыУказанияСерий.ПроверкаОтбора = Истина;
		ПараметрыУказанияСерий.ФактОтбора = Ложь;
			
	ИначеЕсли Объект.Статус = Перечисления.СтатусыОрдеровНаПеремещение.КОтгрузке
		Или Объект.Статус = Перечисления.СтатусыОрдеровНаПеремещение.Принят Тогда
		
		ПараметрыУказанияСерий.ПланированиеОтгрузки = Ложь;
		ПараметрыУказанияСерий.ПланированиеОтбора = Ложь;
		ПараметрыУказанияСерий.ПроверкаОтбора = Ложь;
		ПараметрыУказанияСерий.ФактОтбора = Истина;
		
	КонецЕсли;
	
	ПараметрыУказанияСерий.ЭтоОрдер = Истина;
	ПараметрыУказанияСерий.ИмяПоляПомещение = "ПомещениеОтправитель";
	
	Возврат ПараметрыУказанияСерий;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеШапки.ДатаОтгрузки КАК Период,
	|	ДанныеШапки.Склад КАК Склад,
	|	ДанныеШапки.ПомещениеОтправитель.ИспользоватьАдресноеХранение КАК ИспользоватьАдресноеХранениеОтправитель,
	|	ДанныеШапки.ПомещениеПолучатель.ИспользоватьАдресноеХранение КАК ИспользоватьАдресноеХранениеПолучатель,
	|	ДанныеШапки.ПомещениеОтправитель КАК ПомещениеОтправитель,
	|	ДанныеШапки.ПомещениеПолучатель КАК ПомещениеПолучатель,
	|	ДанныеШапки.ЗонаОтгрузки КАК ЗонаОтгрузки,
	|	ДанныеШапки.ЗонаПриемки КАК ЗонаПриемки,
	|	ДанныеШапки.Статус КАК Статус 
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров КАК ДанныеШапки
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();

	Запрос.УстановитьПараметр("Статус",                                    Реквизиты.Статус);
	Запрос.УстановитьПараметр("Период",                                    Реквизиты.Период);
	Запрос.УстановитьПараметр("Склад",                                     Реквизиты.Склад);
	Запрос.УстановитьПараметр("ПомещениеОтправитель",                      Реквизиты.ПомещениеОтправитель);
	Запрос.УстановитьПараметр("ПомещениеПолучатель",                       Реквизиты.ПомещениеПолучатель);
	Запрос.УстановитьПараметр("ЗонаОтгрузки",                              Реквизиты.ЗонаОтгрузки);
	Запрос.УстановитьПараметр("ЗонаПриемки",                               Реквизиты.ЗонаПриемки);
	Запрос.УстановитьПараметр("ИспользоватьАдресноеХранениеОтправитель",   Реквизиты.ИспользоватьАдресноеХранениеОтправитель);
	Запрос.УстановитьПараметр("ИспользоватьАдресноеХранениеПолучатель",    Реквизиты.ИспользоватьАдресноеХранениеПолучатель);

	Запрос.Текст = "
	// 0 ВтТаблицаТовары
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура                          КАК Номенклатура,
	|	ТаблицаТовары.Характеристика                        КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ТаблицаТовары.Количество                            КАК Количество,
	|	ТаблицаТовары.СтатусУказанияСерий                   КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ВтТаблицаСерииТоваров
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.Количество <> 0
	|	И (НЕ ТаблицаТовары.СтатусУказанияСерий В (4, 6, 8, 10))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.Количество,
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий)
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров.Серии                            КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОрдерНаПеремещениеТоваров.Товары КАК ТаблицаТовары
	|		ПО ТаблицаСерии.Ссылка = ТаблицаТовары.Ссылка
	|			И ТаблицаСерии.Номенклатура   = ТаблицаТовары.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаТовары.Характеристика
	|ГДЕ
	|	ТаблицаСерии.Ссылка       = &Ссылка
	|	И ТаблицаТовары.Ссылка    = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.Количество,
	|	ТаблицаСерии.НомерСтроки
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий) В (4, 6, 8, 10)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 1 ВтТаблицаТоварыЯчейки
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура                          КАК Номенклатура,
	|	ТаблицаТовары.Характеристика                        КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ТаблицаТовары.Упаковка                              КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок                    КАК КоличествоУпаковок,
	|	ТаблицаТовары.СтатусУказанияСерий                   КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ВтТаблицаТоварыЯчейки
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.Количество <> 0
	|	И (НЕ ТаблицаТовары.СтатусУказанияСерий В (4, 6, 8, 10))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.Упаковка,
	|	ТаблицаСерии.КоличествоУпаковок,
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий)
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров.Серии                            КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОрдерНаПеремещениеТоваров.Товары КАК ТаблицаТовары
	|		ПО ТаблицаСерии.Ссылка = ТаблицаТовары.Ссылка
	|			И ТаблицаСерии.Номенклатура   = ТаблицаТовары.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаТовары.Характеристика
	|ГДЕ
	|	ТаблицаСерии.Ссылка       = &Ссылка
	|	И ТаблицаТовары.Ссылка    = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Упаковка,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.КоличествоУпаковок,
	|	ТаблицаСерии.НомерСтроки
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий) В (4, 6, 8, 10)
	|;
	|
	// 2 ТаблицаТоварыНаСкладах
	// Статус "Выполнено" - списываем из отправителя и приходуем в получатель
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Склад                                 КАК Склад,
	|	&ПомещениеОтправитель                  КАК Помещение,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.Количество               КАК ВНаличии,
	|	0                                      КАК КОтгрузке,
	|	ТаблицаТовары.Серия                    КАК Серия,
	|   ЛОЖЬ                                   КАК КонтролироватьОстатки
	|ИЗ
	|	ВтТаблицаСерииТоваров КАК ТаблицаТовары
	|ГДЕ
	|	&Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.Принят)
	|	И ТаблицаТовары.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Склад,
	|	&ПомещениеПолучатель,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Количество,
	|	0,
	|	ТаблицаТовары.Серия,
	|   ЛОЖЬ
	|ИЗ
	|	ВтТаблицаСерииТоваров КАК ТаблицаТовары
	|ГДЕ
	|	&Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.Принят)
	|	И ТаблицаТовары.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Склад,
	|	&ПомещениеОтправитель,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	0,
	|	ТаблицаТовары.Количество,
	|	ТаблицаТовары.Серия,
	|   ИСТИНА
	|ИЗ
	|	ВтТаблицаСерииТоваров КАК ТаблицаТовары
	|ГДЕ
	|	&Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.КОтбору)
	|	И ТаблицаТовары.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Склад,
	|	&ПомещениеОтправитель,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	0,
	|	ТаблицаТовары.Количество,
	|	ТаблицаТовары.Серия,
	|   ЛОЖЬ
	|ИЗ
	|	ВтТаблицаСерииТоваров КАК ТаблицаТовары
	|ГДЕ
	|	(&Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.КОтгрузке)
	|	ИЛИ &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.КПроверке))
	|	И ТаблицаТовары.Количество <> 0
	|;
	|
	// 3 ТаблицаТоварыВСкладскихЯчейках
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.Упаковка                 КАК Упаковка,
	|	&ЗонаПриемки                           КАК Ячейка,
	|	ТаблицаТовары.КоличествоУпаковок       КАК ВНаличии,
	|	0                                      КАК ВРезерве,
	|	ТаблицаТовары.Серия                    КАК Серия
	|ИЗ
	|	ВтТаблицаТоварыЯчейки КАК ТаблицаТовары
	|ГДЕ
	|	&ИспользоватьАдресноеХранениеПолучатель
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.Принят)
	|	И ТаблицаТовары.КоличествоУпаковок <> 0
	|
	|;
	|
	// 4 ТаблицаТоварыКОтбору
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	&Ссылка                                КАК Распоряжение,
	|	0                                      КАК КОтбору,
	|	ТаблицаТовары.Количество               КАК Отобрано,
	|	ТаблицаТовары.Серия                    КАК Серия
	|ИЗ
	|	ВтТаблицаСерииТоваров КАК ТаблицаТовары
	|ГДЕ
	|	&ИспользоватьАдресноеХранениеОтправитель
	|	И (&Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.Принят)
	|			ИЛИ &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.КОтгрузке)
	|			ИЛИ &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.КПроверке))
	|	И ТаблицаТовары.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	&Ссылка,
	|	ТаблицаТовары.Количество,
	|	0,
	|	ТаблицаТовары.Серия
	|ИЗ
	|	ВтТаблицаСерииТоваров КАК ТаблицаТовары
	|ГДЕ
	|	&ИспользоватьАдресноеХранениеОтправитель
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.КОтбору)
	|	И ТаблицаТовары.Количество <> 0
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 5 ТаблицаДвиженияСерийТоваров
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.Количество               КАК Количество,
	|	&Склад КАК Склад,
	|	&ПомещениеОтправитель КАК Помещение,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПеремещениеМеждуПомещениями) КАК СкладскаяОперация,
	|	&Ссылка                               КАК Документ,
	|	&Период                               КАК Период,
	|	&Ссылка                               КАК Регистратор,
	|	ТаблицаСерии.НомерСтроки              КАК НомерСтроки
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров.Серии КАК ТаблицаСерии
	|
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.Принят)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.Количество               КАК Количество,
	|	&Склад КАК Склад,
	|	&ПомещениеОтправитель КАК Помещение,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПеремещениеМеждуПомещениями) КАК СкладскаяОперация,
	|	&Ссылка                               КАК Документ,
	|	&Период                               КАК Период,
	|	&Ссылка                               КАК Регистратор,
	|	ТаблицаСерии.НомерСтроки              КАК НомерСтроки
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров.Серии КАК ТаблицаСерии
	|
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.Принят)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результат = Запрос.ВыполнитьПакет();

	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТоварыНаСкладах",         Результат[2].Выгрузить());
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТоварыВСкладскихЯчейках", Результат[3].Выгрузить());
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТоварыКОтбору",           Результат[4].Выгрузить());
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДвиженияСерийТоваров",    Результат[5].Выгрузить());
КонецПроцедуры // ИнициализироватьДанныеДокумента()

////////////////////////////////////////////////////////////////////////////////
// Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОрдерНаПеремещениеТоваров") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ОрдерНаПеремещениеТоваров",
			"Перемещение между помещениями",
			ПечатьПеремещенияТоваровМеждуПомещениями(МассивОбъектов, ОбъектыПечати)
		);
	КонецЕсли;
КонецПроцедуры

Функция ПечатьПеремещенияТоваровМеждуПомещениями(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РасходныйОрдерНаТовары_РасходныйОрдерНаТовары";
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	ВыводитьКоды = ЗначениеЗаполнено(КолонкаКодов);
	
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	СинонимДокумента = НСтр("ru='Ордер на перемещение товаров'");
	
	ИспользоватьУпаковкиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	
	ЗапросПоШапке = Новый Запрос;
	ЗапросПоШапке.Текст = 
	"ВЫБРАТЬ
	|	ОрдерНаПеремещениеТоваров.Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.Склад) КАК ПредставлениеСклада,
	|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.ПомещениеОтправитель) КАК ПредставлениеПомещенияОтправителя,
	|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.ПомещениеПолучатель) КАК ПредставлениеПомещенияПолучателя,
	|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.ЗонаОтгрузки) КАК ПредставлениеЗоныОтгрузки,
	|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.ЗонаОтгрузки) КАК ПредставлениеЗоныПриемки,
	|	ОрдерНаПеремещениеТоваров.Дата,
	|	ОрдерНаПеремещениеТоваров.Номер,
	|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.Ответственный.ФизическоеЛицо) КАК Ответственный,
	|	ОрдерНаПеремещениеТоваров.Склад КАК Склад,
	|	ОрдерНаПеремещениеТоваров.ПомещениеОтправитель КАК ПомещениеОтправитель,
	|	ОрдерНаПеремещениеТоваров.ПомещениеПолучатель КАК ПомещениеПолучатель,
	|	ОрдерНаПеремещениеТоваров.Склад.ИспользоватьСерииНоменклатуры КАК ИспользоватьСерииНоменклатуры
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров КАК ОрдерНаПеремещениеТоваров
	|ГДЕ
	|	ОрдерНаПеремещениеТоваров.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОрдерНаПеремещениеТоваров.Ссылка
	|
	|ИТОГИ ПО
	|	ИспользоватьСерииНоменклатуры
	|";
	
	ЗапросПоШапке.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ДеревоОбъектов = ЗапросПоШапке.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Для Каждого ИспользованиеСерий Из ДеревоОбъектов.Строки Цикл
						
		Если ИспользованиеСерий.ИспользоватьСерииНоменклатуры Тогда
			
			ТекстЗапросаПоТоварам = 
			"ВЫБРАТЬ
			|	ОрдерНаПеремещениеТоваровТовары.Ссылка КАК Ссылка,
			|	ОрдерНаПеремещениеТоваровТовары.Номенклатура.НаименованиеПолное КАК ПредставлениеНоменклатуры,
			|	" + ?(ВыводитьКоды, "ОрдерНаПеремещениеТоваровТовары.Номенклатура." + КолонкаКодов +" КАК ДопКолонка,", "") + "
			|	ОрдерНаПеремещениеТоваровТовары.Характеристика.НаименованиеПолное КАК ПредставлениеХарактеристики,
			|	"""" КАК ПредставлениеСерии,
			|	ОрдерНаПеремещениеТоваровТовары.КоличествоУпаковок,
			|	ОрдерНаПеремещениеТоваровТовары.Количество,
			|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваровТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
			|	ВЫБОР
			|		КОГДА ОрдерНаПеремещениеТоваровТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
			|			ТОГДА ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваровТовары.Упаковка)
			|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваровТовары.Номенклатура.ЕдиницаИзмерения)
			|	КОНЕЦ КАК ПредставлениеЕдининицыИзмеренияУпаковки
			|ИЗ
			|	Документ.ОрдерНаПеремещениеТоваров.Товары КАК ОрдерНаПеремещениеТоваровТовары
			|ГДЕ
			|	ОрдерНаПеремещениеТоваровТовары.Ссылка В(&МассивОбъектов)
			|	И (НЕ ОрдерНаПеремещениеТоваровТовары.СтатусУказанияСерий В (2, 4, 6, 8, 10))
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОрдерНаПеремещениеТоваровСерии.Ссылка КАК Ссылка,
			|	ОрдерНаПеремещениеТоваровСерии.Номенклатура.НаименованиеПолное КАК ПредставлениеНоменклатуры,
			|	" + ?(ВыводитьКоды, "ОрдерНаПеремещениеТоваровСерии.Номенклатура." + КолонкаКодов +" КАК ДопКолонка,", "") + "
			|	ОрдерНаПеремещениеТоваровСерии.Характеристика.НаименованиеПолное КАК ПредставлениеХарактеристики,
			|	ОрдерНаПеремещениеТоваровСерии.Серия КАК ПредставлениеСерии,
			|	ОрдерНаПеремещениеТоваровСерии.КоличествоУпаковок,
			|	ОрдерНаПеремещениеТоваровСерии.Количество,
			|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваровСерии.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
			|	ВЫБОР
			|		КОГДА ОрдерНаПеремещениеТоваровСерии.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
			|			ТОГДА ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваровСерии.Упаковка)
			|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваровСерии.Номенклатура.ЕдиницаИзмерения)
			|	КОНЕЦ КАК ПредставлениеЕдининицыИзмеренияУпаковки
			|ИЗ
			|	Документ.ОрдерНаПеремещениеТоваров.Серии КАК ОрдерНаПеремещениеТоваровСерии
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОрдерНаПеремещениеТоваров.Товары КАК ТаблицаТовары
			|		ПО ОрдерНаПеремещениеТоваровСерии.Ссылка = ТаблицаТовары.Ссылка
			|			И ОрдерНаПеремещениеТоваровСерии.Номенклатура = ТаблицаТовары.Номенклатура
			|			И ОрдерНаПеремещениеТоваровСерии.Характеристика = ТаблицаТовары.Характеристика
			|			И (ОрдерНаПеремещениеТоваровСерии.Упаковка = ТаблицаТовары.Упаковка
			|				ИЛИ ОрдерНаПеремещениеТоваровСерии.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.КОтбору)
			|				ИЛИ НЕ ВЫБОР КОГДА ОрдерНаПеремещениеТоваровСерии.Ссылка.Склад.ИспользоватьСкладскиеПомещения ТОГДА
			|					ОрдерНаПеремещениеТоваровСерии.Ссылка.ПомещениеПолучатель.ИспользоватьАдресноеХранение
			|					ИЛИ ОрдерНаПеремещениеТоваровСерии.Ссылка.ПомещениеОтправитель.ИспользоватьАдресноеХранение
			|				ИНАЧЕ
			|					ОрдерНаПеремещениеТоваровСерии.Ссылка.Склад.ИспользоватьАдресноеХранение
			|				КОНЕЦ)
			|	
			|ГДЕ
			|	ОрдерНаПеремещениеТоваровСерии.Ссылка В(&МассивОбъектов)
			|
			|СГРУППИРОВАТЬ ПО
			|	ОрдерНаПеремещениеТоваровСерии.Ссылка,
			|	ОрдерНаПеремещениеТоваровСерии.Номенклатура,
			|	ОрдерНаПеремещениеТоваровСерии.Характеристика,
			|	ОрдерНаПеремещениеТоваровСерии.Серия,
			|	ОрдерНаПеремещениеТоваровСерии.Упаковка,
			|	ОрдерНаПеремещениеТоваровСерии.КоличествоУпаковок,
			|	ОрдерНаПеремещениеТоваровСерии.Количество,
			|	ОрдерНаПеремещениеТоваровСерии.НомерСтроки
			|
			|ИМЕЮЩИЕ
			|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий) В (2, 4, 6, 8, 10)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ссылка,
			|	ПредставлениеНоменклатуры,
			|	ПредставлениеХарактеристики,
			|	ПредставлениеСерии
			|ИТОГИ ПО
			|	Ссылка";
			
		Иначе
			
			ТекстЗапросаПоТоварам = 
			"ВЫБРАТЬ
			|	ОрдерНаПеремещениеТоваров.Ссылка КАК Ссылка,
			|	ОрдерНаПеремещениеТоваров.Номенклатура.НаименованиеПолное КАК ПредставлениеНоменклатуры,
			|	" + ?(ВыводитьКоды, "ОрдерНаПеремещениеТоваров.Номенклатура." + КолонкаКодов +" КАК ДопКолонка,", "") + "
			|	ОрдерНаПеремещениеТоваров.Характеристика.НаименованиеПолное КАК ПредставлениеХарактеристики,
			|	"""" КАК ПредставлениеСерии,
			|	ОрдерНаПеремещениеТоваров.КоличествоУпаковок,
			|	ОрдерНаПеремещениеТоваров.Количество,
			|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
			|	ВЫБОР
			|		КОГДА ОрдерНаПеремещениеТоваров.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
			|			ТОГДА ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.Упаковка)
			|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.Номенклатура.ЕдиницаИзмерения)
			|	КОНЕЦ КАК ПредставлениеЕдининицыИзмеренияУпаковки
			|ИЗ
			|	Документ.ОрдерНаПеремещениеТоваров.Товары КАК ОрдерНаПеремещениеТоваров
			|ГДЕ
			|	ОрдерНаПеремещениеТоваров.Ссылка В(&МассивОбъектов)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ссылка,
			|	НомерСтроки
			|ИТОГИ ПО
			|	Ссылка";
			
		КонецЕсли;
	
		ЗапросПоТоварам = Новый Запрос;
		ЗапросПоТоварам.Текст = ТекстЗапросаПоТоварам;
		ЗапросПоТоварам.УстановитьПараметр("МассивОбъектов", ИспользованиеСерий.Строки.ВыгрузитьКолонку("Ссылка"));
		
		ВыборкаПоТабличнымЧастям = ЗапросПоТоварам.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ПервыйДокумент = Истина;
	
		Для Каждого ВыборкаПоДокументам Из ИспользованиеСерий.Строки Цикл
			
			Если НЕ ВыборкаПоТабличнымЧастям.НайтиСледующий(Новый Структура("Ссылка",ВыборкаПоДокументам.Ссылка)) Тогда
				Продолжить;
			КонецЕсли;
			
			ИспользуетсяАдресноеХранениеОтправитель = 
					ПолучитьФункциональнуюОпцию("ИспользоватьАдресноеХранение", Новый Структура("Склад,Помещение",ВыборкаПоДокументам.Склад,ВыборкаПоДокументам.ПомещениеОтправитель));
			ИспользуетсяАдресноеХранениеПолучатель  = 
					ПолучитьФункциональнуюОпцию("ИспользоватьАдресноеХранение", Новый Структура("Склад,Помещение",ВыборкаПоДокументам.Склад,ВыборкаПоДокументам.ПомещениеПолучатель));
			
			ВыводитьУпаковки = ИспользоватьУпаковкиНоменклатуры Или ИспользуетсяАдресноеХранениеОтправитель Или ИспользуетсяАдресноеХранениеПолучатель;
			
			//Макет получаем в цикле,т.к. ширина колонок зависит от склада и помещения в документе
			Макет = УправлениеПечатью.ПолучитьМакет("Документ.ОрдерНаПеремещениеТоваров.ПФ_MXL_ОрдерНаПеремещениеТоваров");
			
			ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
			ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка");
			
			ОбластьШапкаТаблицыНачало 	= Макет.ПолучитьОбласть("ШапкаТаблицы|НачалоСтроки");
			ОбластьСтрокаТаблицыНачало 	= Макет.ПолучитьОбласть("СтрокаТаблицы|НачалоСтроки");
			ОбластьПодвалТаблицыНачало 	= Макет.ПолучитьОбласть("ПодвалТаблицы|НачалоСтроки");
			
			ОбластьШапкаТаблицыКолонкаКодов 	= Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
			ОбластьСтрокаТаблицыКолонкаКодов 	= Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаКодов");
			ОбластьПодвалТаблицыКолонкаКодов 	= Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаКодов");
			
			ОбластьШапкаТаблицыКолонкаКодов.Параметры.ИмяКолонкиКодов = КолонкаКодов; 
			
			ОбластьШапкаТаблицыКолонкаУпаковок 		= Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаУпаковок");
			ОбластьСтрокаТаблицыКолонкаУпаковок 	= Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаУпаковок");
			ОбластьПодвалТаблицыКолонкаУпаковок		= Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаУпаковок");

			ОбластьПодписей = Макет.ПолучитьОбласть("Подписи");
			
			ОбластьКолонкаТоваров = Макет.Область("КолонкаТоваров");
			
			Если НЕ ВыводитьКоды Тогда
				ОбластьКолонкаТоваров.ШиринаКолонки = ОбластьКолонкаТоваров.ШиринаКолонки + Макет.Область("КолонкаКодов").ШиринаКолонки;
			КонецЕсли;
			
			Если НЕ ВыводитьУпаковки Тогда
				ОбластьКолонкаТоваров.ШиринаКолонки = ОбластьКолонкаТоваров.ШиринаКолонки + Макет.Область("КолонкаУпаковокКоличество").ШиринаКолонки
				+ Макет.Область("КолонкаУпаковокПредставление").ШиринаКолонки; 		
			КонецЕсли;
			
			ОбластьШапкаТаблицыКолонкаТоваров 	= Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаТоваров");
			ОбластьСтрокаТаблицыКолонкаТоваров 	= Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаТоваров");
			ОбластьПодвалТаблицыКолонкаТоваров 	= Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаТоваров");
			
			ОбластьШапкаТаблицыКонец 	= Макет.ПолучитьОбласть("ШапкаТаблицы|КонецСтроки");
			ОбластьСтрокаТаблицыКонец 	= Макет.ПолучитьОбласть("СтрокаТаблицы|КонецСтроки");
			ОбластьПодвалТаблицыКонец 	= Макет.ПолучитьОбласть("ПодвалТаблицы|КонецСтроки");
			
			ВыборкаПоСтрокамТЧ = ВыборкаПоТабличнымЧастям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ПервыйДокумент = Ложь;
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
				
			Если ИспользуетсяАдресноеХранениеОтправитель Тогда
				
				ТекстЗоныОтгрузки = НСтр("ru='Зона отгрузки:'");
				ЗонаОтгрузкиПредставление = ВыборкаПоДокументам.ПредставлениеЗоныОтгрузки;
				
			Иначе
				
				ТекстЗоныОтгрузки = "";
				ЗонаОтгрузкиПредставление = "";
				
			КонецЕсли;
			
			Если ИспользуетсяАдресноеХранениеПолучатель Тогда
				
				ТекстЗоныПриемки = НСтр("ru='Зона приемки:'");
				ЗонаПриемкиПредставление = ВыборкаПоДокументам.ПредставлениеЗоныОтгрузки;
				
			Иначе
				
				ТекстЗоныПриемки = "";
				ЗонаПриемкиПредставление = "";
				
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(РеквизитыДокумента,ВыборкаПоДокументам);
			
			ОбластьЗаголовок.Параметры.ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента, СинонимДокумента);
			
			ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьЗаголовок, ВыборкаПоДокументам.Ссылка);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			ОбластьШапка.Параметры.Заполнить(ВыборкаПоДокументам);	
			ОбластьШапка.Параметры.ТекстЗоныПриемки 			= ТекстЗоныПриемки;
			ОбластьШапка.Параметры.ПредставлениеЗоныПриемки 	= ЗонаПриемкиПредставление;
			ОбластьШапка.Параметры.ТекстЗоныОтгрузки 			= ТекстЗоныОтгрузки;
			ОбластьШапка.Параметры.ПредставлениеЗоныОтгрузки 	= ЗонаОтгрузкиПредставление;
			
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			ТабличныйДокумент.Вывести(ОбластьШапкаТаблицыНачало);
			
			Если ВыводитьКоды Тогда
				ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыКолонкаКодов);
			КонецЕсли;
			
			ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыКолонкаТоваров);
			
			Если ВыводитьУпаковки Тогда
				ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыКолонкаУпаковок);
			КонецЕсли;
			
			ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыКонец);

			НомерСтроки = 1;
			
			Пока ВыборкаПоСтрокамТЧ.Следующий() Цикл
				
				ОбластьСтрокаТаблицыНачало.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
				ОбластьСтрокаТаблицыНачало.Параметры.НомерСтроки = НомерСтроки;
				
				ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицыНачало);
				
				Если ВыводитьКоды Тогда
					ОбластьСтрокаТаблицыКолонкаКодов.Параметры.Артикул = ВыборкаПоСтрокамТЧ.ДопКолонка;
					ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыКолонкаКодов);
				КонецЕсли;
				
				ОбластьСтрокаТаблицыКолонкаТоваров.Параметры.Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					ВыборкаПоСтрокамТЧ.ПредставлениеНоменклатуры,
					ВыборкаПоСтрокамТЧ.ПредставлениеХарактеристики,
					, // Упаковка
					ВыборкаПоСтрокамТЧ.ПредставлениеСерии
				);
				
				ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыКолонкаТоваров);
				
				Если ВыводитьУпаковки Тогда
					ОбластьСтрокаТаблицыКолонкаУпаковок.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
					ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыКолонкаУпаковок);
				КонецЕсли;
				
				ОбластьСтрокаТаблицыКонец.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
				ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыКонец);	
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла;
			
			ТабличныйДокумент.Вывести(ОбластьПодвалТаблицыНачало);
			Если ВыводитьКоды Тогда
				ТабличныйДокумент.Присоединить(ОбластьПодвалТаблицыКолонкаКодов);
			КонецЕсли;
			ТабличныйДокумент.Присоединить(ОбластьПодвалТаблицыКолонкаТоваров);
			Если ВыводитьУпаковки Тогда
				ТабличныйДокумент.Присоединить(ОбластьПодвалТаблицыКолонкаУпаковок);
			КонецЕсли;
			ТабличныйДокумент.Присоединить(ОбластьПодвалТаблицыКонец);
			
			ОбластьПодписей.Параметры.ПредставлениеОтветственного = ФизическиеЛица.ФамилияИнициалыФизЛица(ВыборкаПоДокументам.Ответственный);
			
			ТабличныйДокумент.Вывести(ОбластьПодписей);
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоДокументам.Ссылка);
		КонецЦикла;
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;	
КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыОтборРазмещениеТоваров(ПараметрыПечати, МассивДокументов) Экспорт
	
	ВидПомещения = ?(ПараметрыПечати.ИмяФормы = "ЗаданиеНаРазмещение" , "Получатель", "Отправитель");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОрдерНаПеремещениеТоваров.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.Ссылка) КАК СсылкаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.Склад) КАК СкладПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ОрдерНаПеремещениеТоваров.Помещение" 	+ ВидПомещения + ") КАК ПомещениеПредставление,
	|	ОрдерНаПеремещениеТоваров.Дата КАК Дата,
	|	ОрдерНаПеремещениеТоваров.Номер КАК Номер,
	|	ЛОЖЬ КАК НарушенаОрдернаяСхема,
	|	ЛОЖЬ КАК СкладыВТЧ,
	|	ВЫБОР
	|		КОГДА ОрдерНаПеремещениеТоваров.Склад.ИспользоватьСкладскиеПомещения
	|			ТОГДА ОрдерНаПеремещениеТоваров.Помещение" 	+ ВидПомещения + ".ИспользоватьАдресноеХранениеСправочно
	|		ИНАЧЕ ОрдерНаПеремещениеТоваров.Склад.ИспользоватьАдресноеХранениеСправочно
	|	КОНЕЦ КАК ИспользуетсяСправочноеХранение,
	|	NULL КАК ВидОперации,
	|	ОрдерНаПеремещениеТоваров.Склад.ИспользоватьСерииНоменклатуры КАК ИспользоватьСерииНоменклатуры
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров КАК ОрдерНаПеремещениеТоваров
	|ГДЕ
	|	ОрдерНаПеремещениеТоваров.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	NULL КАК Серия,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|		ТОГДА
	|			ТаблицаТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ
	|			ТаблицаТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество КАК Количество
	|ПОМЕСТИТЬ
	|	ТаблицаТовары
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
	|	И (НЕ ТаблицаТовары.СтатусУказанияСерий В (2, 4, 6, 8, 10))
	|	И ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.Склад.ИспользоватьСкладскиеПомещения
	|			ТОГДА ТаблицаТовары.Ссылка.Помещение" 	+ ВидПомещения + ".ИспользоватьАдресноеХранениеСправочно
	|		ИНАЧЕ ТаблицаТовары.Ссылка.Склад.ИспользоватьАдресноеХранениеСправочно
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Ссылка КАК Ссылка,
	|	ТаблицаСерии.Номенклатура КАК Номенклатура,
	|	ТаблицаСерии.Характеристика КАК Характеристика,
	|	ТаблицаСерии.Серия КАК Серия,
	|	ТаблицаСерии.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	ТаблицаСерии.Количество КАК КоличествоУпаковок,
	|	ТаблицаСерии.Количество КАК Количество
	|ИЗ
	|	Документ.ОрдерНаПеремещениеТоваров.Серии КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОрдерНаПеремещениеТоваров.Товары КАК ТаблицаТовары
	|		ПО ТаблицаСерии.Ссылка = ТаблицаТовары.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТаблицаТовары.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаТовары.Характеристика
	|	
	|ГДЕ
	|	ТаблицаСерии.Ссылка В(&МассивОбъектов)
	|	И ВЫБОР
	|		КОГДА ТаблицаСерии.Ссылка.Склад.ИспользоватьСкладскиеПомещения
	|			ТОГДА ТаблицаСерии.Ссылка.Помещение" 	+ ВидПомещения + ".ИспользоватьАдресноеХранениеСправочно
	|		ИНАЧЕ ТаблицаСерии.Ссылка.Склад.ИспользоватьАдресноеХранениеСправочно
	|	КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерии.Ссылка,
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.Упаковка,
	|	ТаблицаСерии.КоличествоУпаковок,
	|	ТаблицаСерии.Количество,
	|	ТаблицаСерии.НомерСтроки
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий) В (2, 4, 6, 8, 10)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.Номенклатура.Код КАК Код,
	|	ТаблицаТовары.Номенклатура.Артикул КАК Артикул,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ПредставлениеНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ТаблицаТовары.Характеристика.НаименованиеПолное КАК ПредставлениеХарактеристики,
	|	ТаблицаТовары.Упаковка.Наименование КАК ПредставлениеЕдининицыИзмеренияУпаковки,
	|	ТаблицаТовары.Серия.Наименование КАК ПредставлениеСерии,
	|	СУММА(ТаблицаТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество,
	|	ЕСТЬNULL(РазмещениеОсновнаяЯчейка.Ячейка.РабочийУчасток, ЗНАЧЕНИЕ(Справочник.РабочиеУчастки.ПустаяСсылка)) КАК РабочийУчасток,
	|	ЕСТЬNULL(РазмещениеОсновнаяЯчейка.Ячейка.ПорядокОбхода, 0) КАК ПорядокОбхода,
	|	ЕСТЬNULL(РазмещениеОсновнаяЯчейка.Ячейка, ЗНАЧЕНИЕ(Справочник.СкладскиеЯчейки.ПустаяСсылка)) КАК ОсновнаяЯчейка,
	|	ЕСТЬNULL(РазмещениеОстальныеЯчейки.Ячейка.ПорядокОбхода, 0) КАК ПорядокОбходаДополнительнаяЯчейка,
	|	ПРЕДСТАВЛЕНИЕ(РазмещениеОсновнаяЯчейка.Ячейка.РабочийУчасток) КАК ПредставлениеРабочегоУчастка,
	|	РазмещениеОсновнаяЯчейка.Ячейка.Код КАК ОсновнаяЯчейкаПредставление,
	|	РазмещениеОстальныеЯчейки.Ячейка.Код КАК ЯчейкаПредставление
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам КАК РазмещениеОсновнаяЯчейка
	|		ПО ТаблицаТовары.Номенклатура = РазмещениеОсновнаяЯчейка.Номенклатура
	|			И (РазмещениеОсновнаяЯчейка.ОсновнаяЯчейка = ИСТИНА)
	|			И (ВЫБОР
	|				КОГДА ТаблицаТовары.Ссылка.Склад.ИспользоватьСкладскиеПомещения
	|					ТОГДА РазмещениеОсновнаяЯчейка.Помещение = ТаблицаТовары.Ссылка.Помещение" 	+ ВидПомещения + "
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам КАК РазмещениеОстальныеЯчейки
	|		ПО ТаблицаТовары.Номенклатура = РазмещениеОстальныеЯчейки.Номенклатура
	|			И (РазмещениеОстальныеЯчейки.ОсновнаяЯчейка = ЛОЖЬ)
	|			И (ВЫБОР
	|				КОГДА ТаблицаТовары.Ссылка.Склад.ИспользоватьСкладскиеПомещения
	|					ТОГДА РазмещениеОстальныеЯчейки.Помещение = ТаблицаТовары.Ссылка.Помещение" 	+ ВидПомещения + "
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.Упаковка,
	|	РазмещениеОсновнаяЯчейка.Ячейка,
	|	РазмещениеОстальныеЯчейки.Ячейка
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ПорядокОбхода,
	|	ОсновнаяЯчейкаПредставление,
	|	ПредставлениеНоменклатуры,
	|	ПредставлениеХарактеристики,
	|	ПредставлениеСерии,
	|	ПредставлениеЕдининицыИзмеренияУпаковки,
	|	ПорядокОбходаДополнительнаяЯчейка,
	|	ЯчейкаПредставление
	|ИТОГИ ПО
	|	Ссылка,
	|	РабочийУчасток,
	|	ОсновнаяЯчейка,
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Упаковка
	|";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивДокументов); 
	МассивРезультатов 			= Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[0];
	РезультатПоТабличнойЧасти 	= МассивРезультатов[2];
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
КонецФункции

#КонецЕсли