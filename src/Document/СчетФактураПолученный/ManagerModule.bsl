#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Функция находит счета-фактуры заданного документа.
//
// Параметры:
//	Основание - ДокументСсылка - Документ, для которого необходимо найти счет-фактуру;
//	Организация - СправочникСсылка.Организации - Организация, для которой формируется счет-фактура;
//	РеквизитыСчетФактуры - Структура - Возвращаемый. Данные счета-фактуры (Ссылка, Номер, Дата, ВИностраннойВалюте).
//
// Возвращаемое значение:
//	Массив - Массив найденных счетов-фактур.
//
Функция СчетаФактурыПоОснованию(Основание, Организация = Неопределено, РеквизитыСчетаФактуры = Неопределено) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОснований.Ссылка КАК Ссылка,
	|	ТаблицаОснований.Ссылка.Номер КАК Номер,
	|	ТаблицаОснований.Ссылка.Дата КАК Дата,
	|	ТаблицаОснований.Ссылка.НомерИсправления КАК НомерИсправления,
	|	ТаблицаОснований.Ссылка.ДатаИсправления КАК ДатаИсправления,
	|	ТаблицаОснований.Ссылка.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	ТаблицаОснований.Ссылка.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	ТаблицаОснований.Ссылка.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
	|	ТаблицаОснований.Ссылка.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
	|	ТаблицаОснований.Ссылка.Валюта КАК Валюта,
	|	ТаблицаОснований.Ссылка.Корректировочный КАК Корректировочный
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК ТаблицаОснований
	|ГДЕ
	|	ТаблицаОснований.Ссылка.Проведен
	|	И ТаблицаОснований.ДокументОснование = &Основание
	|	И (ТаблицаОснований.Ссылка.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОснований.Ссылка.НомерИсправления УБЫВ
	|");
	Запрос.УстановитьПараметр("Основание",   Основание);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РеквизитыСчетаФактуры = Новый Структура;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		РеквизитыСчетаФактуры.Вставить(Колонка.Имя);
	КонецЦикла;
	
	ТаблицаСчетовФактур = Новый ТаблицаЗначений;
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ТаблицаСчетовФактур = РезультатЗапроса.Выгрузить();
		ЗаполнитьЗначенияСвойств(РеквизитыСчетаФактуры, ТаблицаСчетовФактур[0]);
	КонецЕсли;
	
	Возврат ТаблицаСчетовФактур;
	
КонецФункции

// Помечает на удаление счет-фактуру, если:
// - организация счета-фактуры не соответствует указанной 
// - или если документ-основание помечен на удаление
//
// Параметры:
//	Основание		- Документ - документ-основание счета-фактуры
//	ПометкаУдаления - Булево - пометка удаления документа-основания
//	Организация		- СправочникСсылка.Организации - Организация, для которой формируется счет-фактура
//
Процедура ПроверитьРеквизитыСчетФактуры(Основание, ПометкаУдаления, Организация = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОснований.Ссылка
	|ПОМЕСТИТЬ СчетаФактуры
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК ТаблицаОснований
	|ГДЕ
	|	ТаблицаОснований.ДокументОснование = &Основание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактуры.Ссылка КАК Ссылка,
	|	СчетаФактуры.Ссылка.Организация КАК Организация,
	|	КОЛИЧЕСТВО(ТаблицаОснований.ДокументОснование) КАК КоличествоОснований
	|ИЗ
	|	СчетаФактуры КАК СчетаФактуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК ТаблицаОснований
	|		ПО СчетаФактуры.Ссылка = ТаблицаОснований.Ссылка
	|ГДЕ
	|	НЕ ТаблицаОснований.Ссылка.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетаФактуры.Ссылка
	|");
	Запрос.УстановитьПараметр("Основание", Основание);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если (Организация <> Неопределено И Выборка.Организация <> Организация)
		 ИЛИ ПометкаУдаления Тогда
			Если Выборка.КоличествоОснований = 1 Тогда			 
				ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
				ДокументОбъект.УстановитьПометкуУдаления(Истина);
			Иначе
				ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
				СтрокаКУдалению = ДокументОбъект.ДокументыОснования.Найти(Основание);
				ДокументОбъект.ДокументыОснования.Удалить(СтрокаКУдалению);
				ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Перезаписывает счет-фактуру, если перепроводится документ-основание 
//
// Параметры:
//	Основание		- Документ - документ-основание счета-фактуры
//	РежимЗаписи 	- РежимЗаписиДокумента - режим записи документа-основания
//	Организация		- СправочникСсылка.Организации - Организация, для которой формируется счет-фактура
//
Процедура АктуализироватьСчетФактуру(Основание, Проведен, НеТребуется = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОснований.Ссылка
	|ПОМЕСТИТЬ СчетаФактуры
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК ТаблицаОснований
	|ГДЕ
	|	ТаблицаОснований.ДокументОснование = &Основание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактуры.Ссылка КАК Ссылка,
	|	СчетаФактуры.Ссылка.Организация КАК Организация,
	|	КОЛИЧЕСТВО(ТаблицаОснований.ДокументОснование) КАК КоличествоОснований
	|ИЗ
	|	СчетаФактуры КАК СчетаФактуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК ТаблицаОснований
	|		ПО СчетаФактуры.Ссылка = ТаблицаОснований.Ссылка
	|ГДЕ
	|	НЕ ТаблицаОснований.Ссылка.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетаФактуры.Ссылка"
	;
	
	Запрос.УстановитьПараметр("Основание", Основание);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Если НЕ Проведен ИЛИ НеТребуется Тогда
			Если Выборка.КоличествоОснований = 1 Тогда			 
				РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
			Иначе
				СтрокаКУдалению = ДокументОбъект.ДокументыОснования.Найти(Основание);
				ДокументОбъект.ДокументыОснования.Удалить(СтрокаКУдалению);
				РежимЗаписи = РежимЗаписиДокумента.Проведение;
			КонецЕсли;	
		Иначе
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		КонецЕсли;
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписи);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось записать %Документ%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%",       ДокументОбъект);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				,
				,
				);

		КонецПопытки;	
			
	КонецЦикла;

КонецПроцедуры

// Возвращает список кодов видов операций,
//	предусмотренных законодательством.
//
// Возвращаемое значение:
//	СписокЗначений - Список кодов видов операций.
//
Функция СписокКодовВидовОпераций() Экспорт
	
	СписокКодов = Новый СписокЗначений;
	
	СписокКодов.Добавить("01", НСтр("ru='01 Полученные товары, работы, услуги'"));
	СписокКодов.Добавить("02", НСтр("ru='02 Авансы выданные'"));
	СписокКодов.Добавить("03", НСтр("ru='03 Возврат от клиента'"));
	СписокКодов.Добавить("04", НСтр("ru='04 Полученные товары, работы, услуги от комитента'"));
	СписокКодов.Добавить("05", НСтр("ru='05 Авансы выданные по договорам комиссии'"));
	СписокКодов.Добавить("10", НСтр("ru='10 Полученные безвозмездно товары, работы, услуги'"));
	СписокКодов.Добавить("11", НСтр("ru='11 Полученные товары, права, п.3,4,5.1 статьи 154, пп.1-4 статьи 155 НК'"));
	СписокКодов.Добавить("12", НСтр("ru='12 Авансы выданные за товары, права, п.3,4,5.1 статьи 154, пп.1-4 статьи 155 НК'"));
	СписокКодов.Добавить("13", НСтр("ru='13 Капитальное строительство, модернизация (реконструкция) объектов недвижимости'"));
	
	Возврат СписокКодов;
	
КонецФункции

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстПринятиеНДСКВычету = "
	|ВЫБРАТЬ //// Принятие НДС к вычету (Дт <68.2> :: Кт <19.х>)
	|	НДСЗаписи.Период КАК Период,
	|	НДСЗаписи.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	НДСЗаписи.НДС КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС) КАК СчетДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог) КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	(ВЫБОР
	|		КОГДА НДСЗаписи.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету) ТОГДА
	|			(ВЫБОР
	|				КОГДА НДСЗаписи.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСприПриобретенииОсновныхСредств)
	|				КОГДА НДСЗаписи.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоПриобретеннымНематериальнымАктивам)
	|				КОГДА НДСЗаписи.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ)
	|				КОГДА НДСЗаписи.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Возврат)
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ)
	|				КОГДА НДСЗаписи.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоПриобретеннымУслугам)
	|				КОГДА НДСЗаписи.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам)
	|				КОГДА НДСЗаписи.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоАвансамИПредоплатамВыданным)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ)
	|		КОГДА НДСЗаписи.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСПоТоварамРеализованнымПоСтавке0)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК СчетКт,
	|
	|	НДСЗаписи.Поставщик КАК СубконтоКт1,
	|	НДСЗаписи.СчетФактура КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписи
	|ГДЕ
	|	НДСЗаписи.Регистратор = &Ссылка И НДСЗаписи.НДС <> 0
	|";
	
	Возврат ТекстПринятиеНДСКВычету;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата					КАК Период,
	|	ДанныеДокумента.ДатаПолучения			КАК ДатаПолучения,
	|	ДанныеДокумента.Организация				КАК Организация,
	|	ДанныеДокумента.Контрагент				КАК Контрагент,
	|	ДанныеДокумента.Корректировочный		КАК Корректировочный,
	|	ДанныеДокумента.Исправление 			КАК Исправление,
	|	ДанныеДокумента.ТипСчетаФактуры			КАК ТипСчетаФактуры,
	|	ДанныеДокумента.ПолученВЭлектронномВиде КАК ПолученВЭлектронномВиде,
	|	ДанныеДокумента.КодВидаОперации			КАК КодВидаОперации,
	|	ДанныеДокумента.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	ДанныеДокумента.Номер					КАК Номер,
	|	ДанныеДокумента.ДатаИсходногоДокумента	КАК ДатаИсходногоДокумента,
	|	ДанныеДокумента.НомерИсправления		КАК НомерИсправления,
	|	ДанныеДокумента.ДатаИсправления			КАК ДатаИсправления,
	|	ДанныеДокумента.Валюта					КАК Валюта,
	|	ДанныеДокумента.СчетФактураОснование	КАК СчетФактураОснование
	|ИЗ
	|	Документ.СчетФактураПолученный КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка"
	;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ВалютаОснования	= ОбщегоНазначения.ПолучитьЗначениеРеквизита(ДокументСсылка.ДокументыОснования[0].ДокументОснование, "Валюта");
	Коэффициенты	= РаботаСКурсамиВалютУТ.ПолучитьКоэффициентыПересчетаВалюты(ВалютаОснования, ВалютаОснования, Реквизиты.Период);
	
	Запрос.УстановитьПараметр("Период",							Реквизиты.Период);
	Запрос.УстановитьПараметр("ДатаПолучения",					Реквизиты.ДатаПолучения);
	Запрос.УстановитьПараметр("Организация",					Реквизиты.Организация);
	Запрос.УстановитьПараметр("Контрагент",						Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("Корректировочный",				Реквизиты.Корректировочный);
	Запрос.УстановитьПараметр("Исправление",					Реквизиты.Исправление);
	Запрос.УстановитьПараметр("ТипСчетаФактуры",				Реквизиты.ТипСчетаФактуры);
	Запрос.УстановитьПараметр("ПолученВЭлектронномВиде",		?(Реквизиты.ПолученВЭлектронномВиде,2,1));
	Запрос.УстановитьПараметр("КодВидаОперации",				Реквизиты.КодВидаОперации);
	Запрос.УстановитьПараметр("НомерИсходногоДокумента",		Реквизиты.НомерИсходногоДокумента);
	Запрос.УстановитьПараметр("Номер",							Реквизиты.Номер);
	Запрос.УстановитьПараметр("ДатаИсходногоДокумента",			Реквизиты.ДатаИсходногоДокумента);
	Запрос.УстановитьПараметр("НомерИсправления",				Реквизиты.НомерИсправления);
	Запрос.УстановитьПараметр("ДатаИсправления",				Реквизиты.ДатаИсправления);
	Запрос.УстановитьПараметр("ВалютаСчетаФактуры",				Реквизиты.Валюта);
	Запрос.УстановитьПараметр("ВалютаОснования",				ВалютаОснования);
	Запрос.УстановитьПараметр("СчетФактураОснование",			Реквизиты.СчетФактураОснование);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл",	Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	Запрос.УстановитьПараметр("СчетФактураПредыдущееИсправление",	?(Реквизиты.Исправление,
		ПредыдущийСчетФактураИсправление(Реквизиты.Номер, Реквизиты.Период, Реквизиты.НомерИсправления),Неопределено));
	
	Запрос.Текст = ТекстЗапросаВтТаблицаДокументов()
		+ ТекстЗапросаВтТаблицаСуммДокументов(Реквизиты.Исправление)
		+ ТекстЗапросаВтТаблицаКорректировки()
		+ ТекстЗапросаТаблицаЖурналУчетаСчетовФактур()
		+ ТекстЗапросаТаблицаНДСЗаписиКнигиПокупок()
		+ ТекстЗапросаТаблицаНДСЗаписиКнигиПродаж()
		;
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	// Результат[0] - ВтСторнируемыеКорректировки
	// Результат[1] - ВтДокументыОснования
	// Результат[2] - ВтПрошлыеКорректировки
	// Результат[3] - ВтПрошлыеИсправленияКорректировки
	// Результат[4] - ВтТаблицаСуммДокументов
	// Результат[5] - ВтТаблицаКоррекировок
	ТаблицыДляДвижений.Вставить("ТаблицаЖурналУчетаСчетовФактур", 	Результат[6].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаНДСЗаписиКнигиПокупок", 	Результат[7].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаНДСЗаписиКнигиПродаж", 		Результат[8].Выгрузить());
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаДокументов()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Документы.ДокументОснование КАК СторнируемаяКорректировка
	|ПОМЕСТИТЬ ВтСторнируемыеКорректировки
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК Документы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактура
	|			ПО Документы.Ссылка = СчетФактура.Ссылка
	|ГДЕ
	|	&Исправление
	|	И &Корректировочный
	|	И СчетФактура.Ссылка <> &Ссылка
	|	И СчетФактура.Номер ПОДОБНО &Номер
	|	И ТИПЗНАЧЕНИЯ(Документы.ДокументОснование) = ТИП(Документ.КорректировкаПоступления)
	|	И НАЧАЛОПЕРИОДА(СчетФактура.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|	И СчетФактура.Корректировочный
	|	И НЕ СчетФактура.Исправление
	|	И СчетФактура.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &Корректировочный И &Исправление
	|			ТОГДА ВложенныйЗапрос.ДокументОснование
	|		КОГДА &Корректировочный И НЕ &Исправление
	|			ТОГДА ДокументыОснования.ДокументОснование
	|		ИНАЧЕ
	|			ЕСТЬNULL(КорректировкаПоступления.ДокументОснование,ДокументыОснования.ДокументОснование)
	|	КОНЕЦ КАК КорректируемыйДокумент,
	|	ДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ДокументыОснования.ДокументОснование.Дата КАК ДатаДокументаОснования,
	|	КорректировкаПоступления.ДокументОснование.ЗакупкаПодДеятельность КАК ЗакупкаПодДеятельность
	|ПОМЕСТИТЬ ВтДокументыОснования
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК ДокументыОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|			ПО ДокументыОснования.ДокументОснование = КорректировкаПоступления.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				СторнируемыеКорректировки.СторнируемаяКорректировка КАК ДокументОснование
	|			ИЗ
	|				ВтСторнируемыеКорректировки КАК СторнируемыеКорректировки
	|			) КАК ВложенныйЗапрос
	|				ПО (ИСТИНА)
	|ГДЕ
	|	ДокументыОснования.Ссылка = &Ссылка
	|	И &ДатаПолучения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	И &Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыОснования.КорректируемыйДокумент КАК КорректируемыйДокумент,
	|	ДокументыОснования.ЗакупкаПодДеятельность КАК ЗакупкаПодДеятельность,
	|	КорректировкаПоступления.Ссылка КАК ДокументОснование
	|ПОМЕСТИТЬ ВтПрошлыеКорректировки
	|ИЗ
	|	ВтДокументыОснования КАК ДокументыОснования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|		ПО ДокументыОснования.КорректируемыйДокумент = КорректировкаПоступления.ДокументОснование
	|ГДЕ
	|	&Исправление
	|	И КорректировкаПоступления.Проведен
	|	И КорректировкаПоступления.Дата < ДокументыОснования.ДатаДокументаОснования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыОснования.КорректируемыйДокумент,
	|	ПоступлениеТоваровУслуг.ЗакупкаПодДеятельность,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	ВтДокументыОснования КАК ДокументыОснования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ПО ДокументыОснования.КорректируемыйДокумент = ПоступлениеТоваровУслуг.Ссылка
	|ГДЕ
	|	&Исправление
	|	И ДокументыОснования.КорректируемыйДокумент <> ДокументыОснования.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеТабличнойЧасти.ДокументОснование КАК ДокументКорректировки,
	|	ВложенныйЗапрос.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВтПрошлыеИсправленияКорректировки
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК ДанныеТабличнойЧасти
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|			ПО ДанныеТабличнойЧасти.Ссылка = СчетФактураПолученный.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				СторнируемыеКорректировки.СторнируемаяКорректировка КАК ДокументОснование
	|			ИЗ
	|				ВтСторнируемыеКорректировки КАК СторнируемыеКорректировки
	|			) КАК ВложенныйЗапрос
	|				ПО (ИСТИНА)
	|ГДЕ
	|	&Исправление
	|	И &Корректировочный
	|	И СчетФактураПолученный.Ссылка <> &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ДанныеТабличнойЧасти.ДокументОснование) = ТИП(Документ.КорректировкаПоступления)
	|	И СчетФактураПолученный.Номер ПОДОБНО &Номер
	|	И НАЧАЛОПЕРИОДА(СчетФактураПолученный.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|	И НАЧАЛОПЕРИОДА(СчетФактураПолученный.ДатаИсправления,ДЕНЬ) < НАЧАЛОПЕРИОДА(&ДатаИсправления, ДЕНЬ)
	|	И СчетФактураПолученный.Корректировочный
	|	И СчетФактураПолученный.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаСуммДокументов(Исправление)
	
	ТекстПоступлениеУслугПрочихАктивов ="
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КАК ВидЦенности,
	|	ТаблицаРасходы.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ((ТаблицаРасходы.СуммаСНДС - ТаблицаРасходы.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаРасходы.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаРасходы.СуммаСНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаРасходы.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаПоСчетуФактуреЖурнал,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаРасходы.СуммаНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаРасходы.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНДСЖурнал,
	|	ДокументыОснования.ДокументОснование КАК Основание,
	|	ВЫБОР
	|		КОГДА ТаблицаРасходы.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика) 
	|			ТОГДА ТаблицаРасходы.Ссылка.ЗакупкаПодДеятельность 
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеоблагаетсяНДС)
	|	КОНЕЦ КАК ЗакупкаПодДеятельность,
	|	ТаблицаРасходы.Ссылка.НалогообложениеНДС КАК НалогообложениеНДС
	|ПОМЕСТИТЬ ВтТаблицаСуммДокументов
	|ИЗ
	|	Документ.ПоступлениеУслугПрочихАктивов.Расходы КАК ТаблицаРасходы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ТаблицаРасходы.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаРасходы.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаРасходы.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|
	|";
	
	ТекстПоступлениеТоваровУслуг ="
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Номенклатура.ТипНоменклатуры В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ,
	|	ТаблицаТовары.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ((ТаблицаТовары.СуммаСНДС - ТаблицаТовары.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаТовары.СуммаСНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаТовары.СуммаНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ДокументыОснования.ДокументОснование,
	|	ТаблицаТовары.Ссылка.ЗакупкаПодДеятельность,
	|	ТаблицаТовары.Ссылка.НалогообложениеНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ТаблицаТовары.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаТовары.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаТовары.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаТовары.Ссылка.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет))
	|	И (ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.ВернутьМногооборотнуюТару
	|			ТОГДА ТаблицаТовары.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИНАЧЕ
	|			ИСТИНА
	|		КОНЕЦ)
	|
	|";
	
	ТекстВозвратТоваровОтКлиента ="
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Возврат),
	|	ТаблицаВидыЗапасов.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаВидыЗапасов.СуммаНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ДокументыОснования.ДокументОснование,
	|	ТаблицаВидыЗапасов.Ссылка.НалогообложениеНДС,
	|	ТаблицаВидыЗапасов.Ссылка.НалогообложениеНДС
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ТаблицаВидыЗапасов.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаВидыЗапасов.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаВидыЗапасов.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
	|	И (ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.Ссылка.ВозвратПереданнойМногооборотнойТары
	|			ТОГДА ТаблицаВидыЗапасов.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИНАЧЕ
	|			ИСТИНА
	|		КОНЕЦ)
	|
	|";
	
	ТекстОтчетКомиссионераВознаграждение = "
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги),
	|	ОтчетКомиссионера.СтавкаНДСВознаграждения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ((ОтчетКомиссионера.СуммаВознаграждения - ОтчетКомиссионера.СуммаНДСВознаграждения) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ОтчетКомиссионера.СуммаНДСВознаграждения * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ОтчетКомиссионера.СуммаВознаграждения
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ОтчетКомиссионера.СуммаВознаграждения * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ОтчетКомиссионера.СуммаНДСВознаграждения
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ОтчетКомиссионера.СуммаНДСВознаграждения * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ДокументыОснования.ДокументОснование,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|	ВЫБОР
	|		КОГДА ОтчетКомиссионера.СтавкаНДСВознаграждения = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС) 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	КОНЕЦ
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ОтчетКомиссионера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ОтчетКомиссионера.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ОтчетКомиссионера.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И СуммыДокументовВВалютеРегл.ИдентификаторСтроки = """"
	|ГДЕ
	|	ОтчетКомиссионера.СуммаВознаграждения <> 0
	|
	|";
	
	ТекстПередачаТоваровМеждуОрганизациямиВидыЗапасов = "
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары),
	|	ТаблицаВидыЗапасов.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаВидыЗапасов.СуммаНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ДокументыОснования.ДокументОснование,
	|	ТаблицаВидыЗапасов.Ссылка.ПередачаПодДеятельность,
	|	ТаблицаВидыЗапасов.Ссылка.НалогообложениеНДС
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ТаблицаВидыЗапасов.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаВидыЗапасов.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаВидыЗапасов.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка.РасчетыЧерезОтдельногоКонтрагента
	|	И ТаблицаВидыЗапасов.Ссылка.ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|
	|";
	
	ТекстПередачаТоваровМеждуОрганизациямиТовары = "
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги),
	|	ТаблицаТовары.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ((ТаблицаТовары.СуммаСНДС - ТаблицаТовары.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаТовары.СуммаСНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаТовары.СуммаНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ДокументыОснования.ДокументОснование,
	|	ТаблицаТовары.Ссылка.ПередачаПодДеятельность,
	|	ТаблицаТовары.Ссылка.НалогообложениеНДС
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ТаблицаТовары.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаТовары.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаТовары.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры НЕ В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТаблицаТовары.Ссылка.РасчетыЧерезОтдельногоКонтрагента
	|	И ТаблицаТовары.Ссылка.ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|
	|";
	
	ТекстВозвратТоваровМеждуОрганизациями = "
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Возврат),
	|	ТаблицаВидыЗапасов.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаВидыЗапасов.СуммаНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ДокументыОснования.ДокументОснование,
	|	ТаблицаВидыЗапасов.Ссылка.НалогообложениеНДС,
	|	ТаблицаВидыЗапасов.Ссылка.НалогообложениеНДС
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями.ВидыЗапасовПолучателя КАК ТаблицаВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ТаблицаВидыЗапасов.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаВидыЗапасов.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаВидыЗапасов.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка.РасчетыЧерезОтдельногоКонтрагента
	|	И ТаблицаВидыЗапасов.Ссылка.ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями)
	|
	|";
	
	ТекстОтчетПоКомиссииМеждуОрганизациямиВознаграждение = "
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги),
	|	ОтчетПоКомиссии.СтавкаНДСВознаграждения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ((ОтчетПоКомиссии.СуммаВознаграждения - ОтчетПоКомиссии.СуммаНДСВознаграждения) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ОтчетПоКомиссии.СуммаНДСВознаграждения * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ОтчетПоКомиссии.СуммаВознаграждения
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ОтчетПоКомиссии.СуммаВознаграждения * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ОтчетПоКомиссии.СуммаНДСВознаграждения
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ОтчетПоКомиссии.СуммаНДСВознаграждения * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ДокументыОснования.ДокументОснование,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|	ВЫБОР
	|		КОГДА ОтчетПоКомиссии.СтавкаНДСВознаграждения = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС) 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	КОНЕЦ
	|ИЗ
	|	Документ.ОтчетПоКомиссииМеждуОрганизациями КАК ОтчетПоКомиссии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ОтчетПоКомиссии.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ОтчетПоКомиссии.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И СуммыДокументовВВалютеРегл.ИдентификаторСтроки = """"
	|ГДЕ
	|	ОтчетПоКомиссии.РасчетыЧерезОтдельногоКонтрагента
	|	И ОтчетПоКомиссии.СуммаВознаграждения <> 0
	|
	|";
	
	ТекстВыкупВозвратнойТарыУПоставщика = "
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Номенклатура.ТипНоменклатуры В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ,
	|	ТаблицаТовары.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ((ТаблицаТовары.СуммаСНДС - ТаблицаТовары.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаТовары.СуммаСНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаТовары.СуммаНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ,
	|	ДокументыОснования.ДокументОснование,
	|	ТаблицаТовары.Ссылка.ЗакупкаПодДеятельность,
	|	ТаблицаТовары.Ссылка.НалогообложениеНДС
	|ИЗ
	|	Документ.ВыкупВозвратнойТарыУПоставщика.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ТаблицаТовары.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаТовары.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаТовары.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|
	|";
	
	ТекстПоступлениеТоваровУслугРасхождения ="
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ,
	|	ТаблицаРасхождения.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВариантРасхождений = ЗНАЧЕНИЕ(Перечисление.ВариантыРасхождений.Излишки)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(-(ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ -(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл)
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВариантРасхождений = ЗНАЧЕНИЕ(Перечисление.ВариантыРасхождений.Излишки)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(-ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ -СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВариантРасхождений = ЗНАЧЕНИЕ(Перечисление.ВариантыРасхождений.Излишки)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|						ТОГДА -ТаблицаРасхождения.СуммаСНДС
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(-ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ -(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл)
	|				КОНЕЦ
	|		ИНАЧЕ
	|				ВЫБОР
	|					КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|						ТОГДА ТаблицаРасхождения.СуммаСНДС
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВариантРасхождений = ЗНАЧЕНИЕ(Перечисление.ВариантыРасхождений.Излишки)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|						ТОГДА -ТаблицаРасхождения.СуммаНДС
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(-ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ -СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|		ИНАЧЕ
	|				ВЫБОР
	|					КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|						ТОГДА ТаблицаРасхождения.СуммаНДС
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|	КОНЕЦ,
	|	ДокументыОснования.ДокументОснование,
	|	ТаблицаРасхождения.Ссылка.ЗакупкаПодДеятельность,
	|	ТаблицаРасхождения.Ссылка.НалогообложениеНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Расхождения КАК ТаблицаРасхождения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ТаблицаРасхождения.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаРасхождения.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаРасхождения.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет))
	|	И (ВЫБОР
	|		КОГДА ТаблицаРасхождения.Ссылка.ВернутьМногооборотнуюТару
	|			ТОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИНАЧЕ
	|			ИСТИНА
	|		КОНЕЦ)
	|
	|";
	
	ТекстЗапроса = ТекстПоступлениеУслугПрочихАктивов 
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстПоступлениеТоваровУслуг 
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстВозвратТоваровОтКлиента
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстОтчетКомиссионераВознаграждение
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстПередачаТоваровМеждуОрганизациямиВидыЗапасов
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстПередачаТоваровМеждуОрганизациямиТовары
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстВозвратТоваровМеждуОрганизациями
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстОтчетПоКомиссииМеждуОрганизациямиВознаграждение
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстВыкупВозвратнойТарыУПоставщика
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстПоступлениеТоваровУслугРасхождения
	;
	
	Если Исправление Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ДокументОснование","КорректируемыйДокумент");
		ТекстЗапроса = ТекстЗапроса
			+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстЗапросаВтТаблицаКорректировки(Истина)
	КонецЕсли;
		
	ТекстЗапроса = ТекстЗапроса + "
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаВтТаблицаКорректировки(ПрошлыеКорректировки = Ложь)
	
	ТекстКорректировкаПоступленияРасхождения = "
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ КАК ВидЦенности,
	|	ТаблицаРасхождения.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаРасхождения.СуммаСНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаПоСчетуФактуреЖурнал,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаРасхождения.СуммаНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНДСЖурнал,
	|	ДокументыОснования.КорректируемыйДокумент КАК Основание,
	|	ДокументыОснования.ЗакупкаПодДеятельность КАК ЗакупкаПодДеятельность,
	|	ТаблицаРасхождения.Ссылка.НалогообложениеНДС КАК НалогообложениеНДС
	|ПОМЕСТИТЬ ВтТаблицаКорректировки
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОснования КАК ДокументыОснования
	|			ПО ТаблицаРасхождения.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаРасхождения.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаРасхождения.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|ГДЕ
	|	&Исправление ИЛИ &Корректировочный
	|
	|";
	
	ТекстПоступлениеТоваровУслугРасхождения ="
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ,
	|	ТаблицаРасхождения.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВариантРасхождений = ЗНАЧЕНИЕ(Перечисление.ВариантыРасхождений.Излишки)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВариантРасхождений = ЗНАЧЕНИЕ(Перечисление.ВариантыРасхождений.Излишки)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВариантРасхождений = ЗНАЧЕНИЕ(Перечисление.ВариантыРасхождений.Излишки)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|						ТОГДА ТаблицаРасхождения.СуммаСНДС
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|		ИНАЧЕ
	|				ВЫБОР
	|					КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|						ТОГДА -ТаблицаРасхождения.СуммаСНДС
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(-ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ -(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл)
	|				КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВариантРасхождений = ЗНАЧЕНИЕ(Перечисление.ВариантыРасхождений.Излишки)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|						ТОГДА ТаблицаРасхождения.СуммаНДС
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|		ИНАЧЕ
	|				ВЫБОР
	|					КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|						ТОГДА -ТаблицаРасхождения.СуммаНДС
	|					КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ВЫРАЗИТЬ(-ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ -СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|				КОНЕЦ
	|	КОНЕЦ,
	|	ДокументыОснования.ДокументОснование,
	|	ТаблицаРасхождения.Ссылка.ЗакупкаПодДеятельность,
	|	ТаблицаРасхождения.Ссылка.НалогообложениеНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Расхождения КАК ТаблицаРасхождения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПрошлыеКорректировки КАК ДокументыОснования
	|			ПО ТаблицаРасхождения.Ссылка = ДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаРасхождения.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаРасхождения.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет))
	|	И (ВЫБОР
	|		КОГДА ТаблицаРасхождения.Ссылка.ВернутьМногооборотнуюТару
	|			ТОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИНАЧЕ
	|			ИСТИНА
	|		КОНЕЦ)
	|	И (&Исправление ИЛИ &Корректировочный)
	|
	|";
	
	ТекстПрошлыеИсправленияКорректировки = "
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ КАК ВидЦенности,
	|	ТаблицаРасхождения.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаРасхождения.СуммаСНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаБезНДСРегл + СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаПоСчетуФактуреЖурнал,
	|	ВЫБОР
	|		КОГДА &ВалютаСчетаФактуры = &ВалютаОснования
	|			ТОГДА ТаблицаРасхождения.СуммаНДС
	|		КОГДА ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыДокументовВВалютеРегл.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНДСЖурнал,
	|	ПрошлыеИсправленияКорректировки.ДокументОснование КАК Основание,
	|	КорректировкаПоступления.ДокументОснование.ЗакупкаПодДеятельность КАК ЗакупкаПодДеятельность,
	|	КорректировкаПоступления.НалогообложениеНДС КАК НалогообложениеНДС
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПрошлыеИсправленияКорректировки КАК ПрошлыеИсправленияКорректировки
	|			ПО ТаблицаРасхождения.Ссылка = ПрошлыеИсправленияКорректировки.ДокументКорректировки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|			ПО ТаблицаРасхождения.Ссылка = КорректировкаПоступления.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|			ПО ТаблицаРасхождения.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|				И ТаблицаРасхождения.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|
	|";
	
	Если ПрошлыеКорректировки Тогда
		
		ТекстЗапроса = ТекстКорректировкаПоступленияРасхождения
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстПоступлениеТоваровУслугРасхождения;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ВтДокументыОснования","ВтПрошлыеКорректировки");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ПОМЕСТИТЬ ВтТаблицаКорректировки","");
		
	Иначе
		
		ТекстЗапроса = ТекстКорректировкаПоступленияРасхождения
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстПрошлыеИсправленияКорректировки;
		ТекстЗапроса = ТекстЗапроса + "
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса

КонецФункции

Функция ТекстЗапросаТаблицаЖурналУчетаСчетовФактур()
	
	ТекстЗапросаПолученныеСчетаФактуры = "
	|
	|ВЫБРАТЬ
	|	&ДатаПолучения КАК Период,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Контрагент,
	|	&Ссылка КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры) КАК ЧастьЖурнала,
	|	&ДатаПолучения КАК ДатаВыставленияПолучения,
	|	&ПолученВЭлектронномВиде КАК КодСпособаВыставленияПолучения,
	|	&КодВидаОперации КАК КодВидаОперации,
	|	ВЫБОР
	|		КОГДА &Корректировочный
	|			ТОГДА &НомерИсходногоДокумента
	|		ИНАЧЕ &Номер
	|	КОНЕЦ КАК НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА &Корректировочный
	|			ТОГДА &ДатаИсходногоДокумента
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК ДатаСчетаФактуры,
	|	ВЫБОР
	|		КОГДА &Корректировочный
	|			ТОГДА &Номер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК НомерКорректировочногоСчетаФактуры,
	|	ВЫБОР
	|		КОГДА &Корректировочный
	|			ТОГДА &Период
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДатаКорректировочногоСчетаФактуры,
	|	&НомерИсправления КАК НомерИсправления,
	|	&ДатаИсправления КАК ДатаИсправления,
	|	&ВалютаСчетаФактуры КАК Валюта,
	|	СУММА(ТаблицаДанных.СуммаПоСчетуФактуре) КАК СуммаПоСчетуФактуре,
	|	СУММА(ТаблицаДанных.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ТаблицаДанных.СуммаПоСчетуФактуреРазницаУменьшение) КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	СУММА(ТаблицаДанных.СуммаПоСчетуФактуреРазницаУвеличение) КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	СУММА(ТаблицаДанных.СуммаНДСРазницаУменьшение) КАК СуммаНДСРазницаУменьшение,
	|	СУММА(ТаблицаДанных.СуммаНДСРазницаУвеличение) КАК СуммаНДСРазницаУвеличение,
	|	ЕСТЬNULL(МАКСИМУМ(ТаблицаДанных.ПоСтавкеБезНДСКорректировка),МАКСИМУМ(ТаблицаДанных.ПоСтавкеБезНДСОснование)) КАК ПоСтавкеБезНДС,
	|	ТаблицаДанных.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ТаблицаСуммДокументов.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ПоСтавкеБезНДСОснование,
	|		NULL КАК ПоСтавкеБезНДСКорректировка,
	|		ТаблицаСуммДокументов.СуммаПоСчетуФактуреЖурнал КАК СуммаПоСчетуФактуре,
	|		ТаблицаСуммДокументов.СуммаНДСЖурнал КАК СуммаНДС,
	|		0 КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|		0 КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|		0 КАК СуммаНДСРазницаУменьшение,
	|		0 КАК СуммаНДСРазницаУвеличение,
	|		ЛОЖЬ КАК СчетФактураНеВыставляется
	|	ИЗ
	|		ВтТаблицаСуммДокументов КАК ТаблицаСуммДокументов
	|	ГДЕ
	|		&ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		NULL,
	|		ВЫБОР
	|			КОГДА ТаблицаКорректировки.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &Корректировочный
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаКорректировки.СуммаПоСчетуФактуреЖурнал
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &Корректировочный
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаКорректировки.СуммаНДСЖурнал
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &Корректировочный И ТаблицаКорректировки.СуммаПоСчетуФактуреЖурнал < 0
	|				ТОГДА -ТаблицаКорректировки.СуммаПоСчетуФактуреЖурнал
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &Корректировочный И ТаблицаКорректировки.СуммаПоСчетуФактуреЖурнал > 0
	|				ТОГДА ТаблицаКорректировки.СуммаПоСчетуФактуреЖурнал
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &Корректировочный И ТаблицаКорректировки.СуммаНДСЖурнал < 0
	|				ТОГДА -ТаблицаКорректировки.СуммаНДСЖурнал
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &Корректировочный И ТаблицаКорректировки.СуммаНДСЖурнал > 0
	|				ТОГДА ТаблицаКорректировки.СуммаНДСЖурнал
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ЛОЖЬ
	|	ИЗ
	|		ВтТаблицаКорректировки КАК ТаблицаКорректировки
	|	ГДЕ
	|		&ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|
	|	) КАК ТаблицаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.СчетФактураНеВыставляется
	|
	|";
	
	ТекстЗапросаСчетаФактурыВыставленныеКомитентом = "
	|
	|ВЫБРАТЬ
	|	&ДатаПолучения,
	|	&Организация,
	|	&Контрагент,
	|	&Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры),
	|	&ДатаПолучения,
	|	&ПолученВЭлектронномВиде,
	|	&КодВидаОперации,
	|	&Номер,
	|	&Период,
	|	NULL,
	|	NULL,
	|	&НомерИсправления,
	|	&ДатаИсправления,
	|	&ВалютаСчетаФактуры,
	|	СУММА(ВсегоПоДокументу.СуммаСНДС),
	|	СУММА(ВсегоПоДокументу.СуммаНДС),
	|	0,
	|	0,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ВсегоПоДокументу.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.СчетФактураПолученный.ВсегоПоДокументу КАК ВсегоПоДокументу
	|ГДЕ
	|	ВсегоПоДокументу.Ссылка = &Ссылка
	|	И &ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.ВыставленныйКомитентом)
	|	И &Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	И &ДатаПолучения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ВсегоПоДокументу.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстЗапроса = ТекстЗапросаПолученныеСчетаФактуры
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстЗапросаСчетаФактурыВыставленныеКомитентом;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаНДСЗаписиКнигиПокупок()
	
	ТекстЗапросаЗакупка = "
	|
	|ВЫБРАТЬ
	|	&ДатаПолучения КАК Период,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Поставщик,
	|	ТаблицаСуммДокументов.Основание КАК СчетФактура,
	|	ТаблицаСуммДокументов.ВидЦенности КАК ВидЦенности,
	|	ТаблицаСуммДокументов.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету) КАК Событие,
	|	&ДатаПолучения КАК ДатаСобытия,
	|	NULL КАК ЗаписьДополнительногоЛиста,
	|	NULL КАК КорректируемыйПериод,
	|	NULL КАК ИсправленныйСчетФактура,
	|	СУММА(ТаблицаСуммДокументов.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(ТаблицаСуммДокументов.СуммаНДС) КАК НДС
	|ИЗ
	|	ВтТаблицаСуммДокументов КАК ТаблицаСуммДокументов
	|ГДЕ
	|	НЕ &Исправление
	|	И НЕ &Корректировочный
	|	И ТаблицаСуммДокументов.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|													ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС))
	|	И ТаблицаСуммДокументов.ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И &ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСуммДокументов.Основание,
	|	ТаблицаСуммДокументов.ВидЦенности,
	|	ТаблицаСуммДокументов.СтавкаНДС
	|
	|";

	ТекстЗапросаИсправлениеСторно = "
	|
	|ВЫБРАТЬ
	|	&ДатаПолучения,
	|	&Организация,
	|	НДСЗаписиКнигиПокупок.Поставщик,
	|	НДСЗаписиКнигиПокупок.СчетФактура,
	|	НДСЗаписиКнигиПокупок.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету),
	|	&ДатаПолучения,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(НДСЗаписиКнигиПокупок.Период,Квартал) < НАЧАЛОПЕРИОДА(&ДатаПолучения,Квартал)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(НДСЗаписиКнигиПокупок.Период,Квартал) < НАЧАЛОПЕРИОДА(&ДатаПолучения,Квартал)
	|			ТОГДА НДСЗаписиКнигиПокупок.Период
	|	КОНЕЦ,
	|	&СчетФактураПредыдущееИсправление,
	|	-НДСЗаписиКнигиПокупок.СуммаБезНДСОборот,
	|	-НДСЗаписиКнигиПокупок.НДСОборот
	|ИЗ
	|	ВтДокументыОснования КАК ДокументыОснования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			,
	|			Регистратор,
	|			Организация = &Организация
	|				И ИсправленныйСчетФактура = &СчетФактураПредыдущееИсправление
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету)) КАК НДСЗаписиКнигиПокупок
	|		ПО ДокументыОснования.КорректируемыйДокумент = НДСЗаписиКнигиПокупок.СчетФактура
	|ГДЕ
	|	&Исправление
	|	И НЕ &Корректировочный
	|	И НДСЗаписиКнигиПокупок.Регистратор <> &Ссылка
	|	И НДСЗаписиКнигиПокупок.СуммаБезНДСОборот > 0
	|	И &ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|
	|";
	
	ТекстЗапросаИсправление = "
	|
	|ВЫБРАТЬ
	|	&ДатаПолучения,
	|	&Организация,
	|	&Контрагент,
	|	ТаблицаДанных.КорректируемыйДокумент,
	|	ТаблицаДанных.ВидЦенности,
	|	ТаблицаДанных.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету),
	|	&ДатаПолучения,
	|	ЛОЖЬ,
	|	NULL,
	|	&Ссылка,
	|	СУММА(ТаблицаДанных.СуммаБезНДС),
	|	СУММА(ТаблицаДанных.СуммаНДС)
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаСуммДокументов.Основание КАК КорректируемыйДокумент,
	|		ТаблицаСуммДокументов.ВидЦенности КАК ВидЦенности,
	|		ТаблицаСуммДокументов.СтавкаНДС КАК СтавкаНДС,
	|		ТаблицаСуммДокументов.СуммаБезНДС КАК СуммаБезНДС,
	|		ТаблицаСуммДокументов.СуммаНДС КАК СуммаНДС
	|	ИЗ
	|		ВтТаблицаСуммДокументов	КАК ТаблицаСуммДокументов
	|	ГДЕ
	|		ТаблицаСуммДокументов.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|													ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС))
	|		И ТаблицаСуммДокументов.ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		И &Исправление
	|		И НЕ &Корректировочный
	|		И &ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаКорректировки.Основание,
	|		ТаблицаКорректировки.ВидЦенности,
	|		ТаблицаКорректировки.СтавкаНДС,
	|		ТаблицаКорректировки.СуммаБезНДС,
	|		ТаблицаКорректировки.СуммаНДС
	|	ИЗ
	|		ВтТаблицаКорректировки КАК ТаблицаКорректировки
	|	ГДЕ
	|		ТаблицаКорректировки.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|													ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС))
	|		И ТаблицаКорректировки.ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		И &Исправление
	|		И НЕ &Корректировочный
	|		И &ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|
	|	) КАК ТаблицаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.КорректируемыйДокумент,
	|	ТаблицаДанных.ВидЦенности,
	|	ТаблицаДанных.СтавкаНДС
	|
	|";
	
	ТекстЗапросаИсправлениеКорректировкиСторно = "
	|
	|ВЫБРАТЬ
	|	&ДатаПолучения,
	|	&Организация,
	|	НДСЗаписиКнигиПокупок.Поставщик,
	|	НДСЗаписиКнигиПокупок.СчетФактура,
	|	НДСЗаписиКнигиПокупок.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету),
	|	&ДатаПолучения,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(НДСЗаписиКнигиПокупок.Период,Квартал) < НАЧАЛОПЕРИОДА(&ДатаПолучения,Квартал)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(НДСЗаписиКнигиПокупок.Период,Квартал) < НАЧАЛОПЕРИОДА(&ДатаПолучения,Квартал)
	|			ТОГДА НДСЗаписиКнигиПокупок.Период
	|	КОНЕЦ,
	|	&СчетФактураПредыдущееИсправление,
	|	-НДСЗаписиКнигиПокупок.СуммаБезНДСОборот,
	|	-НДСЗаписиКнигиПокупок.НДСОборот
	|ИЗ
	|	ВтСторнируемыеКорректировки КАК СторнируемыеКорректировки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			,
	|			Регистратор,
	|			Организация = &Организация
	|				И ИсправленныйСчетФактура = &СчетФактураПредыдущееИсправление
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету)) КАК НДСЗаписиКнигиПокупок
	|		ПО СторнируемыеКорректировки.СторнируемаяКорректировка = НДСЗаписиКнигиПокупок.СчетФактура
	|ГДЕ
	|	&Исправление
	|	И &Корректировочный
	|	И НДСЗаписиКнигиПокупок.Регистратор <> &Ссылка
	|	И НДСЗаписиКнигиПокупок.СуммаБезНДСОборот > 0
	|	И &ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|
	|";
	
	ТекстЗапросаКорректировка = "
	|
	|ВЫБРАТЬ
	|	&ДатаПолучения,
	|	&Организация,
	|	&Контрагент,
	|	ТаблицаКорректировки.Основание,
	|	ТаблицаКорректировки.ВидЦенности,
	|	ТаблицаКорректировки.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету),
	|	&ДатаПолучения,
	|	ЛОЖЬ,
	|	NULL,
	|	ВЫБОР
	|		КОГДА &Исправление
	|			ТОГДА &Ссылка
	|	КОНЕЦ,
	|	СУММА(ТаблицаКорректировки.СуммаБезНДС),
	|	СУММА(ТаблицаКорректировки.СуммаНДС)
	|ИЗ
	|	ВтТаблицаКорректировки КАК ТаблицаКорректировки
	|ГДЕ
	|	(ТаблицаКорректировки.СуммаБезНДС > 0
	|		ИЛИ ТаблицаКорректировки.СуммаНДС > 0)
	|	И ТаблицаКорректировки.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|												ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС))
	|	И ТаблицаКорректировки.ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И &Корректировочный
	|	И &ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаКорректировки.Основание,
	|	ТаблицаКорректировки.ВидЦенности,
	|	ТаблицаКорректировки.СтавкаНДС
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапросаЗакупка
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстЗапросаИсправлениеСторно
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстЗапросаИсправление
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстЗапросаИсправлениеКорректировкиСторно
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстЗапросаКорректировка;
	
КонецФункции

Функция ТекстЗапросаТаблицаНДСЗаписиКнигиПродаж()
	
	ТекстЗапросаКорректировочный = "
	|
	|ВЫБРАТЬ
	|	&ДатаПолучения КАК Период,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Покупатель,
	|	ТаблицаКорректировки.Основание КАК СчетФактура,
	|	ТаблицаКорректировки.ВидЦенности КАК ВидЦенности,
	|	ТаблицаКорректировки.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС) КАК Событие,
	|	&ДатаПолучения КАК ДатаСобытия,
	|	NULL КАК ЗаписьДополнительногоЛиста,
	|	NULL КАК КорректируемыйПериод,
	|	NULL КАК СторнирующаяЗаписьДопЛиста,
	|	ВЫБОР
	|		КОГДА &Исправление
	|			ТОГДА &Ссылка
	|	КОНЕЦ КАК ИсправленныйСчетФактура,
	|	&Исправление КАК Исправление,
	|	СУММА(-ТаблицаКорректировки.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(-ТаблицаКорректировки.СуммаНДС) КАК НДС
	|ИЗ
	|	ВтТаблицаКорректировки КАК ТаблицаКорректировки
	|ГДЕ
	|	(ТаблицаКорректировки.СуммаБезНДС < 0
	|		ИЛИ ТаблицаКорректировки.СуммаНДС < 0)
	|	И ТаблицаКорректировки.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|												ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС))
	|	И ТаблицаКорректировки.ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И &Корректировочный
	|	И &ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаКорректировки.Основание,
	|	ТаблицаКорректировки.ВидЦенности,
	|	ТаблицаКорректировки.СтавкаНДС
	|
	|";
	
	ТекстЗапросаКорректировочныйИсправлениеСторно = "
	|
	|ВЫБРАТЬ
	|	&ДатаПолучения,
	|	&Организация,
	|	НДСЗаписиКнигиПродаж.Покупатель,
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС),
	|	&ДатаПолучения,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(НДСЗаписиКнигиПродаж.Период,Квартал) < НАЧАЛОПЕРИОДА(&ДатаПолучения,Квартал)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(НДСЗаписиКнигиПродаж.Период,Квартал) < НАЧАЛОПЕРИОДА(&ДатаПолучения,Квартал)
	|			ТОГДА НДСЗаписиКнигиПродаж.Период
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(НДСЗаписиКнигиПродаж.Период,Квартал) < НАЧАЛОПЕРИОДА(&ДатаПолучения,Квартал)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	&СчетФактураПредыдущееИсправление,
	|	НДСЗаписиКнигиПродаж.Исправление,
	|	-НДСЗаписиКнигиПродаж.СуммаБезНДСОборот,
	|	-НДСЗаписиКнигиПродаж.НДСОборот
	|ИЗ
	|	ВтСторнируемыеКорректировки КАК СторнируемыеКорректировки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			,
	|			,
	|			Регистратор,
	|			Организация = &Организация
	|				И ИсправленныйСчетФактура = &СчетФактураПредыдущееИсправление
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС)) КАК НДСЗаписиКнигиПродаж
	|		ПО СторнируемыеКорректировки.СторнируемаяКорректировка = НДСЗаписиКнигиПродаж.СчетФактура
	|ГДЕ
	|	&Исправление
	|	И &Корректировочный
	|	И НДСЗаписиКнигиПродаж.Регистратор <> &Ссылка
	|	И &ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|	И (НДСЗаписиКнигиПродаж.СуммаБезНДСОборот > 0
	|		ИЛИ НДСЗаписиКнигиПродаж.НДСОборот > 0)
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапросаКорректировочный
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстЗапросаКорректировочныйИсправлениеСторно;
	
КонецФункции

Функция ПредыдущийСчетФактураИсправление(НомерСчетаФактуры, ДатаСчетафактуры, НомерИсправления)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетФактура.Ссылка КАК ИсправленныйСчетФактура,
	|	СчетФактура.НомерИсправления КАК НомерИсправления
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактура
	|ГДЕ
	|	СчетФактура.Номер ПОДОБНО &Номер
	|	И НАЧАЛОПЕРИОДА(СчетФактура.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|	И СчетФактура.Проведен
	|	И СчетФактура.Исправление
	|	И СчетФактура.НомерИсправления <> &НомерИсправления
	|";
	Запрос.УстановитьПараметр("Номер",				НомерСчетаФактуры);
	Запрос.УстановитьПараметр("НомерИсправления",	НомерИсправления);
	Запрос.УстановитьПараметр("Дата",				ДатаСчетафактуры);
	
	ПредыдущийНомер = 0;
	ИсправленныйСчетФактура = Неопределено;
	НомерИсправленияЧислом = Число(НомерИсправления);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НомерИзВыборки = Число(Выборка.НомерИсправления);
		Если НомерИзВыборки < НомерИсправленияЧислом
			И НомерИзВыборки > ПредыдущийНомер Тогда
			ПредыдущийНомер = НомерИзВыборки;
			ИсправленныйСчетФактура = Выборка.ИсправленныйСчетФактура;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИсправленныйСчетФактура;
	
КонецФункции

#КонецЕсли