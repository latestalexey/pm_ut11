
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВсегоПоДокументу.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресТаблицыВХранилище));
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Ответ = Вопрос("Данные были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ПеренестиДанные(Отказ);
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВсегоПоДокументуСуммаСНДСПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВсегоПоДокументу.ТекущиеДанные;
	
	ТекПроцентНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(ТекущиеДанные.СтавкаНДС);
	ТекущиеДанные.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(ТекущиеДанные.СуммаСНДС, ТекПроцентНДС, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВсегоПоДокументуСтавкаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВсегоПоДокументу.ТекущиеДанные;
	
	ТекПроцентНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(ТекущиеДанные.СтавкаНДС);
	ТекущиеДанные.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(ТекущиеДанные.СуммаСНДС, ТекПроцентНДС, Истина);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ПеренестиДанные();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Процедура ПеренестиДанные(Отказ = Ложь)
	
	ОчиститьСообщения();
	
	Для Индекс = 0 По ВсегоПоДокументу.Количество() - 1 Цикл
		
		СтрокаТаблицы = ВсегоПоДокументу[Индекс];
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СуммаСНДС) Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В строке %1 не заполнено поле ""Сумма с НДС"".'"),
				Индекс + 1
			);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				,
				"ДокументыОснования["+Индекс+"].СуммаСНДС",
				,
				Отказ
			);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В строке %1 не заполнено поле ""Ставка НДС"".'"),
				Индекс + 1
			);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				,
				"ДокументыОснования["+Индекс+"].СтавкаНДС",
				,
				Отказ
			);
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		Модифицированность = Ложь;
		ОповеститьОВыборе(ПоместитьВсегоПоДокументуВХранилище());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьВсегоПоДокументуВХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(ВсегоПоДокументу.Выгрузить());
	
КонецФункции
