////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьПараметрыДинамическихСписков();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_ТаможеннаяДекларацияИмпорт" Тогда
		Элементы.КОформлению.Обновить();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СоздатьТаможеннуюДекларацию(Команда)
	Строка = Элементы.КОформлению.ТекущиеДанные;
	Если Неопределено = Строка Тогда
		Возврат;
	КонецЕсли;
	Текст = "
	|ВЫБРАТЬ
	|	Номенклатура,
	|	Характеристика,
	|	Склад,
	|	ВидЗапасов,
	|	ВидЗапасов.Сделка КАК Сделка,
	|	Неопределено КАК Упаковка,
	|	КоличествоОстаток КАК Количество,
	|	КоличествоОстаток КАК КоличествоУпаковок,
	|	0 КАК Цена,
	|	0 КАК Сумма,
	|	0 КАК СуммаСНДС
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизацийКОформлению.Остатки(&ДатаОстатков,
	|		Организация = &Организация
	|		И Поставщик = &Поставщик
	|	)
	|ГДЕ
	|	КоличествоОстаток > 0
	|";
	ДатаОстатков = ?(ЗначениеЗаполнено(Период.ДатаОкончания), Период.ДатаОкончания, '39991231');
	Основание = Новый Структура("Организация, Поставщик, ДатаОстатков, ЗапросТовары",
		Строка.Организация,
		Строка.Поставщик,
		ДатаОстатков,
		Текст
	);
	ПараметрыОткрытия = Новый Структура("Основание", Основание);
	ОткрытьФорму("Документ.ТаможеннаяДекларацияИмпорт.ФормаОбъекта", ПараметрыОткрытия, Элементы.Список);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПериодВариантПриИзменении(Элемент)
	
	УстановитьПараметрыДинамическихСписков();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДатаОкончанияПриИзменении(Элемент)
	
	УстановитьПараметрыДинамическихСписков();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаКОформлению Тогда
		УстановитьПараметрыДинамическихСписков();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ К ОФОРМЛЕНИЮ

&НаКлиенте
Процедура КОформлениюПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ОтобратьКОформлениюНоменклатура", 0.2, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КОформлениюВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОтобратьКОформлениюНоменклатура();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаСервере
Процедура УстановитьПараметрыДинамическихСписков()
	
	ПараметрДата = КонецДня(?(ЗначениеЗаполнено(Период.ДатаОкончания),Период.ДатаОкончания,'39991231'));
	КОформлению.Параметры.УстановитьЗначениеПараметра("Граница", Новый Граница(ПараметрДата, ВидГраницы.Включая));
	КОформлениюНоменклатура.Параметры.УстановитьЗначениеПараметра("Граница", Новый Граница(ПараметрДата, ВидГраницы.Включая));
	
	ОтобратьКОформлениюНоменклатураСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьКОформлениюНоменклатура()
	
	ОтборНоменклатуры = Элементы.КОформлению.ТекущиеДанные;
	ОтборПоСтроке = ОтборНоменклатуры <> Неопределено И ОтборНоменклатуры.Свойство("Поставщик") И ОтборНоменклатуры.Свойство("Организация");
	
	ОтборПоставщик = ?(ОтборПоСтроке, ОтборНоменклатуры.Поставщик, ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка"));
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(КОформлениюНоменклатура.Отбор, "Поставщик", ОтборПоставщик);
	
	ОтборОрганизация = ?(ОтборПоСтроке, ОтборНоменклатуры.Организация, ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	
	ОтобратьКОформлениюНоменклатураСервер(ОтборПоставщик, ОтборОрганизация);
	
КонецПроцедуры

&НаСервере
Процедура ОтобратьКОформлениюНоменклатураСервер(ОтборПоставщик = Неопределено, ОтборОрганизация = Неопределено)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(КОформлениюНоменклатура.Отбор, "Поставщик", ОтборПоставщик);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(КОформлениюНоменклатура.Отбор, "Организация", ОтборОрганизация);
	
КонецПроцедуры
