#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.БанковскийСчет КАК БанковскийСчет,
	|	ДанныеДокумента.БанковскийСчет.ВалютаДенежныхСредств КАК Валюта
	|ИЗ
	|	Документ.ВыпискаПоРасчетномуСчету КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.Текст = ТекстЗапросаТаблицаДенежныеСредстваБезналичные()
		+ ТекстЗапросаТаблицаДенежныеСредстваКПоступлениюБезналичные()
		+ ТекстЗапросаТаблицаДенежныеСредстваКСписаниюБезналичные()
		;
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("БанковскийСчет", Реквизиты.БанковскийСчет);
	Запрос.УстановитьПараметр("Валюта", Реквизиты.Валюта);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваБезналичные",             МассивРезультатов[0].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваКПоступлениюБезналичные", МассивРезультатов[1].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваКСписаниюБезналичные",    МассивРезультатов[2].Выгрузить());
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаДенежныеСредстваБезналичные()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Платежи.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&БанковскийСчет КАК БанковскийСчет,
	|	Платежи.ПлатежныйДокумент КАК ПлатежныйДокумент,
	|	
	|	ДенежныеСредстваКПоступлению.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДенежныеСредстваКПоступлению.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДенежныеСредстваКПоступлению.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ДенежныеСредстваКПоступлению.Заказ КАК Заказ,
	|	
	|	ЕСТЬNULL(ДенежныеСредстваКПоступлению.Сумма, Платежи.Сумма) КАК Сумма
	|	
	|ИЗ
	|	Документ.ВыпискаПоРасчетномуСчету.ВходящиеПлатежи КАК Платежи
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваКПоступлениюБезналичные КАК ДенежныеСредстваКПоступлению
	|	ПО
	|		Платежи.ПлатежныйДокумент = ДенежныеСредстваКПоступлению.Регистратор
	|		И ДенежныеСредстваКПоступлению.ТипДенежныхСредств <> ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредствКПоступлению.КонвертацияВалюты)	
	|	
	|ГДЕ
	|	Платежи.Ссылка = &Ссылка
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	Платежи.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&БанковскийСчет КАК БанковскийСчет,
	|	Платежи.ПлатежныйДокумент КАК ПлатежныйДокумент,
	|	
	|	ДенежныеСредстваКСписанию.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДенежныеСредстваКСписанию.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДенежныеСредстваКСписанию.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ДенежныеСредстваКСписанию.Заказ КАК Заказ,
	|	
	|	ЕСТЬNULL(ДенежныеСредстваКСписанию.Сумма, Платежи.Сумма) КАК Сумма
	|	
	|ИЗ
	|	Документ.ВыпискаПоРасчетномуСчету.ИсходящиеПлатежи КАК Платежи
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваКСписаниюБезналичные КАК ДенежныеСредстваКСписанию
	|	ПО
	|		Платежи.ПлатежныйДокумент = ДенежныеСредстваКСписанию.Регистратор
	|	
	|ГДЕ
	|	Платежи.Ссылка = &Ссылка
	|	
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваКПоступлениюБезналичные()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|
	|	ВЫБОР КОГДА Платежи.ПлатежныйДокумент ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредствКПоступлению.ПеречислениеСДругогоСчета)
	|
	|	КОГДА Платежи.ПлатежныйДокумент ССЫЛКА Документ.ОтчетБанкаПоОперациямЭквайринга ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредствКПоступлению.ПоступлениеОтБанкаПоЭквайрингу)
	|
	|	КОГДА Платежи.ПлатежныйДокумент ССЫЛКА Документ.РасходныйКассовыйОрдер ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредствКПоступлению.ВыдачаИзКассы)
	|
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредствКПоступлению.ПоступлениеПоПлатежномуДокументу)
	|
	|	КОНЕЦ КАК ТипДенежныхСредств,
	|	
	|	&Организация КАК Организация,
	|	&БанковскийСчет КАК БанковскийСчет,
	|	Платежи.ПлатежныйДокумент КАК Документ,
	|	
	|	ДенежныеСредстваКПоступлению.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДенежныеСредстваКПоступлению.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДенежныеСредстваКПоступлению.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ДенежныеСредстваКПоступлению.Заказ КАК Заказ,
	|	
	|	ЕСТЬNULL(ДенежныеСредстваКПоступлению.Сумма, Платежи.Сумма) КАК Сумма
	|	
	|ИЗ
	|	Документ.ВыпискаПоРасчетномуСчету.ВходящиеПлатежи КАК Платежи
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваКПоступлениюБезналичные КАК ДенежныеСредстваКПоступлению
	|	ПО
	|		Платежи.ПлатежныйДокумент = ДенежныеСредстваКПоступлению.Регистратор
	|		И ДенежныеСредстваКПоступлению.ТипДенежныхСредств <> ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредствКПоступлению.КонвертацияВалюты)	
	|		
	|ГДЕ
	|	Платежи.Ссылка = &Ссылка
	|	
	|УПОРЯДОЧИТЬ ПО
	|	Платежи.НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваКСписаниюБезналичные()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВЫБОР КОГДА Платежи.ПлатежныйДокумент ССЫЛКА Документ.ПриходныйКассовыйОрдер ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредствКСписанию.СнятиеПоЧеку)
	|	КОГДА Платежи.ПлатежныйДокумент ССЫЛКА Документ.ОтчетБанкаПоОперациямЭквайринга ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредствКСписанию.СписаниеБанкомПоЭквайрингу)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредствКСписанию.СписаниеПоПлатежномуДокументу)
	|	КОНЕЦ КАК ТипДенежныхСредств,
	|	&Организация КАК Организация,
	|	&БанковскийСчет КАК БанковскийСчет,
	|	Платежи.ПлатежныйДокумент КАК Документ,
	|	
	|	ДенежныеСредстваКСписанию.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДенежныеСредстваКСписанию.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДенежныеСредстваКСписанию.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ДенежныеСредстваКСписанию.Заказ КАК Заказ,
	|	
	|	ЕСТЬNULL(ДенежныеСредстваКСписанию.Сумма, Платежи.Сумма) КАК Сумма
	|	
	|ИЗ
	|	Документ.ВыпискаПоРасчетномуСчету.ИсходящиеПлатежи КАК Платежи
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваКСписаниюБезналичные КАК ДенежныеСредстваКСписанию
	|	ПО
	|		Платежи.ПлатежныйДокумент = ДенежныеСредстваКСписанию.Регистратор
	|		
	|ГДЕ
	|	Платежи.Ссылка = &Ссылка
	|	
	|УПОРЯДОЧИТЬ ПО
	|	Платежи.НомерСтроки
	|	
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецЕсли