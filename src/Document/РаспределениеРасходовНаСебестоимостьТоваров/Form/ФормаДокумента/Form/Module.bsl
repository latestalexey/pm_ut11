
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Обработчик подсистемы "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);

	// Обработчик подсистемы "Внешние обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	Валюта = Константы.ВалютаУправленческогоУчета.Получить();
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_РаспределениеРасходовНаСебестоимостьТоваров");
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СтатьяРасходовПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Расходы.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(СтрокаТаблицы.СтатьяРасходов) Тогда
		СтрокаТаблицы.АналитикаРасходов = Неопределено;
	Иначе
		СтрокаТаблицы.ПравилоРаспределения = ПолучитьПравилоРаспределенияСтатьиРасходов(СтрокаТаблицы.СтатьяРасходов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантРаспределенияПоВсемОрганизациямПриИзменении(Элемент)
	
	ВариантРаспределенияПоОрганизациямПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантРаспределенияПоВыбраннымОрганизациямПриИзменении(Элемент)
	
	ВариантРаспределенияПоОрганизациямПриИзменении();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаполнитьРасходы()
	
	СтруктураРеквизитов = Новый Структура;
	Если Не Объект.РаспределениеПоВсемОрганизациям Тогда
		СтруктураРеквизитов.Вставить("Организация");
	КонецЕсли;
	
	Если ОбщегоНазначенияУТКлиент.ВозможноЗаполнениеТабличнойЧасти(ЭтаФорма, Объект.Расходы, СтруктураРеквизитов) Тогда
		ЗаполнитьРасходыПоОстаткам();
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаСервере
Процедура ВариантРаспределенияПоОрганизациямПриИзменении()
	
	Объект.РаспределениеПоВсемОрганизациям = (ВариантРаспределенияПоОрганизациям = 1);
	УправлениеЭлементамиФормы();
	
	Если Объект.РаспределениеПоВсемОрганизациям
	 И ЗначениеЗаполнено(Объект.Организация) Тогда
	 
		Для Каждого СтрокаТаблицы Из Объект.Расходы Цикл
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Организация) Тогда
				СтрокаТаблицы.Организация = Объект.Организация;
			КонецЕсли;
		КонецЦикла;
	 
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	Если Объект.РаспределениеПоВсемОрганизациям Тогда
		ВариантРаспределенияПоОрганизациям = 1;
	Иначе
		ВариантРаспределенияПоОрганизациям = 0;
	КонецЕсли;
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Элементы.Организация.Доступность = Не Объект.РаспределениеПоВсемОрганизациям;
	Элементы.РасходыОрганизация.Видимость = Объект.РаспределениеПоВсемОрганизациям;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасходыПоОстаткам()

	Запрос = Новый Запрос("
	| ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПрочиеРасходы.Организация КАК Организация,
	|	ПрочиеРасходы.Подразделение КАК Подразделение,
	|	ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	ПрочиеРасходы.СтатьяРасходов.ПравилоРаспределенияНаСебестоимость КАК ПравилоРаспределения,
	|	ПрочиеРасходы.СуммаОстаток КАК Сумма,
	|	ПрочиеРасходы.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	ПрочиеРасходы.СуммаРеглОстаток КАК СуммаРегл
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Остатки(&Граница,
	|		(Организация = &Организация
	|			ИЛИ &ПоВсемОрганизациям)
	|		И СтатьяРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	) КАК ПрочиеРасходы
	|ГДЕ
	|	ПрочиеРасходы.СуммаОстаток <> 0
	|	ИЛИ ПрочиеРасходы.СуммаБезНДСОстаток <> 0
	|	ИЛИ ПрочиеРасходы.СуммаРеглОстаток <> 0
	|");
	Запрос.УстановитьПараметр("Граница", Новый Граница(КонецМесяца(Объект.Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", Объект.РаспределениеПоВсемОрганизациям);
	Объект.Расходы.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПравилоРаспределенияСтатьиРасходов(СтатьяРасходов)
	
	Возврат ПланыВидовХарактеристик.СтатьиРасходов.ПолучитьПравилоРаспределения(СтатьяРасходов);

КонецФункции