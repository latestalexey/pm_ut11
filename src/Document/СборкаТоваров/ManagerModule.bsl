#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Формирует запрос проверки при смене статуса списка документов
//
// Параметры:
// 		МассивДокументов - Массив - Массив ссылок на документы, которые надо проверять
// 		НовыйСтатус - Строка - Имя нового статуса
// 		ДополнительныеПараметры - Структура - Структура дополнительных параметров
//
// Возвращаемое значение:
// 		Запрос - Запрос проверки перед сменой статуса
//
Функция СформироватьЗапросПроверкиПриСменеСтатуса(МассивДокументов, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыСборокТоваров[НовыйСтатус];
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка                КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Ссылка) КАК Представление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Статус) КАК ПредставлениеТекущегоСтатуса,
	|	ПРЕДСТАВЛЕНИЕ(&Статус)                  КАК ПредставлениеНовогоСтатуса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Статус = &Статус ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ                                   КАК СтатусСовпадает,
	|	ТаблицаДокументов.Проведен              КАК Проведен,
	|	ТаблицаДокументов.ПометкаУдаления       КАК ПометкаУдаления,
	|	ИСТИНА                                  КАК ЗаписьПроведением
	|ИЗ
	|	Документ.СборкаТоваров КАК ТаблицаДокументов
	|ГДЕ
	|	ТаблицаДокументов.Ссылка В(&МассивДокументов)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Статус", ЗначениеНовогоСтатуса);
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Возврат Запрос;
	
КонецФункции// СформироватьЗапросПроверкиПриСменеСтатуса();

// Возвращает результат проверки при смене статуса документа
//
// Параметры:
// 		ВыборкаПроверки - ВыборкаИзРезультатаЗапроса - Текущая строка выборки
// 		НовыйСтатус - Перечисление - Новый статус
// 		ДополнительныеПараметры - Структура - Структура дополнительных параметров
//
// Возвращаемое значение:
// 		Булево - Истина, в случае успешного завершения проверки
//
Функция ПроверкаПередСменойСтатуса(ВыборкаПроверки, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Возврат Истина; // Для документа "СборкаТоваров" отсутствуют дополнительные проверки
	
КонецФункции

// Процедура формирует результат запроса по неотгруженной части заказа.
//
Функция ПолучитьРезультатЗапросаПоОстаткамЗаказа(ЗаказНаСборку, СборкаТоваров) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗаказНаСборку", ЗаказНаСборку);
	Запрос.УстановитьПараметр("Регистратор",   СборкаТоваров);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказыНаСборку.Номенклатура       КАК Номенклатура,
	|	ЗаказыНаСборку.Характеристика     КАК Характеристика,
	|	ЗаказыНаСборку.КодСтроки          КАК КодСтроки,
	|	ЗаказыНаСборку.ТипСборки          КАК ТипСборки,
	|	СУММА(ЗаказыНаСборку.КОформлению) КАК Количество
	|
	|ПОМЕСТИТЬ ТаблицаОстатки
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		Заказы.Номенклатура              КАК Номенклатура,
	|		Заказы.Характеристика            КАК Характеристика,
	|		Заказы.КодСтроки                 КАК КодСтроки,
	|		Заказы.ТипСборки                 КАК ТипСборки,
	|		Заказы.КОформлениюОстаток        КАК КОформлению
	|	ИЗ
	|		РегистрНакопления.ЗаказыНаСборку.Остатки(,
	|				ЗаказНаСборку = &ЗаказНаСборку) КАК Заказы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Заказы.Номенклатура,
	|		Заказы.Характеристика,
	|		Заказы.КодСтроки,
	|		Заказы.ТипСборки,
	|		ВЫБОР КОГДА Заказы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Заказы.КОформлению
	|			ИНАЧЕ
	|				Заказы.КОформлению
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЗаказыНаСборку КАК Заказы
	|	ГДЕ
	|		Заказы.Регистратор = &Регистратор
	|		И Заказы.ЗаказНаСборку = &ЗаказНаСборку
	|		И Заказы.Активность
	|	) КАК ЗаказыНаСборку
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыНаСборку.Характеристика,
	|	ЗаказыНаСборку.Номенклатура,
	|	ЗаказыНаСборку.ТипСборки,
	|	ЗаказыНаСборку.КодСтроки
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЗаказыНаСборку.КОформлению) > 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(Товары.НомерСтроки)     КАК НомерСтроки,
	|	Товары.Номенклатура              КАК Номенклатура,
	|	Товары.Характеристика            КАК Характеристика,
	|	Товары.КодСтроки                 КАК КодСтроки,
	|	Товары.УказыватьСерии            КАК УказыватьСерии,
	|	МАКСИМУМ(Товары.Упаковка)        КАК Упаковка
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.ЗаказНаСборку.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &ЗаказНаСборку
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|   Товары.УказыватьСерии,
	|	Товары.КодСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// Формирование т.ч. товары.
	|ВЫБРАТЬ
	|	ТаблицаОстатки.Номенклатура КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.КодСтроки КАК КодСтроки,
	|	1 КАК ДоляСтоимости,
	|	ТаблицаОстатки.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.УказыватьСерии
	|			ТОГДА &ЗаказНаСборку
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказНаСборку.ПустаяСсылка)
	|	КОНЕЦ КАК ДокументРезерваСерий,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	ТаблицаТовары.Упаковка.Коэффициент КАК Коэффициент
	|ИЗ
	|	ТаблицаОстатки КАК ТаблицаОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК ТаблицаТовары
	|		ПО ТаблицаОстатки.Номенклатура = ТаблицаТовары.Номенклатура
	|			И ТаблицаОстатки.Характеристика = ТаблицаТовары.Характеристика
	|			И ТаблицаОстатки.КодСтроки = ТаблицаТовары.КодСтроки
	|ГДЕ
	|	(ТаблицаОстатки.ТипСборки = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияЗапасов.Отгрузка)
	|				И ВЫРАЗИТЬ(&ЗаказНаСборку КАК Документ.ЗаказНаСборку).ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|			ИЛИ ТаблицаОстатки.ТипСборки = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияЗапасов.Поступление)
	|				И ВЫРАЗИТЬ(&ЗаказНаСборку КАК Документ.ЗаказНаСборку).ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// Формирование данных комплекта.
	|ВЫБРАТЬ
	|	ТаблицаОстатки.Номенклатура        КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика      КАК Характеристика,
	|	ТаблицаОстатки.Количество          КАК Количество,
	|	ТаблицаТовары.ВариантКомплектации  КАК ВариантКомплектации,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.УказыватьСерии
	|			ТОГДА &ЗаказНаСборку
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказНаСборку.ПустаяСсылка)
	|	КОНЕЦ КАК ДокументРезерваСерий,
	|	ТаблицаТовары.Упаковка             КАК Упаковка,
	|	ТаблицаТовары.Упаковка.Коэффициент КАК Коэффициент
	|ИЗ
	|	ТаблицаОстатки КАК ТаблицаОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаСборку КАК ТаблицаТовары
	|		ПО  ТаблицаТовары.Ссылка = &ЗаказНаСборку
	|			И ТаблицаОстатки.Номенклатура   = ТаблицаТовары.Номенклатура
	|			И ТаблицаОстатки.Характеристика = ТаблицаТовары.Характеристика
	|ГДЕ
	|	(ТаблицаОстатки.ТипСборки = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияЗапасов.Поступление)
	|		И ВЫРАЗИТЬ(&ЗаказНаСборку КАК Документ.ЗаказНаСборку).ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих))
	|	ИЛИ
	|	(ТаблицаОстатки.ТипСборки = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияЗапасов.Отгрузка)
	|		И ВЫРАЗИТЬ(&ЗаказНаСборку КАК Документ.ЗаказНаСборку).ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие))
	|";
	МассивРезультатов = Запрос.ВыполнитьПакет();

	Возврат Новый Структура("РезультатЗапросаПоКомплектующим, РезультатЗапросаПоКомплекту",
							МассивРезультатов[2], МассивРезультатов[3]);

КонецФункции

//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	ИменаРеквизитов = "Склад,Статус,ТипОперации";
	
	Возврат ИменаРеквизитов;
КонецФункции

//Возвращает параметры указания серий для товаров, указанных в документе
//
//	Параметры
//			Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий
//	Возвращаемое значение
//			Тип Структура
//				Состав полей задается в функции ОбработкаТабличнойЧастиКлиентСервер.ПараметрыУказанияСерий
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ИспользоватьСерии                   = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатурыСклад",Новый Структура("Склад",Объект.Склад));
	ИспользоватьОрдернуюСхемуПриПриемке = ПолучитьФункциональнуюОпцию("ИспользоватьОрдернуюСхемуПриПоступлении",Новый Структура("Склад",Объект.Склад));
	
	Шапка = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	Если Объект.ТипОперации = Перечисления.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих Тогда
		Шапка.ИспользоватьСерииНоменклатуры = ИспользоватьСерии
											И Не ИспользоватьОрдернуюСхемуПриПриемке
											И Объект.Статус = Перечисления.СтатусыСборокТоваров.СобраноРазобрано;
		Шапка.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПриемкаСобранныхКомплектов);
	Иначе
		Шапка.ИспользоватьСерииНоменклатуры = ИспользоватьСерии;
		Шапка.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ОтгрузкаКомплектовДляРазборки);
	КонецЕсли;	
		
	Шапка.ПоляСвязи.Добавить("ДокументРезерваСерий");
	
	Шапка.ЭтоНакладная = Истина;
	Шапка.ПланированиеОтгрузки = Объект.Статус = Перечисления.СтатусыСборокТоваров.КСборкеРазборке
												Или Объект.Статус = Перечисления.СтатусыСборокТоваров.ВРаботе;
	Шапка.ПланированиеОтбора   = Объект.Статус = Перечисления.СтатусыСборокТоваров.КСборкеРазборке
												Или Объект.Статус = Перечисления.СтатусыСборокТоваров.ВРаботе;
	Шапка.ФактОтбора           = Объект.Статус = Перечисления.СтатусыСборокТоваров.СобраноРазобрано;
	
	Шапка.ТоварВШапке = Истина;
	
	
	ТЧ = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	Если Объект.ТипОперации = Перечисления.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих Тогда
		ТЧ.ИспользоватьСерииНоменклатуры = ИспользоватьСерии;
		ТЧ.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ОтгрузкаКомплектующихДляСборки);
	Иначе
		ТЧ.ИспользоватьСерииНоменклатуры = ИспользоватьСерии
										И Не ИспользоватьОрдернуюСхемуПриПриемке
										И Объект.Статус = Перечисления.СтатусыСборокТоваров.СобраноРазобрано;
		ТЧ.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПриемкаКомплектующихПослеРазборки);
	КонецЕсли;	
	
	ТЧ.ПоляСвязи.Добавить("ДокументРезерваСерий");
	
	ТЧ.ЭтоНакладная = Истина;
	ТЧ.ПланированиеОтгрузки = Объект.Статус = Перечисления.СтатусыСборокТоваров.КСборкеРазборке
										  Или Объект.Статус = Перечисления.СтатусыСборокТоваров.ВРаботе;
	ТЧ.ПланированиеОтбора   = Объект.Статус = Перечисления.СтатусыСборокТоваров.КСборкеРазборке
										Или Объект.Статус = Перечисления.СтатусыСборокТоваров.ВРаботе;
	ТЧ.ФактОтбора           = Объект.Статус = Перечисления.СтатусыСборокТоваров.СобраноРазобрано;
	ТЧ.ТоварВШапке = Ложь;
	
	ПараметрыУказанияСерий = Новый Структура;
	
	ПараметрыУказанияСерий.Вставить("Шапка",Шапка);
	ПараметрыУказанияСерий.Вставить("ТЧ",ТЧ);	
	
	Возврат ПараметрыУказанияСерий;

КонецФункции

// Функция определяет реквизиты выбранного перемещения товаров.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.СборкаТоваров - Ссылка на документ
//
// Возвращаемое значение:
//	Структура - реквизиты сборки товаров
//
Функция ПолучитьРеквизитыДокумента(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Организация КАК Организация
	|ИЗ
	|	Документ.СборкаТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &ДокументСсылка
	|");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Дата = Выборка.Дата;
		Организация = Выборка.Организация;
	Иначе
		Дата = Дата(1,1,1);
		Организация = Неопределено;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("Дата, Организация",
		Дата,
		Организация,
	);
	
	Возврат СтруктураРеквизитов;

КонецФункции // ПолучитьРеквизитыДокумента()

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстСписаниеСобственныхКомплектующихНаСборку = "
	|ВЫБРАТЬ //// Списание собственных комплектующих на сборку (Дт 20.01 :: Кт 41.01)
	|
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(СтоимостьТоваров.СтоимостьРегл * Строки.Количество,0) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство) КАК СчетДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаКт,
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|		ИНАЧЕ Строки.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчетаКт,
	|	Операция.Склад КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Склад.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Строки.Номенклатура	КАК СубконтоКт1,
	|	Операция.Склад КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|ИЗ
	|	Документ.СборкаТоваров КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.ВидыЗапасов КАК Строки
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК СтоимостьТоваров
	|			ПО  СтоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура = Строки.Номенклатура
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика = Строки.Характеристика
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Склад = Строки.Ссылка.Склад
	|				И СтоимостьТоваров.Организация = Строки.Ссылка.Организация
	|				И НАЧАЛОПЕРИОДА(Строки.Ссылка.Дата, Месяц) = СтоимостьТоваров.Период
	|				И (Строки.ВидЗапасов = СтоимостьТоваров.ВидЗапасов И &УчитыватьСебестоимостьТоваровПоВидамЗапасов 
	|				ИЛИ Не &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
	|		ПО (Строки.Ссылка = Операция.Ссылка)
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И Операция.Статус = ЗНАЧЕНИЕ (Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ (Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|";
	
	ТекстОприходованиеКомплекта = "
	|ВЫБРАТЬ //// Оприходование собранного комплекта (Дт 41.01 :: Кт 20.01) 
	|
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(СтоимостьТоваров.СтоимостьРегл * Операция.Количество,0) КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА Операция.ВидЗапасов.ГруппаФинансовогоУчета
	|		ИНАЧЕ Операция.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчетаДт,
	|	Операция.Склад КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Склад.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Операция.Номенклатура	КАК СубконтоДт1,
	|	Операция.Склад КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	Операция.Количество КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.СборкаТоваров КАК Операция
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК СтоимостьТоваров
	|			ПО  СтоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура = Операция.Номенклатура
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика = Операция.Характеристика
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Склад = Операция.Склад
	|				И СтоимостьТоваров.Организация = Операция.Организация
	|				И НАЧАЛОПЕРИОДА(Операция.Дата, Месяц) = СтоимостьТоваров.Период
	|				И (Операция.ВидЗапасов = СтоимостьТоваров.ВидЗапасов И &УчитыватьСебестоимостьТоваровПоВидамЗапасов 
	|				ИЛИ Не &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.Статус = ЗНАЧЕНИЕ (Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ (Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|";
	
	ТекстСписаниеКомиссионныхКомплектующихНаСборку = "
	|ВЫБРАТЬ //// Списание комиссионных комплектующих на сборку (Дт 20.01 :: Кт 76.ОК)
	|
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(СтоимостьТоваров.СтоимостьРегл * Строки.Количество,0) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство) КАК СчетДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыКОформлениюОтчетовКомитенту) КАК СчетКт,
	|
	|	Строки.ВидЗапасов.Контрагент КАК СубконтоКт1,
	|	Строки.Номенклатура КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.СборкаТоваров КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.ВидыЗапасов КАК Строки
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК СтоимостьТоваров
	|			ПО СтоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура = Строки.Номенклатура
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика = Строки.Характеристика
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Склад = Строки.Ссылка.Склад
	|				И СтоимостьТоваров.Организация = Строки.Ссылка.Организация
	|				И НАЧАЛОПЕРИОДА(Строки.Ссылка.Дата, Месяц) = СтоимостьТоваров.Период
	|				И (Строки.ВидЗапасов = СтоимостьТоваров.ВидЗапасов И &УчитыватьСебестоимостьТоваровПоВидамЗапасов 
	|				ИЛИ Не &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
	|		ПО (Строки.Ссылка = Операция.Ссылка)
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И Операция.Статус = ЗНАЧЕНИЕ (Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ (Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|";
	
	ТекстСписаниеНаРасходыКомиссионныхКомплектующих = "
	|ВЫБРАТЬ //// Списание на расходы комиссионных комплектующих (Дт  :: Кт 004.01)
	|
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|   НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	0 КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Склад.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе) КАК СчетКт,
	|
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	Операция.Склад КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.СборкаТоваров КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|			Документ.СборкаТоваров.ВидыЗапасов КАК Строки
	|		ПО 
	|			(Строки.Ссылка = Операция.Ссылка)
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.ВидЗапасов.ТипЗапасов 			= ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И Операция.Статус = ЗНАЧЕНИЕ (Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ (Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|";
	
	ТекстСписаниеСобственногоКомплектаНаРазборку = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ //// Списание собственного комплекта на разборку (Дт 20.01 :: Кт 41.01)
	|
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(СтоимостьТоваров.СтоимостьРегл * Операция.Количество,0) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство) КАК СчетДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаКт,
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|		ИНАЧЕ Строки.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчетаКт,
	|	Операция.Склад КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Склад.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Строки.Номенклатура	КАК СубконтоКт1,
	|	Операция.Склад КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт,
	|	Операция.Количество КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|ИЗ
	|	Документ.СборкаТоваров КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.ВидыЗапасов КАК Строки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК СтоимостьТоваров
	|			ПО СтоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура = Строки.Номенклатура
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика = Строки.Характеристика
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Склад = Строки.Ссылка.Склад
	|				И СтоимостьТоваров.Организация = Строки.Ссылка.Организация
	|				И НАЧАЛОПЕРИОДА(Строки.Ссылка.Дата, Месяц) = СтоимостьТоваров.Период
	|				И (Строки.ВидЗапасов = СтоимостьТоваров.ВидЗапасов И &УчитыватьСебестоимостьТоваровПоВидамЗапасов 
	|				ИЛИ Не &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
	|		ПО (Строки.Ссылка = Операция.Ссылка)
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И Операция.Статус = ЗНАЧЕНИЕ (Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ (Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|";
	
	ТекстОприходованиеКомплектующих = "
	|ВЫБРАТЬ //// Оприходование комплектующих полученных из разборки  комплекта (Дт 41.01 :: Кт 20.01) 
	|
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(СтоимостьТоваров.СтоимостьРегл * Строки.Количество,0) КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|		ИНАЧЕ Строки.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчетаДт,
	|	Операция.Склад КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Склад.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Строки.НоменклатураОприходование КАК СубконтоДт1,
	|	Операция.Склад КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.КоличествоОприходование КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.СборкаТоваров КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.ВидыЗапасов КАК Строки
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК СтоимостьТоваров
	|			ПО  СтоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура = Строки.Номенклатура
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика = Строки.Характеристика
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Склад = Строки.Ссылка.Склад
	|				И СтоимостьТоваров.Организация = Строки.Ссылка.Организация
	|				И НАЧАЛОПЕРИОДА(Строки.Ссылка.Дата, Месяц) = СтоимостьТоваров.Период
	|				И (Строки.ВидЗапасов = СтоимостьТоваров.ВидЗапасов И &УчитыватьСебестоимостьТоваровПоВидамЗапасов 
	|				ИЛИ Не &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
	|		ПО (Строки.Ссылка = Операция.Ссылка)
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.Статус = ЗНАЧЕНИЕ (Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ (Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|";
	
	ТекстСписаниеКомиссионногоКомплектаНаРазборку = "
	|ВЫБРАТЬ //// Списание комиссионного комплекта на разборку (Дт 20.01 :: Кт 76.ОК)
	|
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(СтоимостьТоваров.СтоимостьРегл * Операция.Количество,0) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Склад.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство) КАК СчетДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыКОформлениюОтчетовКомитенту) КАК СчетКт,
	|
	|	Строки.ВидЗапасов.Контрагент	КАК СубконтоКт1,
	|	Строки.Номенклатура КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|ИЗ
	|	Документ.СборкаТоваров КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.ВидыЗапасов КАК Строки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК СтоимостьТоваров
	|			ПО СтоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура = Строки.Номенклатура
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика = Строки.Характеристика
	|				И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Склад = Строки.Ссылка.Склад
	|				И СтоимостьТоваров.Организация = Строки.Ссылка.Организация
	|				И НАЧАЛОПЕРИОДА(Строки.Ссылка.Дата, Месяц) = СтоимостьТоваров.Период
	|				И (Строки.ВидЗапасов = СтоимостьТоваров.ВидЗапасов И &УчитыватьСебестоимостьТоваровПоВидамЗапасов 
	|				ИЛИ Не &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
	|		ПО (Строки.Ссылка = Операция.Ссылка)
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И Операция.Статус = ЗНАЧЕНИЕ (Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ (Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|";
	
	ТекстСписаниеНаРасходыКомиссионногоКомплекта = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ //// Списание на расходы комиссионного комплекта (Дт  :: Кт 004.01)
	|
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|   НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	0 КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Склад.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе) КАК СчетКт,
	|
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	Операция.Склад КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Операция.Количество КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт
	|
	|ИЗ
	|	Документ.СборкаТоваров КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.ВидыЗапасов КАК Строки
	|		ПО (Строки.Ссылка = Операция.Ссылка)
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И Операция.Статус = ЗНАЧЕНИЕ (Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ (Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|";
	
	Возврат	ТекстСписаниеСобственныхКомплектующихНаСборку
	+ "ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеКомиссионныхКомплектующихНаСборку
	+ "ОБЪЕДИНИТЬ ВСЕ " + ТекстОприходованиеКомплекта
	+ "ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеНаРасходыКомиссионныхКомплектующих
	+ "ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеСобственногоКомплектаНаРазборку
	+ "ОБЪЕДИНИТЬ ВСЕ " + ТекстОприходованиеКомплектующих
	+ "ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеКомиссионногоКомплектаНаРазборку
	+ "ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеНаРасходыКомиссионногоКомплекта
	;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
//Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата             КАК Период,
	|	ДанныеДокумента.Ссылка           КАК Ссылка,
	|	ДанныеДокумента.Организация      КАК Организация,
	|	ДанныеДокумента.ЗаказНаСборку    КАК ЗаказНаСборку,
	|	ДанныеДокумента.ДатаРаспоряжения КАК ДатаРаспоряжения,
	|	ДанныеДокумента.Склад            КАК Склад,
	|	ДанныеДокумента.ТипОперации      КАК ТипОперации,
	|	ДанныеДокумента.Номенклатура     КАК Номенклатура,
	|	ДанныеДокумента.Характеристика   КАК Характеристика,
	|	ДанныеДокумента.Статус           КАК Статус,
	|	ДанныеДокумента.СборкаПодДеятельность                         КАК СборкаПодДеятельность,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)                      КАК ТипЗапасовИзШапки,
	|	ДанныеДокумента.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке    КАК ОрдернаяСхемаПриОтгрузке,
	|	ДанныеДокумента.Склад.ИспользоватьОрдернуюСхемуПриПоступлении КАК ОрдернаяСхемаПриПоступлении,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)    КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.СборкаТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();

	ИнициализироватьКлючиАналитикиНоменклатуры(Реквизиты);
	
	Запрос.УстановитьПараметр("Склад",                                       Реквизиты.Склад);
	Запрос.УстановитьПараметр("Период",                                      Реквизиты.Период);
	Запрос.УстановитьПараметр("Статус",                                      Реквизиты.Статус);
	Запрос.УстановитьПараметр("ДатаРаспоряжения",                            Реквизиты.ДатаРаспоряжения);
	Запрос.УстановитьПараметр("Склад",                                       Реквизиты.Склад);
	Запрос.УстановитьПараметр("Организация",                                 Реквизиты.Организация);
	Запрос.УстановитьПараметр("ТипОперации",                                 Реквизиты.ТипОперации);
	Запрос.УстановитьПараметр("ЗаказНаСборку",                               Реквизиты.ЗаказНаСборку);
	Запрос.УстановитьПараметр("СборкаПоЗаказу",                              ЗначениеЗаполнено(Реквизиты.ЗаказНаСборку));
	Запрос.УстановитьПараметр("АналитикаКомплекта",                          РегистрыСведений.АналитикаУчетаНоменклатуры.ЗначениеКлючаАналитики(Реквизиты));
	Запрос.УстановитьПараметр("СборкаКомплектующих",                         Реквизиты.ТипОперации = Перечисления.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",                       Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("ОрдернаяСхемаПриОтгрузке",                    Реквизиты.ОрдернаяСхемаПриОтгрузке);
	Запрос.УстановитьПараметр("ОрдернаяСхемаПриПоступлении",                 Реквизиты.ОрдернаяСхемаПриПоступлении);
	Запрос.УстановитьПараметр("ТипЗапасовИзШапки",                           Реквизиты.ТипЗапасовИзШапки);
	Запрос.УстановитьПараметр("СборкаПодДеятельность",                       Реквизиты.СборкаПодДеятельность);
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов", ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоВидамЗапасов"));
	Запрос.УстановитьПараметр("ИспользоватьОбособленноеОбеспечениеЗаказов",  ПолучитьФункциональнуюОпцию("ИспользоватьОбособленноеОбеспечениеЗаказов"));

	Запрос.Текст =
	// 0 ВтТаблицаТовары
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура                          КАК Номенклатура,
	|	ТаблицаТовары.Характеристика                        КАК Характеристика,
	|	ТаблицаТовары.ДокументРезерваСерий                  КАК ДокументРезерваСерий,
	|	ТаблицаТовары.Количество                            КАК Количество,
	|	ТаблицаТовары.СтатусУказанияСерий                   КАК СтатусУказанияСерий,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ТаблицаТовары.НомерСтроки,
	|	0 КАК Порядок
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.Количество <> 0
	|	И (НЕ ТаблицаТовары.СтатусУказанияСерий В (4, 6, 8, 10))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.ДокументРезерваСерий,
	|	ТаблицаСерии.Количество,
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий),
	|	ТаблицаСерии.Серия,
	|	МИНИМУМ(ТаблицаСерии.НомерСтроки),
	|	1
	|ИЗ
	|	Документ.СборкаТоваров.Серии КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.Товары КАК ТаблицаТовары
	|		ПО ТаблицаСерии.Ссылка = ТаблицаТовары.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТаблицаТовары.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаТовары.Характеристика
	|			И ТаблицаСерии.ДокументРезерваСерий = ТаблицаТовары.ДокументРезерваСерий
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.ДокументРезерваСерий,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.Количество
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий) В (4, 6, 8, 10)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 1 ВтТаблицаТоварыШапка
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.ДокументРезерваСерий КАК ДокументРезерваСерий,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	1 КАК НомерСтроки,
	|	2 КАК Порядок
	|ПОМЕСТИТЬ ВтТаблицаТоварыШапка
	|ИЗ
	|	Документ.СборкаТоваров КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.Количество <> 0
	|	И (НЕ ТаблицаТовары.СтатусУказанияСерий В (4, 6, 8, 10))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.ДокументРезерваСерий,
	|	ТаблицаСерии.Количество,
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий),
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.НомерСтроки,
	|	3
	|ИЗ
	|	Документ.СборкаТоваров.Серии КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК ТаблицаТовары
	|		ПО ТаблицаСерии.Ссылка = ТаблицаТовары.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТаблицаТовары.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаТовары.Характеристика
	|			И ТаблицаСерии.ДокументРезерваСерий = ТаблицаТовары.ДокументРезерваСерий
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.ДокументРезерваСерий,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.Количество,
	|	ТаблицаСерии.НомерСтроки
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий) В (4, 6, 8, 10)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 2 ВтТаблицаКомплекты
	|ВЫБРАТЬ
	|	0 КАК НомерСтроки,
	|	ТаблицаКомплекты.Номенклатура   КАК Номенклатура,
	|	ТаблицаКомплекты.Характеристика КАК Характеристика,
	|	ТаблицаКомплекты.ВидЗапасов     КАК ВидЗапасов,
	|	ТаблицаКомплекты.НомерГТД       КАК НомерГТД,
	|	ТаблицаКомплекты.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаКомплекты.Количество     КАК Количество
	|ПОМЕСТИТЬ ВтТаблицаКомплекты
	|ИЗ
	|	Документ.СборкаТоваров КАК ТаблицаКомплекты
	|ГДЕ
	|	ТаблицаКомплекты.Ссылка = &Ссылка
	|	И &СборкаКомплектующих
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаКомплекты.НомерСтроки,
	|	ТаблицаКомплекты.Номенклатура,
	|	ТаблицаКомплекты.Характеристика,
	|	ТаблицаКомплекты.ВидЗапасов,
	|	ТаблицаКомплекты.НомерГТД,
	|	ТаблицаКомплекты.АналитикаУчетаПартий,
	|	ТаблицаКомплекты.Количество
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ТаблицаКомплекты
	|ГДЕ
	|	ТаблицаКомплекты.Ссылка = &Ссылка
	|	И (НЕ &СборкаКомплектующих)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 3 ВтТаблицаОбеспечения
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки          КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура         КАК Номенклатура,
	|	ТаблицаТовары.Характеристика       КАК Характеристика,
	|	ТаблицаТовары.Количество           КАК Количество,
	|	ТаблицаТовары.ДокументРезерваСерий КАК ДокументРезерваСерий,
	|	ТоварыЗаказа.ВариантОбеспечения    КАК ВариантОбеспечения,
	|
	|	ВЫБОР КОГДА &СборкаПоЗаказу И ТаблицаТовары.СтатусУказанияСерий = 10
	|			И ТаблицаТовары.ДокументРезерваСерий <> Неопределено ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ                                  КАК СтрокаОбеспеченаЗаказом,
	|	ВЫБОР КОГДА &СборкаПоЗаказу
	|			И ТоварыЗаказа.ВариантОбеспечения
	|					В 	(ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ПодЗаказ),
	|							ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада)) ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ                                  КАК ОбособленнаяСтрока,
	|
	|	ВЫБОР
	|		КОГДА &СборкаПоЗаказу И ТоварыЗаказа.ВариантОбеспечения <> ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТовары.Ссылка.ЗаказНаСборку.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
	|						ТаблицаТовары.Ссылка.ЗаказНаСборку.Назначение
	|					ИНАЧЕ
	|						ТаблицаТовары.Ссылка.ЗаказНаСборку.НазначениеТовары
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                              КАК Назначение
	|ПОМЕСТИТЬ ВтТаблицаОбеспечения
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаСборку.Товары КАК ТоварыЗаказа
	|		ПО ТаблицаТовары.Ссылка.ЗаказНаСборку = ТоварыЗаказа.Ссылка
	|			И ТаблицаТовары.Номенклатура      = ТоварыЗаказа.Номенклатура
	|			И ТаблицаТовары.Характеристика    = ТоварыЗаказа.Характеристика
	|			И ТаблицаТовары.КодСтроки         = ТоварыЗаказа.КодСтроки
	|			И &СборкаПоЗаказу
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 4 ТаблицаЗаказыНаСборку
	|ВЫБРАТЬ
	|	1                                      КАК Порядок,
	|	0                                      КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&ДатаРаспоряжения                      КАК Период,
	|	Реквизиты.ЗаказНаСборку                КАК ЗаказНаСборку,
	|	Реквизиты.Номенклатура                 КАК Номенклатура,
	|	Реквизиты.Характеристика               КАК Характеристика,
	|	1                                      КАК КодСтроки,
	|	ВЫБОР
	|		КОГДА &СборкаКомплектующих ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияЗапасов.Поступление)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияЗапасов.Отгрузка)
	|	КОНЕЦ КАК ТипСборки,
	|	Реквизиты.Количество КАК Заказано,
	|	Реквизиты.Количество КАК КОформлению
	|ИЗ
	|	Документ.СборкаТоваров КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|	И &СборкаПоЗаказу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ТаблицаТовары.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&ДатаРаспоряжения,
	|	&ЗаказНаСборку,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.КодСтроки,
	|	ВЫБОР
	|		КОГДА &СборкаКомплектующих ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияЗапасов.Отгрузка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияЗапасов.Поступление)
	|	КОНЕЦ,
	|	ТаблицаТовары.Количество,
	|	ТаблицаТовары.Количество
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И &СборкаПоЗаказу
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	// 5 ТаблицаСвободныеОстатки
	//Cборка
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Склад                                 КАК Склад,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &СборкаПоЗаказу ТОГДА
	|			ТаблицаТовары.Количество
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                  КАК ВРезерве,
	|	ТаблицаТовары.Количество               КАК ВНаличии
	|ИЗ
	|	ВтТаблицаОбеспечения КАК ТаблицаТовары
	|ГДЕ
	|	&СборкаКомплектующих
	|	И НЕ ТаблицаТовары.СтрокаОбеспеченаЗаказом 
	|	И НЕ ТаблицаТовары.ОбособленнаяСтрока
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Склад                                 КАК Склад,
	|	Т.Номенклатура                         КАК Номенклатура,
	|	Т.Характеристика                       КАК Характеристика,
	|	0                                      КАК ВРезерве,
	|	Т.Количество                           КАК ВНаличии
	|ИЗ
	|	Документ.СборкаТоваров КАК Т
	|ГДЕ
	|	Т.Ссылка = &Ссылка
	|	И &СборкаКомплектующих
	|	И ЕСТЬNULL(Т.ЗаказНаСборку.Назначение,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И (НЕ &ОрдернаяСхемаПриПоступлении ИЛИ &ИспользоватьОбособленноеОбеспечениеЗаказов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//Разборка
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Склад                                 КАК Склад,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	0                                      КАК ВРезерве,
	|	ТаблицаТовары.Количество               КАК ВНаличии
	|ИЗ
	|	ВтТаблицаОбеспечения КАК ТаблицаТовары
	|ГДЕ
	|	НЕ &СборкаКомплектующих
	|	И (НЕ &ОрдернаяСхемаПриПоступлении)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Склад                                 КАК Склад,
	|	Т.Номенклатура                         КАК Номенклатура,
	|	Т.Характеристика                       КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &СборкаПоЗаказу ТОГДА
	|			Т.Количество
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                  КАК ВРезерве,
	|	Т.Количество                           КАК НаличиеПодЗаказ
	|ИЗ
	|	Документ.СборкаТоваров КАК Т
	|ГДЕ
	|	Т.Ссылка = &Ссылка
	|	И НЕ &СборкаКомплектующих
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 6 ТаблицаТоварыКОтгрузке
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Склад КАК Склад,
	|	&Ссылка КАК ДокументОтгрузки,
	|	ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ДанныеДокумента.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтатусУказанияСерий = 10 ТОГДА
	|			ДанныеДокумента.Серия
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	ДанныеДокумента.Количество КАК КОтгрузке,
	|	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокумента.Порядок КАК Порядок
	|ИЗ
	|	ВтТаблицаТоварыШапка КАК ДанныеДокумента
	|ГДЕ
	|	(НЕ &СборкаКомплектующих)
	|	И &ОрдернаяСхемаПриОтгрузке
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Склад,
	|	&Ссылка,
	|	ТаблицаКомплектующие.Номенклатура,
	|	ТаблицаКомплектующие.Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаКомплектующие.СтатусУказанияСерий = 10
	|			ТОГДА ТаблицаКомплектующие.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаКомплектующие.Количество,
	|	ТаблицаКомплектующие.НомерСтроки,
	|	ТаблицаКомплектующие.Порядок
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаКомплектующие
	|ГДЕ
	|	&СборкаКомплектующих
	|	И &ОрдернаяСхемаПриОтгрузке
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 7 ТаблицаТоварыКПоступлению
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	1                                      КАК НомерСтроки,
	|	0                                      КАК Порядок,
	|	&Период                                КАК Период,
	|	ДанныеДокумента.Склад                  КАК Склад,
	|	ВЫБОР
	|		КОГДА &СборкаПоЗаказу ТОГДА
	|			&ЗаказНаСборку
	|		ИНАЧЕ
	|			ДанныеДокумента.Ссылка
	|	КОНЕЦ                                  КАК ДокументПоступления,
	|	ДанныеДокумента.Номенклатура           КАК Номенклатура,
	|	ДанныеДокумента.Характеристика         КАК Характеристика,
	|	ДанныеДокумента.Количество             КАК КПоступлению
	|ИЗ
	|	Документ.СборкаТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &СборкаКомплектующих
	|	И &ОрдернаяСхемаПриПоступлении
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.ВРаботе),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаКомплектующие.НомерСтроки,
	|	1,
	|	&Период,
	|	&Склад,
	|	ВЫБОР
	|		КОГДА &СборкаПоЗаказу
	|			ТОГДА &ЗаказНаСборку
	|		ИНАЧЕ ТаблицаКомплектующие.Ссылка
	|	КОНЕЦ,
	|	ТаблицаКомплектующие.Номенклатура,
	|	ТаблицаКомплектующие.Характеристика,
	|	ТаблицаКомплектующие.Количество
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ТаблицаКомплектующие
	|ГДЕ
	|	ТаблицаКомплектующие.Ссылка = &Ссылка
	|	И (НЕ &СборкаКомплектующих)
	|	И &ОрдернаяСхемаПриПоступлении
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.ВРаботе), ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 8 ТаблицаТоварыНаСкладах
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Склад КАК Склад,
	|	ТаблицаКомплекты.Номенклатура КАК Номенклатура,
	|	ТаблицаКомплекты.Характеристика КАК Характеристика,
	|	ТаблицаКомплекты.Серия КАК Серия,
	|	ТаблицаКомплекты.Количество КАК ВНаличии,
	|	0 КАК КОтгрузке,
	|	ТаблицаКомплекты.НомерСтроки КАК НомерСтроки,
	|	ТаблицаКомплекты.Порядок КАК Порядок,
	|   ЛОЖЬ КАК КонтролироватьОстатки
	|ИЗ
	|	ВтТаблицаТоварыШапка КАК ТаблицаКомплекты
	|ГДЕ
	|	&ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|	И (НЕ &ОрдернаяСхемаПриПоступлении)
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Склад,
	|	ТаблицаКомплектующие.Номенклатура,
	|	ТаблицаКомплектующие.Характеристика,
	|	ТаблицаКомплектующие.Серия,
	|	0,
	|	ТаблицаКомплектующие.Количество,
	|	ТаблицаКомплектующие.НомерСтроки,
	|	ТаблицаКомплектующие.Порядок,
	|	ВЫБОР
	|		КОГДА ТаблицаКомплектующие.СтатусУказанияСерий В (6, 8)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаКомплектующие
	|ГДЕ
	|	&ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|	И (НЕ &ОрдернаяСхемаПриОтгрузке)
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.КСборкеРазборке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Период,
	|	&Склад,
	|	ТаблицаКомплектующие.Номенклатура,
	|	ТаблицаКомплектующие.Характеристика,
	|	ТаблицаКомплектующие.Серия,
	|	ТаблицаКомплектующие.Количество,
	|	0,
	|	ТаблицаКомплектующие.НомерСтроки,
	|	ТаблицаКомплектующие.Порядок,
	|	ВЫБОР
	|		КОГДА ТаблицаКомплектующие.СтатусУказанияСерий В (6, 8)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаКомплектующие
	|ГДЕ
	|	&ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|	И (НЕ &ОрдернаяСхемаПриОтгрузке)
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.ВРаботе), ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Склад,
	|	ТаблицаКомплекты.Номенклатура,
	|	ТаблицаКомплекты.Характеристика,
	|	ТаблицаКомплекты.Серия,
	|	0,
	|	ТаблицаКомплекты.Количество,
	|	ТаблицаКомплекты.НомерСтроки,
	|	ТаблицаКомплекты.Порядок,
	|	ВЫБОР
	|		КОГДА ТаблицаКомплекты.СтатусУказанияСерий В (6, 8)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	ВтТаблицаТоварыШапка КАК ТаблицаКомплекты
	|ГДЕ
	|	&ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.КСборкеРазборке)
	|	И (НЕ &ОрдернаяСхемаПриОтгрузке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Период,
	|	&Склад,
	|	ТаблицаКомплекты.Номенклатура,
	|	ТаблицаКомплекты.Характеристика,
	|	ТаблицаКомплекты.Серия,
	|	ТаблицаКомплекты.Количество,
	|	0,
	|	ТаблицаКомплекты.НомерСтроки,
	|	ТаблицаКомплекты.Порядок,
	|	ВЫБОР
	|		КОГДА ТаблицаКомплекты.СтатусУказанияСерий В (6, 8)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	ВтТаблицаТоварыШапка КАК ТаблицаКомплекты
	|ГДЕ
	|	&ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|	И (НЕ &ОрдернаяСхемаПриОтгрузке)
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.ВРаботе), ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Склад,
	|	ТаблицаКомплектующие.Номенклатура,
	|	ТаблицаКомплектующие.Характеристика,
	|	ТаблицаКомплектующие.Серия,
	|	ТаблицаКомплектующие.Количество,
	|	0,
	|	ТаблицаКомплектующие.НомерСтроки,
	|	ТаблицаКомплектующие.Порядок,
	|	ЛОЖЬ
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаКомплектующие
	|ГДЕ
	|	&ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|	И (НЕ &ОрдернаяСхемаПриПоступлении)
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 9 ТаблицаДвижениеТоваров
	|ВЫБРАТЬ
	|	1                                   КАК НомерСтроки,
	|	0                                   КАК Порядок,
	|	&Период                             КАК Период,
	|	&ЗаказНаСборку                      КАК Распоряжение,
	|	Реквизиты.Склад                     КАК Склад,
	|	Реквизиты.Номенклатура              КАК Номенклатура,
	|	Реквизиты.Характеристика            КАК Характеристика,
	|	Реквизиты.ЗаказНаСборку.Назначение  КАК Назначение,
	|	НЕОПРЕДЕЛЕНО                        КАК ВариантОбеспечения,
	|	0                                   КАК ПланируемаяОтгрузка,
	|	-Реквизиты.Количество               КАК ПланируемоеПоступление
	|ИЗ
	|	Документ.СборкаТоваров КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка И &СборкаПоЗаказу
	|	И &СборкаКомплектующих
	|	И (Реквизиты.СтатусУказанияСерий <> 10
	|			ИЛИ Реквизиты.ДокументРезерваСерий = ЗНАЧЕНИЕ(Документ.ЗаказНаСборку.ПустаяСсылка))
	|	И (НЕ &ОрдернаяСхемаПриПоступлении ИЛИ &ИспользоватьОбособленноеОбеспечениеЗаказов)
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки,
	|	3,
	|	&Период,
	|	&ЗаказНаСборку,
	|	&Склад,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Назначение         КАК Назначение,
	|	ТаблицаТовары.ВариантОбеспечения КАК ВариантОбеспечения,
	|	-ТаблицаТовары.Количество        КАК ПланируемаяОтгрузка,
	|	0                                КАК ПланируемоеПоступление
	|ИЗ
	|	ВтТаблицаОбеспечения КАК ТаблицаТовары
	|ГДЕ
	|	&СборкаПоЗаказу
	|	И &СборкаКомплектующих
	|	И НЕ ТаблицаТовары.СтрокаОбеспеченаЗаказом
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Разборка
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки,
	|	1,
	|	&Период,
	|	&ЗаказНаСборку,
	|	&Склад,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	НЕОПРЕДЕЛЕНО                     КАК Назначение,
	|	НЕОПРЕДЕЛЕНО                     КАК ВариантОбеспечения,
	|	0                                КАК ПланируемаяОтгрузка,
	|	-ТаблицаТовары.Количество        КАК ПланируемоеПоступление
	|ИЗ
	|	ВтТаблицаОбеспечения КАК ТаблицаТовары
	|ГДЕ
	|	&СборкаПоЗаказу
	|	И (НЕ &СборкаКомплектующих)
	|	И НЕ ТаблицаТовары.СтрокаОбеспеченаЗаказом
	|	И (НЕ &ОрдернаяСхемаПриПоступлении)
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	2,
	|	&Период,
	|	&ЗаказНаСборку,
	|	Реквизиты.Склад,
	|	Реквизиты.Номенклатура,
	|	Реквизиты.Характеристика,
	|	НЕОПРЕДЕЛЕНО                    КАК Назначение,
	|	НЕОПРЕДЕЛЕНО                    КАК ВариантОбеспечения,
	|	-Реквизиты.Количество           КАК ПланируемаяОтгрузка,
	|	0                               КАК ПланируемоеПоступление
	|ИЗ
	|	Документ.СборкаТоваров КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка И &СборкаПоЗаказу
	|	И (НЕ &СборкаКомплектующих)
	|	И (Реквизиты.СтатусУказанияСерий <> 10
	|			ИЛИ Реквизиты.ДокументРезерваСерий = ЗНАЧЕНИЕ(Документ.ЗаказНаСборку.ПустаяСсылка))
	|
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 10 ТаблицаДвиженияСерийТоваров
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура КАК Номенклатура,
	|	ТаблицаСерии.Характеристика КАК Характеристика,
	|	ТаблицаСерии.Серия КАК Серия,
	|	ТаблицаСерии.Количество КАК Количество,
	|	&Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка) КАК Помещение,
	|	ВЫБОР
	|		КОГДА &ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаСобранныхКомплектов)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтгрузкаКомплектовДляРазборки)
	|	КОНЕЦ КАК СкладскаяОперация,
	|	ТаблицаСерии.Ссылка КАК Документ,
	|	&Период КАК Период,
	|	ТаблицаСерии.Ссылка КАК Регистратор,
	|	ТаблицаСерии.НомерСтроки КАК НомерСтроки,
	|	0 КАК Порядок
	|ИЗ
	|	Документ.СборкаТоваров.Серии КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК СборкаТоваров
	|		ПО ТаблицаСерии.Ссылка = СборкаТоваров.Ссылка
	|			И ТаблицаСерии.Номенклатура = СборкаТоваров.Номенклатура
	|			И ТаблицаСерии.Характеристика = СборкаТоваров.Характеристика
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.Количество,
	|	&Склад,
	|	ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА &ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтгрузкаКомплектующихДляСборки)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаКомплектующихПослеРазборки)
	|	КОНЕЦ,
	|	ТаблицаСерии.Ссылка,
	|	&Период,
	|	ТаблицаСерии.Ссылка,
	|	ТаблицаСерии.НомерСтроки,
	|	1
	|ИЗ
	|	Документ.СборкаТоваров.Серии КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.Товары КАК СборкаТоваров
	|		ПО ТаблицаСерии.Ссылка = СборкаТоваров.Ссылка
	|			И ТаблицаСерии.Номенклатура = СборкаТоваров.Номенклатура
	|			И ТаблицаСерии.Характеристика = СборкаТоваров.Характеристика
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 11 ТаблицаРезервыСерийТоваров
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаСерии.ДокументРезерваСерий КАК ДокументРезерва,
	|	&Склад КАК Склад,
	|	ТаблицаСерии.Номенклатура КАК Номенклатура,
	|	ТаблицаСерии.Характеристика КАК Характеристика,
	|	ТаблицаСерии.Серия КАК Серия,
	|	ТаблицаСерии.Количество КАК Количество,
	|	ТаблицаСерии.НомерСтроки КАК НомерСтроки,
	|	0 КАК Порядок
	|ИЗ
	|	Документ.СборкаТоваров.Серии КАК ТаблицаСерии
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И &СборкаПоЗаказу
	|	И ТаблицаСерии.ДокументРезерваСерий <> ЗНАЧЕНИЕ(Документ.ЗаказНаСборку.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&ЗаказНаСборку,
	|	&Склад,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	ТаблицаТовары.Количество,
	|	ТаблицаТовары.НомерСтроки,
	|	1
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И &СборкаПоЗаказу
	|	И ТаблицаТовары.СтатусУказанияСерий = 10
	|	И ТаблицаТовары.ДокументРезерваСерий = ЗНАЧЕНИЕ(Документ.ЗаказНаСборку.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&ЗаказНаСборку,
	|	&Склад,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	ТаблицаТовары.Количество,
	|	1,
	|	2
	|ИЗ
	|	Документ.СборкаТоваров КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И &СборкаПоЗаказу
	|	И ТаблицаТовары.СтатусУказанияСерий = 10
	|	И ТаблицаТовары.ДокументРезерваСерий = ЗНАЧЕНИЕ(Документ.ЗаказНаСборку.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	Запрос.Текст = Запрос.Текст
		+ ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций() 		  //12
		+ ТекстЗапросаТаблицаТоварыОрганизаций()                 		  //13
		+ ТекстЗапросаТаблицаТоварыОрганизацийКПередаче()        		  //14
		+ ТекстЗапросаТаблицаТоварыКОформлениюОтчетовКомитенту() 		  //15
		+ ТекстЗапросаВтАналитикаКомплектующих()                 		  //16
		+ ТекстЗапросаВтАналитикаКомплектов()                             //17
		+ ТекстЗапросаТаблицаКомплектов()                       	  	  //18
		+ ТекстЗапросаТаблицаКомплектующих()                     		  //19
		+ ТекстЗапросаТаблицаПартииТоваровОрганизацийПоследовательность() //20
		+ ТекстЗапросаТаблицаПартииТоваровОрганизаций()				 	  //21
		+
		"
		//21
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	Т.Номенклатура                         КАК Номенклатура,
		|	Т.Характеристика                       КАК Характеристика,
		|	Т.Назначение                           КАК Назначение,
		|	&Склад                                 КАК Склад,
		|	ВЫБОР
		|		КОГДА Т.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ПодЗаказ) ТОГДА
		|			Т.Количество
		|		ИНАЧЕ 0
		|	КОНЕЦ                                  КАК Потребность,
		|	0                                      КАК Заказано,
		|	0                                      КАК КЗаказу,
		|	ВЫБОР КОГДА Т.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада) ТОГДА
		|		Т.Количество
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ                                  КАК НаличиеСоСклада,
		|	ВЫБОР КОГДА  Т.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ПодЗаказ) ТОГДА
		|		Т.Количество
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ                                  КАК НаличиеПодЗаказ
		|
		|ИЗ
		|	ВтТаблицаОбеспечения КАК Т
		|ГДЕ
		|	Т.ОбособленнаяСтрока
		|	И &СборкаКомплектующих
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	Т.Номенклатура                         КАК Номенклатура,
		|	Т.Характеристика                       КАК Характеристика,
		|	Т.ЗаказНаСборку.Назначение             КАК Назначение,
		|	&Склад                                 КАК Склад,
		|	0                                      КАК Потребность,
		|	ВЫБОР КОГДА &СборкаПоЗаказу  ТОГДА
		|			-Т.Количество
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ                                  КАК Заказано,
		|	ВЫБОР КОГДА &СборкаПоЗаказу  ТОГДА
		|			0
		|		ИНАЧЕ
		|			-Т.Количество 
		|	КОНЕЦ                                  КАК КЗаказу,
		|	0                                      КАК НаличиеСоСклада,
		|	Т.Количество                           КАК НаличиеПодЗаказ
		|ИЗ
		|	Документ.СборкаТоваров КАК Т
		|ГДЕ
		|	Т.Ссылка = &Ссылка
		|	И &СборкаКомплектующих
		|	И Т.ЗаказНаСборку.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|";
	Результат = Запрос.ВыполнитьПакет();
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	// Результат[0] - ВтТаблицаТовары
	// Результат[1] - ВтТаблицаТоварыШапка
	// Результат[2] - ВтТаблицаКомплекты
	// Результат[3] - ВтТаблицаОбеспечения
	ТаблицыДляДвижений.Вставить("ТаблицаЗаказыНаСборку",                    		Результат[4].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаСвободныеОстатки",                  		Результат[5].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыКОтгрузке",                   		Результат[6].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыКПоступлению",                		Результат[7].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыНаСкладах",                   		Результат[8].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДвижениеТоваров",                   		Результат[9].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДвиженияСерийТоваров",              		Результат[10].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаРезервыСерийТоваров",               		Результат[11].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДатыПоступленияТоваровОрганизаций", 		Результат[12].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыОрганизаций",                 		Результат[13].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыОрганизацийКПередаче",        		Результат[14].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыКОформлениюОтчетовКомитенту", 		Результат[15].Выгрузить());
	// Результат[16] - ВтАналитикаКомплектующих
	// Результат[17] - ВтАналитикаКомплектов
	ТаблицыДляДвижений.Вставить("ТаблицаСебестоимостьТоваров", РаспределитьСтоимостьКомплектующихНаКомплекты(
		Результат[18].Выгрузить(), // ТаблицаКомплектов
		Результат[19].Выгрузить(), // ТаблицаКомплектующих
		Реквизиты));
	ТаблицыДляДвижений.Вставить("ТаблицаПартииТоваровОрганизацийПоследовательность", Результат[20].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПартииТоваровОрганизаций",                 	 Результат[21].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаОбеспечениеЗаказов",                         Результат[22].Выгрузить());
	
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиНоменклатуры(Реквизиты)

	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика
	|ИЗ (
	|	ВЫБРАТЬ
	|		&Склад КАК Склад,
	|		ДанныеДокумента.Номенклатура КАК Номенклатура,
	|		ДанныеДокумента.Характеристика КАК Характеристика
	|	ИЗ
	|		Документ.СборкаТоваров КАК ДанныеДокумента
	|	ГДЕ
	|		ДанныеДокумента.Ссылка = &Ссылка
	|		И &ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Склад КАК Склад,
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика
	|	ИЗ
	|		Документ.СборкаТоваров.Товары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И &ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|			ТаблицаВидыЗапасов.ВидЗапасов.Комитент
	|		ИНАЧЕ
	|			&Склад
	|		КОНЕЦ КАК Склад,
	|		ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|		ТаблицаВидыЗапасов.Характеристика КАК Характеристика
	|	ИЗ
	|		Документ.СборкаТоваров.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|	ГДЕ
	|		ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|
	|	) КАК ТаблицаТовары
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|		И ТаблицаТовары.Характеристика = Аналитика.Характеристика
	|		И ТаблицаТовары.Склад = Аналитика.Склад
	|ГДЕ
	|	Аналитика.Номенклатура ЕСТЬ NULL
	|");
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Склад", Реквизиты.Склад);
	Запрос.УстановитьПараметр("ТипОперации", Реквизиты.ТипОперации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка)
	КонецЦикла;

КонецПроцедуры

Функция ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаПоступления,
	|	ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.ВидЗапасов     КАК ВидЗапасов,
	|	ТаблицаТовары.НомерГТД       КАК НомерГТД
	|ИЗ
	|	ВтТаблицаКомплекты КАК ТаблицаТовары
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|	ПО
	|		ТаблицаТовары.ВидЗапасов = ПоступленияТоваров.ВидЗапасов 
	|		И ТаблицаТовары.Номенклатура = ПоступленияТоваров.Номенклатура
	|		И ТаблицаТовары.Характеристика = ПоступленияТоваров.Характеристика 
	|		И ТаблицаТовары.НомерГТД = ПоступленияТоваров.НомерГТД 
	|
	|ГДЕ
	|	(ПоступленияТоваров.ДатаПоступления ЕСТЬ NULL
	|		ИЛИ ПоступленияТоваров.ДатаПоступления < НАЧАЛОПЕРИОДА(&Период, ДЕНЬ))
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.ВидЗапасов,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.НомерГТД
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизаций()
	
	ТекстЗапроса = "
	| ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Склад КАК Склад,
	|	&Организация КАК ОрганизацияОтгрузки,
	|	&Организация КАК Организация,
	|	ТаблицаТовары.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.НомерГТД КАК НомерГТД,
	|	ТаблицаТовары.Количество КАК Количество,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Неопределено КАК НалогообложениеНДС,
	|	КлючиАналитики.КлючАналитики КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ВтТаблицаКомплекты КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
	|		ПО ТаблицаТовары.Номенклатура = КлючиАналитики.Номенклатура
	|			И ТаблицаТовары.Характеристика = КлючиАналитики.Характеристика
	|			И (&Склад = КлючиАналитики.Склад)
	|ГДЕ
	|	&Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Период,
	|	&Склад,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.Организация
	|		ИНАЧЕ &Организация
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца
	|		ИНАЧЕ ТаблицаВидыЗапасов.ВидЗапасов
	|	КОНЕЦ,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД,
	|	ТаблицаВидыЗапасов.Количество,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&СборкаПодДеятельность КАК НалогообложениеНДС,
	|	КлючиАналитики.КлючАналитики
	|ИЗ
	|	Документ.СборкаТоваров.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
	|		ПО ТаблицаВидыЗапасов.Номенклатура = КлючиАналитики.Номенклатура
	|			И ТаблицаВидыЗапасов.Характеристика = КлючиАналитики.Характеристика
	|			И (&Склад = КлючиАналитики.Склад)
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизацийКПередаче()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Склад КАК Склад,
	|	ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.Организация КАК ОрганизацияВладелец,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасовПродавца,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество КАК Количество
	|ИЗ
	|	Документ.СборкаТоваров.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыКОформлениюОтчетовКомитенту()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаВидыЗапасов.ВидЗапасов.Валюта КАК Валюта,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	0 КАК Количество,
	|	ТаблицаВидыЗапасов.Количество КАК КоличествоСписано,
	|	0 КАК СуммаВыручки
	|
	|ИЗ
	|	Документ.СборкаТоваров.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтАналитикаКомплектующих()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикаНоменклатуры.КлючАналитики КАК АналитикаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика
	|
	|ПОМЕСТИТЬ ВтАналитикаКомплектующих
	|ИЗ
	|	Документ.СборкаТоваров.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура = АналитикаНоменклатуры.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = АналитикаНоменклатуры.Характеристика
	|		И (ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|			ТаблицаВидыЗапасов.ВидЗапасов.Комитент
	|		ИНАЧЕ
	|			&Склад
	|		КОНЕЦ = АналитикаНоменклатуры.Склад)
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	ВидЗапасов
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтАналитикаКомплектов()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикаНоменклатуры.КлючАналитики КАК АналитикаНоменклатуры,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика
	|
	|ПОМЕСТИТЬ ВтАналитикаКомплектов
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ТаблицаТовары
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|	ПО
	|		ТаблицаТовары.Номенклатура = АналитикаНоменклатуры.Номенклатура
	|		И ТаблицаТовары.Характеристика = АналитикаНоменклатуры.Характеристика
	|		И &Склад = АналитикаНоменклатуры.Склад
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И Не &СборкаКомплектующих // При разборке
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаКомплектов()

	ТекстЗапроса = "
	|// При сборке
	|ВЫБРАТЬ
	|	1 КАК НомерСтрокиДокумента,
	|	&Период КАК Период,
	|	&АналитикаКомплекта КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|	
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	| 		ДанныеДокумента.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	1 КАК ДоляСтоимости,
	|	ДанныеДокумента.Количество КАК Количество
	|ИЗ
	|	Документ.СборкаТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &СборкаКомплектующих // При сборке
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// При разборке
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтрокиДокумента,
	|	&Период КАК Период,
	|	Аналитика.АналитикаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|	
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	| 		ТаблицаТовары.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаТовары.ДоляСтоимости КАК ДоляСтоимости,
	|	ТаблицаТовары.Количество КАК Количество
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ТаблицаТовары
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитикаКомплектов КАК Аналитика
	|	ПО
	|		ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|		И ТаблицаТовары.Характеристика = Аналитика.Характеристика
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И НЕ &СборкаКомплектующих // При разборке
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаКомплектующих()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаВидыЗапасов.НомерСтроки) КАК НомерСтрокиДокумента,
	|	&Период КАК Период,
	|	1 КАК ДоляСтоимости,
	|	Аналитика.АналитикаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|	
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	| 		ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	СУММА(ТаблицаВидыЗапасов.Количество) КАК Количество,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.СборкаТоваров.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитикаКомплектующих КАК Аналитика
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура = Аналитика.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = Аналитика.Характеристика
	|		И ТаблицаВидыЗапасов.ВидЗапасов = Аналитика.ВидЗапасов
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|СГРУППИРОВАТЬ ПО
	|	Аналитика.АналитикаНоменклатуры,
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	| 		ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиДокумента
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииТоваровОрганизацийПоследовательность()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика
	|ИЗ
	|	ВтТаблицаКомплекты КАК ТаблицаТовары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Ссылка,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика
	|ИЗ
	|	Документ.СборкаТоваров.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииТоваровОрганизаций()
	
	ТекстЗапроса = "
	| ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Склад КАК Склад,
	|	&Организация КАК Организация,
	|	КлючиАналитики.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	&Ссылка КАК ДокументПоступления,
	|	ТаблицаТовары.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаТовары.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	ВтТаблицаКомплекты КАК ТаблицаТовары
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
	|		ПО 
	|			ТаблицаТовары.Номенклатура = КлючиАналитики.Номенклатура
	|			И ТаблицаТовары.Характеристика = КлючиАналитики.Характеристика
	|			И (&Склад = КлючиАналитики.Склад)
	|ГДЕ
	|	&Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано))
	|
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция РаспределитьСтоимостьКомплектующихНаКомплекты(ТаблицаКомплектов, ТаблицаКомплектующих, Реквизиты)

	ТаблицаСебестоимостьТоваров = РегистрыНакопления.СебестоимостьТоваров.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТаблицаСебестоимостьТоваров.Колонки.Удалить("Активность");

	Если ТаблицаКомплектов.Количество() = 0 Или ТаблицаКомплектующих.Количество() = 0 Тогда
		Возврат ТаблицаСебестоимостьТоваров;
	КонецЕсли;
	
	МассивРаспределения = ТаблицаКомплектов.ВыгрузитьКолонку("ДоляСтоимости");
	ВсегоДолейКоличествоВыхода = ТаблицаКомплектующих.Количество();

	Для Каждого СтрокаВхода Из ТаблицаКомплектующих Цикл

		РаспределенноеКоличество = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СтрокаВхода.Количество, МассивРаспределения, 2);

		Счетчик = 0;
		Для Каждого СтрокаВыхода Из ТаблицаКомплектов Цикл

			// Количество выхода нада рассчитать на каждую строку входа.
			Если ВсегоДолейКоличествоВыхода <> 0 Тогда
				КоличествоВыхода = Окр(СтрокаВыхода.Количество / ВсегоДолейКоличествоВыхода, 3, РежимОкругления.Окр15как20);
			Иначе
				КоличествоВыхода = СтрокаВыхода.Количество;
			КонецЕсли;

			СтрокаВыхода.Количество = СтрокаВыхода.Количество - КоличествоВыхода;

			ЗаписьКомплектующие = ТаблицаСебестоимостьТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьКомплектующие, СтрокаВхода);
			ЗаписьКомплектующие.ВидДвижения = ВидДвиженияНакопления.Расход;
			ЗаписьКомплектующие.КорАналитикаУчетаНоменклатуры = СтрокаВыхода.АналитикаУчетаНоменклатуры;
			ЗаписьКомплектующие.КорРазделУчета = СтрокаВыхода.РазделУчета;
			ЗаписьКомплектующие.КорВидЗапасов = СтрокаВыхода.ВидЗапасов;
			ЗаписьКомплектующие.Количество = РаспределенноеКоличество[Счетчик];

			ЗаписьКомплект = ТаблицаСебестоимостьТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьКомплект, СтрокаВыхода);
			ЗаписьКомплект.ВидДвижения = ВидДвиженияНакопления.Приход;
			ЗаписьКомплект.Количество  = КоличествоВыхода;

			Счетчик = Счетчик + 1;

		КонецЦикла;

		ВсегоДолейКоличествоВыхода = ВсегоДолейКоличествоВыхода - 1;

	КонецЦикла;

	ТаблицаСебестоимостьТоваров.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");
	Возврат ТаблицаСебестоимостьТоваров;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
//Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СборкаТоваров") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СборкаТоваров",
				"Сборка товаров",
				ПечатьСборкаТоваров(МассивОбъектов, ОбъектыПечати));

	КонецЕсли;

КонецПроцедуры

Функция ПечатьСборкаТоваров(МассивОбъектов, ОбъектыПечати)
	
	ДопКолонка = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	ВыводитьДопКолонку = ЗначениеЗаполнено(ДопКолонка);
	
	ЗапросПоДокументам = Новый Запрос;
	ЗапросПоДокументам.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	ЗапросПоДокументам.Текст =
	"
	|ВЫБРАТЬ
	|	Документ.Ссылка                          КАК Ссылка,
	|	Документ.Номер                           КАК Номер,
	|	Документ.Дата                            КАК Дата,
	|	Документ.Склад                           КАК Склад,
	|	Документ.Организация                     КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Склад)            КАК СкладПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Организация)      КАК ОрганизацияПредставление,
	|	Документ.Организация.Префикс             КАК Префикс,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Ответственный)    КАК ОтветственныйПредставление,
	|	" + ?(ВыводитьДопКолонку, "Документ.Номенклатура." + ДопКолонка +" КАК ДопКолонка,", "") + "
	|	Документ.Номенклатура                    КАК Номенклатура,
	|	Документ.Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
	|	Документ.Характеристика.НаименованиеПолное КАК ХарактеристикаПредставление,
	|
	|	ВЫБОР КОГДА Документ.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|			ПРЕДСТАВЛЕНИЕ(Документ.Номенклатура.ЕдиницаИзмерения)
	|		ИНАЧЕ
	|			ПРЕДСТАВЛЕНИЕ(Документ.Упаковка)
	|	КОНЕЦ                                    КАК Упаковка,
	|	Документ.КоличествоУпаковок              КАК Количество,
	|	Документ.ТипОперации                     КАК ТипОперации,
	|	Документ.Товары.(
	|
	|		НомерСтроки КАК НомерСтроки,
	|		" + ?(ВыводитьДопКолонку, "Номенклатура." + ДопКолонка +" КАК ДопКолонка,", "") + "
	|		Номенклатура                        КАК Номенклатура,
	|		Номенклатура.НаименованиеПолное     КАК НоменклатураПредставление,
	|		Характеристика.НаименованиеПолное 	КАК ХарактеристикаПредставление,
	|		Характеристика                      КАК Характеристика,
	|		ВЫБОР
	|			КОГДА Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|				ПРЕДСТАВЛЕНИЕССЫЛКИ(Номенклатура.ЕдиницаИзмерения)
	|			ИНАЧЕ
	|				ПРЕДСТАВЛЕНИЕССЫЛКИ(Упаковка)
	|		КОНЕЦ              КАК Упаковка,
	|		КоличествоУпаковок КАК Количество
	|
	|	) КАК ТаблицаТовары
	|ИЗ
	|	Документ.СборкаТоваров КАК Документ
	|
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|УПОРЯДОЧИТЬ ПО
	|	Дата, Ссылка, НомерСтроки
	|";
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СборкаТоваров";
	
	Макет = УправлениеПечатью.ПолучитьМакет("Документ.СборкаТоваров.ПФ_MXL_СборкаТоваров");
	
	Если ВыводитьДопКолонку Тогда
		ОбластьШапки  = "ШапкаСДопКолонкой";
		ОбластьСтроки = "СтрокаСДопКолонкой";
	Иначе
		ОбластьШапки  = "ШапкаТаблицы";
		ОбластьСтроки = "Строка";
	Конецесли;
	
	ПервыйДокумент = Истина;
	ВыборкаДокументы = ЗапросПоДокументам.Выполнить().Выбрать();
	Пока ВыборкаДокументы.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Вывод заголовка.
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ВыборкаДокументы);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента, Строка(ВыборкаДокументы.ТипОперации));
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабДокумент, Макет, ОбластьМакета, ВыборкаДокументы.Ссылка);
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Вывод комплекта.
		Если ВыводитьДопКолонку Тогда
			
			ОбластьМакета = Макет.ПолучитьОбласть("КомплектКод");
			ОбластьМакета.Параметры.ДопКолонка = ДопКолонка + ":";
			ОбластьМакета.Параметры.ЗначениеДопКолонки = ВыборкаДокументы.ДопКолонка;
			ТабДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Комплект");
		ОбластьМакета.Параметры.Комплект = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
			ВыборкаДокументы.НоменклатураПредставление,
			ВыборкаДокументы.ХарактеристикаПредставление
		);
		ОбластьМакета.Параметры.Количество = Строка(ВыборкаДокументы.Количество) + " " + ВыборкаДокументы.Упаковка;
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Вывод шапки отчета.
		ОбластьМакета = Макет.ПолучитьОбласть(ОбластьШапки);
		Если ВыводитьДопКолонку Тогда
			ОбластьМакета.Параметры.ДопКолонка = ДопКолонка;
		КонецЕсли;
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Вывод строк документа.
		ВыборкаПоСтрокам = ВыборкаДокументы.ТаблицаТовары.Выбрать();
		Пока ВыборкаПоСтрокам.Следующий() Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть(ОбластьСтроки);
			ОбластьМакета.Параметры.Заполнить(ВыборкаПоСтрокам);
			
			ОбластьМакета.Параметры.НоменклатураПредставление = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаПоСтрокам.НоменклатураПредставление,
				ВыборкаПоСтрокам.ХарактеристикаПредставление
			);
			
			ТабДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
		// Вывод подписей.
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		ОбластьМакета.Параметры.ОтветственныйПредставление = "/"+ ВыборкаДокументы.ОтветственныйПредставление + "/";
		ТабДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыОтборРазмещениеТоваров(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СборкаТоваров.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(СборкаТоваров.Ссылка) КАК СсылкаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(СборкаТоваров.Склад) КАК СкладПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка)) КАК ПомещениеПредставление,
	|	ЛОЖЬ КАК СкладыВТЧ,
	|	СборкаТоваров.Дата КАК Дата,
	|	СборкаТоваров.Номер КАК Номер,
	|	СборкаТоваров.Склад.ИспользоватьАдресноеХранениеСправочно КАК ИспользуетсяСправочноеХранение,
	|	СборкаТоваров.Склад.ИспользоватьОрдернуюСхемуПриПоступлении КАК НарушенаОрдернаяСхема,
	|	NULL КАК ВидОперации,
	|	СборкаТоваров.Склад.ИспользоватьСерииНоменклатуры КАК ИспользоватьСерииНоменклатуры
	|ИЗ
	|	Документ.СборкаТоваров КАК СборкаТоваров
	|ГДЕ
	|	СборкаТоваров.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	NULL КАК Серия,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ТаблицаТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество КАК Количество
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.СборкаТоваров КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
	|	И (НЕ ТаблицаТовары.СтатусУказанияСерий В (2, 4, 6, 8, 10))
	|	И ВЫБОР
	|			КОГДА ТаблицаТовары.Ссылка.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|				ТОГДА &ИмяФормы = ""ЗаданиеНаОтбор""
	|			КОГДА ТаблицаТовары.Ссылка.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|				ТОГДА &ИмяФормы = ""ЗаданиеНаРазмещение""
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Ссылка,
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия,
	|	ВЫБОР
	|		КОГДА ТаблицаСерии.Ссылка.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаСерии.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ТаблицаСерии.Ссылка.Упаковка
	|	КОНЕЦ,
	|	ТаблицаСерии.Количество,
	|	ТаблицаСерии.Количество
	|ИЗ
	|	Документ.СборкаТоваров.Серии КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК ТаблицаТовары
	|		ПО ТаблицаСерии.Ссылка = ТаблицаТовары.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТаблицаТовары.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаТовары.Характеристика
	|			И ТаблицаСерии.ДокументРезерваСерий = ТаблицаТовары.ДокументРезерваСерий
	|ГДЕ
	|	ТаблицаСерии.Ссылка В(&МассивОбъектов)
	|	И ВЫБОР
	|			КОГДА ТаблицаСерии.Ссылка.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|				ТОГДА &ИмяФормы = ""ЗаданиеНаОтбор""
	|			КОГДА ТаблицаСерии.Ссылка.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|				ТОГДА &ИмяФормы = ""ЗаданиеНаРазмещение""
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерии.Ссылка,
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.ДокументРезерваСерий,
	|	ТаблицаСерии.Количество,
	|	ТаблицаСерии.НомерСтроки
	|
	|ИМЕЮЩИЕ
    |
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий) В (2, 4, 6, 8, 10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	NULL,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ТаблицаТовары.Упаковка
	|	КОНЕЦ,
	|	ТаблицаТовары.КоличествоУпаковок,
	|	ТаблицаТовары.Количество
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
	|	И (НЕ ТаблицаТовары.СтатусУказанияСерий В (2, 4, 6, 8, 10))
	|	И ВЫБОР
	|			КОГДА ТаблицаТовары.Ссылка.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|				ТОГДА &ИмяФормы = ""ЗаданиеНаРазмещение""
	|			КОГДА ТаблицаТовары.Ссылка.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|				ТОГДА &ИмяФормы = ""ЗаданиеНаОтбор""
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Ссылка,
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.Номенклатура.ЕдиницаИзмерения,
	|	ТаблицаСерии.Количество,
	|	ТаблицаСерии.Количество
	|ИЗ
	|	Документ.СборкаТоваров.Серии КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.Товары КАК ТаблицаТовары
	|		ПО ТаблицаСерии.Ссылка = ТаблицаТовары.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТаблицаТовары.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаТовары.Характеристика
	|			И ТаблицаСерии.ДокументРезерваСерий = ТаблицаТовары.ДокументРезерваСерий
	|	
	|ГДЕ
	|	ТаблицаСерии.Ссылка В(&МассивОбъектов)
	|	И ТаблицаСерии.Номенклатура <> ТаблицаСерии.Ссылка.Номенклатура
	|	И ВЫБОР
	|			КОГДА ТаблицаСерии.Ссылка.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.РазборкаНаКомплектующие)
	|				ТОГДА &ИмяФормы = ""ЗаданиеНаРазмещение""
	|			КОГДА ТаблицаСерии.Ссылка.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих)
	|				ТОГДА &ИмяФормы = ""ЗаданиеНаОтбор""
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерии.Ссылка,
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.ДокументРезерваСерий,
	|	ТаблицаСерии.Количество,
	|	ТаблицаСерии.НомерСтроки
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий) В (2, 4, 6, 8, 10)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.Номенклатура.Код КАК Код,
	|	ТаблицаТовары.Номенклатура.Артикул КАК Артикул,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ПредставлениеНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ТаблицаТовары.Характеристика.НаименованиеПолное КАК ПредставлениеХарактеристики,
	|	ТаблицаТовары.Упаковка.Наименование КАК ПредставлениеЕдининицыИзмеренияУпаковки,
	|	ТаблицаТовары.Серия.Наименование КАК ПредставлениеСерии,
	|	СУММА(ТаблицаТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество,
	|	ЕСТЬNULL(РазмещениеОсновнаяЯчейка.Ячейка.РабочийУчасток, ЗНАЧЕНИЕ(Справочник.РабочиеУчастки.ПустаяСсылка)) КАК РабочийУчасток,
	|	ЕСТЬNULL(РазмещениеОсновнаяЯчейка.Ячейка.ПорядокОбхода, 0) КАК ПорядокОбхода,
	|	ЕСТЬNULL(РазмещениеОсновнаяЯчейка.Ячейка, ЗНАЧЕНИЕ(Справочник.СкладскиеЯчейки.ПустаяСсылка)) КАК ОсновнаяЯчейка,
	|	ЕСТЬNULL(РазмещениеОстальныеЯчейки.Ячейка.ПорядокОбхода, 0) КАК ПорядокОбходаДополнительнаяЯчейка,
	|	ПРЕДСТАВЛЕНИЕ(РазмещениеОсновнаяЯчейка.Ячейка.РабочийУчасток) КАК ПредставлениеРабочегоУчастка,
	|	РазмещениеОсновнаяЯчейка.Ячейка.Код КАК ОсновнаяЯчейкаПредставление,
	|	РазмещениеОстальныеЯчейки.Ячейка.Код КАК ЯчейкаПредставление
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам КАК РазмещениеОсновнаяЯчейка
	|		ПО ТаблицаТовары.Номенклатура = РазмещениеОсновнаяЯчейка.Номенклатура
	|			И (РазмещениеОсновнаяЯчейка.ОсновнаяЯчейка = ИСТИНА)
	|			И (РазмещениеОсновнаяЯчейка.Склад = ТаблицаТовары.Ссылка.Склад)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам КАК РазмещениеОстальныеЯчейки
	|		ПО ТаблицаТовары.Номенклатура = РазмещениеОстальныеЯчейки.Номенклатура
	|			И (РазмещениеОстальныеЯчейки.ОсновнаяЯчейка = ЛОЖЬ)
	|			И (РазмещениеОстальныеЯчейки.Склад = ТаблицаТовары.Ссылка.Склад)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.Упаковка,
	|	РазмещениеОсновнаяЯчейка.Ячейка,
	|	РазмещениеОстальныеЯчейки.Ячейка,
	|	ТаблицаТовары.Номенклатура.Код,
	|	ТаблицаТовары.Номенклатура.Артикул,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное,
	|	ТаблицаТовары.Характеристика.НаименованиеПолное,
	|	ТаблицаТовары.Упаковка.Наименование,
	|	ТаблицаТовары.Серия.Наименование,
	|	РазмещениеОсновнаяЯчейка.Ячейка.Код,
	|	РазмещениеОстальныеЯчейки.Ячейка.Код,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения),
	|	ЕСТЬNULL(РазмещениеОсновнаяЯчейка.Ячейка.РабочийУчасток, ЗНАЧЕНИЕ(Справочник.РабочиеУчастки.ПустаяСсылка)),
	|	ЕСТЬNULL(РазмещениеОсновнаяЯчейка.Ячейка.ПорядокОбхода, 0),
	|	ЕСТЬNULL(РазмещениеОстальныеЯчейки.Ячейка.ПорядокОбхода, 0),
	|	ПРЕДСТАВЛЕНИЕ(РазмещениеОсновнаяЯчейка.Ячейка.РабочийУчасток)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ПорядокОбхода,
	|	ОсновнаяЯчейкаПредставление,
	|	ПредставлениеНоменклатуры,
	|	ПредставлениеХарактеристики,
	|	ПредставлениеСерии,
	|	ПредставлениеЕдининицыИзмеренияУпаковки,
	|	ПорядокОбходаДополнительнаяЯчейка,
	|	ЯчейкаПредставление
	|ИТОГИ ПО
	|	Ссылка,
	|	РабочийУчасток,
	|	ОсновнаяЯчейка,
	|	Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	Серия,
	|	ТаблицаТовары.Упаковка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ИмяФормы", ПараметрыПечати.ИмяФормы);
	МассивРезультатов 			= Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[0];
	РезультатПоТабличнойЧасти 	= МассивРезультатов[2];
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
КонецФункции

/////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы

Процедура ЗаполнитьРеквизитСборкаПодДеятельность() Экспорт 
	
	НалогообложениеОрганизаций = Новый Соответствие;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СборкаТоваров КАК Документ
	|ГДЕ
	|	Документ.СборкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		НалогообложениеНДС = НалогообложениеОрганизаций.Получить(ДокументОбъект.Организация);
		Если НалогообложениеНДС = Неопределено Тогда
			НалогообложениеНДС = Справочники.Организации.НалогообложениеНДС(ДокументОбъект.Организация, ДокументОбъект.Склад, ДокументОбъект.Дата);
			НалогообложениеОрганизаций.Вставить(ДокументОбъект.Организация, НалогообложениеНДС);
		КонецЕсли;
		ДокументОбъект.СборкаПодДеятельность = НалогообложениеНДС;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаДляРассчетаСостоянийОтгрузки() Экспорт
	
	ТекстЗапроса = 	
	"ВЫБРАТЬ
	|	СборкаТоваров.Ссылка КАК Ссылка,
	|	СборкаТоваров.Проведен КАК Проведен,
	|	ВЫБОР
	|		КОГДА СборкаТоваров.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.КСборкеРазборке)
	|			ТОГДА ""КОтгрузке""
	|		КОГДА СборкаТоваров.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.ВРаботе)
	|			ТОГДА ""Отгружено""
	|		КОГДА СборкаТоваров.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|			ТОГДА ""Отгружено""
	|		ИНАЧЕ ""ПустаяСсылка""
	|	КОНЕЦ КАК Статус,
	|	NULL КАК СпособДоставки,
	|	СборкаТоваров.Склад КАК Склад,
	|	СборкаТоваров.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке КАК ИспользоватьОрдернуюСхемуПриОтгрузке,
	|	СборкаТоваров.Склад.НачинатьОтгрузкуПослеФормированияЗаданияНаПеревозку КАК НачинатьОтгрузкуПослеФормированияЗаданияНаПеревозку
	|ИЗ
	|	Документ.СборкаТоваров КАК СборкаТоваров
	|ГДЕ
	|	СборкаТоваров.Ссылка В(&ДокументыОтгрузки)";
	
	Возврат ТекстЗапроса;
	
КОнецФункции

#КонецЕсли