////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Обработчик подсистемы "Дополнительные отчеты и обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	СтавкаНДС18 = Перечисления.СтавкиНДС.НДС18_118;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Элементы.ГруппаКомментарий.Картинка = ОбщегоНазначенияУТ.ПолучитьКартинкуКомментария(Объект.Комментарий);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Элементы.ГруппаКомментарий.Картинка = ОбщегоНазначенияУТ.ПолучитьКартинкуКомментария(Объект.Комментарий);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура НеПодтвержденПриИзменении(Элемент)
	
	УстановитьВидимость();
	ОбновитьСтавкуНДСРеализации();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ДОКУМЕНТЫ РЕАЛИЗАЦИИ

&НаКлиенте
Процедура ДокументыРеализацииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НЕ Объект.ПодтвержденаСтавкаНДС0 И НоваяСтрока Тогда
		СтрокаТабличнойЧасти = Элементы.ДокументыРеализации.ТекущиеДанные;
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) ТОгда
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДС18;
		КонецЕсли;	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыРеализацииСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ДокументыРеализации.ТекущиеДанные;
	
	СтруктураПересчетаСуммы = ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Процедура УстановитьВидимость()

	// Изменим видимость суммы НДС документов реализации
	Элементы.ДокументыРеализацииСтавкаНДС.Видимость = НЕ Объект.ПодтвержденаСтавкаНДС0;
	Элементы.ДокументыРеализацииСуммаНДС.Видимость  = НЕ Объект.ПодтвержденаСтавкаНДС0;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтавкуНДСРеализации()

	СтруктураПересчетаСуммы = ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	
	Для каждого ТекущаяСтрока Из Объект.ДокументыРеализации Цикл
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.СтавкаНДС)
			ИЛИ ТекущаяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда
			
			ТекущаяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118;
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ()

	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("ЦенаВключаетНДС", Истина);
	
	Возврат СтруктураЗаполненияЦены;

КонецФункции
