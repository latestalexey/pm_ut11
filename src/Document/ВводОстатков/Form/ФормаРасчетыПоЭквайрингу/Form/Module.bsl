
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Элементы.ГруппаКомментарий.Картинка = ОбщегоНазначенияУТ.ПолучитьКартинкуКомментария(Объект.Комментарий);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Элементы.ГруппаКомментарий.Картинка = ОбщегоНазначенияУТ.ПолучитьКартинкуКомментария(Объект.Комментарий);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОММАНД ФОРМЫ

&НаКлиенте
Процедура ЗаполнитьПоДаннымОтчетовЭквайринга(Команда)
	
	Если Объект.РасчетыПоЭквайрингу.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru='Перед выполнением операции табличная часть будет очищена. Продолжить?'");
		
		Ответ = Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
		
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьПоДаннымОтчетовЭквайрингаСервер();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоДаннымОтчетовЭквайрингаСервер()
	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.ЗаполнитьДанныеЭквайринга();
	ЗначениеВРеквизитФормы(ТекОбъект, "Объект");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОЙ ЧАСТИ РАСЧЕТЫ ПО ЭКВАЙРИНГУ

&НаКлиенте
Процедура РасчетыПоЭквайрингуЭквайринговыйТерминалПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.РасчетыПоЭквайрингу.ТекущиеДанные;

	Если Не ВидПлатежнойКартыОбсуживаетсяТерминалом(ТекущаяСтрока.ЭквайринговыйТерминал, ТекущаяСтрока.ВидПлатежнойКарты) Тогда
		ТекущаяСтрока.ВидПлатежнойКарты = "";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РасчетыПоЭквайрингуВидПлатежнойКартыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ТекущаяСтрока = Элементы.РасчетыПоЭквайрингу.ТекущиеДанные;

	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(ВидыПлатежныхКартПоТерминалу(ТекущаяСтрока.ЭквайринговыйТерминал));

	СтандартнаяОбработка = Ложь;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ТипОперации", Объект.ТипОперации);
	Оповестить("Запись_ВводОстатков", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидПлатежнойКартыОбсуживаетсяТерминалом(Терминал, ВидПлатежнойКарты)
	
	Возврат Справочники.ДоговорыЭквайринга.ВидыПлатежныхКарт(Терминал).Найти(ВидПлатежнойКарты)<> Неопределено;	
	
КонецФункции

&НаСервереБезКонтекста
Функция ВидыПлатежныхКартПоТерминалу(Терминал)
	
	Возврат Справочники.ДоговорыЭквайринга.ВидыПлатежныхКарт(Терминал.Владелец);
	
КонецФункции






