
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	МассивОрганизаций = Новый ФиксированныйМассив(
			Документы.РасчетСебестоимостиТоваров.МассивОрганизацийДляРасчетаСебестоимости(Параметры.Дата,
					Параметры.РасчетСебестоимостиТоваров, Истина, Истина, Параметры.ПредварительныйРасчет));

	Счетчик = 0;
	Для Каждого Элемент Из МассивОрганизаций Цикл

		НоваяСтрока = ТаблицаОрганизаций.Добавить();

		Представление              = "";
		ВсеОрганизацииПрисутствуют = Истина;
		Если ТипЗнч(Элемент) = Тип("ФиксированныйМассив") Тогда

			Для Каждого Значение Из Элемент Цикл

				Представление = Представление + ?(ПустаяСтрока(Представление), "", ", ") + Значение;
				ВсеОрганизацииПрисутствуют = ВсеОрганизацииПрисутствуют И Параметры.СписокОрганизацийДокумента.Найти(Значение) <> Неопределено;

			КонецЦикла;
			НоваяСтрока.Выбрана = ВсеОрганизацииПрисутствуют;
		Иначе

			ВсеОрганизацииПрисутствуют = Параметры.СписокОрганизацийДокумента.Найти(Элемент) <> Неопределено;
			Представление = Строка(Элемент);

		КонецЕсли;

		НоваяСтрока.Организация   = Представление;
		НоваяСтрока.Идентификатор = Счетчик;
		НоваяСтрока.Выбрана       = ВсеОрганизацииПрисутствуют;

		Счетчик = Счетчик + 1;

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиОрганизацииВДокумент(Команда)

	Результат = Новый Массив;
	Для Каждого СтрокаТаб Из ТаблицаОрганизаций Цикл

		Если СтрокаТаб.Выбрана Тогда

			ЭлементМассива = МассивОрганизаций[СтрокаТаб.Идентификатор];
			Если ТипЗнч(ЭлементМассива) = Тип("ФиксированныйМассив") Тогда

				Для Каждого Значение Из ЭлементМассива Цикл
					Результат.Добавить(Значение);
				КонецЦикла;

			Иначе

				Результат.Добавить(ЭлементМассива);

			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	Закрыть(Результат);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСтроки(Команда)

	ОтметитьСтроки(Истина);

КонецПроцедуры

&НаКлиенте
Процедура ИсключитьСтроки(Команда)

	ОтметитьСтроки(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ОтметитьСтроки(Значение)

	Для Каждого СтрокаТЧ Из ТаблицаОрганизаций Цикл
		СтрокаТЧ.Выбрана = Значение;
	КонецЦикла;

КонецПроцедуры
