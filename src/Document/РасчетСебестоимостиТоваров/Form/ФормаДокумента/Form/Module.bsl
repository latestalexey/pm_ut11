
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Обработчик подсистемы "Внешние обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);

	ВариантРасчета = Объект.ПредварительныйРасчет;

	Элементы.ПредставлениеОрганизаций.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	МассивОрганизаций = Новый Массив;
	Для Каждого СтрокаТЧ ИЗ Объект.Организации Цикл
		МассивОрганизаций.Добавить(СтрокаТЧ.Организация);
	КонецЦикла;

	Результат = ПроверитьМетодыОценкиСтоимости(Объект.Дата, МассивОрганизаций, Объект.МетодОценки);
	Если Результат.ВыдатьПредупреждение Тогда

		ТекстВопроса = НСтр("ru = 'Продолжить?'");
		Ответ = Вопрос(Результат.ТекстПредупреждения + Символы.ПС + ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда

			Отказ = Истина;

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ОткрытьФормуРедактированияКомментария(Элемент.ТекстРедактирования, Объект.Комментарий, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантРасчетаПриИзменении(Элемент)

	Объект.ПредварительныйРасчет = ВариантРасчета > 0;

	Если Не Объект.ПредварительныйРасчет Тогда 

		ОпределитьМетодОценкиСтоимости();

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеОрганизацийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	МассивОрганизаций = Новый Массив;
	Для Каждого СтрокаТЧ Из Объект.Организации Цикл

		МассивОрганизаций.Добавить(СтрокаТЧ.Организация);

	КонецЦикла;

	Результат = ОткрытьФормуМодально("Документ.РасчетСебестоимостиТоваров.Форма.ФормаРедактированияОрганизаций",
				Новый Структура("СписокОрганизацийДокумента, Дата, РасчетСебестоимостиТоваров, ПредварительныйРасчет",
								МассивОрганизаций, Объект.Дата, Объект.Ссылка, Объект.ПредварительныйРасчет));

	Если ТипЗнч(Результат) = Тип("Массив") Тогда

		ПредставлениеОрганизаций ="";
		Объект.Организации.Очистить();
		Для Каждого Элемент Из Результат Цикл

			Объект.Организации.Добавить().Организация = Элемент;

			ПредставлениеОрганизаций = ПредставлениеОрганизаций + ?(ПустаяСтрока(ПредставлениеОрганизаций), "", ", ")
										+ Элемент;

		КонецЦикла;

		Объект.ПредставлениеОрганизаций = ПредставлениеОрганизаций;

		ОпределитьМетодОценкиСтоимости();

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеОрганизацийОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Объект.Организации.Очистить();
	Объект.ПредставлениеОрганизаций = НСтр("ru = 'Укажите организации для расчета'");

КонецПроцедуры

&НаКлиенте
Процедура МетодОценкиОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ОпределитьМетодОценкиСтоимости()

	МассивОрганизаций = Новый Массив;
	Для Каждого СтрокаТЧ ИЗ Объект.Организации Цикл
		МассивОрганизаций.Добавить(СтрокаТЧ.Организация);
	КонецЦикла;

	Объект.МетодОценки = Документы.РасчетСебестоимостиТоваров.ОпределитьМетодОценкиСтоимости(МассивОрганизаций, Объект.Дата);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьМетодыОценкиСтоимости(Период, Организации, МетодОценки)

	Результат = Новый Структура("ВыдатьПредупреждение, ТекстПредупреждения", Ложь, "");

	Запрос = Новый Запрос(
	"
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Т.Организация) КАК Организация,
	|	Т.МетодОценки                      КАК МетодОценки
	|ИЗ
	|	РегистрСведений.НастройкаМетодовОценкиСтоимостиТоваров.СрезПоследних(&Период,
	|		Организация В (&Организации)) КАК Т
	|ГДЕ
	|	Т.МетодОценки <> &МетодОценки
	|");
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("МетодОценки", МетодОценки);

	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда

		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл

			СтрокаОрганизаций = ?(ПустаяСтрока(СтрокаОрганизаций), "", СтрокаОрганизаций + ", ") + Выборка.Организация;

		КонецЦикла;

		ШаблонПредупреждения = "Для организаций %СтрокаОрганизаций% указан метод оценки отличный от выбранного в документе.";
		ТекстПредупреждения  = СтрЗаменить(ШаблонПредупреждения, "%СтрокаОрганизаций%", СтрокаОрганизаций);

		Результат.ВыдатьПредупреждение = Истина;
		Результат.ТекстПредупреждения  = ТекстПредупреждения;
		
	КонецЕсли;

	Возврат Результат;

КонецФункции