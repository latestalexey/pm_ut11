
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
				
		ПериодНачисления = Новый СтандартныйПериод;
		ПериодНачисления.Вариант = ВариантСтандартногоПериода.ПрошлыйМесяц;
		Объект.ДатаНачала = ПериодНачисления.ДатаНачала;
		Объект.ДатаОкончания = ПериодНачисления.ДатаОкончания;
		
	КонецЕсли;
	
	УстановитьЗаголовкиПоОперацииСервер();
	Элементы.НачисленияДоговор.ПараметрыВыбора = ПараметрыВыбораДоговоров();
	
	// Обработчик подсистемы "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(РезультатВыбора, ИсточникВыбора)
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		Если РезультатВыбора.Свойство("АдресНачисленийВХранилище") Тогда
			ПолучитьНачисленияИзХранилища(РезультатВыбора.АдресНачисленийВХранилище);
		КонецЕсли;
	КонецЕсли;
	
	Если Окно <> Неопределено Тогда
		Окно.Активизировать();
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Элементы.НачисленияДоговор.ПараметрыВыбора = ПараметрыВыбораДоговоров();
	
КонецПроцедуры

&НаКлиенте
Процедура ХозяйственнаяОперацияПриИзменении(Элемент)
	
	УстановитьЗаголовкиПоОперацииСервер();
	Элементы.НачисленияДоговор.ПараметрыВыбора = ПараметрыВыбораДоговоров();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ "НАЧИСЛЕНИЯ"

&НаКлиенте
Процедура НачисленияДоговорПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Начисления.ТекущиеДанные;
	Договор = РеквизитыДоговораСервер(СтрокаТаблицы.Договор);
	Если СтрокаТаблицы.ВалютаВзаиморасчетов <> Договор.ВалютаВзаиморасчетов Тогда
		СтрокаТаблицы.ВалютаВзаиморасчетов = Договор.ВалютаВзаиморасчетов;
		СтрокаТаблицы.СуммаВзаиморасчетов = 0;
	КонецЕсли;
	
	Если СтрокаТаблицы.ТипСуммыГрафика = ПредопределенноеЗначение("Перечисление.ТипСуммыГрафикаКредитовИДепозитов.Проценты") Тогда
		СтрокаТаблицы.СтатьяДоходовРасходов = Договор.СтатьяДоходовРасходовПроцентов;
	ИначеЕсли СтрокаТаблицы.ТипСуммыГрафика = ПредопределенноеЗначение("Перечисление.ТипСуммыГрафикаКредитовИДепозитов.Комиссия") Тогда
		СтрокаТаблицы.СтатьяДоходовРасходов = Договор.СтатьяДоходовРасходовКомиссии;
	ИначеЕсли Договор.ХарактерДоговора = ПредопределенноеЗначение("Перечисление.ХарактерДоговораКредитовИДепозитов.КредитИлиЗайм") Тогда
		СтрокаТаблицы.СтатьяДоходовРасходов = ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка");
	Иначе
		СтрокаТаблицы.СтатьяДоходовРасходов = ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияСуммаВалютаПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Начисления.ТекущиеДанные;
	СтрокаТаблицы.СуммаВзаиморасчетов = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияСтатьяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элементы.Начисления.ТекущиеДанные;	
	НоваяСтатья = ТекущаяСтрока.СтатьяДоходовРасходов;
	Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.НачисленияПоКредитам") Тогда
		ОграничивающиеТипы = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Иначе
		ОграничивающиеТипы = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиДоходов");
	КонецЕсли;
	Элемент.ОграничениеТипа = ОграничивающиеТипы;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ХозяйственнаяОперация) Тогда
		Текст  = НСтр("ru='Не указана хозяйственная операция!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст,,"ХозяйственнаяОперация","Объект");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Текст  = НСтр("ru='Не указана организация!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст,,"Организация","Объект");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Текст  = НСтр("ru='Не указано начало периода начислений!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст,,"ДатаНачала","Объект");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		Текст  = НСтр("ru='Не указан конец периода начислений!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст,,"ДатаОкончания","Объект");
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияУТКлиент.ВозможноЗаполнениеТабличнойЧасти(ЭтаФорма, Объект.Начисления, Неопределено) Тогда
		
		АдресНачисленийВХранилище = ПоместитьНачисленияВХранилище();
		ПараметрыОтбора = Новый Структура("
			|АдресНачисленийВХранилище,
			|Организация,
			|ИдентификаторФормыДокумента,
			|ХозяйственнаяОперация,
			|ДатаНачала,
			|ДатаОкончания",
			АдресНачисленийВХранилище,
			Объект.Организация,
			УникальныйИдентификатор,
			Объект.ХозяйственнаяОперация,
			Объект.ДатаНачала,
			Объект.ДатаОкончания
		);		
		
		ОткрытьФорму("Документ.НачисленияКредитовИДепозитов.Форма.ФормаЗаполнения", 
							ПараметрыОтбора,
							ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаСервере
Функция РеквизитыДоговораСервер(Договор)
	
	Результат = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Договор, 
															"ВалютаВзаиморасчетов,
															|СтатьяДоходовРасходовПроцентов,
															|СтатьяДоходовРасходовКомиссии,
															|ХарактерДоговора");
	
	Возврат Результат;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Управление элементами формы

&НаСервере
Процедура УстановитьЗаголовкиПоОперацииСервер()
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.НачисленияПоКредитам Тогда
		Элементы.НачисленияСтатья.Заголовок = НСтр("ru='Статья расходов'");
	Иначе
		Элементы.НачисленияСтатья.Заголовок = НСтр("ru='Статья доходов'");
	КонецЕсли;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Функция ПараметрыВыбораДоговоров()

	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Организация", Объект.Организация));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Статус", Перечисления.СтатусыДоговоровКонтрагентов.Действует));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ХарактерДоговора", Справочники.ДоговорыКредитовИДепозитов.ХарактерДоговораПоОперации(Объект.ХозяйственнаяОперация)));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления",Ложь));
	
	Возврат Новый ФиксированныйМассив(МассивПараметров);
	
КонецФункции

&НаСервере
Функция ПоместитьНачисленияВХранилище()
	
	АдресВХранилище = ПоместитьВоВременноеХранилище(Объект.Начисления.Выгрузить(), УникальныйИдентификатор);	
	Возврат АдресВХранилище;
	
КонецФункции

&НаСервере
Процедура ПолучитьНачисленияИзХранилища(АдресНачисленийВХранилище)

	Объект.Начисления.Загрузить(ПолучитьИзВременногоХранилища(АдресНачисленийВХранилище));
	
КонецПроцедуры

