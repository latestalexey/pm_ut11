

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	АдресНачисленийВХранилище = Параметры.АдресНачисленийВХранилище;
	ИдентификаторФормыДокумента = Параметры.ИдентификаторФормыДокумента;
	ХозяйственнаяОперация = Параметры.ХозяйственнаяОперация;
	Элементы.ДоговорКредитаДепозита.ПараметрыВыбора = ПараметрыВыбораДоговоров();
	ДатаНачала = Параметры.ДатаНачала;
	ДатаОкончания = Параметры.ДатаОкончания;
		
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура РеквизитПриИзменении(Элемент)
	
	Элементы.ДоговорКредитаДепозита.ПараметрыВыбора = ПараметрыВыбораДоговоров();
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитОчистка(Элемент, СтандартнаяОбработка)
	
	Элементы.ДоговорКредитаДепозита.ПараметрыВыбора = ПараметрыВыбораДоговоров();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Заполнить(Команда)
	
	НачисленияСервер();
	
	Структура = Новый Структура("АдресНачисленийВХранилище", АдресНачисленийВХранилище);
	ОповеститьОВыборе(Структура);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Функция ПараметрыВыбораДоговоров()

	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Организация", Организация));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Статус", Перечисления.СтатусыДоговоровКонтрагентов.Действует));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ХарактерДоговора", Справочники.ДоговорыКредитовИДепозитов.ХарактерДоговораПоОперации(ХозяйственнаяОперация)));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления",Ложь));
	Если НЕ Контрагент.Пустая() Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Контрагент", Контрагент));
	КонецЕсли;
	
	Если НЕ Ответственный.Пустая() Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ответственный",Ответственный));
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(МассивПараметров);
	
КонецФункции

&НаСервере
Функция ТекстЗапросаПоНачислениям()
	
	Возврат 
	"ВЫБРАТЬ
	|	ГрафикНачислений.Период,
	|	ВариантыГрафиков.Владелец КАК Договор,
	|	ГрафикНачислений.ВариантГрафика,
	|	ГрафикНачислений.Проценты,
	|	ГрафикНачислений.Комиссия
	|ПОМЕСТИТЬ НачисленияПоДоговорам
	|ИЗ
	|	РегистрСведений.ГрафикНачисленийКредитовИДепозитов КАК ГрафикНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыГрафиковКредитовИДепозитов КАК ВариантыГрафиков
	|		ПО ГрафикНачислений.ВариантГрафика = ВариантыГрафиков.Ссылка
	|ГДЕ
	|	ГрафикНачислений.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ВариантыГрафиков.Используется
	|	И НЕ ВариантыГрафиков.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияПоДоговорам.Период,
	|	ДоговорыКредитовИДепозитов.Организация,
	|	ДоговорыКредитовИДепозитов.Партнер,
	|	ДоговорыКредитовИДепозитов.Контрагент,
	|	НачисленияПоДоговорам.Договор,
	|	НачисленияПоДоговорам.ВариантГрафика,
	|	НачисленияПоДоговорам.Проценты,
	|	НачисленияПоДоговорам.Комиссия,
	|	ДоговорыКредитовИДепозитов.СтатьяДоходовРасходовПроцентов КАК СтатьяПроцентов,
	|	ДоговорыКредитовИДепозитов.СтатьяДоходовРасходовКомиссии КАК СтатьяКомиссии,
	|	ДоговорыКредитовИДепозитов.ВалютаВзаиморасчетов,
	|	ДоговорыКредитовИДепозитов.ХарактерДоговора
	|ПОМЕСТИТЬ ТекущиеНачисления
	|ИЗ
	|	НачисленияПоДоговорам КАК НачисленияПоДоговорам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов
	|		ПО НачисленияПоДоговорам.Договор = ДоговорыКредитовИДепозитов.Ссылка
	|ГДЕ
	|	ДоговорыКредитовИДепозитов.Организация = &Организация
	|	И НЕ ДоговорыКредитовИДепозитов.ПометкаУдаления
	|	И ДоговорыКредитовИДепозитов.ХарактерДоговора = &ХарактерДоговора
	|{ГДЕ
	|	ДоговорыКредитовИДепозитов.Контрагент.*,
	|	НачисленияПоДоговорам.Договор.*,
	|	ДоговорыКредитовИДепозитов.Ответственный.*}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоДоговорамКредитовИДепозитов.Период,
	|	РасчетыПоДоговорамКредитовИДепозитов.АналитикаУчетаПоПартнерам,
	|	КлючиАналитикиУчетаПоПартнерам.Организация,
	|	КлючиАналитикиУчетаПоПартнерам.Партнер,
	|	КлючиАналитикиУчетаПоПартнерам.Контрагент,
	|	РасчетыПоДоговорамКредитовИДепозитов.Договор,
	|	РасчетыПоДоговорамКредитовИДепозитов.СуммаВВалюте КАК Проценты,
	|	0 КАК Комиссия,
	|	РасчетыПоДоговорамКредитовИДепозитов.ТипГрафика,
	|	РасчетыПоДоговорамКредитовИДепозитов.ТипСуммы,
	|	РасчетыПоДоговорамКредитовИДепозитов.СтатьяАналитики
	|ПОМЕСТИТЬ РасчетыПоКредитамДепозитам
	|ИЗ
	|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов КАК РасчетыПоДоговорамКредитовИДепозитов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
	|		ПО РасчетыПоДоговорамКредитовИДепозитов.АналитикаУчетаПоПартнерам = КлючиАналитикиУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	РасчетыПоДоговорамКредитовИДепозитов.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И РасчетыПоДоговорамКредитовИДепозитов.ТипГрафика = ЗНАЧЕНИЕ(Перечисление.ТипГрафикаКредитовИДепозитов.Начисления)
	|	И КлючиАналитикиУчетаПоПартнерам.Организация = &Организация
	|	И РасчетыПоДоговорамКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипСуммыГрафикаКредитовИДепозитов.Проценты)
	|{ГДЕ
	|	КлючиАналитикиУчетаПоПартнерам.Контрагент.*,
	|	РасчетыПоДоговорамКредитовИДепозитов.Договор.*,
	|	РасчетыПоДоговорамКредитовИДепозитов.Договор.Ответственный.* КАК Ответственный}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыПоДоговорамКредитовИДепозитов.Период,
	|	РасчетыПоДоговорамКредитовИДепозитов.АналитикаУчетаПоПартнерам,
	|	КлючиАналитикиУчетаПоПартнерам.Организация,
	|	КлючиАналитикиУчетаПоПартнерам.Партнер,
	|	КлючиАналитикиУчетаПоПартнерам.Контрагент,
	|	РасчетыПоДоговорамКредитовИДепозитов.Договор,
	|	0,
	|	РасчетыПоДоговорамКредитовИДепозитов.СуммаВВалюте,
	|	РасчетыПоДоговорамКредитовИДепозитов.ТипГрафика,
	|	РасчетыПоДоговорамКредитовИДепозитов.ТипСуммы,
	|	РасчетыПоДоговорамКредитовИДепозитов.СтатьяАналитики
	|ИЗ
	|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов КАК РасчетыПоДоговорамКредитовИДепозитов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
	|		ПО РасчетыПоДоговорамКредитовИДепозитов.АналитикаУчетаПоПартнерам = КлючиАналитикиУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	РасчетыПоДоговорамКредитовИДепозитов.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И РасчетыПоДоговорамКредитовИДепозитов.ТипГрафика = ЗНАЧЕНИЕ(Перечисление.ТипГрафикаКредитовИДепозитов.Начисления)
	|	И КлючиАналитикиУчетаПоПартнерам.Организация = &Организация
	|	И РасчетыПоДоговорамКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипСуммыГрафикаКредитовИДепозитов.Комиссия)
	|{ГДЕ
	|	КлючиАналитикиУчетаПоПартнерам.Контрагент.*,
	|	РасчетыПоДоговорамКредитовИДепозитов.Договор.*,
	|	РасчетыПоДоговорамКредитовИДепозитов.Договор.Ответственный.* КАК Ответственный}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоКредитамДепозитам.Период КАК Период,
	|	РасчетыПоКредитамДепозитам.АналитикаУчетаПоПартнерам,
	|	РасчетыПоКредитамДепозитам.Организация,
	|	РасчетыПоКредитамДепозитам.Партнер,
	|	РасчетыПоКредитамДепозитам.Контрагент,
	|	РасчетыПоКредитамДепозитам.Договор,
	|	РасчетыПоКредитамДепозитам.ТипГрафика
	|ПОМЕСТИТЬ ПредыдущиеНачисления
	|ИЗ
	|	РасчетыПоКредитамДепозитам КАК РасчетыПоКредитамДепозитам
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоКредитамДепозитам.Период,
	|	РасчетыПоКредитамДепозитам.АналитикаУчетаПоПартнерам,
	|	РасчетыПоКредитамДепозитам.Организация,
	|	РасчетыПоКредитамДепозитам.Партнер,
	|	РасчетыПоКредитамДепозитам.Контрагент,
	|	РасчетыПоКредитамДепозитам.Договор,
	|	РасчетыПоКредитамДепозитам.ТипГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущиеНачисления.Период КАК Дата,
	|	ТекущиеНачисления.Организация,
	|	ТекущиеНачисления.Партнер,
	|	ТекущиеНачисления.Контрагент,
	|	ТекущиеНачисления.Договор,
	|	ТекущиеНачисления.ВариантГрафика,
	|	ТекущиеНачисления.Проценты,
	|	ТекущиеНачисления.Комиссия,
	|	ТекущиеНачисления.СтатьяПроцентов,
	|	ТекущиеНачисления.СтатьяКомиссии,
	|	ТекущиеНачисления.ВалютаВзаиморасчетов,
	|	ПредыдущиеНачисления.Период КАК ПериодНачислено,
	|	ТекущиеНачисления.ХарактерДоговора
	|ИЗ
	|	ТекущиеНачисления КАК ТекущиеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПредыдущиеНачисления КАК ПредыдущиеНачисления
	|		ПО ТекущиеНачисления.Период = ПредыдущиеНачисления.Период
	|			И ТекущиеНачисления.Контрагент = ПредыдущиеНачисления.Контрагент
	|			И ТекущиеНачисления.Договор = ПредыдущиеНачисления.Договор
	|ГДЕ
	|	ПредыдущиеНачисления.Период ЕСТЬ NULL ";
	
КонецФункции

&НаСервере
Процедура НачисленияСервер()
	
	//получим таблицу начислений по графику
	ПостроительЗапроса = Новый ПостроительЗапроса(ТекстЗапросаПоНачислениям());
	ПостроительЗапроса.Параметры.Вставить("ДатаНачала", ДатаНачала);
	ПостроительЗапроса.Параметры.Вставить("ДатаОкончания", ДатаОкончания);
	ПостроительЗапроса.Параметры.Вставить("Организация",Организация);
	ПостроительЗапроса.Параметры.Вставить("ХарактерДоговора", Справочники.ДоговорыКредитовИДепозитов.ХарактерДоговораПоОперации(ХозяйственнаяОперация));
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		ОтборКонтрагента = ПостроительЗапроса.Отбор.Добавить("Контрагент");
		ОтборКонтрагента.Установить(Контрагент);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоговорКредитаДепозита) Тогда
		
		ОтборДоговора = ПостроительЗапроса.Отбор.Добавить("Договор");
		ОтборДоговора.Установить(ДоговорКредитаДепозита);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ответственный) Тогда
		
		Отбор = ПостроительЗапроса.Отбор.Добавить("Ответственный");
		Отбор.Установить(Ответственный);
		
	КонецЕсли;
	
	ПостроительЗапроса.Выполнить();
	НачисленияГрафика = ПостроительЗапроса.Результат.Выгрузить();
	Начисления = НачисленияГрафика.СкопироватьКолонки("Дата,Контрагент,Договор,ВалютаВзаиморасчетов");
	
	МассивТипов = Новый Массив();
	Начисления.Колонки.Добавить("СтатьяДоходовРасходов",Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиДоходов,ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	Начисления.Колонки.Добавить("ТипСуммыГрафика",Новый ОписаниеТипов("ПеречислениеСсылка.ТипСуммыГрафикаКредитовИДепозитов"));
	Начисления.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 2)));
	Начисления.Колонки.Добавить("СуммаВзаиморасчетов",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 2)));	
	
	//перенесем таблицу начислений по графику в табличную часть документа
	НеЗаполненныеДоговора = Новый Соответствие;	
	ПустаяСтатьяДоходов = ПланыВидовХарактеристик.СтатьиДоходов.ПустаяСсылка();
	ПустаяСтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	КурсРегл = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Константы.ВалютаРегламентированногоУчета.Получить(), ДатаОкончания);	
	Для Каждого Стр Из НачисленияГрафика Цикл
		
		Если Стр.Проценты + Стр.Комиссия = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ Справочники.ДоговорыКредитовИДепозитов.ЗаполненыСтатьиУчета(Стр.Договор, Ложь) Тогда
			НеЗаполненныеДоговора.Вставить(Стр.Договор, Стр.Договор);
			Продолжить;			
		КонецЕсли;
		
		Если Стр.Проценты > 0 Тогда
			
			НоваяСтрока = Начисления.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
			НоваяСтрока.СтатьяДоходовРасходов = Стр.СтатьяПроцентов;
			НоваяСтрока.ТипСуммыГрафика = Перечисления.ТипСуммыГрафикаКредитовИДепозитов.Проценты;
			НоваяСтрока.СуммаВзаиморасчетов = Стр.Проценты;
												
		КонецЕсли;//есть проценты
		
		Если Стр.Комиссия > 0 Тогда
			
			НоваяСтрока = Начисления.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
			НоваяСтрока.СтатьяДоходовРасходов = Стр.СтатьяКомиссии;			
			НоваяСтрока.ТипСуммыГрафика = Перечисления.ТипСуммыГрафикаКредитовИДепозитов.Комиссия;
			НоваяСтрока.СуммаВзаиморасчетов = Стр.Комиссия;
												
		КонецЕсли;//есть комисия
		
	КонецЦикла;// по графику начислений
	
	//покажем сообщения проверки договоров
	Справочники.ДоговорыКредитовИДепозитов.СообщитьОбОшибкахЗаполнения(НеЗаполненныеДоговора, ИдентификаторФормыДокумента, НСтр("ru='Начисления по договору №%1 от %2 ""%3"" не заполнены,'"));
	
	АдресНачисленийВХранилище = ПоместитьВоВременноеХранилище(Начисления, УникальныйИдентификатор);
	
КонецПроцедуры
