#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			
			Если ДанныеЗаполнения.Свойство("ДокументОснование") 
				И ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
				ЗаполнитьНаОсновании(ДанныеЗаполнения.ДокументОснование);
			Иначе
				ЗаполнитьПоОтбору(ДанныеЗаполнения);
			КонецЕсли;
		
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
			
			Если НЕ ДанныеЗаполнения.Проведен Тогда
				
				ТекстОшибки = НСтр("ru='Документ %Документ% не проведен. Ввод на основании непроведенного документа невозможен.'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
				
				ВызватьИсключение ТекстОшибки;
				
			КонецЕсли;
			
			Если НЕ ДанныеЗаполнения.ЕстьРасхождения Тогда
				
				ТекстОшибки = НСтр("ru='По документу %Документ% нет расхождений. Ввод на основании невозможен.'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
				
				ВызватьИсключение ТекстОшибки;
				
			КонецЕсли;
				
			ЗаполнитьНаОсновании(ДанныеЗаполнения);
			ПартнерыИКонтрагенты.ЗаполнитьКонтактноеЛицоПартнераПоУмолчанию(Партнер, КонтактноеЛицо);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		ТекстОшибки = НСтр("ru='Не указан документ поступления, по которому оформляется акт. Акт должен вводиться только на основании документа-поступления'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "ДокументОснование",,Отказ);
	Иначе
		
		РеквизитыОснования = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДокументОснование, "Организация,Партнер,Валюта");
		
		Если Организация <> РеквизитыОснования.Организация Тогда
			ТекстОшибки = НСтр("ru='Указанная организация отличается от организации, указанной в документе поступления. Необходимо заполнить данные по поступлению'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Организация",,Отказ);
		КонецЕсли;
		
		Если Партнер <> РеквизитыОснования.Партнер Тогда
			ТекстОшибки = НСтр("ru='Указанный поставщик отличается от поставщика, указанного в документе поступления. Необходимо заполнить данные по поступлению'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Партнер",,Отказ);
		КонецЕсли;
		
		Если Валюта <> РеквизитыОснования.Валюта Тогда
			ТекстОшибки = НСтр("ru='Указанная валюта отличается от валюты, указанной в документе поступления. Необходимо заполнить данные по поступлению'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Валюта",,Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	ОтветственныеЛицаСервер.ПроверитьЗаполнениеОтветственныхЛицДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Инициализация и заполнение

Процедура ЗаполнитьПоОтбору(СтруктураОтбора)
	
	Если СтруктураОтбора.Свойство("Организация") Тогда
		Организация = СтруктураОтбора.Организация;
	КонецЕсли;
	
	Если СтруктураОтбора.Свойство("Партнер") Тогда
		Партнер = СтруктураОтбора.Партнер;
	КонецЕсли;
	
	Если СтруктураОтбора.Свойство("Контрагент") Тогда
		Контрагент = СтруктураОтбора.Контрагент;
	КонецЕсли;
	
	Если СтруктураОтбора.Свойство("Валюта") Тогда
		Валюта = СтруктураОтбора.Валюта;
	КонецЕсли;
			
КонецПроцедуры

Процедура ЗаполнитьНаОсновании(Основание)
	
	ДокументОснование 	= Основание;
	Партнер 			= ДокументОснование.Партнер;
	Контрагент 			= ДокументОснование.Контрагент;
	Организация 		= ДокументОснование.Организация;
	Валюта 				= ДокументОснование.Валюта;
	
	Если ЗначениеЗаполнено(ДокументОснование.Грузоотправитель) Тогда
		Грузоотправитель = ДокументОснование.Грузоотправитель;
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Валюта      = ДоходыИРасходыСервер.ПолучитьВалютуРегламентированногоУчета(Валюта);
	
	ОтветственныеЛицаСервер.ЗаполнитьОтветственныхЛицДокумента(ЭтотОбъект);	
КонецПроцедуры

#КонецЕсли