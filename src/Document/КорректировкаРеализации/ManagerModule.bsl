#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                      КАК Ссылка,
	|	ДанныеДокумента.Дата                                        КАК Период,
	|	ДанныеДокумента.ДокументОснование                           КАК ДокументОснование,
	|	ДанныеДокумента.ДокументОснование.Дата                      КАК ПериодПродажи,
	|	ДанныеДокумента.Организация                                 КАК Организация,
	|	ДанныеДокумента.Партнер                                     КАК Партнер,
	|	ДанныеДокумента.Контрагент                                  КАК Контрагент,
	|	ДанныеДокумента.Подразделение                               КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходов                              КАК СтатьяРасходов,
	|	ДанныеДокумента.СтатьяРасходов.ВариантРаспределенияРасходов КАК ВариантРаспределенияРасходов,
	|	ДанныеДокумента.АналитикаРасходов                           КАК АналитикаРасходов,
	|	ДанныеДокумента.СтатьяДоходов                               КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов                            КАК АналитикаДоходов,
	|	ДанныеДокумента.ВалютаВзаиморасчетов                        КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.Валюта                                      КАК Валюта,
	|	ДанныеДокумента.ПродажаПоЗаказам                            КАК ПродажаПоЗаказам,
	|	ДанныеДокумента.НалогообложениеНДС                          КАК НалогообложениеНДС,
	|	ДанныеДокумента.ДатаПлатежа                                 КАК ДатаПлатежа,
	|	ДанныеДокумента.ФормаОплаты                                 КАК ФормаОплаты,
	|	ДанныеДокумента.Договор                                     КАК Договор,
	|	ДанныеДокумента.ХозяйственнаяОперация                       КАК ХозяйственнаяОперация,
	|	
	|	ВЫБОР КОГДА ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК РасчетыПоДоговорам,
	|	
	|	ВЫБОР КОГДА ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК РасчетыПоНакладным
	|
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ИнициализироватьКлючиАналитикиНоменклатуры(Реквизиты);
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("ДокументОснование", Реквизиты.ДокументОснование);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Подразделение", Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("СтатьяРасходов", Реквизиты.СтатьяРасходов);
	Запрос.УстановитьПараметр("АналитикаРасходов", Реквизиты.АналитикаРасходов);
	Запрос.УстановитьПараметр("ВариантРаспределенияРасходов", Реквизиты.ВариантРаспределенияРасходов);
	Запрос.УстановитьПараметр("СтатьяДоходов", Реквизиты.СтатьяДоходов);
	Запрос.УстановитьПараметр("АналитикаДоходов", Реквизиты.АналитикаДоходов);
	Запрос.УстановитьПараметр("ПродажаПоЗаказам", Реквизиты.ПродажаПоЗаказам);
	Запрос.УстановитьПараметр("ДатаПлатежа", Реквизиты.ДатаПлатежа);
	Запрос.УстановитьПараметр("ФормаОплаты", Реквизиты.ФормаОплаты);
	Запрос.УстановитьПараметр("Договор", Реквизиты.Договор);
	Запрос.УстановитьПараметр("Контрагент", Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("РасчетыПоДоговорам", Реквизиты.РасчетыПоДоговорам);
	Запрос.УстановитьПараметр("РасчетыПоНакладным", Реквизиты.РасчетыПоНакладным);
	Запрос.УстановитьПараметр("ВалютаВзаиморасчетов", Реквизиты.ВалютаВзаиморасчетов);
	Запрос.УстановитьПараметр("Валюта", Реквизиты.Валюта);
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(Реквизиты));
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов", ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоВидамЗапасов"));
	Запрос.УстановитьПараметр("ИспользоватьУчетПрочихДоходовРасходов", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов")); 
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("НалогообложениеНДС", Реквизиты.НалогообложениеНДС);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Реквизиты.ХозяйственнаяОперация);
	
	КорректировкаПрошлогоПериода = Ложь;
	КорректировкаТекущегоПериода = Ложь;
	Если ЗначениеЗаполнено(Реквизиты.ПериодПродажи) Тогда
		Если Реквизиты.ПериодПродажи < НачалоМесяца(Реквизиты.Период) Тогда
			КорректировкаПрошлогоПериода = Истина;
		Иначе
			КорректировкаТекущегоПериода = Истина;
		КонецЕсли;
	КонецЕсли;
	Запрос.УстановитьПараметр("ПериодПродажи", Реквизиты.ПериодПродажи);
	Запрос.УстановитьПараметр("КорректировкаПрошлогоПериода", КорректировкаПрошлогоПериода);
	Запрос.УстановитьПараметр("КорректировкаТекущегоПериода", КорректировкаТекущегоПериода);
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Реквизиты.Валюта, Реквизиты.ВалютаВзаиморасчетов, Реквизиты.ПериодПродажи);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр", Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуВзаиморасчетов", Коэффициенты.КоэффициентПересчетаВВалютуВзаиморасчетов);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);

	Запрос.Текст = 
	"
	// 0 Временная таблица ВтТаблицаРасхожденияТоваров
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки			  КАК НомерСтроки,
	|	ТаблицаРасхождения.Номенклатура			  КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика		  КАК Характеристика,
	|	ТаблицаРасхождения.Склад				  КАК Склад,
	|	ТаблицаРасхождения.ЗаказКлиента			  КАК ЗаказКлиента,
	|	ТаблицаРасхождения.КодСтроки			  КАК КодСтроки,
	|	ТаблицаРасхождения.РаспоряжениеНаИнвентаризацию КАК РаспоряжениеНаИнвентаризацию,
	|	ТаблицаРасхождения.ВариантОтражения 	  КАК ВариантОтражения,
	|	ТаблицаРасхождения.Количество			  КАК Количество
	|ПОМЕСТИТЬ ВтТаблицаРасхожденияТоваров
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 1 Временная таблица ВтТаблицаВидыЗапасовСписание
	|ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки                  КАК НомерСтроки,
	|	ВидыЗапасов.Номенклатура                 КАК Номенклатура,
	|	ВидыЗапасов.Характеристика               КАК Характеристика,
	|	ВидыЗапасов.ВидЗапасов                   КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасов.ТипЗапасов        КАК ТипЗапасов,
	|	ВидыЗапасов.НомерГТД                     КАК НомерГТД,
	|	ВидыЗапасов.Количество                   КАК Количество,
	|	ВидыЗапасов.СуммаСНДС					 КАК СуммаСНДС,
	|
	|	ВЫРАЗИТЬ(ВидыЗапасов.СуммаСНДС
	|	 * &КоэффициентПересчетаВВалютуУпр
	|		КАК ЧИСЛО(15,2))                     КАК СуммаСНДСУпр,
	|
	|	ВЫРАЗИТЬ((ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС)
	|	 * &КоэффициентПересчетаВВалютуУпр
	|		КАК ЧИСЛО(15,2))                     КАК СуммаБезНДСУпр,
	|
	|	ВЫРАЗИТЬ((ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС)
	|	 * &КоэффициентПересчетаВВалютуРегл
	|		КАК ЧИСЛО(15,2))                     КАК СуммаБезНДСРегл,

	|
	|	ВидыЗапасов.Склад                        КАК Склад,
	|	ВидыЗапасов.ЗаказКлиента                 КАК ЗаказКлиента
	|ПОМЕСТИТЬ ВтТаблицаВидыЗапасовСписание
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И (НЕ ВидыЗапасов.НаДоходыРасходы)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 2 Временная таблица ВтТаблицаВидыЗапасовОприходование
	|ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки                             КАК НомерСтроки,
	|	ВидыЗапасов.Номенклатура                            КАК Номенклатура,
	|	ВидыЗапасов.Характеристика                          КАК Характеристика,
	|	ВидыЗапасов.ВидЗапасов                              КАК ВидЗапасов,
	|	ВидыЗапасов.АналитикаУчетаПартий					КАК АналитикаУчетаПартий,
	|	ВидыЗапасов.ВидЗапасов.ТипЗапасов                   КАК ТипЗапасов,
	|	ВидыЗапасов.НомерГТД                     			КАК НомерГТД,
	|	ВидыЗапасов.Количество                              КАК Количество,
	|	ВидыЗапасов.СуммаСНДС					 			КАК СуммаСНДС,
	|	ВидыЗапасов.СуммаНДС					 			КАК СуммаНДС,
	|
	|	ВЫРАЗИТЬ(ВидыЗапасов.СуммаСНДС
	|		* &КоэффициентПересчетаВВалютуУпр 
	|		КАК ЧИСЛО(15,2))                                КАК СуммаСНДСУпр,
	|
	|	ВЫРАЗИТЬ((ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС)
	|		* &КоэффициентПересчетаВВалютуУпр 
	|		КАК ЧИСЛО(15,2))                                КАК СуммаБезНДСУпр,
	|
	|	ВЫРАЗИТЬ((ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС)
	|		* &КоэффициентПересчетаВВалютуРегл 
	|		КАК ЧИСЛО(15,2))                                КАК СуммаБезНДСРегл,
	|
	|	ВидыЗапасов.Склад				                    КАК Склад,
	|	ВидыЗапасов.СкладОтгрузки                           КАК СкладОтгрузки,
	|	ВидыЗапасов.ЗаказКлиента                 			КАК ЗаказКлиента,
	|	ВидыЗапасов.ВидЗапасовОтгрузки                      КАК ВидЗапасовОтгрузки
	|ПОМЕСТИТЬ ВтТаблицаВидыЗапасовОприходование
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И (НЕ ВидыЗапасов.НаДоходыРасходы)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 3 ТаблицаЗаказыКлиентов
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                       		   КАК Период,
	|	ТаблицаРасхождения.ЗаказКлиента        КАК ЗаказКлиента,
	|	ТаблицаРасхождения.Номенклатура   	   КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика 	   КАК Характеристика,
	|	ТаблицаРасхождения.КодСтроки		   КАК КодСтроки,
	|
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ТаблицаРасхождения.Склад
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                  КАК Склад,
	|
	|	ТаблицаРасхождения.Количество          КАК Заказано,
	|	ТаблицаРасхождения.Количество          КАК КОформлению,
	|	ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуВзаиморасчетов КАК Сумма
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.КодСтроки <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаРасхождения.НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 4 ТаблицаСвободныеОстатки
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период 							   КАК Период,
	|	ТаблицаРасхождения.Склад			   КАК Склад,
	|	ТаблицаРасхождения.Номенклатура		   КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика	   КАК Характеристика,
	|
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.КодСтроки <> 0
	|			ТОГДА ТаблицаРасхождения.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ								   КАК ВРезерве,
	|
	|	ТаблицаРасхождения.Количество		   КАК ВНаличии
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУменьшитьСкладскиеОстатки)
	|	ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаРасхождения.НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 5 ТаблицаТоварыНаСкладах
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период								   КАК Период,
	|	ТаблицаРасхождения.Склад			   КАК Склад,
	|	ТаблицаРасхождения.Номенклатура		   КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика	   КАК Характеристика,
	|	ТаблицаРасхождения.Количество		   КАК ВНаличии,
	|	ИСТИНА								   КАК КонтролироватьОстатки
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУменьшитьСкладскиеОстатки)
	|	ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаРасхождения.НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	// 6 ТаблицаДвижениеТоваров
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки	  КАК НомерСтроки,
	|	&Период						 	  КАК Период,
	|	ТаблицаРасхождения.Склад		  КАК Склад,
	|	ТаблицаРасхождения.Номенклатура   КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.ЗаказКлиента	  КАК Распоряжение,
	|	ТаблицаРасхождения.Количество 	  КАК ПланируемаяОтгрузка,
	|	ИСТИНА 							  КАК Корректировка
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.КодСтроки <> 0
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУчестьПриИнвентаризации)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки	  КАК НомерСтроки,
	|	&Период 						  КАК Период,
	|	ТаблицаРасхождения.Склад 		  КАК Склад,
	|	ТаблицаРасхождения.Номенклатура   КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.ЗаказКлиента	  КАК Распоряжение,
	|	-ТаблицаРасхождения.Количество	  КАК ПланируемаяОтгрузка,
	|	ЛОЖЬ							  КАК Корректировка
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.КодСтроки <> 0
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУменьшитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУчестьПриИнвентаризации)
	|	)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаРасхождения.НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	// 7 ТаблицаТоварыКОформлениюИзлишковНедостач
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 	  КАК ВидДвижения,
	|	&Период							  	 	  КАК Период,
	|	ТаблицаРасхождения.Склад		  		  КАК Склад,
	|	ТаблицаРасхождения.Номенклатура	  		  КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика 		  КАК Характеристика,
	|	ТаблицаРасхождения.РаспоряжениеНаИнвентаризацию КАК ДокументОснование,
	|	-ТаблицаРасхождения.Количество 			  КАК КОформлениюАктов
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУчестьПриИнвентаризации)
	|	ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУчестьПриИнвентаризации)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаРасхождения.НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////";
	Запрос.Текст = Запрос.Текст
		+ ТекстЗапросаТаблицаТоварыОрганизаций()
		+ ТекстЗапросаТаблицаТоварыОрганизацийКПередаче()
		+ ТекстЗапросаТаблицаТоварыКОформлениюОтчетовКомитенту()
		+ ТекстЗапросаВтАналитика()
		+ ТекстЗапросаТаблицаСебестоимостьТоваров()
		+ ТекстЗапросаТаблицаВыручкаИСебестоимостьПродаж()
		+ ТекстЗапросаТаблицаРасчетыСКлиентами()
		+ ТекстЗапросаТаблицаРасчетыСКлиентамиПоследовательность()
		+ ТекстЗапросаТаблицаПрочиеДоходы()
		+ ТекстЗапросаТаблицаПрочиеРасходы()
		+ ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл()
		+ ТекстЗапросаТаблицаПартииТоваровОрганизацийПоследовательность()
		+ ТекстЗапросаТаблицаПартииПрочихРасходов()
		+ ТекстЗапросаТаблицаПартииТоваровОрганизаций()
		;
	
	Результат = Запрос.ВыполнитьПакет();
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	// МассивРезультатов[0] - ВтТаблицаРасхожденияТоваров
	// МассивРезультатов[1] - ВтТаблицаВидыЗапасовСписание
	// МассивРезультатов[2] - ВтТаблицаВидыЗапасовОприходование
	ТаблицыДляДвижений.Вставить("ТаблицаЗаказыКлиентов",                      			Результат[3].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаСвободныеОстатки",                    			Результат[4].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыНаСкладах",                     			Результат[5].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДвижениеТоваров",                     			Результат[6].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыКОформлениюИзлишковНедостач",   			Результат[7].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыОрганизаций",                   			Результат[8].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыОрганизацийКПередаче",          			Результат[9].Выгрузить());
	// МассивРезультатов[10] - КурсыВалют
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыКОформлениюОтчетовКомитенту",   			Результат[11].Выгрузить());
	// МассивРезультатов[12] - ВтАналитика
	// МассивРезультатов[13] - ВтАналитикаРеализации
	ТаблицыДляДвижений.Вставить("ТаблицаСебестоимостьТоваров",                			Результат[14].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаВыручкаИСебестоимостьПродаж",         			Результат[15].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСКлиентами",                   			Результат[16].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСКлиентамиПоследовательность", 			Результат[17].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПрочиеДоходы",						  			Результат[18].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПрочиеРасходы",						  			Результат[19].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаСуммыДокументовВВалютеРегл",          			Результат[20].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПартииТоваровОрганизацийПоследовательность",	Результат[21].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПартииПрочихРасходов",							Результат[22].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПартииТоваровОрганизаций",						Результат[23].Выгрузить());
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиНоменклатуры(Реквизиты)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаТовары.Склад          КАК Склад,
	|	ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаТовары.Склад          КАК Склад,
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика
	|	ИЗ
	|		Документ.КорректировкаРеализации.Товары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	) КАК ТаблицаТовары
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТаблицаТовары.Номенклатура     = Аналитика.Номенклатура
	|		И ТаблицаТовары.Характеристика = Аналитика.Характеристика
	|		И ТаблицаТовары.Склад          = Аналитика.Склад
	|ГДЕ
	|	Аналитика.Номенклатура ЕСТЬ NULL
	|");
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка)
	КонецЦикла;

КонецПроцедуры

Функция ТекстЗапросаТаблицаТоварыОрганизаций()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК ОрганизацияОтгрузки,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.Организация
	|		ИНАЧЕ &Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца
	|		ИНАЧЕ ТаблицаВидыЗапасов.ВидЗапасов
	|	КОНЕЦ КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Склад КАК Склад,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	&ДокументОснование КАК ДокументРеализации,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ВидыАналитики.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&НалогообложениеНДС КАК НалогообложениеНДС
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК ВидыАналитики
	|		ПО ТаблицаВидыЗапасов.Номенклатура = ВидыАналитики.Номенклатура
	|			И ТаблицаВидыЗапасов.Характеристика = ВидыАналитики.Характеристика
	|			И ТаблицаВидыЗапасов.Склад = ВидыАналитики.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Период,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.Организация
	|		ИНАЧЕ &Организация
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца
	|		ИНАЧЕ ТаблицаВидыЗапасов.ВидЗапасов
	|	КОНЕЦ,
	|	ТаблицаВидыЗапасов.Склад,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД,
	|	&ДокументОснование,
	|	-ТаблицаВидыЗапасов.Количество,
	|	ВидыАналитики.КлючАналитики,
	|	&ХозяйственнаяОперация,
	|	&НалогообложениеНДС 
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК ВидыАналитики
	|		ПО ТаблицаВидыЗапасов.Номенклатура = ВидыАналитики.Номенклатура
	|			И ТаблицаВидыЗапасов.Характеристика = ВидыАналитики.Характеристика
	|			И ТаблицаВидыЗапасов.Склад = ВидыАналитики.Склад
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизацийКПередаче()
	
	ТекстЗапроса = "
	// Виды запасов к списанию
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки								  КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)						  КАК ВидДвижения,
	|	&Период														  КАК Период,
	|	ТаблицаВидыЗапасов.Склад									  КАК Склад,
	|	ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.Организация КАК ОрганизацияВладелец,
	|	ТаблицаВидыЗапасов.Номенклатура								  КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика							  КАК Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов								  КАК ВидЗапасовПродавца,
	|	ТаблицаВидыЗапасов.НомерГТД									  КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество								  КАК Количество,
	|	0															  КАК Возвращено
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к оприходованию
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки								  КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)						  КАК ВидДвижения,
	|	&Период														  КАК Период,
	|	ТаблицаВидыЗапасов.Склад									  КАК Склад,
	|	ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.Организация КАК ОрганизацияВладелец,
	|	ТаблицаВидыЗапасов.Номенклатура								  КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика							  КАК Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов								  КАК ВидЗапасовПродавца,
	|	ТаблицаВидыЗапасов.НомерГТД									  КАК НомерГТД,
	|	0															  КАК Количество,
	|	ТаблицаВидыЗапасов.Количество								  КАК Возвращено
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыКОформлениюОтчетовКомитенту()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	КурсыВалют.Валюта КАК Валюта,
	|
	|	(КурсВалютыДокумента.Курс * КурсыВалют.Кратность)
	|   / (КурсВалютыДокумента.Кратность * КурсыВалют.Курс) КАК КоэффициентПересчета
	|
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&Период,
	|		Валюта В (
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|						ТОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.Валюта
	|					ИНАЧЕ ТаблицаВидыЗапасов.ВидЗапасов.Валюта
	|				КОНЕЦ КАК Валюта
	|			ИЗ
	|				ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|			ГДЕ
	|				ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|				ИЛИ ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|			ОБЪЕДИНИТЬ
	|
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|						ТОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.Валюта
	|					ИНАЧЕ ТаблицаВидыЗапасов.ВидЗапасов.Валюта
	|				КОНЕЦ КАК Валюта
	|			ИЗ
	|				ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|			ГДЕ
	|				ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|				ИЛИ ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|			ОБЪЕДИНИТЬ
	|
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|						ТОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.Валюта
	|					ИНАЧЕ ТаблицаВидыЗапасов.ВидЗапасов.Валюта
	|				КОНЕЦ КАК Валюта
	|			ИЗ
	|				Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ТаблицаВидыЗапасов
	|			ГДЕ
	|				ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|				И (ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|				ИЛИ ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар))
	|			)
	|	) КАК КурсыВалют
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.КурсыВалют.СрезПоследних(&ПериодПродажи, Валюта = &Валюта) КАК КурсВалютыДокумента
	|	ПО
	|		Истина
	|
	|ГДЕ
	|	КурсВалютыДокумента.Кратность <> 0
	|;
	|/////////////////////////////////////////////////////////////////////////////
	// Виды запасов к списанию
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки								   КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)						   КАК ВидДвижения,
	|	&Период														   КАК Период,
	|	ТаблицаВидыЗапасов.ВидЗапасов.Валюта						   КАК Валюта,
	|	ТаблицаВидыЗапасов.ВидЗапасов								   КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура								   КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика							   КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД								  	   КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация,
	|	&ДокументОснование											   КАК ДокументРеализации,
	|	ТаблицаВидыЗапасов.Количество								   КАК Количество,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.Валюта = &Валюта ТОГДА
	|		ТаблицаВидыЗапасов.СуммаСНДС
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.СуммаСНДС * КурсыВалют.КоэффициентПересчета
	|	КОНЕЦ														   КАК СуммаВыручки
	|
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсыВалют
	|	ПО
	|		ТаблицаВидыЗапасов.ВидЗапасов.Валюта = КурсыВалют.Валюта
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВидыЗапасовВладельца.Валюта									   КАК Валюта,
	|	ВидыЗапасовВладельца.Ссылка									   КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура								   КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика							   КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД									   КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация,
	|	&ДокументОснование											   КАК ДокументРеализации,
	|	ТаблицаВидыЗапасов.Количество								   КАК Количество,
	|
	|	ВЫБОР КОГДА ВидыЗапасовВладельца.Валюта = &Валюта ТОГДА
	|		ТаблицаВидыЗапасов.СуммаСНДС
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.СуммаСНДС * КурсыВалют.КоэффициентПересчета
	|	КОНЕЦ														   КАК СуммаВыручки
	|
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК ВидыЗапасовВладельца
	|	ПО
	|       ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца = ВидыЗапасовВладельца.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсыВалют
	|	ПО
	|		ВидыЗапасовВладельца.Валюта = КурсыВалют.Валюта
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к оприходованию
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки										 КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)								 КАК ВидДвижения,
	|	&Период																 КАК Период,
	|	ТаблицаВидыЗапасов.ВидЗапасов.Валюта								 КАК Валюта,
	|	ТаблицаВидыЗапасов.ВидЗапасов										 КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура										 КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика									 КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД											 КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента) КАК ХозяйственнаяОперация,
	|	&ДокументОснование													 КАК ДокументРеализации,
	|	(-ТаблицаВидыЗапасов.Количество)									 КАК Количество,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.Валюта = &Валюта ТОГДА
	|		-ТаблицаВидыЗапасов.СуммаСНДС
	|	ИНАЧЕ
	|		-ТаблицаВидыЗапасов.СуммаСНДС * КурсыВалют.КоэффициентПересчета
	|	КОНЕЦ																 КАК СуммаВыручки
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсыВалют
	|	ПО
	|		ТаблицаВидыЗапасов.ВидЗапасов.Валюта = КурсыВалют.Валюта
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к корректировке выручки
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки 									КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 							КАК ВидДвижения,
	|	&Период 														КАК Период,
	|	ТаблицаВидыЗапасов.ВидЗапасов.Валюта 							КАК Валюта,
	|	ТаблицаВидыЗапасов.ВидЗапасов 									КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура 								КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика 								КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД										КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) 	КАК ХозяйственнаяОперация,
	|	&ДокументОснование												КАК ДокументРеализации,
	|	0 																КАК Количество,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.Валюта = &Валюта ТОГДА
	|		ТаблицаВидыЗапасов.СуммаСНДС
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.СуммаСНДС * КурсыВалют.КоэффициентПересчета
	|	КОНЕЦ 															КАК СуммаВыручки
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсыВалют
	|	ПО
	|		ТаблицаВидыЗапасов.ВидЗапасов.Валюта = КурсыВалют.Валюта
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикаНоменклатуры.КлючАналитики           КАК АналитикаНоменклатуры,
	|	ТаблицаРасхождения.Номенклатура               КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика             КАК Характеристика,
	|	ТаблицаРасхождения.Склад                      КАК Склад
	|
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|	ПО 
	|		ТаблицаРасхождения.Номенклатура     = АналитикаНоменклатуры.Номенклатура
	|		И ТаблицаРасхождения.Характеристика = АналитикаНоменклатуры.Характеристика
	|		И ТаблицаРасхождения.Склад          = АналитикаНоменклатуры.Склад
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикаНоменклатуры.КлючАналитики КАК АналитикаРеализации,
	|	ТаблицаВидыЗапасов.Номенклатура 	КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика 	КАК Характеристика,
	|	ТаблицаВидыЗапасов.СкладОтгрузки 	КАК СкладОтгрузки
	|
	|ПОМЕСТИТЬ ВтАналитикаРеализации
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура 	= АналитикаНоменклатуры.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = АналитикаНоменклатуры.Характеристика
	|		И ТаблицаВидыЗапасов.СкладОтгрузки 	= АналитикаНоменклатуры.Склад
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСебестоимостьТоваров()

	ТекстЗапроса = "
	// Виды запасов к списанию
	|ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВтАналитика.АналитикаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|   	ВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ВидыЗапасов.Количество КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация,
	|	Неопределено КАК КорРазделУчета,
	|
	|	Неопределено КАК КорВидЗапасов,
	|
	|	Неопределено КАК КорАналитикаУчетаНоменклатуры,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК ВтАналитика
	|	ПО
	|		ВидыЗапасов.Склад = ВтАналитика.Склад
	|		И ВидыЗапасов.Номенклатура = ВтАналитика.Номенклатура
	|		И ВидыЗапасов.Характеристика = ВтАналитика.Характеристика
	|ГДЕ
	|	ВидыЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к оприходованию
	//
	// Корректировка прошлого периода.
	|ВЫБРАТЬ
	|	2 КАК Порядок,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВтАналитика.АналитикаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,	
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|   	ВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ВидыЗапасов.Количество КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов) КАК ХозяйственнаяОперация,
	|	Неопределено КАК КорРазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВЫБОР КОГДА ВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|			ВидыЗапасов.ВидЗапасовОтгрузки
	|		ИНАЧЕ
	|			Неопределено
	|		КОНЕЦ
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК КорВидЗапасов,
	|
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
	|
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|	ВидыЗапасов.ЗаказКлиента КАК ЗаказКлиента,
	|	&ПериодПродажи КАК ПериодПродажи
	|	
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК ВтАналитика
	|	ПО
	|		ВидыЗапасов.Номенклатура = ВтАналитика.Номенклатура
	|		И ВидыЗапасов.Характеристика = ВтАналитика.Характеристика
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитикаРеализации КАК ВтАналитикаРеализации
	|	ПО
	|		ВидыЗапасов.Номенклатура = ВтАналитикаРеализации.Номенклатура
	|		И ВидыЗапасов.Характеристика = ВтАналитикаРеализации.Характеристика
	|		И ВидыЗапасов.СкладОтгрузки = ВтАналитикаРеализации.СкладОтгрузки
	|ГДЕ
	|	&КорректировкаПрошлогоПериода
	|	И ВидыЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Корректировка реализации текущего периода. Используется аналитика продажи.
	//
	|ВЫБРАТЬ
	|	3 КАК Порядок,
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВтАналитикаРеализации.АналитикаРеализации КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|   		ТаблицаВидыЗапасов.ВидЗапасовОтгрузки
	|		ИНАЧЕ
	|   		ТаблицаВидыЗапасов.ВидЗапасов
	|		КОНЕЦ
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	-ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноРеализации) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|	ТаблицаВидыЗапасов.ЗаказКлиента КАК ЗаказКлиента,
	|	&ПериодПродажи КАК ПериодПродажи
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитикаРеализации КАК ВтАналитикаРеализации
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура  = ВтАналитикаРеализации.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = ВтАналитикаРеализации.Характеристика
	|		И ТаблицаВидыЗапасов.СкладОтгрузки = ВтАналитикаРеализации.СкладОтгрузки
	|ГДЕ
	|	&КорректировкаТекущегоПериода
	|	И ТаблицаВидыЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Перемещение с реализации на корректировку - списание
	|ВЫБРАТЬ
	|	4 КАК Порядок,
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВтАналитикаРеализации.АналитикаРеализации КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|   		ТаблицаВидыЗапасов.ВидЗапасовОтгрузки
	|		ИНАЧЕ
	|   		ТаблицаВидыЗапасов.ВидЗапасов
	|		КОНЕЦ
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК КорРазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК КорВидЗапасов,
	|
	|	ВтАналитика.АналитикаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
	|	ДатаВремя(1,1,1) КАК ПериодПродажи
	|	
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК ВтАналитика
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура = ВтАналитика.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = ВтАналитика.Характеристика
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитикаРеализации КАК ВтАналитикаРеализации
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура  = ВтАналитикаРеализации.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = ВтАналитикаРеализации.Характеристика
	|		И (
	|			ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|				ТаблицаВидыЗапасов.ВидЗапасов.Комитент
	|			ИНАЧЕ
	|				ТаблицаВидыЗапасов.СкладОтгрузки
	|			КОНЕЦ = ВтАналитикаРеализации.СкладОтгрузки
	|		)
	|ГДЕ
	|	&КорректировкаТекущегоПериода
	|	И ТаблицаВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|	И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Перемещение с реализации на корректировку - оприходование
	|ВЫБРАТЬ
	|	5 КАК Порядок,
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|
	|	ВтАналитика.АналитикаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
	|	ДатаВремя(1,1,1) КАК ПериодПродажи
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК ВтАналитика
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура = ВтАналитика.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = ВтАналитика.Характеристика
	|ГДЕ
	|	&КорректировкаТекущегоПериода
	|	И ТаблицаВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|	И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтрокиДокумента
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВыручкаИСебестоимостьПродаж()
	
	ТекстЗапроса = "
	// Виды запасов к списанию
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки     КАК НомерСтроки,
	|	&Период                            КАК Период,
	|	&Подразделение                     КАК Подразделение,
	|	ВтАналитика.АналитикаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|	&АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ТаблицаВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|	ТаблицаВидыЗапасов.ТипЗапасов 	   КАК ТипЗапасов,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|   	ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ                              КАК ВидЗапасов,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаСНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ                              КАК Себестоимость,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ                              КАК СебестоимостьБезНДС,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ                              КАК СебестоимостьРегл,
	|
	|	ТаблицаВидыЗапасов.Количество      КАК Количество,
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр    КАК СуммаВыручки,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр  КАК СуммаВыручкиБезНДС,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК СуммаВыручкиРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК ВтАналитика
	|	ПО 
	|		ТаблицаВидыЗапасов.Склад 		  	= ВтАналитика.Склад
	|		И ТаблицаВидыЗапасов.Номенклатура 	= ВтАналитика.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = ВтАналитика.Характеристика
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Расхождения по услугам 
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки    				КАК НомерСтроки,
	|	&Период                           				КАК Период,
	|	&Подразделение                    				КАК Подразделение,
	|	ВтАналитика.АналитикаНоменклатуры 				КАК АналитикаУчетаНоменклатуры,
	|	&АналитикаУчетаПоПартнерам        				КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ТаблицаРасхождения.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ 											КАК ЗаказКлиента,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)     	КАК ТипЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) 	КАК ВидЗапасов,
	|	0                                             	КАК Себестоимость,
	|	0                                             	КАК СебестоимостьБезНДС,
	|	0                                             	КАК СебестоимостьРегл,
	|	ТаблицаРасхождения.Количество                 	КАК Количество,
	|	ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС
	|		* &КоэффициентПересчетаВВалютуУпр 
	|			КАК ЧИСЛО(15,2))                      	КАК СуммаВыручки,
	|	ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС
	|			- ТаблицаРасхождения.СуммаНДС)
	|		* &КоэффициентПересчетаВВалютуУпр
	|			КАК ЧИСЛО(15,2))                      	КАК СуммаВыручкиБезНДС,
	|
	|	ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС
	|			- ТаблицаРасхождения.СуммаНДС)
	|		* &КоэффициентПересчетаВВалютуРегл 
	|			КАК ЧИСЛО(15,2))                      	КАК СуммаВыручкиРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК ВтАналитика
	|	ПО 
	|		ТаблицаРасхождения.Номенклатура = ВтАналитика.Номенклатура
	|		И ТаблицаРасхождения.Характеристика = ВтАналитика.Характеристика
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры НЕ В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к оприходованию
	|ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	&Подразделение КАК Подразделение,
	|	ВтАналитикаРеализации.АналитикаРеализации КАК АналитикаУчетаНоменклатуры,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	ВидыЗапасов.ТипЗапасов КАК ТипЗапасов,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВЫБОР КОГДА ВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|   		ВидыЗапасов.ВидЗапасовОтгрузки
	|		ИНАЧЕ
	|   		ВидыЗапасов.ВидЗапасов
	|		КОНЕЦ
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ВЫБОР КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|		- ВидыЗапасов.СуммаСНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК Себестоимость,
	|	ВЫБОР КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|		- ВидыЗапасов.СуммаБезНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СебестоимостьБезНДС,
	|	ВЫБОР КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|		- ВидыЗапасов.СуммаБезНДСРегл
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СебестоимостьРегл,
	|
	|	- ВидыЗапасов.Количество      КАК Количество,
	|	- ВидыЗапасов.СуммаСНДСУпр    КАК СуммаВыручки,
	|	- ВидыЗапасов.СуммаБезНДСУпр  КАК СуммаВыручкиБезНДС,
	|	- ВидыЗапасов.СуммаБезНДСРегл КАК СуммаВыручкиРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноРеализации) КАК ХозяйственнаяОперация
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ВидыЗапасов
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитикаРеализации КАК ВтАналитикаРеализации
	|	ПО
	|		ВидыЗапасов.Номенклатура 	 = ВтАналитикаРеализации.Номенклатура
	|		И ВидыЗапасов.Характеристика = ВтАналитикаРеализации.Характеристика
	|		И ВидыЗапасов.СкладОтгрузки  = ВтАналитикаРеализации.СкладОтгрузки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к корректировке выручки
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки    КАК НомерСтроки,
	|	&Период                           КАК Период,
	|	&Подразделение                    КАК Подразделение,
	|	ВтАналитика.АналитикаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	&АналитикаУчетаПоПартнерам        КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ТаблицаВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|	ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|   	ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ                             КАК ВидЗапасов,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС
	|				* &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ                             КАК Себестоимость,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС
	|				- ТаблицаВидыЗапасов.СуммаНДС)
	|					* &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ                             КАК СебестоимостьБезНДС,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС
	|				- ТаблицаВидыЗапасов.СуммаНДС)
	|				* &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ                             КАК СебестоимостьРегл,
	|	0							      КАК Количество,
	|	ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС 
	|		* &КоэффициентПересчетаВВалютуУпр
	|						 КАК ЧИСЛО(15,2)) КАК СуммаВыручки,
	|
	|	ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС
	|		- ТаблицаВидыЗапасов.СуммаНДС )
	|		* &КоэффициентПересчетаВВалютуУпр
	|		КАК ЧИСЛО(15,2))                  КАК СуммаВыручкиБезНДС,
	|
	|	ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС
	|		- ТаблицаВидыЗапасов.СуммаНДС )
	|		* &КоэффициентПересчетаВВалютуРегл
	|		КАК ЧИСЛО(15,2))                  КАК СуммаВыручкиРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК ВтАналитика
	|	ПО 
	|		ТаблицаВидыЗапасов.Склад		    = ВтАналитика.Склад
	|		И ТаблицаВидыЗапасов.Номенклатура   = ВтАналитика.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = ВтАналитика.Характеристика
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСКлиентами()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период																			 КАК Период,
	|	&Период																			 КАК ДатаРегистратора,
	|	&ДатаПлатежа																	 КАК ДатаПлатежа,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ																			 КАК ВидДвижения,
	|
	|	&АналитикаУчетаПоПартнерам														 КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ТаблицаРасхождения.ЗаказКлиента
	|	КОНЕЦ																			 КАК ЗаказКлиента,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		ТаблицаРасхождения.ЗаказКлиента
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ																			 КАК ПродажаПоЗаказу,
	|
	|	&ВалютаВзаиморасчетов 															 КАК Валюта,
	|	Истина 																			 КАК ИсключатьПриКонтроле,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) 		 КАК ХозяйственнаяОперация,
	|	Неопределено 																	 КАК ФормаОплаты,
	|
	|	СУММА(ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		-ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|	ИНАЧЕ
	|		ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|	КОНЕЦ)																			 КАК Сумма,
	|
	|	0 																				 КАК КОплате,
	|
	|	СУММА(ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		-ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|	ИНАЧЕ
	|		ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|	КОНЕЦ)																			 КАК КОтгрузке
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.КодСтроки <> 0
	|	И Не &РасчетыПоНакладным
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период																			 КАК Период,
	|	&Период																			 КАК ДатаРегистратора,
	|	&ДатаПлатежа																	 КАК ДатаПлатежа,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ																			 КАК ВидДвижения,
	|
	|	&АналитикаУчетаПоПартнерам														 КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ПродажаПоЗаказам И Не &РасчетыПоНакладным ТОГДА
	|			ТаблицаРасхождения.ЗаказКлиента
	|		ИНАЧЕ
	|			&ДокументОснование
	|		КОНЕЦ
	|	КОНЕЦ																			 КАК ЗаказКлиента,
	|
	|	Неопределено																	 КАК ПродажаПоЗаказу,
	|	&ВалютаВзаиморасчетов															 КАК Валюта,
	|	Истина																			 КАК ИсключатьПриКонтроле,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)			 КАК ХозяйственнаяОперация,
	|	Неопределено																	 КАК ФормаОплаты,
	|
	|	СУММА(ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		-ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|	ИНАЧЕ
	|		ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|	КОНЕЦ)																			 КАК Сумма,
	|
	|	0																				 КАК КОплате,
	|	0																				 КАК КОтгрузке
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И (ТаблицаРасхождения.КодСтроки = 0
	|		Или &РасчетыПоНакладным)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(&ДатаПлатежа, День) 												 КАК Период,
	|	&Период																			 КАК ДатаРегистратора,
	|	&ДатаПлатежа																	 КАК ДатаПлатежа,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ																			 КАК ВидДвижения,
	|
	|	&АналитикаУчетаПоПартнерам														 КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ПродажаПоЗаказам И Не &РасчетыПоНакладным ТОГДА
	|			ТаблицаРасхождения.ЗаказКлиента
	|		ИНАЧЕ
	|			&ДокументОснование
	|		КОНЕЦ
	|	КОНЕЦ																			 КАК ЗаказКлиента,
	|
	|	Неопределено																	 КАК ПродажаПоЗаказу,
	|	&ВалютаВзаиморасчетов															 КАК Валюта,
	|	Истина																			 КАК ИсключатьПриКонтроле,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) 		 КАК ХозяйственнаяОперация,
	|	&ФормаОплаты																	 КАК ФормаОплаты,
	|	0																				 КАК Сумма,
	|
	|	СУММА(ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		-ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|	ИНАЧЕ
	|		ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|	КОНЕЦ)																			 КАК КОплате,
	|
	|	0																				 КАК КОтгрузке
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И (ТаблицаРасхождения.КодСтроки = 0
	|		Или &РасчетыПоНакладным)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСКлиентамиПоследовательность()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеДоходы()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                               			     					КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)			     					КАК ВидДвижения,
	|	&Организация                          			     					КАК Организация,
	|	&Подразделение                        			     					КАК Подразделение,
	|	&СтатьяДоходов          			     								КАК СтатьяДоходов,
	|	&АналитикаДоходов       			     								КАК АналитикаДоходов,
	|
	|	ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС
	|	* &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2))						КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.НаДоходыРасходы
	|	И &ИспользоватьУчетПрочихДоходовРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаВидыЗапасов.НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                               			     					КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)			     					КАК ВидДвижения,
	|	&Организация                          			     					КАК Организация,
	|	&Подразделение                        			     					КАК Подразделение,
	|	&СтатьяРасходов          			     								КАК СтатьяРасходов,
	|	&АналитикаРасходов       			     								КАК АналитикаРасходов,
	|
	|	ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС
	|	* &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2))						КАК Сумма,
	|
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.НаДоходыРасходы
	|	И &ВариантРаспределенияРасходов <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ИспользоватьУчетПрочихДоходовРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаВидыЗапасов.НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                               			     					КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)			     					КАК ВидДвижения,
	|	&Организация                          			     					КАК Организация,
	|	&Подразделение                        			     					КАК Подразделение,
	|	&СтатьяРасходов          			     								КАК СтатьяРасходов,
	|	&АналитикаРасходов       			     								КАК АналитикаРасходов,
	|
	|	ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2)) КАК Стоимость,
	|
	|	ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2)) КАК СтоимостьБезНДС,
	|
	|	ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2)) КАК СтоимостьРегл,
	|
	|	ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2)) КАК НДСРегл,
	|
	|	&Ссылка 							КАК ДокументПоступленияРасходов,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий КАК АналитикаУчетаПартий
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.НаДоходыРасходы
	|	И &ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ИспользоватьУчетПрочихДоходовРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаВидыЗапасов.НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииТоваровОрганизаций()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 			КАК ВидДвижения,
	|	&Период 										КАК Период,
	|	&Организация 									КАК Организация,
	|	ВидыАналитики.КлючАналитики 					КАК АналитикаУчетаНоменклатуры,
	|	&Ссылка 										КАК ДокументПоступления,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца
	|		ИНАЧЕ ТаблицаВидыЗапасов.ВидЗапасов
	|	КОНЕЦ 											КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий 		КАК АналитикаУчетаПартий,
	|
	|	-ТаблицаВидыЗапасов.Количество 					КАК Количество,
	|	-ТаблицаВидыЗапасов.СуммаСНДСУпр				КАК Стоимость,
	|	-ТаблицаВидыЗапасов.СуммаБезНДСУпр				КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(-ТаблицаВидыЗапасов.СуммаСНДС
	|		* &КоэффициентПересчетаВВалютуРегл 
	|			КАК ЧИСЛО(15,2))						КАК СтоимостьРегл,
	|	ВЫРАЗИТЬ(- ТаблицаВидыЗапасов.СуммаНДС
	|		* &КоэффициентПересчетаВВалютуРегл 
	|			КАК ЧИСЛО(15,2))						КАК НДСРегл,
	|
	|	ТаблицаВидыЗапасов.Номенклатура					КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика				КАК Характеристика,
	|	&ХозяйственнаяОперация							КАК ХозяйственнаяОперация,
	|	&НалогообложениеНДС								КАК НалогообложениеНДС 
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК ВидыАналитики
	|		ПО 
	|			ТаблицаВидыЗапасов.Номенклатура		= ВидыАналитики.Номенклатура
	|			И ТаблицаВидыЗапасов.Характеристика = ВидыАналитики.Характеристика
	|			И ТаблицаВидыЗапасов.Склад			= ВидыАналитики.Склад
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
//Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная", "Расходная накладная", СформироватьПечатнуюФормуНакладная(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РеестрНомеровГТД") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РеестрНомеровГТД", "Реестр номеров ГТД)", СформироватьПечатнуюФормуРеестрНомеровГТД(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

Функция ТекстЗапросаВтВидыЗапасов() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТекущийДокумент.Ссылка КАК Основной,
	|	ТекущийДокумент.ДокументОснование КАК Подчиненный,
	|	ТекущийДокумент.ДокументОснование.МоментВремени КАК МоментВремени
	|ПОМЕСТИТЬ ВтСвязанныеДокументы
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ТекущийДокумент
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ТекущийДокумент.Ссылка = ДанныеДокументов.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущийДокумент.Ссылка КАК Основной,
	|	ПредыдущиеДокументы.Ссылка КАК Подчиненный,
	|	ПредыдущиеДокументы.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ТекущийДокумент
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ТекущийДокумент.Ссылка = ДанныеДокументов.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ПредыдущиеДокументы
	|		ПО ТекущийДокумент.ДокументОснование = ПредыдущиеДокументы.ДокументОснование
	|			И (ТекущийДокумент.Дата >= ПредыдущиеДокументы.Дата
	|			ИЛИ (ТекущийДокумент.Дата = ПредыдущиеДокументы.Дата
	|			И ТекущийДокумент.Ссылка >= ПредыдущиеДокументы.Ссылка))
	|ГДЕ
	|	ПредыдущиеДокументы.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК Основной,
	|	Корректировки.Ссылка КАК Подчиненный,
	|	Корректировки.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.КорректировкаРеализации КАК Корректировки
	|ГДЕ
	|	Корректировки.ДокументОснование = &ОснованиеНеЗаписанногоДокумента
	|	И Корректировки.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК Основной,
	|	ДанныеОснования.Ссылка КАК Подчиненный,
	|	ДанныеОснования.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеОснования
	|ГДЕ
	|	ДанныеОснования.Ссылка = &ОснованиеНеЗаписанногоДокумента
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ДоКорректировки,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.Характеристика КАК Характеристика,
	|	ВидыЗапасов.Упаковка КАК Упаковка,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	) КАК СуммаБезНДС,
	|	
	|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	) КАК СуммаНДС,
	|	
	|	ВидыЗапасов.ЗаказКлиента КАК ЗаказКлиента
	|ПОМЕСТИТЬ ВсеВидыЗапасов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ИСТИНА КАК ДоКорректировки,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.Характеристика КАК Характеристика,
	|	ВидыЗапасов.Упаковка КАК Упаковка,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	) КАК СуммаБезНДС,
	|	
	|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	) КАК СуммаНДС,
	|	
	|	ВидыЗапасов.ЗаказКлиента КАК ЗаказКлиента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ЛОЖЬ,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.Номенклатура,
	|	ВидыЗапасов.Характеристика,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	0,
	|	0,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ИСТИНА,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.Номенклатура,
	|	ВидыЗапасов.Характеристика,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	0,
	|	0,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|			И ВидыЗапасов.Ссылка <> СвязанныеДокументы.Основной
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ЛОЖЬ,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.Номенклатура,
	|	ВидыЗапасов.Характеристика,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	-ВидыЗапасов.КоличествоУпаковок,
	|	-ВидыЗапасов.Количество,
	|	
	|	-ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	-ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ИСТИНА,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.Номенклатура,
	|	ВидыЗапасов.Характеристика,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	-ВидыЗапасов.КоличествоУпаковок,
	|	-ВидыЗапасов.Количество,
	|	
	|	-ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	-ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|			И ВидыЗапасов.Ссылка <> СвязанныеДокументы.Основной
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ЛОЖЬ,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.Номенклатура,
	|	ВидыЗапасов.Характеристика,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	ВидыЗапасов.КоличествоУпаковок,
	|	ВидыЗапасов.Количество,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ИСТИНА,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.Номенклатура,
	|	ВидыЗапасов.Характеристика,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	ВидыЗапасов.КоличествоУпаковок,
	|	ВидыЗапасов.Количество,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|			И ВидыЗапасов.Ссылка <> СвязанныеДокументы.Основной
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеВидыЗапасов.Ссылка                                        КАК Ссылка,
	|	ВсеВидыЗапасов.ДоКорректировки                               КАК ДоКорректировки,
	|	МАКСИМУМ(ВсеВидыЗапасов.НомерСтроки)                         КАК НомерСтроки,
	|	ВсеВидыЗапасов.Номенклатура                                  КАК Номенклатура,
	|	ВсеВидыЗапасов.Характеристика                                КАК Характеристика,
	|	ВсеВидыЗапасов.Упаковка                                      КАК Упаковка,
	|	ВсеВидыЗапасов.НомерГТД                                      КАК НомерГТД,
	|	ВсеВидыЗапасов.ВидЗапасов                                    КАК ВидЗапасов,
	|	СУММА(ВсеВидыЗапасов.КоличествоУпаковок)                     КАК КоличествоУпаковок,
	|	СУММА(ВсеВидыЗапасов.Количество)                             КАК Количество,
	|	СУММА(ВсеВидыЗапасов.СуммаБезНДС)                            КАК СуммаБезНДС,
	|	ВсеВидыЗапасов.СтавкаНДС                                     КАК СтавкаНДС,
	|	СУММА(ВсеВидыЗапасов.СуммаНДС)                               КАК СуммаНДС,
	|	СУММА(ВсеВидыЗапасов.СуммаБезНДС + ВсеВидыЗапасов.СуммаНДС)  КАК СуммаСНДС
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	ВсеВидыЗапасов КАК ВсеВидыЗапасов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеВидыЗапасов.Ссылка,
	|	ВсеВидыЗапасов.ДоКорректировки,
	|	ВсеВидыЗапасов.Номенклатура,
	|	ВсеВидыЗапасов.Характеристика,
	|	ВсеВидыЗапасов.Упаковка,
	|	ВсеВидыЗапасов.НомерГТД,
	|	ВсеВидыЗапасов.ВидЗапасов,
	|	ВсеВидыЗапасов.СтавкаНДС
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВсеВидыЗапасов
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьПараметрыВтВидыЗапасов(Запрос, ПересчитыватьВВалютуРегл, ВключаяДоКорректировки, ОснованиеНеЗаписанногоДокумента = Неопределено) Экспорт
	
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",        ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяДоКорректировки",          ВключаяДоКорректировки);
	Запрос.УстановитьПараметр("ОснованиеНеЗаписанногоДокумента", ОснованиеНеЗаписанногоДокумента);
	
КонецПроцедуры

// Функция получает данные для формирования печатной формы ТОРГ - 12
//
Функция ПолучитьДанныеДляПечатнойФормыТОРГ12(ПараметрыПечати, МассивОбъектов) Экспорт 

	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК ДокументОснование,
	|	ЛОЖЬ КАК Корректировочный
	|
	|ПОМЕСТИТЬ ТаблицаСчетовФактур
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ВыводитьГТД = ?(ПараметрыПечати.Свойство("ВыводитьГТД"), ПараметрыПечати.ВыводитьГТД, Ложь);
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ВыводитьГТД);
	ПродажиСервер.ПоместитьВременнуюТаблицуКоэффициентыУпаковок(МенеджерВременныхТаблиц);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	КорректировкаРеализации.ДокументОснование.Номер КАК Номер,
	|	КорректировкаРеализации.ДокументОснование.Дата КАК Дата,
	|	КорректировкаРеализации.Партнер КАК Партнер,
	|	КорректировкаРеализации.Контрагент КАК Контрагент,
	|	КорректировкаРеализации.Организация КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	КорректировкаРеализации.Отпустил.Наименование КАК Кладовщик,
	|	КорректировкаРеализации.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	КорректировкаРеализации.Организация.Префикс КАК Префикс,
	|	КорректировкаРеализации.Основание КАК Основание,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Контрагент
	|		ИНАЧЕ КорректировкаРеализации.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Организация
	|		ИНАЧЕ КорректировкаРеализации.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	КорректировкаРеализации.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	КорректировкаРеализации.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	КорректировкаРеализации.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	КорректировкаРеализации.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|	КорректировкаРеализации.АдресДоставки КАК АдресДоставки,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	КорректировкаРеализации.Валюта КАК Валюта,
	|	КорректировкаРеализации.ДоверенностьНомер КАК ДоверенностьНомер,
	|	КорректировкаРеализации.ДоверенностьДата КАК ДоверенностьДата,
	|	КорректировкаРеализации.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	КорректировкаРеализации.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	&ЕдиницаИзмеренияВеса КАК ЕдиницаИзмеренияВеса
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО КорректировкаРеализации.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО КорректировкаРеализации.Ссылка = ДанныеДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваров.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ИЛИ ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ИЛИ ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Представление
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ИЛИ ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Код
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТоваров.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА """"
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.Наименование
	|	КОНЕЦ КАК УпаковкаНаименование,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|					ТОГДА ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Представление
	|				ИНАЧЕ КоэффициентыУпаковок.ВидУпаковки.ЕдиницаИзмерения.Представление
	|			КОНЕЦ
	|	КОНЕЦ КАК ВидУпаковки,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
	|	ТаблицаТоваров.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ КоэффициентыУпаковок.Количество / КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР
	|					КОГДА КоэффициентыУпаковок.Количество < КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|						ТОГДА КоэффициентыУпаковок.Количество
	|					ИНАЧЕ КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|					ТОГДА 1
	|				ИНАЧЕ ТаблицаТоваров.Упаковка.Коэффициент
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Цена,
	|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаТоваров.Количество * ТаблицаТоваров.Номенклатура.Вес КАК МассаНетто,
	|	ВЫБОР
	|		КОГДА &ЗаполненаЕдиницаИзмеренияВеса
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL 
	|						ТОГДА ТаблицаТоваров.Количество * ТаблицаТоваров.Номенклатура.Вес
	|					ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок * ТаблицаТоваров.Упаковка.Вес
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК МассаБрутто,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыУпаковок КАК КоэффициентыУпаковок
	|		ПО ТаблицаТоваров.Ссылка = КоэффициентыУпаковок.Ссылка
	|			И ТаблицаТоваров.НомерСтроки = КоэффициентыУпаковок.НомерСтроки
	|ГДЕ
	|	(ТаблицаТоваров.ЭтоТовар
	|			ИЛИ &ВыводитьУслуги)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	Запрос.УстановитьПараметр("ВыводитьУслуги",                  ПараметрыПечати.ВыводитьУслуги);
	Запрос.УстановитьПараметр("КолонкаКодов",                    КолонкаКодов);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса",            Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Функция формирует печатную форму Счета-фактуры
//
Функция ПолучитьДанныеДляПечатнойФормыСчетФактура(ПараметрыПечати, МассивОбъектов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.Валюта               КАК Валюта,
	|	ДанныеДокументов.Организация          КАК Организация,
	|	ДанныеДокументов.Подразделение        КАК Подразделение,
	|	ДанныеДокументов.Склад                КАК Склад
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПродажиСервер.ПоместитьВременнуюТаблицуСчетовФактур(МенеджерВременныхТаблиц);
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, Истина, Истина, НЕ ПараметрыПечати.ПечатьВВалюте);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	&ПредставлениеСчетФактура КАК ПредставлениеДокумента,
	|	ЕСТЬNULL(СчетаФактуры.Номер, НЕОПРЕДЕЛЕНО) КАК Номер,
	|	ЕСТЬNULL(СчетаФактуры.Дата, НЕОПРЕДЕЛЕНО) КАК Дата,
	|	ЕСТЬNULL(СчетаФактуры.НомерИсправления, НЕОПРЕДЕЛЕНО) КАК НомерИсправления,
	|	ЕСТЬNULL(СчетаФактуры.ДатаИсправления, НЕОПРЕДЕЛЕНО) КАК ДатаИсправления,
	|	ЕСТЬNULL(СчетаФактуры.Исправление, ЛОЖЬ) КАК Исправление,
	|	ЕСТЬNULL(СчетаФактуры.НомерСчетаФактуры, НЕОПРЕДЕЛЕНО) КАК НомерСчетаФактуры,
	|	ЕСТЬNULL(СчетаФактуры.ДатаСчетаФактуры, НЕОПРЕДЕЛЕНО) КАК ДатаСчетаФактуры,
	|	ЕСТЬNULL(СчетаФактуры.НомерИсправленияСчетаФактуры, НЕОПРЕДЕЛЕНО) КАК НомерИсправленияСчетаФактуры,
	|	ЕСТЬNULL(СчетаФактуры.ДатаИсправленияСчетаФактуры, НЕОПРЕДЕЛЕНО) КАК ДатаИсправленияСчетаФактуры,
	|	ЕСТЬNULL(СчетаФактуры.Корректировочный, ЛОЖЬ) КАК КорректировочныйСчетФактура,
	|	ЕСТЬNULL(СчетаФактуры.СтрокаПлатежноРасчетныеДокументы, """") КАК СтрокаПоДокументу,
	|	ЕСТЬNULL(СчетаФактуры.Валюта, НЕОПРЕДЕЛЕНО) КАК ВалютаСчетаФактуры,
	|	КорректировкаРеализации.Партнер КАК Партнер,
	|	КорректировкаРеализации.Контрагент КАК Контрагент,
	|	ЕСТЬNULL(СчетаФактуры.ГоловнаяОрганизация, КорректировкаРеализации.Организация) КАК Организация,
	|	КорректировкаРеализации.Организация.Префикс КАК Префикс,
	|	ЕСТЬNULL(СчетаФактуры.ИндексПодразделения, 0) КАК ИндексПодразделения,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Контрагент
	|		ИНАЧЕ КорректировкаРеализации.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(СчетаФактуры.Грузоотправитель, КорректировкаРеализации.Организация)
	|		ИНАЧЕ КорректировкаРеализации.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ЕСТЬNULL(СчетаФактуры.ВыводитьКППГрузоотправителя, ЛОЖЬ) КАК ВыводитьКППГрузоотправителя,
	|	КорректировкаРеализации.АдресДоставки КАК АдресДоставки,
	|	КорректировкаРеализации.Валюта КАК Валюта,
	|	КорректировкаРеализации.Валюта.НаименованиеПолное КАК ВалютаНаименованиеПолное,
	|	КорректировкаРеализации.Валюта.Код КАК ВалютаКод,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоУслуги,
	|	ЛОЖЬ КАК ЭтоПередачаНаКомиссию
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО КорректировкаРеализации.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСчетовФактур КАК СчетаФактуры
	|		ПО КорректировкаРеализации.Ссылка = СчетаФактуры.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТаблицаТоваров.Ссылка КАК Ссылка,
	|			МАКСИМУМ(ВЫБОР
	|					КОГДА НЕ ТаблицаТоваров.ЭтоТовар
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ) КАК ЕстьУслуги,
	|			МАКСИМУМ(ВЫБОР
	|					КОГДА ТаблицаТоваров.ЭтоТовар
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ) КАК ЕстьТовары
	|		ИЗ
	|			ТаблицаТоваров КАК ТаблицаТоваров
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТаблицаТоваров.Ссылка) КАК НоменклатураДокументов
	|		ПО КорректировкаРеализации.Ссылка = НоменклатураДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО КорректировкаРеализации.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ИЛИ ТаблицаДокумента.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ТаблицаДокумента.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ИЛИ ТаблицаДокумента.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ ТаблицаДокумента.Упаковка.ЕдиницаИзмерения.Представление
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ИЛИ ТаблицаДокумента.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ ТаблицаДокумента.Упаковка.ЕдиницаИзмерения.Код
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0)
	|	КОНЕЦ КАК КоличествоДо,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) / ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 1)
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) / ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 1)
	|	КОНЕЦ КАК ЦенаДо,
	|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) КАК СуммаБезНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУменьшение,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) КАК СуммаНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУменьшение,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) КАК СуммаСНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС - (ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС - (ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУменьшение,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара,
	|	1 КАК Порядок
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ТаблицаТоваровПредыдущие
	|		ПО (ТаблицаТоваровПредыдущие.ДоКорректировки)
	|			И ТаблицаДокумента.Ссылка = ТаблицаТоваровПредыдущие.Ссылка
	|			И ТаблицаДокумента.Номенклатура = ТаблицаТоваровПредыдущие.Номенклатура
	|			И ТаблицаДокумента.Характеристика = ТаблицаТоваровПредыдущие.Характеристика
	|			И ТаблицаДокумента.Упаковка = ТаблицаТоваровПредыдущие.Упаковка
	|			И ТаблицаДокумента.НомерГТД = ТаблицаТоваровПредыдущие.НомерГТД
	|			И ТаблицаДокумента.СтавкаНДС = ТаблицаТоваровПредыдущие.СтавкаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСчетовФактур КАК СчетаФактуры
	|		ПО ТаблицаДокумента.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	НЕ ТаблицаДокумента.ДоКорректировки
	|	И (НЕ СчетаФактуры.Корректировочный
	|			ИЛИ ТаблицаДокумента.СуммаНДС <> ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ИЛИ ТаблицаДокумента.СуммаБезНДС <> ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваровПредыдущие.Ссылка,
	|	ТаблицаТоваровПредыдущие.Номенклатура,
	|	ТаблицаТоваровПредыдущие.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ИЛИ ТаблицаТоваровПредыдущие.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ТаблицаТоваровПредыдущие.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ИЛИ ТаблицаТоваровПредыдущие.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ ТаблицаТоваровПредыдущие.Упаковка.ЕдиницаИзмерения.Представление
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ИЛИ ТаблицаТоваровПредыдущие.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ ТаблицаТоваровПредыдущие.Упаковка.ЕдиницаИзмерения.Код
	|	КОНЕЦ,
	|	ТаблицаТоваровПредыдущие.Характеристика,
	|	ТаблицаТоваровПредыдущие.Характеристика.НаименованиеПолное,
	|	ТаблицаТоваровПредыдущие.СтавкаНДС,
	|	ТаблицаТоваровПредыдущие.НомерГТД,
	|	ТаблицаТоваровПредыдущие.НомерГТД.СтранаПроисхождения,
	|	ТаблицаТоваровПредыдущие.НомерГТД.СтранаПроисхождения.Код,
	|	0,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваровПредыдущие.Количество
	|		ИНАЧЕ ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.Количество
	|		ИНАЧЕ ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|	КОНЕЦ,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС + ТаблицаТоваровПредыдущие.СуммаНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС + ТаблицаТоваровПредыдущие.СуммаНДС,
	|	ТаблицаТоваровПредыдущие.НомерСтроки,
	|	ЛОЖЬ,
	|	2
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваровПредыдущие
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ТаблицаТоваровПослеКорректировки
	|		ПО (НЕ ТаблицаТоваровПослеКорректировки.ДоКорректировки)
	|			И ТаблицаТоваровПредыдущие.Ссылка = ТаблицаТоваровПослеКорректировки.Ссылка
	|			И ТаблицаТоваровПредыдущие.Номенклатура = ТаблицаТоваровПослеКорректировки.Номенклатура
	|			И ТаблицаТоваровПредыдущие.Характеристика = ТаблицаТоваровПослеКорректировки.Характеристика
	|			И ТаблицаТоваровПредыдущие.Упаковка = ТаблицаТоваровПослеКорректировки.Упаковка
	|			И ТаблицаТоваровПредыдущие.НомерГТД = ТаблицаТоваровПослеКорректировки.НомерГТД
	|			И ТаблицаТоваровПредыдущие.СтавкаНДС = ТаблицаТоваровПослеКорректировки.СтавкаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСчетовФактур КАК СчетаФактуры
	|		ПО ТаблицаТоваровПредыдущие.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	ТаблицаТоваровПредыдущие.ДоКорректировки
	|	И ТаблицаТоваровПослеКорректировки.Ссылка ЕСТЬ NULL 
	|	И СчетаФактуры.Корректировочный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru='счет-фактура'"));
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Функция получения данных печатной формы Акт документа
//
Функция ПолучитьДанныеДляПечатнойФормыАктОбОказанииУслуг(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА ПрочаяРеализация.Ссылка ЕСТЬ NULL ТОГДА
	|		ЛОЖЬ
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ КАК КорректировкаПрочейРеализации
	|ПОМЕСТИТЬ ДанныеКорректировки
	|
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РеализацияУслугПрочихАктивов КАК ПрочаяРеализация
	|	ПО
	|		КорректировкаРеализации.ДокументОснование = ПрочаяРеализация.Ссылка
	|
	|ГДЕ
	|	КорректировкаРеализации.Ссылка В (&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ШАПКЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка              	КАК Ссылка,
	|	КорректировкаРеализации.ДокументОснование.Номер КАК Номер,
	|	КорректировкаРеализации.ДокументОснование.Дата	КАК Дата,
	|	КорректировкаРеализации.Партнер             	КАК Партнер,
	|	КорректировкаРеализации.Контрагент          	КАК Контрагент,
	|	КорректировкаРеализации.Организация         	КАК Организация,
	|	КорректировкаРеализации.Организация.Префикс 	КАК Префикс,
	|	КорректировкаРеализации.Валюта              	КАК Валюта,
	|	КорректировкаРеализации.ЦенаВключаетНДС     	КАК ЦенаВключаетНДС,
	|	ВЫБОР
	|		КОГДА
	|			КорректировкаРеализации.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|			ИЛИ КорректировкаРеализации.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|		ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ                                                            КАК УчитыватьНДС,
	|	КорректировкаРеализации.Склад.ТекущийОтветственный.Наименование   КАК ОтпускПроизвел
	|
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|
	|ГДЕ
	|	КорректировкаРеализации.Ссылка В (&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка                                                     КАК Ссылка,
	|	ВложенныйЗапрос.Номенклатура                                               КАК Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.НаименованиеПолное                            КАК УслугаНаименованиеПолное,
	|	ВложенныйЗапрос.Номенклатура.Код                                           КАК Код,
	|	ВложенныйЗапрос.Номенклатура.Артикул                                       КАК Артикул,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Наименование                              КАК ЕдиницаЦены,
	|	ВложенныйЗапрос.ЕдиницаИзмерения                                           КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.Характеристика                                             КАК Характеристика,
	|	ВложенныйЗапрос.Характеристика.НаименованиеПолное                          КАК ХарактеристикаНаименованиеПолное,
	|	ВЫБОР КОГДА ЕСТЬNULL(ВложенныйЗапрос.Упаковка.Коэффициент, 1) = 1
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВложенныйЗапрос.Упаковка.Наименование
	|	КОНЕЦ                                                                      КАК УпаковкаНаименование,
	|	ВложенныйЗапрос.СтавкаНДС                                                  КАК СтавкаНДС,
	|	ВложенныйЗапрос.Цена                                                       КАК Цена,
	|	ВложенныйЗапрос.Количество                                                 КАК Количество,
	|	ВложенныйЗапрос.Сумма                                                      КАК Сумма,
	|	ВложенныйЗапрос.СуммаНДС                                                   КАК СуммаНДС,
	|	ВложенныйЗапрос.СуммаСкидки                                                КАК СуммаСкидки,
	|	ВложенныйЗапрос.СуммаБезСкидки                                             КАК СуммаБезСкидки,
	|	ВложенныйЗапрос.НомерСтроки                                                КАК НомерСтроки,
	|	Ложь                                                                       КАК ЭтоВозвратнаяТара
	|ИЗ (
	|	ВЫБРАТЬ
	|		КорректировкаРеализации.Ссылка КАК Ссылка,
	|		КорректировкаРеализации.Номенклатура КАК Номенклатура,
	|
	|		ВЫБОР КОГДА КорректировкаРеализации.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|			1
	|		ИНАЧЕ
	|			КорректировкаРеализации.Упаковка.Коэффициент
	|		КОНЕЦ КАК Коэффициент,
	|
	|		ВЫБОР КОГДА КорректировкаРеализации.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|			КорректировкаРеализации.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ
	|			КорректировкаРеализации.Упаковка.ЕдиницаИзмерения
	|		КОНЕЦ КАК ЕдиницаИзмерения,
	|
	|		0 КАК ПроцентСкидки,
	|		КорректировкаРеализации.Характеристика              КАК Характеристика,
	|		КорректировкаРеализации.Упаковка                    КАК Упаковка,
	|		КорректировкаРеализации.СтавкаНДС                   КАК СтавкаНДС,
	|		КорректировкаРеализации.Цена                        КАК Цена,
	|		КорректировкаРеализации.КоличествоУпаковок          КАК Количество,
	|		КорректировкаРеализации.Сумма                       КАК Сумма,
	|		0												    КАК СуммаСкидки,
	|		КорректировкаРеализации.Сумма 						КАК СуммаБезСкидки,
	|		КорректировкаРеализации.СуммаНДС                    КАК СуммаНДС,
	|		КорректировкаРеализации.НомерСтроки                 КАК НомерСтроки
	|	ИЗ
	|		Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализации
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеКорректировки КАК ДанныеКорректировки
	|		ПО КорректировкаРеализации.Ссылка = ДанныеКорректировки.Ссылка
	|
	|	ГДЕ
	|		КорректировкаРеализации.Ссылка В (&МассивДокументов)
	|		И КорректировкаРеализации.Номенклатура.ТипНоменклатуры НЕ В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		И НЕ ДанныеКорректировки.КорректировкаПрочейРеализации
	|	) КАК ВложенныйЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	КорректировкаРеализации.Содержание КАК УслугаНаименованиеПолное,
	|	"""" КАК Код,
	|	"""" КАК Артикул,
	|	"""" КАК ЕдиницаЦены,
	|	НЕОПРЕДЕЛЕНО КАК ЕдиницаИзмерения,
	|	НЕОПРЕДЕЛЕНО КАК Характеристика,
	|	"""" КАК ХарактеристикаНаименованиеПолное,
	|	"""" КАК УпаковкаНаименование,
	|	КорректировкаРеализации.СтавкаНДС КАК СтавкаНДС,
	|	КорректировкаРеализации.Цена КАК Цена,
	|	КорректировкаРеализации.Количество КАК Количество,
	|	КорректировкаРеализации.Сумма КАК Сумма,
	|	КорректировкаРеализации.СуммаНДС КАК СуммаНДС,
	|	0 КАК СуммаСкидки,
	|	КорректировкаРеализации.Сумма КАК СуммаБезСкидки,
	|	КорректировкаРеализации.НомерСтроки КАК НомерСтроки,
	|	Ложь КАК ЭтоВозвратнаяТара
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализации
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеКорректировки КАК ДанныеКорректировки
	|	ПО КорректировкаРеализации.Ссылка = ДанныеКорректировки.Ссылка
	|
	|ГДЕ
	|	КорректировкаРеализации.Ссылка В (&МассивДокументов)
	|	И ДанныеКорректировки.КорректировкаПрочейРеализации
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ
	|	СУММА(СуммаСкидки)
	|ПО
	|	Ссылка
	|");
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		МассивРезультатов[1],
		МассивРезультатов[2]
	);
	
КонецФункции

// Функция получения данных печатной формы "1-Т"
//
Функция ПолучитьДанныеДляПечатнойФормыТТН(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК ДокументОснование,
	|	ЛОЖЬ КАК Корректировочный
	|
	|ПОМЕСТИТЬ ТаблицаСчетовФактур
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, Ложь);
	ПродажиСервер.ПоместитьВременнуюТаблицуКоэффициентыУпаковок(МенеджерВременныхТаблиц);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(1) КАК Количество,
	|	СписокТоваров.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ КоличествоТоваров
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаТоваров.Ссылка КАК Ссылка,
	|		ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|		ТаблицаТоваров.Характеристика КАК Характеристика
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|	ГДЕ
	|		ТаблицаТоваров.ЭтоТовар) КАК СписокТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокТоваров.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	КорректировкаРеализации.Номер КАК Номер,
	|	КорректировкаРеализации.Дата КАК Дата,
	|	КорректировкаРеализации.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Контрагент
	|		ИНАЧЕ КорректировкаРеализации.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Организация
	|		ИНАЧЕ КорректировкаРеализации.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	КорректировкаРеализации.Контрагент КАК Контрагент,
	|	КорректировкаРеализации.Контрагент КАК ЗаказчикПеревозок,
	|	КорректировкаРеализации.Организация.Префикс КАК Префикс,
	|	КорректировкаРеализации.Отпустил.Наименование КАК Кладовщик,
	|	КорректировкаРеализации.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	КорректировкаРеализации.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	КорректировкаРеализации.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	КорректировкаРеализации.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	КорректировкаРеализации.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетЗаказчикаПеревозок,
	|	КорректировкаРеализации.БанковскийСчетПеревозчика КАК БанковскийСчетПеревозчика,
	|	КорректировкаРеализации.Перевозчик КАК Перевозчик,
	|	КорректировкаРеализации.Водитель КАК Водитель,
	|	КорректировкаРеализации.Автомобиль.ВидПеревозки КАК ВидПеревозки,
	|	КорректировкаРеализации.Автомобиль.ЛицензионнаяКарточкаРегистрационныйНомер КАК ЛицензионнаяКарточкаРегистрационныйНомер,
	|	КорректировкаРеализации.Автомобиль.ЛицензионнаяКарточкаСерия КАК ЛицензионнаяКарточкаСерия,
	|	КорректировкаРеализации.Автомобиль.ЛицензионнаяКарточкаНомер КАК ЛицензионнаяКарточкаНомер,
	|	КорректировкаРеализации.Автомобиль.ЛицензионнаяКарточкаВид КАК ЛицензионнаяКарточкаВид,
	|	КорректировкаРеализации.Автомобиль.Марка КАК МаркаАвтомобиля,
	|	КорректировкаРеализации.Автомобиль.Код КАК ГосНомерАвтомобиля,
	|	КорректировкаРеализации.Автомобиль.Прицеп КАК Прицеп,
	|	КорректировкаРеализации.Автомобиль.ГосударственныйНомерПрицепа КАК ГосНомерПрицепа,
	|	КорректировкаРеализации.ДоверенностьНомер КАК ДоверенностьНомер,
	|	КорректировкаРеализации.ДоверенностьДата КАК ДоверенностьДата,
	|	КорректировкаРеализации.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	КорректировкаРеализации.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	ЛОЖЬ КАК ЦенаВключаетНДС,
	|	КорректировкаРеализации.Валюта КАК Валюта,
	|	СкладыКонтактнаяОтправитель.Представление КАК ПунктПогрузки,
	|	КорректировкаРеализации.АдресДоставки КАК ПунктРазгрузки,
	|	КорректировкаРеализации.СрокДоставки КАК СрокДоставки,
	|	ЕСТЬNULL(КоличествоТоваров.Количество, 0) КАК КоличествоНаименований
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО КорректировкаРеализации.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады.КонтактнаяИнформация КАК СкладыКонтактнаяОтправитель
	|		ПО КорректировкаРеализации.Склад = СкладыКонтактнаяОтправитель.Ссылка
	|			И (СкладыКонтактнаяОтправитель.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресСклада))
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоТоваров КАК КоличествоТоваров
	|		ПО КорректировкаРеализации.Ссылка = КоличествоТоваров.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО КорректировкаРеализации.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Номенклатура."+КолонкаКодов+" КАК ТоварКод,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТоваров.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.Наименование
	|	КОНЕЦ КАК Упаковка,
	|	ТаблицаТоваров.КоличествоУпаковок КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Наименование
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Наименование
	|	КОНЕЦ КАК ВидУпаковки,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Наименование
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Наименование
	|	КОНЕЦ КАК БазоваяЕдиницаНаименование,
	|	ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок КАК Цена,
	|	ВЫБОР
	|		КОГДА &ЗаполненаЕдиницаИзмеренияВеса
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL 
	|						ТОГДА ТаблицаТоваров.Количество * ТаблицаТоваров.Номенклатура.Вес
	|					ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок * ТаблицаТоваров.Упаковка.Вес
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК МассаБрутто,
	|	ТаблицаТоваров.Количество * ТаблицаТоваров.Номенклатура.Вес КАК МассаНетто,
	|	ТаблицаТоваров.КоличествоУпаковок КАК КоличествоМест,
	|	ТаблицаТоваров.СуммаБезНДС КАК Сумма,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ЛОЖЬ КАК Весовой,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.ЭтоТовар
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[1];
	РезультатПоТабличнойЧасти = МассивРезультатов[2];
	
	СтруктураДанныхДляПечати = Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоТабличнойЧасти
	);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Функция получает данные для формирования ЭД "Соглашение об изменении стоимости"
//
Функция ПолучитьДанныеДляПечатнойФормыСоглашениеОбИзмененииСтоимости(ПараметрыПечати, МассивОбъектов) Экспорт 

	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК ДокументОснование,
	|	ЛОЖЬ КАК Корректировочный
	|
	|ПОМЕСТИТЬ ТаблицаСчетовФактур
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ВыводитьГТД = ?(ПараметрыПечати.Свойство("ВыводитьГТД"), ПараметрыПечати.ВыводитьГТД, Ложь);
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ВыводитьГТД, Истина);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА УпаковкиНоменклатуры.Ссылка ЕСТЬ NULL 
	|				ТОГДА 1
	|			ИНАЧЕ УпаковкиНоменклатуры.Коэффициент
	|		КОНЕЦ) КАК Коэффициент
	|ПОМЕСТИТЬ Упаковки
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиНоменклатуры КАК УпаковкиНоменклатуры
	|		ПО (УпаковкиНоменклатуры.Родитель = ТаблицаТоваров.Упаковка)
	|			И (УпаковкиНоменклатуры.Владелец = ТаблицаТоваров.Номенклатура)
	|ГДЕ
	|	НЕ УпаковкиНоменклатуры.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Упаковки.Номенклатура,
	|	Упаковки.Упаковка КАК Упаковка,
	|	Упаковки.Упаковка.Коэффициент КАК КоэффициентУпаковки,
	|	МИНИМУМ(УпаковкиНоменклатуры.Ссылка) КАК ВложеннаяУпаковка,
	|	МИНИМУМ(УпаковкиНоменклатуры.Коэффициент) КАК КоэффициентВложеннойУпаковки
	|ПОМЕСТИТЬ ВложенныеУпаковки
	|ИЗ
	|	Упаковки КАК Упаковки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиНоменклатуры КАК УпаковкиНоменклатуры
	|		ПО Упаковки.Номенклатура = УпаковкиНоменклатуры.Владелец
	|			И Упаковки.Коэффициент = УпаковкиНоменклатуры.Коэффициент
	|ГДЕ
	|	УпаковкиНоменклатуры.Родитель = Упаковки.Упаковка
	|
	|СГРУППИРОВАТЬ ПО
	|	Упаковки.Номенклатура,
	|	Упаковки.Упаковка,
	|	Упаковки.Упаковка.Коэффициент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.НомерСтроки,
	|	ТаблицаТоваров.Содержание,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Упаковка,
	|	ТаблицаТоваров.НомерГТД,
	|	ТаблицаТоваров.Количество,
	|	ТаблицаТоваров.КоличествоУпаковок,
	|	ТаблицаТоваров.СуммаБезНДС,
	|	ТаблицаТоваров.СтавкаНДС,
	|	ТаблицаТоваров.СуммаНДС,
	|	ТаблицаТоваров.ЭтоТовар,
	|	ТаблицаТоваров.ДоКорректировки,
	|	ВЫБОР
	|		КОГДА ВложенныеУпаковки.ВложеннаяУпаковка ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|						ТОГДА 1
	|					ИНАЧЕ ТаблицаТоваров.Упаковка.Коэффициент
	|				КОНЕЦ
	|		ИНАЧЕ ВложенныеУпаковки.КоэффициентУпаковки
	|	КОНЕЦ КАК КоэффициентУпаковки,
	|	ВЫБОР
	|		КОГДА ВложенныеУпаковки.ВложеннаяУпаковка ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|						ТОГДА 1
	|					ИНАЧЕ ТаблицаТоваров.Упаковка.Коэффициент
	|				КОНЕЦ
	|		ИНАЧЕ ВложенныеУпаковки.КоэффициентВложеннойУпаковки
	|	КОНЕЦ КАК КоэффициентВложеннойУпаковки,
	|	ВЫБОР
	|		КОГДА ВложенныеУпаковки.ВложеннаяУпаковка ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|						ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|					ИНАЧЕ ТаблицаТоваров.Упаковка
	|				КОНЕЦ
	|		ИНАЧЕ ВложенныеУпаковки.ВложеннаяУпаковка
	|	КОНЕЦ КАК ВидУпаковки,
	|	ТаблицаТоваров.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ТаблицаТоваровСКоэффициентамиУпаковок
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВложенныеУпаковки КАК ВложенныеУпаковки
	|		ПО ТаблицаТоваров.Номенклатура = ВложенныеУпаковки.Номенклатура
	|			И ТаблицаТоваров.Упаковка = ВложенныеУпаковки.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Упаковки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВложенныеУпаковки
	|;";
	Запрос.Выполнить();
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст = "
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ШАПКЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка                                                 КАК Ссылка,
	|	КорректировкаРеализации.ДокументОснование.Номер                                КАК Номер,
	|	КорректировкаРеализации.ДокументОснование.Дата                                 КАК Дата,
	|	КорректировкаРеализации.Партнер                                                КАК Партнер,
	|	КорректировкаРеализации.Контрагент                                             КАК Контрагент,
	|	КорректировкаРеализации.Организация                                            КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование                              КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность                                 КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование                          КАК ГлавныйБухгалтер,
	|	КорректировкаРеализации.Отпустил.Наименование                                  КАК Кладовщик,
	|	КорректировкаРеализации.ОтпустилДолжность                                      КАК ДолжностьКладовщика,
	|	КорректировкаРеализации.Организация.Префикс                                    КАК Префикс,
	|	КорректировкаРеализации.Основание                                              КАК Основание,
	|
	|	ВЫБОР КОГДА КорректировкаРеализации.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА
	|		КорректировкаРеализации.Контрагент
	|	ИНАЧЕ
	|		КорректировкаРеализации.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|
	|	ВЫБОР КОГДА КорректировкаРеализации.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА
	|		КорректировкаРеализации.Организация
	|	ИНАЧЕ
	|		КорректировкаРеализации.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|
	|	КорректировкаРеализации.БанковскийСчетОрганизации                             КАК БанковскийСчетОрганизации,
	|	КорректировкаРеализации.БанковскийСчетКонтрагента                             КАК БанковскийСчетКонтрагента,
	|	КорректировкаРеализации.БанковскийСчетГрузоотправителя                        КАК БанковскийСчетГрузоотправителя,
	|	КорректировкаРеализации.БанковскийСчетГрузополучателя                         КАК БанковскийСчетГрузополучателя,
	|	КорректировкаРеализации.АдресДоставки                                         КАК АдресДоставки,
	|	Неопределено                                                                  КАК Подразделение,
	|	КорректировкаРеализации.Валюта                                                КАК Валюта,
	|	КорректировкаРеализации.ДоверенностьНомер                                     КАК ДоверенностьНомер,
	|	КорректировкаРеализации.ДоверенностьДата                                      КАК ДоверенностьДата,
	|	КорректировкаРеализации.ДоверенностьВыдана                                    КАК ДоверенностьВыдана,
	|	КорректировкаРеализации.ДоверенностьЛицо                                      КАК ДоверенностьЛицо,
	|	&ЕдиницаИзмеренияВеса                                                         КАК ЕдиницаИзмеренияВеса
	|
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО
	|			КорректировкаРеализации.Ссылка = ДанныеДокументов.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО 
	|			КорректировкаРеализации.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|	
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.НоменклатураНаименование,
	|	ВложенныйЗапрос.НоменклатураКод,
	|	ВложенныйЗапрос.ЕдиницаИзмерения,
	|	ВложенныйЗапрос.ЕдиницаИзмеренияНаименование,
	|	ВложенныйЗапрос.ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.ХарактеристикаНаименование,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.УпаковкаНаименование,
	|	ВложенныйЗапрос.ВидУпаковки,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.СтранаПроисхождения,
	|	СУММА(ВложенныйЗапрос.Количество)                           КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоДоКорректировки)            КАК КоличествоДоКорректировки,
	|	СУММА(ВложенныйЗапрос.КоличествоМест)                       КАК КоличествоМест,
	|	СУММА(ВложенныйЗапрос.КоличествоМестДоКорректировки)        КАК КоличествоМестДоКорректировки,
	|	СУММА(ВложенныйЗапрос.КоличествоВОдномМесте)                КАК КоличествоВОдномМесте,
	|	СУММА(ВложенныйЗапрос.КоличествоВОдномМестеДоКорректировки) КАК КоличествоВОдномМестеДоКорректировки,
	|	СУММА(ВложенныйЗапрос.Цена)                                 КАК Цена,
	|	СУММА(ВложенныйЗапрос.ЦенаДоКорректировки)                  КАК ЦенаДоКорректировки,
	|	СУММА(ВложенныйЗапрос.СуммаБезНДС)                          КАК СуммаБезНДС,
	|	СУММА(ВложенныйЗапрос.СуммаБезНДСДоКорректировки)           КАК СуммаБезНДСДоКорректировки,
	|	СУММА(ВложенныйЗапрос.СуммаСНДС)                            КАК СуммаСНДС,
	|	СУММА(ВложенныйЗапрос.СуммаСНДСДоКорректировки)             КАК СуммаСНДСДоКорректировки,
	|	СУММА(ВложенныйЗапрос.СуммаНДС)                             КАК СуммаНДС,
	|	СУММА(ВложенныйЗапрос.СуммаНДСДоКорректировки)              КАК СуммаНДСДоКорректировки,
	|	СУММА(ВложенныйЗапрос.МассаНетто)                           КАК МассаНетто,
	|	СУММА(ВложенныйЗапрос.МассаНеттоДоКорректировки)            КАК МассаНеттоДоКорректировки,
	|	СУММА(ВложенныйЗапрос.МассаБрутто)                          КАК МассаБрутто,
	|	СУММА(ВложенныйЗапрос.МассаБруттоДоКорректировки)           КАК МассаБруттоДоКорректировки,
	|	МАКСИМУМ(ВложенныйЗапрос.НомерСтроки)                       КАК НомерСтроки,
	|	ВложенныйЗапрос.ЭтоВозвратнаяТара
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТоваров.Ссылка                                       КАК Ссылка,
	|		ТаблицаТоваров.Номенклатура                                 КАК Номенклатура,
	|		ТаблицаТоваров.Номенклатура.НаименованиеПолное              КАК НоменклатураНаименование,
	|		
	|		ВЫБОР КОГДА &КолонкаКодов = ""Артикул"" ТОГДА
	|			ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ
	|			ТаблицаТоваров.Номенклатура.Код
	|		КОНЕЦ                                                       КАК НоменклатураКод,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|		 ИЛИ ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ
	|			ТаблицаТоваров.Упаковка.ЕдиницаИзмерения
	|		КОНЕЦ                                                       КАК ЕдиницаИзмерения,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|		 ИЛИ ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ
	|			ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Представление
	|		КОНЕЦ                                                       КАК ЕдиницаИзмеренияНаименование,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|		 ИЛИ ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ
	|			ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Код
	|		КОНЕЦ                                                       КАК ЕдиницаИзмеренияКод,
	|		
	|		ТаблицаТоваров.Характеристика                               КАК Характеристика,
	|		ТаблицаТоваров.Характеристика.НаименованиеПолное            КАК ХарактеристикаНаименование,
	|		ТаблицаТоваров.Упаковка                                     КАК Упаковка,
	|		
	|		ВЫБОР КОГДА ЕСТЬNULL(ТаблицаТоваров.Упаковка.Коэффициент, 1) = 1 ТОГДА
	|			""""
	|		ИНАЧЕ
	|			ТаблицаТоваров.Упаковка.Наименование
	|		КОНЕЦ                                                       КАК УпаковкаНаименование,
	|		
	|		ВЫБОР КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ
	|			ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|				ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Представление
	|			ИНАЧЕ
	|				ТаблицаТоваров.ВидУпаковки.ЕдиницаИзмерения.Представление
	|			КОНЕЦ
	|		КОНЕЦ                                                       КАК ВидУпаковки,
	|		
	|		ТаблицаТоваров.СтавкаНДС                                    КАК СтавкаНДС,
	|		ТаблицаТоваров.НомерГТД                                     КАК НомерГТД,
	|		ТаблицаТоваров.НомерГТД.СтранаПроисхождения                 КАК СтранаПроисхождения,
	|		
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.Количество
	|		КОНЕЦ                                                       КАК Количество,
	|		0                                                           КАК КоличествоДоКорректировки,
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.Количество / ТаблицаТоваров.КоэффициентВложеннойУпаковки
	|		КОНЕЦ                                                       КАК КоличествоМест,
	|		0                                                           КАК КоличествоМестДоКорректировки,
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ВЫБОР КОГДА ТаблицаТоваров.Количество < ТаблицаТоваров.КоэффициентВложеннойУпаковки ТОГДА
	|				ТаблицаТоваров.Количество
	|			ИНАЧЕ
	|				ТаблицаТоваров.КоэффициентВложеннойУпаковки
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|				1
	|			ИНАЧЕ
	|				ТаблицаТоваров.Упаковка.Коэффициент
	|			КОНЕЦ
	|		КОНЕЦ                                                       КАК КоличествоВОдномМесте,
	|		0                                                           КАК КоличествоВОдномМестеДоКорректировки,
	|		
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество
	|		КОНЕЦ                                                       КАК Цена,
	|		0                                                           КАК ЦенаДоКорректировки,
	|		
	|		ТаблицаТоваров.СуммаБезНДС                                  КАК СуммаБезНДС,
	|		0                                                           КАК СуммаБезНДСДоКорректировки,
	|		
	|		ТаблицаТоваров.СуммаНДС                                     КАК СуммаНДС,
	|		0                                                           КАК СуммаНДСДоКорректировки,
	|		
	|		ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС        КАК СуммаСНДС,
	|		0                                                           КАК СуммаСНДСДоКорректировки,
	|		
	|		ТаблицаТоваров.Количество * ТаблицаТоваров.Номенклатура.Вес КАК МассаНетто,
	|		0                                                           КАК МассаНеттоДоКорректировки,
	|		
	|		ВЫБОР КОГДА &ЗаполненаЕдиницаИзмеренияВеса ТОГДА
	|			ВЫБОР КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL ТОГДА
	|				ТаблицаТоваров.Количество * ТаблицаТоваров.Номенклатура.Вес
	|			ИНАЧЕ
	|				ТаблицаТоваров.КоличествоУпаковок * ТаблицаТоваров.Упаковка.Вес
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ                                                       КАК МассаБрутто,
	|		0                                                           КАК МассаБруттоДоКорректировки,
	|		ТаблицаТоваров.НомерСтроки                                  КАК НомерСтроки,
	|		ЛОЖЬ                                                        КАК ЭтоВозвратнаяТара
	|	
	|	ИЗ
	|		ТаблицаТоваровСКоэффициентамиУпаковок КАК ТаблицаТоваров
	|		
	|	ГДЕ
	|		НЕ ТаблицаТоваров.ДоКорректировки
	|		И (ТаблицаТоваров.ЭтоТовар ИЛИ &ВыводитьУслуги)
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|		
	|	ВЫБРАТЬ
	|		ТаблицаТоваров.Ссылка,
	|		ТаблицаТоваров.Номенклатура,
	|		ТаблицаТоваров.Номенклатура.НаименованиеПолное,
	|		
	|		ВЫБОР КОГДА &КолонкаКодов = ""Артикул"" ТОГДА
	|			ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ
	|			ТаблицаТоваров.Номенклатура.Код
	|		КОНЕЦ,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|		 ИЛИ ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ
	|			ТаблицаТоваров.Упаковка.ЕдиницаИзмерения
	|		КОНЕЦ,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|		 ИЛИ ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|		ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ
	|		ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Представление
	|		КОНЕЦ,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|		 ИЛИ ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ
	|			ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Код
	|		КОНЕЦ,
	|		
	|		ТаблицаТоваров.Характеристика,
	|		ТаблицаТоваров.Характеристика.НаименованиеПолное,
	|		ТаблицаТоваров.Упаковка,
	|		
	|		ВЫБОР КОГДА ЕСТЬNULL(ТаблицаТоваров.Упаковка.Коэффициент, 1) = 1 ТОГДА
	|			""""
	|		ИНАЧЕ
	|			ТаблицаТоваров.Упаковка.Наименование
	|		КОНЕЦ,
	|		
	|		ВЫБОР КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ
	|			ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|				ТаблицаТоваров.Упаковка.ЕдиницаИзмерения.Представление
	|			ИНАЧЕ
	|				ТаблицаТоваров.ВидУпаковки.ЕдиницаИзмерения.Представление
	|			КОНЕЦ
	|		КОНЕЦ,
	|		
	|		ТаблицаТоваров.СтавкаНДС,
	|		ТаблицаТоваров.НомерГТД,
	|		ТаблицаТоваров.НомерГТД.СтранаПроисхождения,
	|		
	|		0,
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.Количество
	|		КОНЕЦ,
	|		
	|		0,
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.Количество / ТаблицаТоваров.КоэффициентВложеннойУпаковки
	|		КОНЕЦ,
	|		
	|		0,
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ВЫБОР КОГДА ТаблицаТоваров.Количество < ТаблицаТоваров.КоэффициентВложеннойУпаковки ТОГДА
	|				ТаблицаТоваров.Количество
	|			ИНАЧЕ
	|				ТаблицаТоваров.КоэффициентВложеннойУпаковки
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
	|				1
	|			ИНАЧЕ
	|				ТаблицаТоваров.Упаковка.Коэффициент
	|			КОНЕЦ
	|		КОНЕЦ,
	|		
	|		0,
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество
	|		КОНЕЦ,
	|		
	|		0,
	|		ТаблицаТоваров.СуммаБезНДС,
	|		
	|		0,
	|		ТаблицаТоваров.СуммаНДС,
	|		
	|		0,
	|		ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС,
	|		
	|		0,
	|		ТаблицаТоваров.Количество * ТаблицаТоваров.Номенклатура.Вес КАК МассаНеттоДоКорректировки,
	|		
	|		0,
	|		ВЫБОР КОГДА &ЗаполненаЕдиницаИзмеренияВеса ТОГДА
	|			ВЫБОР КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL ТОГДА
	|				ТаблицаТоваров.Количество * ТаблицаТоваров.Номенклатура.Вес
	|			ИНАЧЕ
	|				ТаблицаТоваров.КоличествоУпаковок * ТаблицаТоваров.Упаковка.Вес
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ,
	|		NULL,
	|		ЛОЖЬ
	|	
	|	ИЗ
	|		ТаблицаТоваровСКоэффициентамиУпаковок КАК ТаблицаТоваров
	|		
	|	ГДЕ
	|		ТаблицаТоваров.ДоКорректировки
	|		И (ТаблицаТоваров.ЭтоТовар ИЛИ &ВыводитьУслуги)) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Ссылка,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.НоменклатураНаименование,
	|	ВложенныйЗапрос.НоменклатураКод,
	|	ВложенныйЗапрос.ЕдиницаИзмерения,
	|	ВложенныйЗапрос.ЕдиницаИзмеренияНаименование,
	|	ВложенныйЗапрос.ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.ХарактеристикаНаименование,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.УпаковкаНаименование,
	|	ВложенныйЗапрос.ВидУпаковки,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.СтранаПроисхождения,
	|	ВложенныйЗапрос.ЭтоВозвратнаяТара
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("ВыводитьУслуги",                  ПараметрыПечати.ВыводитьУслуги);
	Запрос.УстановитьПараметр("КолонкаКодов",                    КолонкаКодов);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса",            Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

Функция СформироватьПечатнуюФормуНакладная(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	ВыводитьКоды = ЗначениеЗаполнено(КолонкаКодов);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	КорректировкаРеализации.ДокументОснование.Номер КАК Номер,
	|	КорректировкаРеализации.ДокументОснование.Дата КАК Дата,
	|	КорректировкаРеализации.Партнер КАК Партнер,
	|	КорректировкаРеализации.Контрагент КАК Получатель,
	|	КорректировкаРеализации.Организация КАК Организация,
	|	КорректировкаРеализации.Организация.Префикс КАК Префикс,
	|	КорректировкаРеализации.Валюта КАК Валюта,
	|	КорректировкаРеализации.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ КорректировкаРеализации.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УчитыватьНДС,
	|	КорректировкаРеализации.Отпустил.Наименование КАК ОтпускПроизвел
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК ТоварНаименованиеПолное,
	|	ВложенныйЗапрос.Номенклатура.Код КАК Код,
	|	ВложенныйЗапрос.Номенклатура.Артикул КАК Артикул,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Наименование КАК ЕдиницаЦены,
	|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.Характеристика.НаименованиеПолное КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВложенныйЗапрос.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВложенныйЗапрос.Упаковка.Наименование
	|	КОНЕЦ КАК Упаковка,
	|	ВложенныйЗапрос.СтавкаНДС КАК СтавкаНДС,
	|	ВложенныйЗапрос.Цена КАК Цена,
	|	ВложенныйЗапрос.Количество КАК Количество,
	|	ВложенныйЗапрос.Сумма КАК Сумма,
	|	ВложенныйЗапрос.СуммаНДС КАК СуммаНДС,
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	(ВЫБРАТЬ
	|		КорректировкаРеализации.Ссылка КАК Ссылка,
	|		КорректировкаРеализации.Номенклатура КАК Номенклатура,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализации.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ КорректировкаРеализации.Упаковка.Коэффициент
	|		КОНЕЦ КАК Коэффициент,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализации.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|				ТОГДА КорректировкаРеализации.Номенклатура.ЕдиницаИзмерения
	|			ИНАЧЕ КорректировкаРеализации.Упаковка.ЕдиницаИзмерения
	|		КОНЕЦ КАК ЕдиницаИзмерения,
	|		КорректировкаРеализации.Характеристика КАК Характеристика,
	|		КорректировкаРеализации.Упаковка КАК Упаковка,
	|		КорректировкаРеализации.СтавкаНДС КАК СтавкаНДС,
	|		КорректировкаРеализации.Цена КАК Цена,
	|		КорректировкаРеализации.КоличествоУпаковок КАК Количество,
	|		КорректировкаРеализации.Сумма КАК Сумма,
	|		КорректировкаРеализации.СуммаНДС КАК СуммаНДС,
	|		КорректировкаРеализации.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализации
	|	ГДЕ
	|		КорректировкаРеализации.Ссылка В(&МассивДокументов)
	|		И КорректировкаРеализации.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");

	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КорректировкаРеализации_Накладная";
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	МассивРезультатов 		= Запрос.ВыполнитьПакет();
	ДанныеПечати			= МассивРезультатов[0].Выбрать();
	ВыборкаПоДокументам 	= МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
	
		// Найдем в выборке товары по текущему документу
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		НайденСледующий = ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска);
		
		// Если в накладной только услуги - перейдем к следующему документу
		
		Если НайденСледующий Тогда
			ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
			ЕстьНДС = ДанныеПечати.УчитыватьНДС;
			ВыборкаПоТоварам.Сбросить();
		Иначе
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В документе %1 отсутствуют товары. Печать накладной не требуется'"),
				ДанныеПечати.Ссылка
				);
				
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДанныеПечати.Ссылка
			);
			Продолжить;
		КонецЕсли;
		
		// Макет необходимо получать для каждого документа, т.к. размеры колонок изменяются динамически
		Макет = УправлениеПечатью.ПолучитьМакет("Документ.КорректировкаРеализации.ПФ_MXL_РеализацияТоваров");
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент    = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выводим шапку накладной
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, ДанныеПечати.Ссылка);
		
		ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеПечати, НСтр("ru='Реализация товаров'"));
		
		ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета                                   = Макет.ПолучитьОбласть("Поставщик");
		ПредставлениеПоставщика                         = ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.Дата), "ПолноеНаименование");
		ОбластьМакета.Параметры.ПредставлениеПоставщика = ПредставлениеПоставщика;
		ОбластьМакета.Параметры.Поставщик               = ДанныеПечати.Организация;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета                                   = Макет.ПолучитьОбласть("Покупатель");
		ПредставлениеПолучателя                         = ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Получатель, ДанныеПечати.Дата), "ПолноеНаименование");
		ОбластьМакета.Параметры.ПредставлениеПолучателя = ПредставлениеПолучателя;
		ОбластьМакета.Параметры.Получатель              = ДанныеПечати.Получатель;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим заголовок таблицы Товары
		
		СуффиксОбластиСтроки = ?(ЕстьНДС, "СНДС", "");
		СуффиксОбластиКолонки = ?(ЕстьНДС, "СНДС", "");
		
		ОбластьКолонкаТовар = Макет.Область("ПерваяКолонкаТовара");
		ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки
			+ ?(ВыводитьКоды, 0, Макет.Область("КолонкаКодов").ШиринаКолонки)
		;
		
		ОбластьНомера  = Макет.ПолучитьОбласть("ШапкаТаблицы" + СуффиксОбластиСтроки + "|НомерСтроки");
		ОбластьКодов   = Макет.ПолучитьОбласть("ШапкаТаблицы" + СуффиксОбластиСтроки + "|КолонкаКодов");
		ОбластьТовар   = Макет.ПолучитьОбласть("ШапкаТаблицы" + СуффиксОбластиСтроки + "|Товар" + СуффиксОбластиКолонки);
		ОбластьДанных  = Макет.ПолучитьОбласть("ШапкаТаблицы" + СуффиксОбластиСтроки + "|Данные" + СуффиксОбластиКолонки);
		
		ТабличныйДокумент.Вывести(ОбластьНомера);
			
		Если ВыводитьКоды Тогда
			ОбластьКодов.Параметры.ИмяКолонкиКодов = КолонкаКодов;
			ТабличныйДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
			
		ТабличныйДокумент.Присоединить(ОбластьТовар);
		ТабличныйДокумент.Присоединить(ОбластьДанных);
		
		ОбластьНомера  = Макет.ПолучитьОбласть("СтрокаТаблицы" + СуффиксОбластиСтроки + "|НомерСтроки");
		ОбластьКодов   = Макет.ПолучитьОбласть("СтрокаТаблицы" + СуффиксОбластиСтроки + "|КолонкаКодов");
		ОбластьТовар   = Макет.ПолучитьОбласть("СтрокаТаблицы" + СуффиксОбластиСтроки + "|Товар" + СуффиксОбластиКолонки);
		ОбластьДанных  = Макет.ПолучитьОбласть("СтрокаТаблицы" + СуффиксОбластиСтроки + "|Данные" + СуффиксОбластиКолонки);
		
		Сумма          = 0;
		СуммаНДС       = 0;
		НомерСтроки    = 0;
		
		// Выводим строки таблицы Товары
			
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			НомерСтроки = НомерСтроки + 1;
			
			ОбластьНомера.Параметры.НомерСтроки = НомерСтроки;
			ТабличныйДокумент.Вывести(ОбластьНомера);
			
			Если ВыводитьКоды Тогда
				
				ОбластьКодов.Параметры.Артикул = ВыборкаПоТоварам[КолонкаКодов];
				ТабличныйДокумент.Присоединить(ОбластьКодов);
				
			КонецЕсли;
			
			ОбластьТовар.Параметры.Заполнить(ВыборкаПоТоварам);
			ОбластьТовар.Параметры.Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаПоТоварам.ТоварНаименованиеПолное,
				ВыборкаПоТоварам.Характеристика
			) + ?(ВыборкаПоТоварам.ЭтоВозвратнаяТара, НСтр("ru=' (возвратная тара)'"), "");
			
			ТабличныйДокумент.Присоединить(ОбластьТовар);
			
			ОбластьДанных.Параметры.Заполнить(ВыборкаПоТоварам);
			
			ТабличныйДокумент.Присоединить(ОбластьДанных);
			
			Сумма          = Сумма          + ВыборкаПоТоварам.Сумма;
			СуммаНДС       = СуммаНДС       + ВыборкаПоТоварам.СуммаНДС;
			
		КонецЦикла;
		
		// Выводим подвал
		
		ОбластьНомера  = Макет.ПолучитьОбласть("ПодвалТаблицы" + СуффиксОбластиСтроки + "|НомерСтроки");
		ОбластьКодов   = Макет.ПолучитьОбласть("ПодвалТаблицы" + СуффиксОбластиСтроки + "|КолонкаКодов");
		ОбластьТовар   = Макет.ПолучитьОбласть("ПодвалТаблицы" + СуффиксОбластиСтроки + "|Товар" + СуффиксОбластиКолонки);
		ОбластьДанных  = Макет.ПолучитьОбласть("ПодвалТаблицы" + СуффиксОбластиСтроки + "|Данные" + СуффиксОбластиКолонки);
		
		ТабличныйДокумент.Вывести(ОбластьНомера);
		
		Если ВыводитьКоды Тогда
			ТабличныйДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьТовар);
		
		ОбластьДанных.Параметры.Всего = ФормированиеПечатныхФорм.ФорматСумм(Сумма);
		ТабличныйДокумент.Присоединить(ОбластьДанных);
		
		// Выводим ИтогоНДС
		
		Область = Макет.ПолучитьОбласть("ПодвалНДС");
		
		Область.Параметры.ВсегоНДС = СуммаНДС;
		Если ЕстьНДС Тогда
			Область.Параметры.НДС = ?(ДанныеПечати.ЦенаВключаетНДС, НСтр("ru='В том числе НДС:'"), НСтр("ru='Сумма НДС:'"));
		Иначе
			Область.Параметры.НДС = НСтр("ru='Без налога (НДС)'");
		КонецЕсли;
		ТабличныйДокумент.Присоединить(Область);
		
		// Выводим Сумму прописью
		
		ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
		СуммаКПрописи = Сумма + ?(ДанныеПечати.ЦенаВключаетНДС, 0, СуммаНДС);
		
		ИтоговаяСтрока = НСтр("ru='Всего наименований %КоличествоНаименований%, на сумму %СуммаДокумента% '");
		ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%КоличествоНаименований%", ВыборкаПоТоварам.Количество());
		ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%СуммаДокумента%", ФормированиеПечатныхФорм.ФорматСумм(СуммаКПрописи, ДанныеПечати.Валюта));

		ОбластьМакета.Параметры.ИтоговаяСтрока = ИтоговаяСтрока;
		ОбластьМакета.Параметры.СуммаПрописью  = РаботаСКурсамиВалют.СформироватьСуммуПрописью(СуммаКПрописи, ДанныеПечати.Валюта);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим подписи
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		
		Если ЗначениеЗаполнено(ДанныеПечати.ОтпускПроизвел) Тогда
			ОбластьМакета.Параметры.ОтпускПроизвел = ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.ОтпускПроизвел);
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;

	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;

КонецФункции

Функция СформироватьПечатнуюФормуРеестрНомеровГТД(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КорректировкаРеализации_РеестрНомеровГТД";
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	ВыводитьКоды = ЗначениеЗаполнено(КолонкаКодов);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Выполнить();
	
	Запрос.Текст = ТекстЗапросаВтВидыЗапасов() + "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаВидыЗапасов.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ВтТаблицаДокументов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	ТаблицаВидыЗапасов.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ДанныеДокумента.ДокументОснование.Номер КАК Номер,
	|	ДанныеДокумента.ДокументОснование.Дата КАК Дата,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтТаблицаДокументов КАК ТаблицаДокументов
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаВидыЗапасов.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ТаблицаВидыЗапасов.Номенклатура.Код КАК Код,
	|	ТаблицаВидыЗапасов.Номенклатура.Артикул КАК Артикул,
	|	ТаблицаВидыЗапасов.Номенклатура.НаименованиеПолное КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика.НаименованиеПолное КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|
	|	СУММА(ТаблицаВидыЗапасов.Количество) КАК Количество
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтТаблицаДокументов КАК ТаблицаДокументов
	|	ПО
	|		ТаблицаВидыЗапасов.Ссылка = ТаблицаДокументов.Ссылка
	|ГДЕ
	|	ТаблицаВидыЗапасов.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВидыЗапасов.Ссылка,
	|	ТаблицаВидыЗапасов.НомерСтроки,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаВидыЗапасов.Количество) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ТаблицаВидыЗапасов.НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтТаблицаДокументов КАК ТаблицаДокументов
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаДокументов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивДокументов)
	|	И ТаблицаДокументов.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|";
	
	УстановитьПараметрыВтВидыЗапасов(
		Запрос,
		Ложь, // ПересчитыватьВВалютуРегл
		Ложь, // ВключаяДоКорректировки
	);
	
	Макет = УправлениеПечатью.ПолучитьМакет("Документ.КорректировкаРеализации.ПФ_MXL_РеестрНомеровГТД");
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	// МассивРезультатов[0] - ВтСвязанныеДокументы
	// МассивРезультатов[1] - ВсеВидыЗапасов
	// МассивРезультатов[2] - ВтВидыЗапасов
	// МассивРезультатов[3] - УничтожитьВсеВидыЗапасов
	// МассивРезультатов[4] - ВтТаблицаДокументов
	ДанныеПечати = МассивРезультатов[5].Выбрать();
	ВыборкаПоДокументам = МассивРезультатов[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаПоДокументамБезГТД = МассивРезультатов[7].Выбрать();
	
	ПервыйДокумент = Истина;
	Пока ДанныеПечати.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Область = Макет.ПолучитьОбласть("Шапка");
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, Область, ДанныеПечати.Ссылка);
		Область.Параметры.ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеПечати, НСтр("ru='Реестр номеров ГТД к накладной'"));
		Область.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.Дата),
			"ПолноеНаименование"
		);
		Область.Параметры.ПредставлениеПартнера = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Контрагент, ДанныеПечати.Дата),
			"ПолноеНаименование"
		);
		ТабличныйДокумент.Вывести(Область);
				
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы|НачалоСтроки");
		ТабличныйДокумент.Вывести(Область);
		Если ВыводитьКоды Тогда
			Область = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
			Область.Параметры.ИмяКолонкиКодов = КолонкаКодов;
			ТабличныйДокумент.Присоединить(Область);
		КонецЕсли;
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаТоваров");
		ТабличныйДокумент.Присоединить(Область);
				
		НомерСтроки = 1;
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска);
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
					
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы|НачалоСтроки");
			Область.Параметры.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
			ТабличныйДокумент.Вывести(Область);
			
			Если ВыводитьКоды Тогда
				Область = Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаКодов");
				Область.Параметры.ЗначениеКода = ВыборкаПоТоварам[КолонкаКодов];
				ТабличныйДокумент.Присоединить(Область);
			КонецЕсли;
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаТоваров");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			Область.Параметры.Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаПоТоварам.Номенклатура,
				ВыборкаПоТоварам.Характеристика
			);
			
			ТабличныйДокумент.Присоединить(Область);
			
		КонецЦикла;
				
		Область = Макет.ПолучитьОбласть("ПодвалТаблицы|НачалоСтроки");
		ТабличныйДокумент.Вывести(Область);
		Если ВыводитьКоды Тогда
			Область = Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаКодов");
			ТабличныйДокумент.Присоединить(Область);
		КонецЕсли;
		Область = Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаТоваров");
		ТабличныйДокумент.Присоединить(Область);
						
		Область = Макет.ПолучитьОбласть("Подписи");
		Область.Параметры.ИтоговаяСтрока = НСтр("ru = 'Всего наименований '") + Строка(НомерСтроки - 1);
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
			
	КонецЦикла;
	
	// Выведем сообщения о документах, для которых не требуется печатать реестр номеров ГТД.
	Пока ВыборкаПоДокументамБезГТД.Следующий() Цикл
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В документе %1 отсутствуют товары с номерами ГТД. Печать реестра номеров ГТД не требуется.'"),
			Строка(ВыборкаПоДокументамБезГТД.Ссылка)
		);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ВыборкаПоДокументамБезГТД.Ссылка
		);
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ЗаполнитьСтруктуруПолучателейПечатныхФорм(СтруктураДанныхОбъектаПечати) Экспорт
	
	СтруктураДанныхОбъектаПечати.ОсновнойПолучатель = "Партнер";
	
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Партнер");
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов") Тогда 
		СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Контрагент");
	КонецЕсли;
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Грузоотправитель");
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Грузополучатель");
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Прочее

Процедура ЗаполнитьИменаРеквизитовПоОснованию(ДокументОснование, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Склад");
	МассивВсехРеквизитов.Добавить("СтатьяДоходов");
	МассивВсехРеквизитов.Добавить("АналитикаДоходов");
	МассивВсехРеквизитов.Добавить("СтатьяРасходов");
	МассивВсехРеквизитов.Добавить("АналитикаРасходов");
	МассивВсехРеквизитов.Добавить("Отпустил");
	МассивВсехРеквизитов.Добавить("ОтпустилДолжность");
	МассивВсехРеквизитов.Добавить("Грузополучатель");
	МассивВсехРеквизитов.Добавить("Грузоотправитель");
	МассивВсехРеквизитов.Добавить("БанковскийСчетОрганизации");
	МассивВсехРеквизитов.Добавить("БанковскийСчетКонтрагента");
	МассивВсехРеквизитов.Добавить("БанковскийСчетГрузоотправителя");
	МассивВсехРеквизитов.Добавить("БанковскийСчетГрузополучателя");
	МассивВсехРеквизитов.Добавить("АдресДоставки");
	МассивВсехРеквизитов.Добавить("ДоверенностьНомер");
	МассивВсехРеквизитов.Добавить("ДоверенностьДата");
	МассивВсехРеквизитов.Добавить("ДоверенностьВыдана");
	МассивВсехРеквизитов.Добавить("ДоверенностьЛицо");
	МассивВсехРеквизитов.Добавить("Товары.Номенклатура");
	МассивВсехРеквизитов.Добавить("Товары.Характеристика");
	МассивВсехРеквизитов.Добавить("Товары.Содержание");
	МассивВсехРеквизитов.Добавить("Товары.Упаковка");
	МассивВсехРеквизитов.Добавить("Товары.КоличествоУпаковок");
	МассивВсехРеквизитов.Добавить("Товары.ВидЦены");
	МассивВсехРеквизитов.Добавить("Товары.ЗаказКлиента");
	МассивВсехРеквизитов.Добавить("Товары.КодСтроки");
	МассивВсехРеквизитов.Добавить("Товары.Склад");
	МассивВсехРеквизитов.Добавить("Расхождения.Номенклатура");
	МассивВсехРеквизитов.Добавить("Расхождения.Характеристика");
	МассивВсехРеквизитов.Добавить("Расхождения.Содержание");
	МассивВсехРеквизитов.Добавить("Расхождения.Упаковка");
	МассивВсехРеквизитов.Добавить("Расхождения.КоличествоУпаковок");
	МассивВсехРеквизитов.Добавить("Расхождения.ЗаказКлиента");
	МассивВсехРеквизитов.Добавить("Расхождения.КодСтроки");
	МассивВсехРеквизитов.Добавить("Расхождения.Склад");
	МассивВсехРеквизитов.Добавить("Расхождения.РаспоряжениеНаИнвентаризацию");
	
	МассивРеквизитовОперации = Новый Массив;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		МассивРеквизитовОперации.Добавить("Склад");
		МассивРеквизитовОперации.Добавить("СтатьяДоходов");
		МассивРеквизитовОперации.Добавить("АналитикаДоходов");
		МассивРеквизитовОперации.Добавить("СтатьяРасходов");
		МассивРеквизитовОперации.Добавить("АналитикаРасходов");
		МассивРеквизитовОперации.Добавить("Отпустил");
		МассивРеквизитовОперации.Добавить("ОтпустилДолжность");
		МассивРеквизитовОперации.Добавить("Грузополучатель");
		МассивРеквизитовОперации.Добавить("Грузоотправитель");
		МассивРеквизитовОперации.Добавить("БанковскийСчетОрганизации");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("БанковскийСчетГрузоотправителя");
		МассивРеквизитовОперации.Добавить("БанковскийСчетГрузополучателя");
		МассивРеквизитовОперации.Добавить("АдресДоставки");
		МассивРеквизитовОперации.Добавить("ДоверенностьНомер");
		МассивРеквизитовОперации.Добавить("ДоверенностьДата");
		МассивРеквизитовОперации.Добавить("ДоверенностьВыдана");
		МассивРеквизитовОперации.Добавить("ДоверенностьЛицо");
		МассивРеквизитовОперации.Добавить("Товары.Номенклатура");
		МассивРеквизитовОперации.Добавить("Товары.Характеристика");
		МассивРеквизитовОперации.Добавить("Товары.Упаковка");
		МассивРеквизитовОперации.Добавить("Товары.КоличествоУпаковок");
		МассивРеквизитовОперации.Добавить("Товары.ВидЦены");
		МассивРеквизитовОперации.Добавить("Товары.ЗаказКлиента");
		МассивРеквизитовОперации.Добавить("Товары.КодСтроки");
		МассивРеквизитовОперации.Добавить("Товары.Склад");
		МассивРеквизитовОперации.Добавить("Расхождения.Номенклатура");
		МассивРеквизитовОперации.Добавить("Расхождения.Характеристика");
		МассивРеквизитовОперации.Добавить("Расхождения.Упаковка");
		МассивРеквизитовОперации.Добавить("Расхождения.КоличествоУпаковок");
		МассивРеквизитовОперации.Добавить("Расхождения.ЗаказКлиента");
		МассивРеквизитовОперации.Добавить("Расхождения.КодСтроки");
		МассивРеквизитовОперации.Добавить("Расхождения.Склад");
		МассивРеквизитовОперации.Добавить("Расхождения.РаспоряжениеНаИнвентаризацию");
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		МассивРеквизитовОперации.Добавить("Товары.Номенклатура");
		МассивРеквизитовОперации.Добавить("Товары.Характеристика");
		МассивРеквизитовОперации.Добавить("Товары.Содержание");
		МассивРеквизитовОперации.Добавить("Товары.ВидЦены");
		МассивРеквизитовОперации.Добавить("Товары.ЗаказКлиента");
		МассивРеквизитовОперации.Добавить("Товары.КодСтроки");
		МассивРеквизитовОперации.Добавить("Расхождения.Номенклатура");
		МассивРеквизитовОперации.Добавить("Расхождения.Характеристика");
		МассивРеквизитовОперации.Добавить("Расхождения.Содержание");
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		МассивРеквизитовОперации.Добавить("Товары.Содержание");
		МассивРеквизитовОперации.Добавить("Расхождения.Содержание");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТоварыПоИсходнымДанным(ДокументОснование, Товары) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, ДокументОснование);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	*
	|ИЗ ИсходныеДанные КАК ИсходныеДанные
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИсходныеДанные.НомерСтроки
	|";
	
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьРасхождения(Объект) Экспорт
	
	Объект.Расхождения.Очистить();
	
	КлючевыеПоля = "
	|	Номенклатура,
	|	Характеристика,
	|	Содержание,
	|	Упаковка,
	|	ЗаказКлиента,
	|	КодСтроки,
	|	Склад,
	|	СтавкаНДС";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, Объект.ДокументОснование);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НовыеДанные", Объект.Товары.Выгрузить());
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НовыеДанные.Номенклатура КАК Номенклатура,
	|	НовыеДанные.Характеристика КАК Характеристика,
	|	НовыеДанные.Содержание КАК Содержание,
	|	НовыеДанные.Упаковка КАК Упаковка,
	|	НовыеДанные.ЗаказКлиента КАК ЗаказКлиента,
	|	НовыеДанные.КодСтроки КАК КодСтроки,
	|	НовыеДанные.Склад КАК Склад,
	|	НовыеДанные.СтавкаНДС КАК СтавкаНДС,
	|	НовыеДанные.КоличествоУпаковок КАК КоличествоУпаковок,
	|	НовыеДанные.Количество КАК Количество,
	|	НовыеДанные.Сумма КАК Сумма,
	|	НовыеДанные.СуммаНДС КАК СуммаНДС,
	|	НовыеДанные.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ НовыеДанныеИзТаблицы
	|ИЗ
	|	&НовыеДанные КАК НовыеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанныеИзТаблицы.Номенклатура,
	|	НовыеДанныеИзТаблицы.Характеристика,
	|	НовыеДанныеИзТаблицы.Содержание,
	|	НовыеДанныеИзТаблицы.Упаковка,
	|	НовыеДанныеИзТаблицы.ЗаказКлиента,
	|	НовыеДанныеИзТаблицы.КодСтроки,
	|	НовыеДанныеИзТаблицы.Склад,
	|	НовыеДанныеИзТаблицы.СтавкаНДС,
	|	СУММА(НовыеДанныеИзТаблицы.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(НовыеДанныеИзТаблицы.Количество) КАК Количество,
	|	СУММА(НовыеДанныеИзТаблицы.Сумма) КАК Сумма,
	|	СУММА(НовыеДанныеИзТаблицы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(НовыеДанныеИзТаблицы.СуммаСНДС) КАК СуммаСНДС
	|ПОМЕСТИТЬ НовыеДанные
	|ИЗ
	|	НовыеДанныеИзТаблицы КАК НовыеДанныеИзТаблицы
	|
	|СГРУППИРОВАТЬ ПО
	|	НовыеДанныеИзТаблицы.Номенклатура,
	|	НовыеДанныеИзТаблицы.Характеристика,
	|	НовыеДанныеИзТаблицы.Содержание,
	|	НовыеДанныеИзТаблицы.Упаковка,
	|	НовыеДанныеИзТаблицы.ЗаказКлиента,
	|	НовыеДанныеИзТаблицы.КодСтроки,
	|	НовыеДанныеИзТаблицы.Склад,
	|	НовыеДанныеИзТаблицы.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НовыеДанныеИзТаблицы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ИсходныеДанные.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(ИсходныеДанные.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	
	|	ВЫБОР КОГДА ИсходныеДанные.Содержание <> Неопределено ТОГДА
	|		ИсходныеДанные.Содержание
	|	ИНАЧЕ
	|		""""
	|	КОНЕЦ КАК Содержание,
	|	
	|	ВЫРАЗИТЬ(ИсходныеДанные.Упаковка КАК Справочник.УпаковкиНоменклатуры) КАК Упаковка,
	|	ИсходныеДанные.ЗаказКлиента,
	|	
	|	ВЫБОР КОГДА ИсходныеДанные.КодСтроки <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.КодСтроки КАК Число(10))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК КодСтроки,
	|	
	|	ВЫРАЗИТЬ(ИсходныеДанные.Склад КАК Справочник.Склады) КАК Склад,
	|	ВЫРАЗИТЬ(ИсходныеДанные.СтавкаНДС КАК Перечисление.СтавкиНДС) КАК СтавкаНДС,
	|	
	|	СУММА(ВЫБОР КОГДА ИсходныеДанные.КоличествоУпаковок <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.КоличествоУпаковок КАК Число(15, 3))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК КоличествоУпаковок,
	|	
	|	СУММА(ВЫБОР КОГДА ИсходныеДанные.Количество <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.Количество КАК Число(15, 3))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК Количество,
	|	
	|	СУММА(ВЫБОР КОГДА ИсходныеДанные.Сумма <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.Сумма КАК Число(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК Сумма,
	|	
	|	СУММА(ВЫБОР КОГДА ИсходныеДанные.СуммаНДС <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.СуммаНДС КАК Число(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК СуммаНДС,
	|	
	|	СУММА(ВЫБОР КОГДА ИсходныеДанные.СуммаСНДС <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.СуммаСНДС КАК Число(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК СуммаСНДС
	|
	|ПОМЕСТИТЬ ИсходныеДанныеДляРасхождений
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсходныеДанные.Характеристика,
	|	ИсходныеДанные.Содержание,
	|	ИсходныеДанные.Упаковка,
	|	ИсходныеДанные.ЗаказКлиента,
	|	ИсходныеДанные.КодСтроки,
	|	ИсходныеДанные.Склад,
	|	ИсходныеДанные.Номенклатура,
	|	ИсходныеДанные.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИсходныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанные.Номенклатура КАК Номенклатура,
	|	НовыеДанные.Характеристика КАК Характеристика,
	|	НовыеДанные.Содержание КАК Содержание,
	|	НовыеДанные.Упаковка КАК Упаковка,
	|	НовыеДанные.ЗаказКлиента КАК ЗаказКлиента,
	|	НовыеДанные.КодСтроки КАК КодСтроки,
	|	НовыеДанные.Склад КАК Склад,
	|	НовыеДанные.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА НовыеДанные.Количество = ИсходныеДанные.Количество
	|			ТОГДА НовыеДанные.Сумма
	|		ИНАЧЕ НовыеДанные.Сумма / НовыеДанные.Количество * ИсходныеДанные.Количество
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ДанныеРасхождений
	|ИЗ
	|	НовыеДанные КАК НовыеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИсходныеДанныеДляРасхождений КАК ИсходныеДанные
	|		ПО НовыеДанные.Номенклатура = ИсходныеДанные.Номенклатура
	|			И НовыеДанные.Характеристика = ИсходныеДанные.Характеристика
	|			И НовыеДанные.Содержание = ИсходныеДанные.Содержание
	|			И НовыеДанные.Упаковка = ИсходныеДанные.Упаковка
	|			И НовыеДанные.ЗаказКлиента = ИсходныеДанные.ЗаказКлиента
	|			И НовыеДанные.КодСтроки = ИсходныеДанные.КодСтроки
	|			И НовыеДанные.Склад = ИсходныеДанные.Склад
	|			И НовыеДанные.СтавкаНДС = ИсходныеДанные.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсходныеДанные.Номенклатура,
	|	ИсходныеДанные.Характеристика,
	|	ИсходныеДанные.Содержание,
	|	ИсходныеДанные.Упаковка,
	|	ИсходныеДанные.ЗаказКлиента,
	|	ИсходныеДанные.КодСтроки,
	|	ИсходныеДанные.Склад,
	|	ИсходныеДанные.СтавкаНДС,
	|	-ИсходныеДанные.Сумма
	|ИЗ
	|	ИсходныеДанныеДляРасхождений КАК ИсходныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НовыеДанные КАК НовыеДанные
	|		ПО ИсходныеДанные.Номенклатура = НовыеДанные.Номенклатура
	|			И ИсходныеДанные.Характеристика = НовыеДанные.Характеристика
	|			И ИсходныеДанные.Содержание = НовыеДанные.Содержание
	|			И ИсходныеДанные.Упаковка = НовыеДанные.Упаковка
	|			И ИсходныеДанные.ЗаказКлиента = НовыеДанные.ЗаказКлиента
	|			И ИсходныеДанные.КодСтроки = НовыеДанные.КодСтроки
	|			И ИсходныеДанные.Склад = НовыеДанные.Склад
	|			И ИсходныеДанные.СтавкаНДС = НовыеДанные.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказКлиента,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.СтавкаНДС,
	|	СУММА(ДанныеРасхождений.Сумма),
	|	ВЫРАЗИТЬ(ДанныеРасхождений.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ДанныеРасхождений КАК ДанныеРасхождений
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказКлиента,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.СтавкаНДС
	|
	|ИМЕЮЩИЕ
	|	ВЫРАЗИТЬ(СУММА(ДанныеРасхождений.Сумма) КАК ЧИСЛО(15,2)) <> 0
	|";
	ТаблицаРасхожденийВСуммах = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ДанныеРасхождения Из ТаблицаРасхожденийВСуммах Цикл
		
		НовоеРасхождение = Объект.Расхождения.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеРасхождение, ДанныеРасхождения);
		
		ТекПроцентНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(НовоеРасхождение.СтавкаНДС);
		НовоеРасхождение.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(НовоеРасхождение.Сумма, ТекПроцентНДС, Объект.ЦенаВключаетНДС);
		
		НовоеРасхождение.СуммаСНДС = НовоеРасхождение.Сумма + ?(Объект.ЦенаВключаетНДС, 0, НовоеРасхождение.СуммаНДС);
		НовоеРасхождение.ВариантОтражения = ВариантОтраженияПоСтроке(НовоеРасхождение, ДанныеРасхождения.ТипНоменклатуры);
		
	КонецЦикла;
	
	Запрос.Текст = "
	|УНИЧТОЖИТЬ ДанныеРасхождений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчтенныеРасхождения.Номенклатура КАК Номенклатура,
	|	УчтенныеРасхождения.Характеристика КАК Характеристика,
	|	УчтенныеРасхождения.Содержание КАК Содержание,
	|	УчтенныеРасхождения.Упаковка КАК Упаковка,
	|	УчтенныеРасхождения.ЗаказКлиента КАК ЗаказКлиента,
	|	УчтенныеРасхождения.КодСтроки КАК КодСтроки,
	|	УчтенныеРасхождения.Склад КАК Склад,
	|	УчтенныеРасхождения.СтавкаНДС КАК СтавкаНДС,
	|	УчтенныеРасхождения.СуммаНДС КАК СуммаНДС,
	|	УчтенныеРасхождения.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ УчтенныеРасхождения
	|ИЗ
	|	&УчтенныеРасхождения КАК УчтенныеРасхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанные.Номенклатура КАК Номенклатура,
	|	НовыеДанные.Характеристика КАК Характеристика,
	|	НовыеДанные.Содержание КАК Содержание,
	|	НовыеДанные.Упаковка КАК Упаковка,
	|	НовыеДанные.ЗаказКлиента КАК ЗаказКлиента,
	|	НовыеДанные.КодСтроки КАК КодСтроки,
	|	НовыеДанные.Склад КАК Склад,
	|	НовыеДанные.СтавкаНДС КАК СтавкаНДС,
	|	НовыеДанные.КоличествоУпаковок КАК КоличествоУпаковок,
	|	НовыеДанные.Количество КАК Количество,
	|	НовыеДанные.СуммаНДС КАК СуммаНДС,
	|	НовыеДанные.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ ДанныеРасхождений
	|ИЗ
	|	НовыеДанные КАК НовыеДанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсходныеДанные.Номенклатура,
	|	ИсходныеДанные.Характеристика,
	|	ИсходныеДанные.Содержание,
	|	ИсходныеДанные.Упаковка,
	|	ИсходныеДанные.ЗаказКлиента,
	|	ИсходныеДанные.КодСтроки,
	|	ИсходныеДанные.Склад,
	|	ИсходныеДанные.СтавкаНДС,
	|	-ИсходныеДанные.КоличествоУпаковок,
	|	-ИсходныеДанные.Количество,
	|	-ИсходныеДанные.СуммаНДС,
	|	-ИсходныеДанные.СуммаСНДС
	|ИЗ
	|	ИсходныеДанныеДляРасхождений КАК ИсходныеДанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УчтенныеРасхождения.Номенклатура,
	|	УчтенныеРасхождения.Характеристика,
	|	УчтенныеРасхождения.Содержание,
	|	УчтенныеРасхождения.Упаковка,
	|	УчтенныеРасхождения.ЗаказКлиента,
	|	УчтенныеРасхождения.КодСтроки,
	|	УчтенныеРасхождения.Склад,
	|	УчтенныеРасхождения.СтавкаНДС,
	|	0,
	|	0,
	|	-УчтенныеРасхождения.СуммаНДС,
	|	-УчтенныеРасхождения.СуммаСНДС
	|ИЗ
	|	УчтенныеРасхождения КАК УчтенныеРасхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказКлиента,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.СтавкаНДС,
	|	СУММА(ДанныеРасхождений.КоличествоУпаковок),
	|	СУММА(ДанныеРасхождений.Количество),
	|	СУММА(ДанныеРасхождений.СуммаНДС),
	|	СУММА(ДанныеРасхождений.СуммаСНДС),
	|	ВЫРАЗИТЬ(ДанныеРасхождений.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ДанныеРасхождений КАК ДанныеРасхождений
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказКлиента,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.СтавкаНДС
	|
	|ИМЕЮЩИЕ
	|	ВЫРАЗИТЬ(СУММА(ДанныеРасхождений.КоличествоУпаковок) КАК ЧИСЛО(15,3)) <> 0
	|	ИЛИ ВЫРАЗИТЬ(СУММА(ДанныеРасхождений.Количество) КАК ЧИСЛО(15,3)) <> 0
	|";
	Запрос.УстановитьПараметр("УчтенныеРасхождения", Объект.Расхождения.Выгрузить());
	
	ДанныеРасхождения = Запрос.Выполнить().Выбрать();
	Пока ДанныеРасхождения.Следующий() Цикл
		
		НовоеРасхождение = Объект.Расхождения.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеРасхождение, ДанныеРасхождения);
		
		ПараметрыПоиска = Новый Структура(КлючевыеПоля);
		ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ДанныеРасхождения);
		
		НовоеРасхождение.Сумма = НовоеРасхождение.СуммаСНДС - ?(Объект.ЦенаВключаетНДС, 0, НовоеРасхождение.СуммаНДС);
		НовоеРасхождение.ВариантОтражения = ВариантОтраженияПоСтроке(НовоеРасхождение, ДанныеРасхождения.ТипНоменклатуры);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВариантОтраженияПоСтроке(СтрокаРасхождений, ТипНоменклатуры)
	
	ВариантОтражения = Неопределено;
	
	Если (ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Товар
		И ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.МногооборотнаяТара)
		ИЛИ СтрокаРасхождений.КоличествоУпаковок = 0 Тогда
		
		Если СтрокаРасхождений.СуммаСНДС < 0 Тогда
			ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УменьшитьВыручку;
		Иначе
			ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УвеличитьВыручку;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВариантОтражения;
	
КонецФункции

Процедура СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, ДокументОснование)
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымРеализацияТоваровУслуг();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымАктВыполненныхРабот();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымРеализацияУслугПрочихАктивов();
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	КорректировкаРеализации.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДанныеПоследнейКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Проведен
	|	И КорректировкаРеализации.ДокументОснование = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаРеализации.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки				   КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура		 		   КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТовары.Характеристика			   КАК Характеристика,
	|	ТаблицаТовары.Содержание				   КАК Содержание,
	|	ТаблицаТовары.Упаковка					   КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок		   КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество				   КАК Количество,
	|	ТаблицаТовары.Сумма						   КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС					   КАК СтавкаНДС,
	|	ТаблицаТовары.СуммаНДС					   КАК СуммаНДС,
	|	ТаблицаТовары.СуммаСНДС					   КАК СуммаСНДС,
	|	ТаблицаТовары.КодСтроки					   КАК КодСтроки,
	|	ТаблицаТовары.Склад						   КАК Склад,
	|	ТаблицаТовары.ЗаказКлиента				   КАК ЗаказКлиента,
	|	ТаблицаТовары.Цена						   КАК Цена,
	|	ТаблицаТовары.ВидЦены					   КАК ВидЦены
	|
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ТаблицаТовары.Ссылка = ДанныеПоследнейКорректировки.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|" + ТекстЗапросаПоОснованию + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеПоследнейКорректировки
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстЗапросаПоИсходнымДаннымРеализацияТоваровУслуг()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки				   КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура				   КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТовары.Характеристика			   КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО							   КАК Содержание,
	|	ТаблицаТовары.Упаковка					   КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок		   КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество				   КАК Количество,
	|	ТаблицаТовары.Сумма						   КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС					   КАК СтавкаНДС,
	|	ТаблицаТовары.СуммаНДС					   КАК СуммаНДС,
	|	ТаблицаТовары.СуммаСНДС					   КАК СуммаСНДС,
	|	ТаблицаТовары.КодСтроки					   КАК КодСтроки,
	|	ТаблицаТовары.Склад						   КАК Склад,
	|	ТаблицаТовары.ЗаказКлиента				   КАК ЗаказКлиента,
	|	
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаТовары.Цена * ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(15,2)) = ТаблицаТовары.Сумма
	|			ТОГДА ТаблицаТовары.Цена
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.Сумма / ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК Цена,
	|	
	|	ВЫБОР КОГДА ВЫРАЗИТЬ(ТаблицаТовары.Цена * ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(15,2)) = ТаблицаТовары.Сумма ТОГДА
	|		ТаблицаТовары.ВидЦены
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
	|	КОНЕЦ КАК ВидЦены
	|
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаТовары.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПоИсходнымДаннымАктВыполненныхРабот()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаУслуги.НомерСтроки				   КАК НомерСтроки,
	|	ТаблицаУслуги.Номенклатура	 			   КАК Номенклатура,
	|	ТаблицаУслуги.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаУслуги.Характеристика			   КАК Характеристика,
	|	ТаблицаУслуги.Содержание				   КАК Содержание,
	|	Неопределено							   КАК Упаковка,
	|	Неопределено							   КАК КоличествоУпаковок,
	|	ТаблицаУслуги.Количество				   КАК Количество,
	|	ТаблицаУслуги.Сумма						   КАК Сумма,
	|	ТаблицаУслуги.СтавкаНДС					   КАК СтавкаНДС,
	|	ТаблицаУслуги.СуммаНДС					   КАК СуммаНДС,
	|	ТаблицаУслуги.СуммаСНДС					   КАК СуммаСНДС,
	|	ТаблицаУслуги.КодСтроки					   КАК КодСтроки,
	|	Неопределено							   КАК Склад,
	|	ТаблицаУслуги.ЗаказКлиента				   КАК ЗаказКлиента,
	|	
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Цена * ТаблицаУслуги.Количество КАК ЧИСЛО(15,2)) = ТаблицаУслуги.Сумма
	|			ТОГДА ТаблицаУслуги.Цена
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаУслуги.Сумма / ТаблицаУслуги.Количество КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК Цена,
	|	
	|	ВЫБОР КОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Цена * ТаблицаУслуги.Количество КАК ЧИСЛО(15,2)) = ТаблицаУслуги.Сумма ТОГДА
	|		ТаблицаУслуги.ВидЦены
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
	|	КОНЕЦ КАК ВидЦены
	|
	|ИЗ
	|	Документ.АктВыполненныхРабот.Услуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаУслуги.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПоИсходнымДаннымРеализацияУслугПрочихАктивов()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаДоходы.НомерСтроки					 КАК НомерСтроки,
	|	Неопределено								 КАК Номенклатура,
	|	Неопределено								 КАК ТипНоменклатуры,
	|	Неопределено								 КАК Характеристика,
	|	ТаблицаДоходы.Содержание					 КАК Содержание,
	|	Неопределено								 КАК Упаковка,
	|	Неопределено								 КАК КоличествоУпаковок,
	|	ТаблицаДоходы.Количество					 КАК Количество,
	|	ТаблицаДоходы.Сумма							 КАК Сумма,
	|	ТаблицаДоходы.СтавкаНДС						 КАК СтавкаНДС,
	|	ТаблицаДоходы.СуммаНДС						 КАК СуммаНДС,
	|	ТаблицаДоходы.СуммаСНДС						 КАК СуммаСНДС,
	|	Неопределено								 КАК КодСтроки,
	|	Неопределено				 				 КАК Склад,
	|	ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка) КАК ЗаказКлиента,
	|
	|	ВЫРАЗИТЬ(ТаблицаДоходы.Сумма / ТаблицаДоходы.Количество КАК ЧИСЛО(15,2)) КАК Цена,
	|
	|	Неопределено								 КАК ВидЦены
	|
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаДоходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаДоходы.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает массив имен ролей с правом "Добавление" для данного документа
//
// Возвращаемое значение:
//	Массив - Массив с именами ролей
//
Функция ИменаРолейСПравомДобавления() Экспорт
	
	МассивРолей = Новый Массив;
	МассивРолей.Добавить("ПолныеПрава");
	МассивРолей.Добавить("ДобавлениеИзменениеКорректировокРеализаций");
	Возврат МассивРолей;
	
КонецФункции

Функция ПолучитьТекстОснования(Объект, ПорядокРасчетов)
	
	ТекстОснования = "";
	
	Если ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
		И ЗначениеЗаполнено(Объект.Договор) Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ДоговорыКонтрагентов.НаименованиеДляПечати
			|ИЗ
			|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
			|ГДЕ
			|	ДоговорыКонтрагентов.Ссылка = &Ссылка"
		);
		Запрос.УстановитьПараметр("Ссылка", Объект.Договор);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ТекстОснования = СокрЛП(Выборка.НаименованиеДляПечати);
		КонецЕсли;
		
	ИначеЕсли Объект.ПродажаПоЗаказам Тогда
		
		Если Объект.Товары.Количество() <> 0 Тогда
		
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ВЫБОР
				|		КОГДА
				|			ЗаказыКлиентов.НомерПоДаннымКлиента <> """" И ЗаказыКлиентов.ДатаПоДаннымКлиента <> ДАТАВРЕМЯ(1,1,1)
				|		ТОГДА
				|			ЗаказыКлиентов.НомерПоДаннымКлиента
				|		ИНАЧЕ
				|			ЗаказыКлиентов.Номер
				|	КОНЕЦ КАК Номер,
				|	ВЫБОР
				|		КОГДА
				|			ЗаказыКлиентов.НомерПоДаннымКлиента <> """" И ЗаказыКлиентов.ДатаПоДаннымКлиента <> ДАТАВРЕМЯ(1,1,1)
				|		ТОГДА
				|			ЗаказыКлиентов.ДатаПоДаннымКлиента
				|		ИНАЧЕ
				|			ЗаказыКлиентов.Дата
				|	КОНЕЦ КАК Дата,
				|	&СинонимЗаказа КАК Синоним
				|ИЗ
				|	Документ.ЗаказКлиента КАК ЗаказыКлиентов
				|ГДЕ
				|	ЗаказыКлиентов.Ссылка В(&МассивЗаказов)
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	ЗаявкиНаВозврат.Номер,
				|	ЗаявкиНаВозврат.Дата,
				|	&СинонимЗаявки
				|ИЗ
				|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкиНаВозврат
				|ГДЕ
				|	ЗаявкиНаВозврат.Ссылка В(&МассивЗаказов)"
			);
			
			Если ТипЗнч(Объект) = Тип("Структура") Тогда
				МассивЗаказов = Объект.Товары.ВыгрузитьКолонку("ЗаказКлиента");
			Иначе
				МассивЗаказов = Объект.Товары.Выгрузить(,"ЗаказКлиента").ВыгрузитьКолонку("ЗаказКлиента");
			КонецЕсли;
			
			Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
			Запрос.УстановитьПараметр("СинонимЗаказа", НСтр("ru='Заказ клиента'"));
			Запрос.УстановитьПараметр("СинонимЗаявки", НСтр("ru='Заявка на замену'"));
			Выборка = Запрос.Выполнить().Выбрать();
			
			ТекстПоЗаказам = "";
			Пока Выборка.Следующий() Цикл
				ТекстПоЗаказам = ТекстПоЗаказам + ", " + ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(Выборка, Выборка.Синоним);
			КонецЦикла;
			ТекстОснования = СокрЛП(Сред(ТекстПоЗаказам, 3));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстОснования; // Возврат значения по-умолчанию
	
КонецФункции

Функция ПолучитьТекстОснованияДляПечати(Объект) Экспорт
	
	ПорядокРасчетов = ВзаиморасчетыСервер.ПорядокРасчетовПоДокументу(Объект);
	ТекстОснования = ПолучитьТекстОснования(Объект, ПорядокРасчетов);
	
	Возврат ТекстОснования;
	
КонецФункции

Функция ПолучитьСписокВыбораОснованияДляПечати(Объект) Экспорт
	
	Список = Новый СписокЗначений;
	ТекстОснования = ПолучитьТекстОснования(Объект, Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов);
	Если ЗначениеЗаполнено(ТекстОснования) Тогда
		Список.Добавить(ТекстОснования);
	КонецЕсли;
	
	ТекстОснования = ПолучитьТекстОснования(Объект, Перечисления.ПорядокРасчетов.ПоЗаказамНакладным);
	Если ЗначениеЗаполнено(ТекстОснования) Тогда
		Список.Добавить(ТекстОснования);
	КонецЕсли;
	
	Возврат Список;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииТоваровОрганизацийПоследовательность()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	&Ссылка КАК Регистратор
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика,
	|	&Ссылка
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&ПериодПродажи КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК СуммаБезНДСРегл,
	|	ТаблицаДокумента.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК СуммаНДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ТипРасчетов
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &Валюта <> &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2 КАК Порядок,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&ПериодПродажи КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК СуммаБезНДСРегл,
	|	ТаблицаДокумента.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК СуммаНДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ТипРасчетов
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &Валюта <> &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3 КАК Порядок,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&ПериодПродажи КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК СуммаБезНДСРегл,
	|	ТаблицаДокумента.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК СуммаНДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ТипРасчетов
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &Валюта <> &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4 КАК Порядок,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&ПериодПродажи КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК СуммаБезНДСРегл,
	|	ТаблицаДокумента.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК СуммаНДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ТипРасчетов
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &Валюта <> &ВалютаРегламентированногоУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл()

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка,
//		Валюта.
//
//	ПересчитыватьВВалютуРегл - Булево - Признак необходимости пересчета сумм табличной части в валюте регламентированного учета.
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ВключаяНомераГТД, ВключаяДоКорректировки = Ложь, ПересчитыватьВВалютуРегл = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",       ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяНомераГТД",               ВключаяНомераГТД);
	
	// Актуализировать расчеты для получения сумм по товарам документа-основания (например, реализации товаров и услуг)
	Если ПересчитыватьВВалютуРегл Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ТаблицаДанныхДокументов КАК ДанныеДокументов
		|	ПО
		|		РасчетыСКлиентами.Регистратор = ДанныеДокументов.Ссылка
		|
		|ГДЕ
		|	ДанныеДокументов.Валюта <> &ВалютаРегламентированногоУчета
		|	И РасчетыСКлиентами.Активность
		|";
		ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
		МассивАналитикУчетаПоПартнерам = ТаблицаАналитик.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
		
		ВзаиморасчетыСервер.ВыполнитьПроведениеДокументовПоРасчетамСКлиентами(МассивАналитикУчетаПоПартнерам);
		
	КонецЕсли;
	
	УстановитьПараметрыВтВидыЗапасов(
		Запрос,
		ПересчитыватьВВалютуРегл,
		ВключаяДоКорректировки
	);
	
	Запрос.Текст = ТекстЗапросаВтВидыЗапасов() + "
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Основной,
	|	СвязанныеДокументы.Подчиненный КАК Предыдущий
	|ИЗ
	|	ВтСвязанныеДокументы КАК СвязанныеДокументы
	|
	|УПОРЯДОЧИТЬ ПО
	|	СвязанныеДокументы.МоментВремени
	|
	|ИТОГИ ПО
	|	СвязанныеДокументы.Основной
	|";
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаПредыдущихДокументов = Новый ТаблицаЗначений;
	ТаблицаПредыдущихДокументов.Колонки.Добавить("Основной",   Новый ОписаниеТипов("ДокументСсылка.КорректировкаРеализации"));
	ТаблицаПредыдущихДокументов.Колонки.Добавить("Предыдущий", Новый ОписаниеТипов("
		|ДокументСсылка.КорректировкаРеализации,
		|ДокументСсылка.РеализацияТоваровУслуг,
		|ДокументСсылка.АктВыполненныхРабот,
		|ДокументСсылка.РеализацияУслугПрочихАктивов")
	);
	
	ВыборкаПоСсылкам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСсылкам.Следующий() Цикл
		Выборка = ВыборкаПоСсылкам.Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаПредыдущихДокументов.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПредыдущиеДокументы", ТаблицаПредыдущихДокументов);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПредыдущиеДокументы.Основной КАК Основной,
	               |	ПредыдущиеДокументы.Предыдущий КАК Предыдущий
	               |ПОМЕСТИТЬ ПредыдущиеДокументы
	               |ИЗ
	               |	&ПредыдущиеДокументы КАК ПредыдущиеДокументы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаТоваров.Ссылка КАК Ссылка,
	               |	ТаблицаТоваров.Содержание КАК Содержание,
	               |	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	               |	ТаблицаТоваров.Характеристика КАК Характеристика,
	               |	ТаблицаТоваров.Упаковка КАК Упаковка,
	               |	МАКСИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки
	               |ПОМЕСТИТЬ СтрокиТоваров
	               |ИЗ
	               |	Документ.КорректировкаРеализации.Товары КАК ТаблицаТоваров
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	               |		ПО ТаблицаТоваров.Ссылка = ДанныеДокументов.Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаТоваров.Ссылка,
	               |	ТаблицаТоваров.Содержание,
	               |	ТаблицаТоваров.Номенклатура,
	               |	ТаблицаТоваров.Характеристика,
	               |	ТаблицаТоваров.Упаковка
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ТаблицаТоваров.Ссылка,
	               |	ТаблицаТоваров.Номенклатура,
	               |	ТаблицаТоваров.Содержание,
	               |	ТаблицаТоваров.Характеристика,
	               |	ТаблицаТоваров.Упаковка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаДокумента.Ссылка КАК Ссылка,
	               |	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999) КАК НомерСтроки,
	               |	"""" КАК Содержание,
	               |	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	               |	ТаблицаДокумента.Характеристика КАК Характеристика,
	               |	ТаблицаДокумента.Упаковка КАК Упаковка,
	               |	ВЫБОР
	               |		КОГДА &ВключаяНомераГТД
	               |				И НЕ СчетаФактуры.Корректировочный
	               |			ТОГДА ТаблицаДокумента.НомерГТД
	               |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	               |	КОНЕЦ КАК НомерГТД,
	               |	СУММА(ТаблицаДокумента.Количество) КАК Количество,
	               |	СУММА(ТаблицаДокумента.КоличествоУпаковок) КАК КоличествоУпаковок,
	               |	СУММА(ТаблицаДокумента.СуммаБезНДС) КАК СуммаБезНДС,
	               |	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	               |	СУММА(ТаблицаДокумента.СуммаНДС) КАК СуммаНДС,
	               |	ИСТИНА КАК ЭтоТовар,
	               |	ТаблицаДокумента.ДоКорректировки КАК ДоКорректировки
	               |ПОМЕСТИТЬ ТаблицаТоваров
	               |ИЗ
	               |	ВтВидыЗапасов КАК ТаблицаДокумента
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	               |		ПО ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ СтрокиТоваров КАК СтрокиТоваров
	               |		ПО ТаблицаДокумента.Ссылка = СтрокиТоваров.Ссылка
	               |			И ТаблицаДокумента.Номенклатура = СтрокиТоваров.Номенклатура
	               |			И (СтрокиТоваров.Содержание = """")
	               |			И ТаблицаДокумента.Характеристика = СтрокиТоваров.Характеристика
	               |			И ТаблицаДокумента.Упаковка = СтрокиТоваров.Упаковка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСчетовФактур КАК СчетаФактуры
	               |		ПО ТаблицаДокумента.Ссылка = СчетаФактуры.ДокументОснование
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаДокумента.Ссылка,
	               |	ТаблицаДокумента.Номенклатура,
	               |	ТаблицаДокумента.Характеристика,
	               |	ТаблицаДокумента.Упаковка,
	               |	ТаблицаДокумента.СтавкаНДС,
	               |	ТаблицаДокумента.ДоКорректировки,
	               |	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	               |	ВЫБОР
	               |		КОГДА &ВключаяНомераГТД
	               |				И НЕ СчетаФактуры.Корректировочный
	               |			ТОГДА ТаблицаДокумента.НомерГТД
	               |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	               |	КОНЕЦ
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ТаблицаДокумента.Количество) > 0
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПредыдущиеДокументы.Основной,
	               |	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	               |	ТаблицаДокумента.Содержание,
	               |	ТаблицаДокумента.Номенклатура,
	               |	ТаблицаДокумента.Характеристика,
	               |	ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка),
	               |	НЕОПРЕДЕЛЕНО,
	               |	ТаблицаДокумента.Количество,
	               |	ТаблицаДокумента.Количество,
	               |	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС,
	               |	ТаблицаДокумента.СтавкаНДС,
	               |	ТаблицаДокумента.СуммаНДС,
	               |	ЛОЖЬ,
	               |	ЛОЖЬ
	               |ИЗ
	               |	Документ.КорректировкаРеализации.Товары КАК ТаблицаДокумента
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПредыдущиеДокументы КАК ПредыдущиеДокументы
	               |		ПО ТаблицаДокумента.Ссылка = ПредыдущиеДокументы.Основной
	               |		ЛЕВОЕ СОЕДИНЕНИЕ СтрокиТоваров КАК СтрокиТоваров
	               |		ПО ТаблицаДокумента.Ссылка = СтрокиТоваров.Ссылка
	               |			И ТаблицаДокумента.Номенклатура = СтрокиТоваров.Номенклатура
	               |			И ТаблицаДокумента.Содержание = СтрокиТоваров.Содержание
	               |			И ТаблицаДокумента.Характеристика = СтрокиТоваров.Характеристика
	               |			И ТаблицаДокумента.Упаковка = СтрокиТоваров.Упаковка
	               |ГДЕ
	               |	(НЕ ТаблицаДокумента.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	               |			ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL )
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПредыдущиеДокументы.Основной,
	               |	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	               |	ТаблицаДокумента.Содержание,
	               |	ТаблицаДокумента.Номенклатура,
	               |	ТаблицаДокумента.Характеристика,
	               |	ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка),
	               |	НЕОПРЕДЕЛЕНО,
	               |	ТаблицаДокумента.Количество,
	               |	ТаблицаДокумента.Количество,
	               |	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС,
	               |	ТаблицаДокумента.СтавкаНДС,
	               |	ТаблицаДокумента.СуммаНДС,
	               |	ЛОЖЬ,
	               |	ИСТИНА
	               |ИЗ
	               |	Документ.КорректировкаРеализации.Товары КАК ТаблицаДокумента
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПредыдущиеДокументы КАК ПредыдущиеДокументы
	               |		ПО ТаблицаДокумента.Ссылка = ПредыдущиеДокументы.Предыдущий
	               |		ЛЕВОЕ СОЕДИНЕНИЕ СтрокиТоваров КАК СтрокиТоваров
	               |		ПО ТаблицаДокумента.Ссылка = СтрокиТоваров.Ссылка
	               |			И ТаблицаДокумента.Номенклатура = СтрокиТоваров.Номенклатура
	               |			И ТаблицаДокумента.Содержание = СтрокиТоваров.Содержание
	               |			И ТаблицаДокумента.Характеристика = СтрокиТоваров.Характеристика
	               |			И ТаблицаДокумента.Упаковка = СтрокиТоваров.Упаковка
	               |ГДЕ
	               |	&ВключаяДоКорректировки
	               |	И (НЕ ТаблицаДокумента.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	               |			ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL )
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПредыдущиеДокументы.Основной,
	               |	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	               |	"""",
	               |	ТаблицаДокумента.Номенклатура,
	               |	ТаблицаДокумента.Характеристика,
	               |	ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка),
	               |	НЕОПРЕДЕЛЕНО,
	               |	ТаблицаДокумента.Количество,
	               |	ТаблицаДокумента.Количество,
	               |	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС,
	               |	ТаблицаДокумента.СтавкаНДС,
	               |	ТаблицаДокумента.СуммаНДС,
	               |	ЛОЖЬ,
	               |	ИСТИНА
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаДокумента
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПредыдущиеДокументы КАК ПредыдущиеДокументы
	               |		ПО ТаблицаДокумента.Ссылка = ПредыдущиеДокументы.Предыдущий
	               |		ЛЕВОЕ СОЕДИНЕНИЕ СтрокиТоваров КАК СтрокиТоваров
	               |		ПО ТаблицаДокумента.Ссылка = СтрокиТоваров.Ссылка
	               |			И ТаблицаДокумента.Номенклатура = СтрокиТоваров.Номенклатура
	               |			И (СтрокиТоваров.Содержание = """")
	               |			И ТаблицаДокумента.Характеристика = СтрокиТоваров.Характеристика
	               |			И ТаблицаДокумента.Упаковка = СтрокиТоваров.Упаковка
	               |ГДЕ
	               |	&ВключаяДоКорректировки
	               |	И НЕ ТаблицаДокумента.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПредыдущиеДокументы.Основной,
	               |	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	               |	ТаблицаДокумента.Содержание,
	               |	ТаблицаДокумента.Номенклатура,
	               |	ТаблицаДокумента.Характеристика,
	               |	ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка),
	               |	НЕОПРЕДЕЛЕНО,
	               |	ТаблицаДокумента.Количество,
	               |	ТаблицаДокумента.Количество,
	               |	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС,
	               |	ТаблицаДокумента.СтавкаНДС,
	               |	ТаблицаДокумента.СуммаНДС,
	               |	ЛОЖЬ,
	               |	ИСТИНА
	               |ИЗ
	               |	Документ.АктВыполненныхРабот.Услуги КАК ТаблицаДокумента
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПредыдущиеДокументы КАК ПредыдущиеДокументы
	               |		ПО ТаблицаДокумента.Ссылка = ПредыдущиеДокументы.Предыдущий
	               |		ЛЕВОЕ СОЕДИНЕНИЕ СтрокиТоваров КАК СтрокиТоваров
	               |		ПО ТаблицаДокумента.Ссылка = СтрокиТоваров.Ссылка
	               |			И ТаблицаДокумента.Номенклатура = СтрокиТоваров.Номенклатура
	               |			И ТаблицаДокумента.Содержание = СтрокиТоваров.Содержание
	               |			И ТаблицаДокумента.Характеристика = СтрокиТоваров.Характеристика
	               |			И (СтрокиТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка))
	               |ГДЕ
	               |	&ВключаяДоКорректировки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПредыдущиеДокументы.Основной,
	               |	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	               |	ТаблицаДокумента.Содержание,
	               |	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	               |	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	               |	ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка),
	               |	НЕОПРЕДЕЛЕНО,
	               |	ТаблицаДокумента.Количество,
	               |	ТаблицаДокумента.Количество,
	               |	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС,
	               |	ТаблицаДокумента.СтавкаНДС,
	               |	ТаблицаДокумента.СуммаНДС,
	               |	ЛОЖЬ,
	               |	ИСТИНА
	               |ИЗ
	               |	Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаДокумента
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПредыдущиеДокументы КАК ПредыдущиеДокументы
	               |		ПО ТаблицаДокумента.Ссылка = ПредыдущиеДокументы.Предыдущий
	               |		ЛЕВОЕ СОЕДИНЕНИЕ СтрокиТоваров КАК СтрокиТоваров
	               |		ПО ТаблицаДокумента.Ссылка = СтрокиТоваров.Ссылка
	               |			И (СтрокиТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	               |			И ТаблицаДокумента.Содержание = СтрокиТоваров.Содержание
	               |			И (СтрокиТоваров.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	               |			И (СтрокиТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка))
	               |ГДЕ
	               |	&ВключаяДоКорректировки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВтСвязанныеДокументы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ПредыдущиеДокументы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ СтрокиТоваров
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВтВидыЗапасов";
	
	Запрос.Выполнить();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
//Обновление информационной базы

// Обработчик обновления УТ 11.1.0.0
// Заполняет новый реквизит ХозяйственнаяОперация в существующих документах КорректировкаРеализации
//
Процедура ЗаполнитьХозяйственнуюОперацию() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка                КАК Ссылка,
	|	КорректировкаРеализации.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	КорректировкаРеализации.МоментВремени         КАК МоментВремени,
	|	КорректировкаРеализации.ДокументОснование     КАК ДокументОснование
	|	
	|ПОМЕСТИТЬ КорректировкиРеализаций
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|	
	|ГДЕ
	|	НЕ КорректировкаРеализации.ПометкаУдаления
	|;
	|
	|/////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкиРеализаций.Ссылка               КАК Ссылка,
	|	КорректировкиРеализаций.ДокументОснование    КАК ДокументОснование,
	|	КорректировкиРеализаций.МоментВремени        КАК МоментВремени
	|	
	|ПОМЕСТИТЬ ДокументыДляОбработки
	|ИЗ
	|	КорректировкиРеализаций КАК КорректировкиРеализаций
	|ГДЕ
	|	КорректировкиРеализаций.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ДокументОснование
	|;
	|
	|/////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкиРеализаций.Ссылка                КАК Ссылка,
	|	КорректировкиРеализаций.МоментВремени         КАК МоментВремени,
	|	КорректировкиРеализаций.ДокументОснование     КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                         КАК Корректировочный
	|
	|ПОМЕСТИТЬ ДанныеПоСчетамФактурам
	|ИЗ
	|	КорректировкиРеализаций КАК КорректировкиРеализаций
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ПО КорректировкиРеализаций.Ссылка = СчетФактураВыданный.ДокументОснование
	|			И (СчетФактураВыданный.Корректировочный)
	|			И НЕ (СчетФактураВыданный.ПометкаУдаления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ДокументОснование
	|;
	|
	|/////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыДляОбработки.Ссылка                  КАК Ссылка,
	|	МИНИМУМ(ВЫБОР
	|				КОГДА СчетаФактурыДокументовДляОбработки.Корректировочный
	|			 			И НЕ ЕСТЬNULL(СчетаФактурыОснований.Корректировочный, ЛОЖЬ)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)                                КАК ИсходныйКорректировочный
	|	
	|ИЗ
	|	ДокументыДляОбработки КАК ДокументыДляОбработки
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ДанныеПоСчетамФактурам КАК СчетаФактурыДокументовДляОбработки
	|	ПО 
	|		ДокументыДляОбработки.Ссылка = СчетаФактурыДокументовДляОбработки.Ссылка
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ДанныеПоСчетамФактурам КАК СчетаФактурыОснований
	|	ПО 
	|		ДокументыДляОбработки.Ссылка <> СчетаФактурыОснований.Ссылка
	|		И ДокументыДляОбработки.ДокументОснование = СчетаФактурыОснований.ДокументОснование
	|		И ДокументыДляОбработки.МоментВремени > СчетаФактурыОснований.МоментВремени
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДокументыДляОбработки.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		ДокументОбъект.ХозяйственнаяОперация = ?(Выборка.ИсходныйКорректировочный,
				Перечисления.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон,
				Перечисления.ХозяйственныеОперации.ИсправлениеОшибок);
				
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли