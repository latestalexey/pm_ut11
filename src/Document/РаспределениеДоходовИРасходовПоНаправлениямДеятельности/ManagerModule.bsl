#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.РаспределениеПоВсемОрганизациям КАК РаспределениеПоВсемОрганизациям
	|ИЗ
	|	Документ.РаспределениеДоходовИРасходовПоНаправлениямДеятельности КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();

	ТекстЗапроса = ТекстЗапросаТаблицаПрочиеДоходы()
		+ ТекстЗапросаТаблицаПрочиеРасходы()
		+ ТекстЗапросаТаблицаФинансовыеРезультаты()
		+ ТекстЗапросаТаблицаСпособовРаспределения()
		+ ТекстЗапросаТаблицаБазыРаспределения()
		;

	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("РаспределениеПоВсемОрганизациям", Реквизиты.РаспределениеПоВсемОрганизациям);
	Запрос.УстановитьПараметр("НачГраница", Новый Граница(НачалоМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонГраница", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
    
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПрочиеДоходы", МассивРезультатов[0].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПрочиеРасходы", МассивРезультатов[1].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаФинансовыеРезультаты", МассивРезультатов[2].Выгрузить());
	// МассивРезультатов[3]- временная таблица "ТаблицаСпособовРаспределения"
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("РезультатЗапросаПоБазе", МассивРезультатов[4]);
    
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрочиеДоходы()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДоходы.Организация КАК Организация,
	|	ТаблицаДоходы.Подразделение КАК Подразделение,
	|	ТаблицаДоходы.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаДоходы.АналитикаДоходов КАК АналитикаДоходов,
	|	Неопределено КАК ХозяйственнаяОперация,
	|	ТаблицаДоходы.Сумма КАК Сумма
	|	
	|ИЗ
	|	Документ.РаспределениеДоходовИРасходовПоНаправлениямДеятельности.Доходы КАК ТаблицаДоходы
	|
	|ГДЕ
	|	ТаблицаДоходы.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДоходы.НомерСтроки
	|
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасходы.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаРасходы.Организация КАК Организация,
	|	ТаблицаРасходы.Подразделение КАК Подразделение,
	|	ТаблицаРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	Неопределено КАК ХозяйственнаяОперация,
	|	ТаблицаРасходы.Сумма КАК Сумма
	|ИЗ
	|	Документ.РаспределениеДоходовИРасходовПоНаправлениямДеятельности.Расходы КАК ТаблицаРасходы
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Корректировка подразделения для погрешности расчета себестоимости.
	|ВЫБРАТЬ
	|	ТаблицаРасходы.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаРасходы.Организация КАК Организация,
	|	Неопределено КАК Подразделение,
	|	ТаблицаРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	Неопределено КАК ХозяйственнаяОперация,
	|	(-ТаблицаРасходы.Сумма) КАК Сумма
	|ИЗ
	|	Документ.РаспределениеДоходовИРасходовПоНаправлениямДеятельности.Расходы КАК ТаблицаРасходы
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка
	|	И ТаблицаРасходы.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|	И ТаблицаРасходы.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Корректировка подразделения для погрешности расчета себестоимости.
	|ВЫБРАТЬ
	|	ТаблицаРасходы.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаРасходы.Организация КАК Организация,
	|	ТаблицаРасходы.Подразделение КАК Подразделение,
	|	ТаблицаРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	Неопределено КАК ХозяйственнаяОперация,
	|	ТаблицаРасходы.Сумма КАК Сумма
	|ИЗ
	|	Документ.РаспределениеДоходовИРасходовПоНаправлениямДеятельности.Расходы КАК ТаблицаРасходы
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка
	|	И ТаблицаРасходы.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|	И ТаблицаРасходы.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаФинансовыеРезультаты()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ТаблицаДоходы.Организация КАК Организация,
	|	ТаблицаДоходы.Подразделение КАК Подразделение,
	|	ТаблицаДоходы.СтатьяДоходов КАК СтатьяДоходов,
	|	Неопределено КАК СтатьяРасходов,
	|	ТаблицаДоходы.СпособРаспределения КАК СпособРаспределения,
	|
	|	ВЫБОР КОГДА ТаблицаДоходы.АналитикаДоходов ССЫЛКА Справочник.НаправленияДеятельности ТОГДА
	|		ТаблицаДоходы.АналитикаДоходов
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	МАКСИМУМ(ТаблицаДоходы.НомерСтроки) КАК НомерСтроки,
	|	СУММА(ТаблицаДоходы.Сумма) КАК Доходы,
	|	0 КАК Расходы
	|	
	|ИЗ
	|	Документ.РаспределениеДоходовИРасходовПоНаправлениямДеятельности.Доходы КАК ТаблицаДоходы
	|
	|ГДЕ
	|	ТаблицаДоходы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДоходы.Организация,
	|	ТаблицаДоходы.Подразделение,
	|	ТаблицаДоходы.СтатьяДоходов,
	|	ТаблицаДоходы.СпособРаспределения,
	|	ВЫБОР КОГДА ТаблицаДоходы.АналитикаДоходов ССЫЛКА Справочник.НаправленияДеятельности ТОГДА
	|		ТаблицаДоходы.АналитикаДоходов
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ
	|
 	|ОБЪЕДИНИТЬ ВСЕ
 	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ТаблицаРасходы.Организация КАК Организация,
	|	ТаблицаРасходы.Подразделение КАК Подразделение,
	|	Неопределено КАК СтатьяДоходов,
	|	ТаблицаРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаРасходы.СпособРаспределения КАК СпособРаспределения,
	|
	|	ВЫБОР КОГДА ТаблицаРасходы.АналитикаРасходов ССЫЛКА Справочник.НаправленияДеятельности ТОГДА
	|		ТаблицаРасходы.АналитикаРасходов
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	МАКСИМУМ(ТаблицаРасходы.НомерСтроки) КАК НомерСтроки,
	|	0 КАК Доходы,
	|	СУММА(ТаблицаРасходы.Сумма) КАК Расходы
	|	
	|ИЗ
	|	Документ.РаспределениеДоходовИРасходовПоНаправлениямДеятельности.Расходы КАК ТаблицаРасходы
	|
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасходы.Организация,
	|	ТаблицаРасходы.Подразделение,
	|	ТаблицаРасходы.СтатьяРасходов,
	|	ТаблицаРасходы.СпособРаспределения,
	|	ВЫБОР КОГДА ТаблицаРасходы.АналитикаРасходов ССЫЛКА Справочник.НаправленияДеятельности ТОГДА
	|		ТаблицаРасходы.АналитикаРасходов
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Подразделение,
	|	СпособРаспределения,
	|	СтатьяДоходов,
	|	СтатьяРасходов
	|
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаСпособовРаспределения()

	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.СпособРаспределения КАК СпособРаспределения,
	|	ДанныеДокумента.СпособРаспределения.ПравилоРаспределения КАК ПравилоРаспределения,
	|	ВЫБОР КОГДА ДанныеДокумента.СпособРаспределения.ПравилоРаспределения <>
	|		ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияПоНаправлениямДеятельности.ПропорциональноКоэффициентам)
	|	ТОГДА
	|		Истина
	|	ИНАЧЕ
	|		Ложь
	|	КОНЕЦ КАК СпособРасчетаАвтоматически,
	|	ЕСТЬNULL(НаправленияДеятельности.НаправлениеДеятельности, Неопределено) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(НаправленияДеятельности.Коэффициент, 1) КАК Коэффициент
	|	
	|ПОМЕСТИТЬ ТаблицаСпособовРаспределения
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТаблицаДоходы.Организация КАК Организация,
	|		ТаблицаДоходы.СпособРаспределения КАК СпособРаспределения
	|	ИЗ
	|		Документ.РаспределениеДоходовИРасходовПоНаправлениямДеятельности.Доходы КАК ТаблицаДоходы
	|
	|	ГДЕ
	|		ТаблицаДоходы.Ссылка = &Ссылка
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаРасходы.Организация КАК Организация,
	|		ТаблицаРасходы.СпособРаспределения
	|	ИЗ
	|		Документ.РаспределениеДоходовИРасходовПоНаправлениямДеятельности.Расходы КАК ТаблицаРасходы
	|
	|	ГДЕ
	|		ТаблицаРасходы.Ссылка = &Ссылка
	|		
	|	) КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СпособыРаспределенияПоНаправлениямДеятельности.НаправленияДеятельности КАК НаправленияДеятельности
	|	ПО
	|		ДанныеДокумента.СпособРаспределения = НаправленияДеятельности.Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаБазыРаспределения()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаСпособовРаспределения.Организация КАК Организация,
	|	ТаблицаСпособовРаспределения.СпособРаспределения КАК СпособРаспределения,
	|	ЕСТЬNULL(
	|		ФинансовыеРезультаты.НаправлениеДеятельности,
	|		ТаблицаСпособовРаспределения.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|
	|	СУММА(
	|		ВЫБОР КОГДА ТаблицаСпособовРаспределения.ПравилоРаспределения =
	|			ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияПоНаправлениямДеятельности.ПропорциональноКоэффициентам)
	|		ТОГДА
	|			ТаблицаСпособовРаспределения.Коэффициент
	|
	|		КОГДА ТаблицаСпособовРаспределения.ПравилоРаспределения =
	|			ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияПоНаправлениямДеятельности.ПропорциональноДоходам)
	|		ТОГДА
	|			ЕСТЬNULL(ФинансовыеРезультаты.ДоходыОборот, 0)
	|		
	|		КОГДА ТаблицаСпособовРаспределения.ПравилоРаспределения =
	|			ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияПоНаправлениямДеятельности.ПропорциональноРасходам)
	|		ТОГДА
	|			ЕСТЬNULL(ФинансовыеРезультаты.РасходыОборот, 0)
	|		
	|		ИНАЧЕ
	|			ЕСТЬNULL(ФинансовыеРезультаты.ДоходыОборот - ФинансовыеРезультаты.РасходыОборот, 0)
	|		
	|		КОНЕЦ
	|	) КАК База
	|ИЗ
	|	ТаблицаСпособовРаспределения КАК ТаблицаСпособовРаспределения
	| 	
	| 	ЛЕВОЕ СОЕДИНЕНИЕ
	| 		РегистрНакопления.ФинансовыеРезультаты.Обороты(&НачГраница, &КонГраница, Регистратор,
	| 			Организация В (
	|				ВЫБРАТЬ
	|					ТаблицаСпособовРаспределения.Организация
	|				ИЗ
	|					ТаблицаСпособовРаспределения КАК ТаблицаСпособовРаспределения
	|				)
	|			И (СтатьяДоходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ВыручкаОтПродаж)
	|				ИЛИ СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.СебестоимостьПродаж)
	|				)
	| 		) КАК ФинансовыеРезультаты
	| 	ПО
	| 		ТаблицаСпособовРаспределения.СпособРасчетаАвтоматически
	| 		И (ТаблицаСпособовРаспределения.НаправлениеДеятельности = ФинансовыеРезультаты.НаправлениеДеятельности
	| 			ИЛИ ТаблицаСпособовРаспределения.НаправлениеДеятельности = Неопределено)
	|ГДЕ
	|	ФинансовыеРезультаты.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
	|	ИЛИ ФинансовыеРезультаты.Регистратор ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	| 	ТаблицаСпособовРаспределения.Организация,
	| 	ТаблицаСпособовРаспределения.СпособРаспределения,
	|	ЕСТЬNULL(
	|		ФинансовыеРезультаты.НаправлениеДеятельности,
	|		ТаблицаСпособовРаспределения.НаправлениеДеятельности)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	| 	СпособРаспределения,
	| 	НаправлениеДеятельности
	|
	|ИТОГИ
	|	СУММА(База)
	|ПО
	|	Организация,
	| 	СпособРаспределения
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;

КонецФункции

#КонецЕсли