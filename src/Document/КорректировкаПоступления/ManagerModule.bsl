#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Заполняет таблицу реквизитов, зависимых от типа документа-основания.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Документ, на основании которого введена корректировка;
//	МассивВсехРеквизитов - Массив - реквизиты, которые не зависят от документа-основания;
//	МассивРеквизитовОперации - Массив - реквизиты, которые зависят от документа-основания.
//
Процедура ЗаполнитьИменаРеквизитовПоОснованию(ДокументОснование, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Склад");
	МассивВсехРеквизитов.Добавить("Товары.НоменклатураПоставщика");
	МассивВсехРеквизитов.Добавить("Товары.Номенклатура");
	МассивВсехРеквизитов.Добавить("Товары.Характеристика");
	МассивВсехРеквизитов.Добавить("Товары.Содержание");
	МассивВсехРеквизитов.Добавить("Товары.Упаковка");
	МассивВсехРеквизитов.Добавить("Товары.КоличествоУпаковок");
	МассивВсехРеквизитов.Добавить("Товары.Склад");
	МассивВсехРеквизитов.Добавить("Товары.ЗаказПоставщику");
	МассивВсехРеквизитов.Добавить("Товары.КодСтроки");
	МассивВсехРеквизитов.Добавить("Товары.НомерГТД");
	МассивВсехРеквизитов.Добавить("Расхождения.Номенклатура");
	МассивВсехРеквизитов.Добавить("Расхождения.Характеристика");
	МассивВсехРеквизитов.Добавить("Расхождения.Содержание");
	МассивВсехРеквизитов.Добавить("Расхождения.Упаковка");
	МассивВсехРеквизитов.Добавить("Расхождения.КоличествоУпаковок");
	МассивВсехРеквизитов.Добавить("Расхождения.Склад");
	МассивВсехРеквизитов.Добавить("Расхождения.ЗаказПоставщику");
	МассивВсехРеквизитов.Добавить("Расхождения.КодСтроки");
	МассивВсехРеквизитов.Добавить("Расхождения.НомерГТД");
	
	МассивРеквизитовОперации = Новый Массив;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		МассивРеквизитовОперации.Добавить("Склад");
		МассивРеквизитовОперации.Добавить("Товары.НоменклатураПоставщика");
		МассивРеквизитовОперации.Добавить("Товары.Номенклатура");
		МассивРеквизитовОперации.Добавить("Товары.Характеристика");
		МассивРеквизитовОперации.Добавить("Товары.Упаковка");
		МассивРеквизитовОперации.Добавить("Товары.КоличествоУпаковок");
		МассивРеквизитовОперации.Добавить("Товары.Склад");
		МассивРеквизитовОперации.Добавить("Товары.ЗаказПоставщику");
		МассивРеквизитовОперации.Добавить("Товары.КодСтроки");
		МассивРеквизитовОперации.Добавить("Товары.НомерГТД");
		МассивРеквизитовОперации.Добавить("Расхождения.Номенклатура");
		МассивРеквизитовОперации.Добавить("Расхождения.Характеристика");
		МассивРеквизитовОперации.Добавить("Расхождения.Упаковка");
		МассивРеквизитовОперации.Добавить("Расхождения.КоличествоУпаковок");
		МассивРеквизитовОперации.Добавить("Расхождения.Склад");
		МассивРеквизитовОперации.Добавить("Расхождения.ЗаказПоставщику");
		МассивРеквизитовОперации.Добавить("Расхождения.КодСтроки");
		МассивРеквизитовОперации.Добавить("Расхождения.НомерГТД");
				
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеУслугПрочихАктивов") Тогда
		МассивРеквизитовОперации.Добавить("Товары.Содержание");
		МассивРеквизитовОперации.Добавить("Расхождения.Содержание");
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает массив имен ролей с правом "Добавление" для данного документа
//
// Возвращаемое значение:
//	Массив - Массив с именами ролей
//
Функция ИменаРолейСПравомДобавления() Экспорт
	
	МассивРолей = Новый Массив;
	МассивРолей.Добавить("ПолныеПрава");
	МассивРолей.Добавить("ДобавлениеИзменениеКорректировокПоступлений");
	Возврат МассивРолей;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                      				 КАК Ссылка,
	|	ДанныеДокумента.Дата                                        				 КАК Период,
	|	ДанныеДокумента.ДокументОснование                           				 КАК ДокументОснование,
	|	ДанныеДокумента.ДокументОснование.Дата                      				 КАК ПериодПоступления,
	|	ДанныеДокумента.ДокументОснование.ЗакупкаПодДеятельность                     КАК НалогообложениеНДС,
	|	ДанныеДокумента.Организация                                 				 КАК Организация,
	|	ДанныеДокумента.Партнер                                     				 КАК Партнер,
	|	ДанныеДокумента.Контрагент                                  				 КАК Контрагент,
	|	ДанныеДокумента.Подразделение                               				 КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходов                              				 КАК СтатьяРасходов,
	|	ДанныеДокумента.СтатьяРасходов.ВариантРаспределенияРасходов 				 КАК ВариантРаспределенияРасходов,
	|	ДанныеДокумента.АналитикаРасходов                           				 КАК АналитикаРасходов,
	|	ДанныеДокумента.СтатьяДоходов                               				 КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов                            				 КАК АналитикаДоходов,	
	|	ДанныеДокумента.ВалютаВзаиморасчетов                        				 КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.Валюта                                      				 КАК Валюта,
	|	ДанныеДокумента.ПоступлениеПоЗаказам                        				 КАК ПоступлениеПоЗаказам,
	|	ДанныеДокумента.ДатаПлатежа                                 				 КАК ДатаПлатежа,
	|	ДанныеДокумента.ФормаОплаты                                 				 КАК ФормаОплаты,
	|	ДанныеДокумента.Договор                                     				 КАК Договор,
	|	ДанныеДокумента.Соглашение                                  				 КАК Соглашение,
	|	ДанныеДокумента.Соглашение.ВариантПриемкиТоваров            				 КАК ВариантПриемкиТоваров,
	|	ДанныеДокумента.ХозяйственнаяОперация                       				 КАК ХозяйственнаяОперация,
	|	
	|	ВЫБОР КОГДА ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК РасчетыПоДоговорам,
	|	
	|	ВЫБОР КОГДА ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК РасчетыПоНакладным
	|
	|ИЗ
	|	Документ.КорректировкаПоступления КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ИнициализироватьКлючиАналитикиНоменклатуры(Реквизиты);
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("ДокументОснование", Реквизиты.ДокументОснование);
	Запрос.УстановитьПараметр("НалогообложениеНДС", Реквизиты.НалогообложениеНДС);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Подразделение", Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("СтатьяРасходов", Реквизиты.СтатьяРасходов);
	Запрос.УстановитьПараметр("АналитикаРасходов", Реквизиты.АналитикаРасходов);
	Запрос.УстановитьПараметр("ВариантРаспределенияРасходов", Реквизиты.ВариантРаспределенияРасходов);
	Запрос.УстановитьПараметр("СтатьяДоходов", Реквизиты.СтатьяДоходов);
	Запрос.УстановитьПараметр("АналитикаДоходов", Реквизиты.АналитикаДоходов);
	Запрос.УстановитьПараметр("ПоступлениеПоЗаказам", Реквизиты.ПоступлениеПоЗаказам);
	Запрос.УстановитьПараметр("ДатаПлатежа", Реквизиты.ДатаПлатежа);
	Запрос.УстановитьПараметр("ФормаОплаты", Реквизиты.ФормаОплаты);
	Запрос.УстановитьПараметр("Договор", Реквизиты.Договор);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("РасчетыПоДоговорам", Реквизиты.РасчетыПоДоговорам);
	Запрос.УстановитьПараметр("РасчетыПоНакладным", Реквизиты.РасчетыПоНакладным);
	Запрос.УстановитьПараметр("ВалютаВзаиморасчетов", Реквизиты.ВалютаВзаиморасчетов);
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(Реквизиты));
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов", ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоВидамЗапасов"));
	Запрос.УстановитьПараметр("ИспользоватьУчетПрочихДоходовРасходов", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов")); 
	Запрос.УстановитьПараметр("ПериодПоступления", Реквизиты.ПериодПоступления);
	Запрос.УстановитьПараметр("Валюта", Реквизиты.Валюта);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Реквизиты.Валюта, Реквизиты.ВалютаВзаиморасчетов, Реквизиты.ПериодПоступления);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр", Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуВзаиморасчетов", Коэффициенты.КоэффициентПересчетаВВалютуВзаиморасчетов);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл",           Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	
	Если Справочники.СоглашенияСПоставщиками.СоглашениеИспользуетсяПриПриемке(Реквизиты.ВариантПриемкиТоваров) Тогда
		Запрос.УстановитьПараметр("СоглашениеЯвляетсяРаспоряжением", Истина);
		Запрос.УстановитьПараметр("Распоряжение",					 Справочники.СоглашенияСПоставщиками.ПолучитьСоздатьДокументРегистрации(Реквизиты.Соглашение));
	Иначе
		Запрос.УстановитьПараметр("СоглашениеЯвляетсяРаспоряжением", Ложь);
		Запрос.УстановитьПараметр("Распоряжение",                    Неопределено);
	КонецЕсли;
	
	Запрос.Текст = 
	"
	// 0 Временная таблица ВтТаблицаРасхожденияТоваров
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки			  КАК НомерСтроки,
	|	ТаблицаРасхождения.Номенклатура			  КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика		  КАК Характеристика,
	|	ТаблицаРасхождения.Склад				  КАК Склад,
	|	ТаблицаРасхождения.ЗаказПоставщику		  КАК ЗаказПоставщику,
	|	ТаблицаРасхождения.КодСтроки			  КАК КодСтроки,
	|	ТаблицаРасхождения.РаспоряжениеНаИнвентаризацию КАК РаспоряжениеНаИнвентаризацию,
	|	ТаблицаРасхождения.ВариантОтражения		  КАК ВариантОтражения,
	|	ТаблицаРасхождения.Количество			  КАК Количество
	|ПОМЕСТИТЬ ВтТаблицаРасхожденияТоваров
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И (ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)))
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 1 Временная таблица ВтТаблицаВидыЗапасовОприходование
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки           КАК НомерСтроки,
	|	ТаблицаРасхождения.Номенклатура          КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика        КАК Характеристика,
	|	ТаблицаРасхождения.ВидЗапасов            КАК ВидЗапасов,
	|	ТаблицаРасхождения.АналитикаУчетаПартий  КАК АналитикаУчетаПартий,
	|	ТаблицаРасхождения.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаРасхождения.НомерГТД              КАК НомерГТД,
	|	ТаблицаРасхождения.Количество            КАК Количество,
	|	ТаблицаРасхождения.СуммаСНДС			 КАК СуммаСНДС,
	|
	|	ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС
	|		* &КоэффициентПересчетаВВалютуУпр 
	|		КАК ЧИСЛО(15,2))                     КАК СуммаСНДСУпр,
	|
	|	ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС-ТаблицаРасхождения.СуммаНДС)
	|		* &КоэффициентПересчетаВВалютуУпр 
	|		КАК ЧИСЛО(15,2))                     КАК СуммаБезНДСУпр,
	|
  	|	ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС-ТаблицаРасхождения.СуммаНДС)
	|		* &КоэффициентПересчетаВВалютуРегл 
	|		КАК ЧИСЛО(15,2))                     КАК СуммаБезНДСРегл,
	|
	|	ТаблицаРасхождения.Склад				 КАК Склад,
	|	ТаблицаРасхождения.Сделка                КАК Сделка
	|ПОМЕСТИТЬ ВтТаблицаВидыЗапасовОприходование
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУчестьПриИнвентаризации)
	|	)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 2 Временная таблица ВтТаблицаВидыЗапасовСписание
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки           КАК НомерСтроки,
	|	ТаблицаРасхождения.Номенклатура          КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика        КАК Характеристика,
	|	ТаблицаРасхождения.ВидЗапасов            КАК ВидЗапасов,
	|	ТаблицаРасхождения.АналитикаУчетаПартий  КАК АналитикаУчетаПартий,
	|	ТаблицаРасхождения.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаРасхождения.НомерГТД              КАК НомерГТД,
	|	-ТаблицаРасхождения.Количество           КАК Количество,
	|	-ТаблицаРасхождения.СуммаСНДС			 КАК СуммаСНДС,
	|
	|	ВЫРАЗИТЬ(-ТаблицаРасхождения.СуммаСНДС
	|		* &КоэффициентПересчетаВВалютуУпр 
	|		КАК ЧИСЛО(15,2))                     КАК СуммаСНДСУпр,
	|
	|	ВЫРАЗИТЬ(-(ТаблицаРасхождения.СуммаСНДС-ТаблицаРасхождения.СуммаНДС)
	|		* &КоэффициентПересчетаВВалютуУпр 
	|		КАК ЧИСЛО(15,2))                     КАК СуммаБезНДСУпр,
	|
  	|	ВЫРАЗИТЬ(-(ТаблицаРасхождения.СуммаСНДС-ТаблицаРасхождения.СуммаНДС)
	|		* &КоэффициентПересчетаВВалютуРегл 
	|		КАК ЧИСЛО(15,2))                     КАК СуммаБезНДСРегл,
	|
	|	ТаблицаРасхождения.Склад				 КАК Склад,
	|	ТаблицаРасхождения.Сделка                КАК Сделка
	|ПОМЕСТИТЬ ВтТаблицаВидыЗапасовСписание
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации)
	|	)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 3 ТаблицаЗаказыПоставщикам
	|
	|ВЫБРАТЬ
	|	1                                      КАК Порядок,
	|	ТаблицаРасхождения.НомерСтроки         КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаРасхождения.ЗаказПоставщику     КАК ЗаказПоставщику,
	|	ТаблицаРасхождения.Номенклатура        КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика      КАК Характеристика,
	|	ТаблицаРасхождения.КодСтроки           КАК КодСтроки,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) ТОГДА
	|			ТаблицаРасхождения.Склад
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                  КАК Склад,
	|
	|	ТаблицаРасхождения.Количество          КАК Заказано,
	|	ТаблицаРасхождения.Количество          КАК КОформлению
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И &ПоступлениеПоЗаказам
	|	И ТаблицаРасхождения.КодСтроки <> 0
	|	И ТаблицаРасхождения.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                      КАК Порядок,
	|	ТаблицаРасхождения.НомерСтроки         КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаРасхождения.ЗаказПоставщику     КАК ЗаказПоставщику,
	|	ТаблицаРасхождения.Номенклатура        КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика      КАК Характеристика,
	|	ТаблицаРасхождения.КодСтроки           КАК КодСтроки,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) ТОГДА
	|			ТаблицаРасхождения.Склад
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                  КАК Склад,
	|
	|	0                                      КАК Заказано,
	|	ТаблицаРасхождения.Количество          КАК КОформлению
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И &ПоступлениеПоЗаказам
	|	И ТаблицаРасхождения.КодСтроки = 0
	|	И ТаблицаРасхождения.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3                                      КАК Порядок,
	|	ТаблицаРасхождения.НомерСтроки         КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаРасхождения.ЗаказПоставщику     КАК ЗаказПоставщику,
	|	ТаблицаРасхождения.Номенклатура        КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика      КАК Характеристика,
	|	ТаблицаРасхождения.КодСтроки           КАК КодСтроки,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) ТОГДА
	|			ТаблицаРасхождения.Склад
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                  КАК Склад,
	|
	|	0                                      КАК Заказано,
	|	ТаблицаРасхождения.Количество          КАК КОформлению
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И &ПоступлениеПоЗаказам
	|	И ТаблицаРасхождения.КодСтроки = 0
	|	И ТаблицаРасхождения.Количество <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 4 ТаблицаТоварыНаСкладах
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаРасхождения.Склад               КАК Склад,
	|	ТаблицаРасхождения.Номенклатура        КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика      КАК Характеристика,
	|	ТаблицаРасхождения.Количество          КАК ВНаличии
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаРасхождения.НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 5 ТаблицаСвободныеОстатки
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период								   КАК Период,
	|	ТаблицаРасхождения.Склад			   КАК Склад,
	|	ТаблицаРасхождения.Номенклатура		   КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика	   КАК Характеристика,
	|	ТаблицаРасхождения.Количество		   КАК ВНаличии
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки)
	|	ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаРасхождения.НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 6 ТаблицаДвижениеТоваров
	|
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки    КАК НомерСтроки,
	|	&Период                      	  КАК Период,
	|
	|	ВЫБОР КОГДА &СоглашениеЯвляетсяРаспоряжением ТОГДА
	|		&Распоряжение
	|	ИНАЧЕ
	|		ТаблицаРасхождения.ЗаказПоставщику
	|	КОНЕЦ                        	  КАК Распоряжение,
	|
	|	ТаблицаРасхождения.Склад          КАК Склад,
	|	ТаблицаРасхождения.Номенклатура   КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.Количество     КАК ПланируемоеПоступление,
	|	ИСТИНА							  КАК Корректировка
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	&ПоступлениеПоЗаказам И НЕ ТаблицаРасхождения.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки    КАК НомерСтроки,
	|	&Период                      	  КАК Период,
	|
	|	ВЫБОР КОГДА &СоглашениеЯвляетсяРаспоряжением ТОГДА
	|		&Распоряжение
	|	ИНАЧЕ
	|		ТаблицаРасхождения.ЗаказПоставщику
	|	КОНЕЦ                        	  КАК Распоряжение,
	|
	|	ТаблицаРасхождения.Склад          КАК Склад,
	|	ТаблицаРасхождения.Номенклатура   КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	-ТаблицаРасхождения.Количество    КАК ПланируемоеПоступление,
	|	ЛОЖЬ							  КАК Корректировка
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	&ПоступлениеПоЗаказам И НЕ ТаблицаРасхождения.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУчестьПриИнвентаризации)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки    КАК НомерСтроки,
	|	&Период                      	  КАК Период,
	|	&Распоряжение                	  КАК Распоряжение,
	|	ТаблицаРасхождения.Склад          КАК Склад,
	|	ТаблицаРасхождения.Номенклатура   КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.Количество     КАК ПланируемоеПоступление,
	|	ИСТИНА							  КАК Корректировка
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	НЕ &ПоступлениеПоЗаказам И &СоглашениеЯвляетсяРаспоряжением
	|	И ТаблицаРасхождения.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки    КАК НомерСтроки,
	|	&Период                      	  КАК Период,
	|	&Распоряжение                	  КАК Распоряжение,
	|	ТаблицаРасхождения.Склад          КАК Склад,
	|	ТаблицаРасхождения.Номенклатура   КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.Количество     КАК ПланируемоеПоступление,
	|	ЛОЖЬ							  КАК Корректировка
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	НЕ &ПоступлениеПоЗаказам И &СоглашениеЯвляетсяРаспоряжением
	|	И ТаблицаРасхождения.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУчестьПриИнвентаризации)
	|	)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	// 7 ТаблицаТоварыКОформлениюИзлишковНедостач
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)	  КАК ВидДвижения,
	|	&Период									  КАК Период,
	|	ТаблицаРасхождения.Склад				  КАК Склад,
	|	ТаблицаРасхождения.Номенклатура			  КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика		  КАК Характеристика,
	|	ТаблицаРасхождения.РаспоряжениеНаИнвентаризацию КАК ДокументОснование,
	|	ТаблицаРасхождения.Количество			  КАК КОформлениюАктов
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУчестьПриИнвентаризации)
	|	ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаРасхождения.НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////";
	Запрос.Текст = Запрос.Текст
		+ ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций()
		+ ТекстЗапросаТаблицаТоварыОрганизаций()
		+ ТекстЗапросаВтАналитика()
		+ ТекстЗапросаТаблицаСебестоимостьТоваров()
		+ ТекстЗапросаТаблицаРасчетыСПоставщиками()
		+ ТекстЗапросаТаблицаРасчетыСПоставщикамиПоследовательность()
		+ ТекстЗапросаТаблицаПрочиеДоходы()
		+ ТекстЗапросаТаблицаПрочиеРасходы()
		+ ТекстЗапросаТаблицаПартииТоваровОрганизацийПоследовательность()
		+ ТекстЗапросаТаблицаПартииТоваровОрганизаций()
		+ ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл()
		+ ТекстЗапросаТаблицаПартииПрочихРасходов()
		;
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	// Результат[0] - ВтТаблицаРасхожденияТоваров
	// Результат[1] - ВтТаблицаВидыЗапасовОприходование
	// Результат[2] - ВтТаблицаВидыЗапасовСписание
	ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПоставщикам",                          Результат[3].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыНаСкладах",                            Результат[4].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаСвободныеОстатки",                           Результат[5].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДвижениеТоваров",                            Результат[6].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыКОформлениюИзлишковНедостач",          Результат[7].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДатыПоступленияТоваровОрганизаций",          Результат[8].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыОрганизаций",                          Результат[9].Выгрузить());
	// Результат[10] - ВтАналитика
	ТаблицыДляДвижений.Вставить("ТаблицаСебестоимостьТоваров",                       Результат[11].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщиками",                       Результат[12].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщикамиПоследовательность",     Результат[13].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПрочиеДоходы",                               Результат[14].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПрочиеРасходы",                              Результат[15].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПартииТоваровОрганизацийПоследовательность", Результат[16].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПартииТоваровОрганизаций",                   Результат[17].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаСуммыДокументовВВалютеРегл",                 Результат[18].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПартииПрочихРасходов",						 Результат[19].Выгрузить());
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиНоменклатуры(Реквизиты)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаТовары.Склад          КАК Склад,
	|	ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаТовары.Склад          КАК Склад,
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика
	|	ИЗ
	|		Документ.КорректировкаПоступления.Товары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) 
	|	) КАК ТаблицаТовары
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТаблицаТовары.Номенклатура     = Аналитика.Номенклатура
	|		И ТаблицаТовары.Характеристика = Аналитика.Характеристика
	|		И ТаблицаТовары.Склад          = Аналитика.Склад
	|ГДЕ
	|	
	|	Аналитика.Номенклатура ЕСТЬ NULL
	|");
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка)
	КонецЦикла;

КонецПроцедуры

Функция ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период							  КАК ДатаПоступления,
	|	ТаблицаВидыЗапасов.Номенклатура   КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов     КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД       КАК НомерГТД
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|	ПО
	|		ТаблицаВидыЗапасов.ВидЗапасов		= ПоступленияТоваров.ВидЗапасов 
	|		И ТаблицаВидыЗапасов.Номенклатура	= ПоступленияТоваров.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = ПоступленияТоваров.Характеристика 
	|		И ТаблицаВидыЗапасов.НомерГТД		= ПоступленияТоваров.НомерГТД 
	|
	|ГДЕ
	|	ПоступленияТоваров.ДатаПоступления ЕСТЬ NULL
	|		ИЛИ ПоступленияТоваров.ДатаПоступления < &Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаВидыЗапасов.ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизаций()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ОрганизацияОтгрузки,
	|	ТаблицаВидыЗапасов.Склад КАК Склад,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	КлючиАналитики.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&НалогообложениеНДС КАК НалогообложениеНДС
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
	|		ПО ТаблицаВидыЗапасов.Номенклатура = КлючиАналитики.Номенклатура
	|			И ТаблицаВидыЗапасов.Характеристика = КлючиАналитики.Характеристика
	|			И ТаблицаВидыЗапасов.Склад = КлючиАналитики.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Период,
	|	&Организация,
	|	&Организация,
	|	ТаблицаВидыЗапасов.Склад,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД,
	|	ТаблицаВидыЗапасов.Количество,
	|	КлючиАналитики.КлючАналитики,
	|	&ХозяйственнаяОперация,
	|	&НалогообложениеНДС	
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
	|		ПО ТаблицаВидыЗапасов.Номенклатура = КлючиАналитики.Номенклатура
	|			И ТаблицаВидыЗапасов.Характеристика = КлючиАналитики.Характеристика
	|			И ТаблицаВидыЗапасов.Склад = КлючиАналитики.Склад
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикаНоменклатуры.КлючАналитики КАК АналитикаНоменклатуры,
	|	ТаблицаРасхождения.Номенклатура     КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика   КАК Характеристика,
	|	ТаблицаРасхождения.Склад            КАК Склад
	|
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|	ПО
	|		ТаблицаРасхождения.Номенклатура		= АналитикаНоменклатуры.Номенклатура
	|		И ТаблицаРасхождения.Характеристика = АналитикаНоменклатуры.Характеристика
	|		И ТаблицаРасхождения.Склад 			= АналитикаНоменклатуры.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСебестоимостьТоваров()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 									КАК ВидДвижения,
	|	&Период                                									КАК Период,
	|	ТаблицаВидыЗапасов.НомерСтроки         									КАК НомерСтрокиДокумента,
	|	Аналитика.АналитикаНоменклатуры											КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|   	ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ                                 									КАК ВидЗапасов,
	|
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр           								КАК Стоимость,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр         								КАК СтоимостьБезНДС,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл        								КАК СтоимостьРегл,
	|	ТаблицаВидыЗапасов.Количество             								КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)			КАК ХозяйственнаяОперация,
	|	&Организация                         									КАК Организация
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК Аналитика
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура     = Аналитика.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = Аналитика.Характеристика
	|		И ТаблицаВидыЗапасов.Склад          = Аналитика.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 									КАК ВидДвижения,
	|	&Период 																КАК Период,
	|	ТаблицаВидыЗапасов.НомерСтроки 											КАК НомерСтрокиДокумента,
	|	Аналитика.АналитикаНоменклатуры 										КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|   	ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ 																	КАК ВидЗапасов,
	|
	|	0 																		КАК Стоимость,
	|	0 																		КАК СтоимостьБезНДС,
	|	0 																		КАК СтоимостьРегл,	
	|	ТаблицаВидыЗапасов.Количество 											КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику) 	КАК ХозяйственнаяОперация,
	|	&Организация 															КАК Организация
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК Аналитика
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура     = Аналитика.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = Аналитика.Характеристика
	|		И ТаблицаВидыЗапасов.Склад          = Аналитика.Склад
	|ГДЕ
	|	ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 									КАК ВидДвижения,
	|	&Период 																КАК Период,
	|	ТаблицаРасхождения.НомерСтроки 											КАК НомерСтрокиДокумента,
	|	Аналитика.АналитикаНоменклатуры 										КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|   	ТаблицаРасхождения.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ 																	КАК ВидЗапасов,
	|
	|	ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС
	|	 * &КоэффициентПересчетаВВалютуУпр
	|		КАК ЧИСЛО(15,2)) 													КАК Стоимость,
	|	ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС-ТаблицаРасхождения.СуммаНДС)
	|	 * &КоэффициентПересчетаВВалютуУпр
	|		КАК ЧИСЛО(15,2)) 													КАК СтоимостьБезНДС,
	|
	|	ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС-ТаблицаРасхождения.СуммаНДС)
	|	 * &КоэффициентПересчетаВВалютуРегл
	|		КАК ЧИСЛО(15,2)) 													КАК СтоимостьРегл,
	|
	|	0 																		КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика) 		КАК ХозяйственнаяОперация,
	|	&Организация 															КАК Организация
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК Аналитика
	|	ПО
	|		ТаблицаРасхождения.Номенклатура     = Аналитика.Номенклатура
	|		И ТаблицаРасхождения.Характеристика = Аналитика.Характеристика
	|		И ТаблицаРасхождения.Склад          = Аналитика.Склад
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара)
	|	ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара))
	|	И ТаблицаРасхождения.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиДокумента
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПоставщиками()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период								   											 КАК Период,
	|	&Период								   											 КАК ДатаРегистратора,
	|	&ДатаПлатежа						   											 КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 											 КАК ВидДвижения,
	|	&АналитикаУчетаПоПартнерам 			   											 КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ТаблицаРасхождения.ЗаказПоставщику
	|	КОНЕЦ								   											 КАК ЗаказПоставщику,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		ТаблицаРасхождения.ЗаказПоставщику
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ								   											 КАК ЗакупкаПоЗаказу,
	|
	|	&ВалютаВзаиморасчетов			  	  											 КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) 		 КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО 																	 КАК ФормаОплаты,
	|	
	|	СУММА(ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС
	|	 * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|		КАК ЧИСЛО(15,2)))															 КАК Сумма,
	|
	|	СУММА(0)																		 КАК КОплате,
	|
	|	СУММА(ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС
	|	 * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|		КАК ЧИСЛО(15,2)))															 КАК КПоступлению
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И &ПоступлениеПоЗаказам
	|	И ТаблицаРасхождения.КодСтроки <> 0
	|	И Не &РасчетыПоНакладным
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождения.ЗаказПоставщику
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период 																КАК Период,
	|	&Период 																КАК ДатаРегистратора,
	|	&ДатаПлатежа 															КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 									КАК ВидДвижения,
	|	&АналитикаУчетаПоПартнерам 												КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ПоступлениеПоЗаказам И Не &РасчетыПоНакладным ТОГДА
	|			ТаблицаРасхождения.ЗаказПоставщику
	|		ИНАЧЕ
	|			&ДокументОснование
	|		КОНЕЦ
	|	КОНЕЦ 																	КАК ЗаказПоставщику,
	|
	|	Неопределено 															КАК ЗакупкаПоЗаказу,
	|	&ВалютаВзаиморасчетов 													КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО 															КАК ФормаОплаты,
	|	СУММА(0) 																КАК Сумма,
	|	СУММА(0) 																КАК КОплате,
	|
	|	СУММА(ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС
	|	 * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|		КАК ЧИСЛО(15,2)))													КАК КПоступлению
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И &ПоступлениеПоЗаказам
	|	И (ТаблицаРасхождения.КодСтроки = 0
	|		Или &РасчетыПоНакладным)
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации)
	|	)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождения.ЗаказПоставщику
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период 																КАК Период,
	|	&Период 																КАК ДатаРегистратора,
	|	&ДатаПлатежа 															КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 									КАК ВидДвижения,
	|	&АналитикаУчетаПоПартнерам 												КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ПоступлениеПоЗаказам И Не &РасчетыПоНакладным ТОГДА
	|			ТаблицаРасхождения.ЗаказПоставщику
	|		ИНАЧЕ
	|			&ДокументОснование
	|		КОНЕЦ
	|	КОНЕЦ 																	КАК ЗаказПоставщику,
	|
	|	Неопределено 															КАК ЗакупкаПоЗаказу,
	|	&ВалютаВзаиморасчетов 													КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО 															КАК ФормаОплаты,
	|	СУММА(0) 																КАК Сумма,
	|	СУММА(0) 																КАК КОплате,
	|
	|	СУММА(ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС
	|	 * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|		КАК ЧИСЛО(15,2))) 													КАК КПоступлению
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И &ПоступлениеПоЗаказам
	|	И (ТаблицаРасхождения.КодСтроки = 0
	|		Или &РасчетыПоНакладным)
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки)
	|		ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации)
	|	)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождения.ЗаказПоставщику
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период 																КАК Период,
	|	&Период 																КАК ДатаРегистратора,
	|	&ДатаПлатежа 															КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 									КАК ВидДвижения,
	|	&АналитикаУчетаПоПартнерам 												КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ПоступлениеПоЗаказам И Не &РасчетыПоНакладным ТОГДА
	|			ТаблицаРасхождения.ЗаказПоставщику
	|		ИНАЧЕ
	|			&ДокументОснование
	|		КОНЕЦ
	|	КОНЕЦ 																	КАК ЗаказПоставщику,
	|
	|	Неопределено 															КАК ЗакупкаПоЗаказу,
	|	&ВалютаВзаиморасчетов 													КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация,
	|	&ФормаОплаты 															КАК ФормаОплаты,
	|
	|	СУММА(ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС
	|	 * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|		КАК ЧИСЛО(15,2))) 													КАК Сумма,
	|
	|	СУММА(0) 																КАК КОплате,
	|	СУММА(0) 																КАК КПоступлению
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И (ТаблицаРасхождения.КодСтроки = 0
	|		Или &РасчетыПоНакладным)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождения.ЗаказПоставщику
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(&ДатаПлатежа, День) 										КАК Период,
	|	&Период 																КАК ДатаРегистратора,
	|	&ДатаПлатежа 															КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)									КАК ВидДвижения,
	|	&АналитикаУчетаПоПартнерам 												КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ПоступлениеПоЗаказам И Не &РасчетыПоНакладным ТОГДА
	|			ТаблицаРасхождения.ЗаказПоставщику
	|		ИНАЧЕ
	|			&ДокументОснование
	|		КОНЕЦ
	|	КОНЕЦ 																	КАК ЗаказПоставщику,
	|
	|	Неопределено 															КАК ЗакупкаПоЗаказу,
	|	&ВалютаВзаиморасчетов 													КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация,
	|	&ФормаОплаты 															КАК ФормаОплаты,
	|
	|	СУММА(0) 																КАК Сумма,
	|	
	|	СУММА(ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС
	|	 * &КоэффициентПересчетаВВалютуВзаиморасчетов
	|		КАК ЧИСЛО(15,2))) 													КАК КОплате,
	|
	|	СУММА(0) 																КАК КПоступлению
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И (ТаблицаРасхождения.КодСтроки = 0
	|		Или &РасчетыПоНакладным)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождения.ЗаказПоставщику
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПоставщикамиПоследовательность()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период					   КАК Период,
	|	&Ссылка 				   КАК Регистратор,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеДоходы()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                               			     					КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)			     					КАК ВидДвижения,
	|	&Организация                          			     					КАК Организация,
	|	&Подразделение                        			     					КАК Подразделение,
	|	&СтатьяДоходов          			     								КАК СтатьяДоходов,
	|	&АналитикаДоходов       			     								КАК АналитикаДоходов,
	|
	|	ВЫРАЗИТЬ(-ТаблицаРасхождения.СуммаСНДС
	|	* &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2))						КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах)
	|	И &ИспользоватьУчетПрочихДоходовРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаРасхождения.НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|   
	|	ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2)) КАК Сумма,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.СтатьяРасходов.ВариантРаспределенияРасходов =
	|       ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|	ТОГДА
	|		ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.СтатьяРасходов.ВариантРаспределенияРасходов =
	|       ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|	ТОГДА
	|		ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация,
	|	Аналитика.АналитикаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК Аналитика
	|	ПО
	|		ТаблицаРасхождения.Номенклатура     = Аналитика.Номенклатура
	|		И ТаблицаРасхождения.Характеристика = Аналитика.Характеристика
	|		И ТаблицаРасхождения.Склад          = Аналитика.Склад
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы)
	|	И &ВариантРаспределенияРасходов <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ИспользоватьУчетПрочихДоходовРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииТоваровОрганизацийПоследовательность()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции 

Функция ТекстЗапросаТаблицаПартииТоваровОрганизаций()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	КлючиАналитики.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	&ДокументОснование КАК ДокументПоступления,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр КАК Стоимость,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СтоимостьРегл,
	|	ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК НДСРегл,
	|	&НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаВидыЗапасов.Номенклатура 	КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика 	КАК Характеристика,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
	|		ПО ТаблицаВидыЗапасов.Номенклатура = КлючиАналитики.Номенклатура
	|			И ТаблицаВидыЗапасов.Характеристика = КлючиАналитики.Характеристика
	|			И ТаблицаВидыЗапасов.Склад = КлючиАналитики.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Период,
	|	&Организация,
	|	КлючиАналитики.КлючАналитики,
	|	ТаблицаВидыЗапасов.ВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий,
	|	&ДокументОснование,
	|	ТаблицаВидыЗапасов.Количество,
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр,
	|	ТаблицаВидыЗапасов.СуммаСНДС,
	|	ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаБезНДСРегл,
	|	&НалогообложениеНДС,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика,
	|	&ХозяйственнаяОперация
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
	|		ПО ТаблицаВидыЗапасов.Номенклатура = КлючиАналитики.Номенклатура
	|			И ТаблицаВидыЗапасов.Характеристика = КлючиАналитики.Характеристика
	|			И ТаблицаВидыЗапасов.Склад = КлючиАналитики.Склад
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции 

Функция ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&ПериодПоступления КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК СуммаБезНДСРегл,
	|	ТаблицаДокумента.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК СуммаНДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ТипРасчетов
	|
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &Валюта <> &ВалютаРегламентированногоУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                               			     					КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)			     					КАК ВидДвижения,
	|	&Организация                          			     					КАК Организация,
	|	&Подразделение                        			     					КАК Подразделение,
	|	&СтатьяРасходов          			     								КАК СтатьяРасходов,
	|	&АналитикаРасходов       			     								КАК АналитикаРасходов,
	|   
	|	ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаСНДС
	|	* &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2))						КАК Стоимость,
	|
	|	ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2)) КАК СтоимостьБезНДС,
	|
	|	ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2)) КАК СтоимостьРегл,
	|	ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2)) КАК НДСРегл,
	|
	|	ТаблицаРасхождения.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	&Ссылка КАК ДокументПоступленияРасходов
	|ИЗ
	|	Документ.КорректировкаПоступления.Расхождения КАК ТаблицаРасхождения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтАналитика КАК Аналитика
	|	ПО
	|		ТаблицаРасхождения.Номенклатура     = Аналитика.Номенклатура
	|		И ТаблицаРасхождения.Характеристика = Аналитика.Характеристика
	|		И ТаблицаРасхождения.Склад          = Аналитика.Склад
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы)
	|	И &ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ИспользоватьУчетПрочихДоходовРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Прочее

Процедура ЗаполнитьТоварыПоИсходнымДанным(ДокументОснование, Товары) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, ДокументОснование);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	*
	|ИЗ ИсходныеДанные КАК ИсходныеДанные
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИсходныеДанные.НомерСтроки
	|";
	
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьРасхождения(Объект) Экспорт
	
	// В дальнейшем потребуется дата документа-основания. Реквизит должен быть заполнен.
	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ВызватьИсключение НСтр("ru='Поле ""Документ основание"" не заполнено'");
	КонецЕсли;
	
	Объект.Расхождения.Очистить();
	
	КлючевыеПоля = "
	|	Номенклатура,
	|	Характеристика,
	|	Содержание,
	|	Упаковка,
	|	ЗаказПоставщику,
	|	КодСтроки,
	|	Склад,
	|	Сделка,
	|	СтатьяРасходов,
	|	АналитикаРасходов,
	|	НомерГТД,
	|	СтавкаНДС";
	
	ПериодКорректировки = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата());
	ПериодПоступления   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "Дата");
	КорректировкаПрошлогоПериода = ПериодПоступления < НачалоМесяца(ПериодКорректировки);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, Объект.ДокументОснование);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НовыеДанные", Объект.Товары.Выгрузить());
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НовыеДанные.Номенклатура КАК Номенклатура,
	|	НовыеДанные.Характеристика КАК Характеристика,
	|	НовыеДанные.Содержание КАК Содержание,
	|	НовыеДанные.Упаковка КАК Упаковка,
	|	НовыеДанные.ЗаказПоставщику КАК ЗаказПоставщику,
	|	НовыеДанные.КодСтроки КАК КодСтроки,
	|	НовыеДанные.Склад КАК Склад,
	|	НовыеДанные.Сделка КАК Сделка,
	|	НовыеДанные.СтатьяРасходов КАК СтатьяРасходов,
	|	НовыеДанные.АналитикаРасходов КАК АналитикаРасходов,
	|	НовыеДанные.НомерГТД КАК НомерГТД,
	|	НовыеДанные.СтавкаНДС КАК СтавкаНДС,
	|	НовыеДанные.КоличествоУпаковок КАК КоличествоУпаковок,
	|
	|	ВЫБОР
	|		КОГДА НовыеДанные.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ НовыеДанные.Количество
	|	КОНЕЦ КАК Количество,
	|
	|	НовыеДанные.Сумма КАК Сумма,
	|	НовыеДанные.СуммаНДС КАК СуммаНДС,
	|	НовыеДанные.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ НовыеДанныеИзТаблицы
	|ИЗ
	|	&НовыеДанные КАК НовыеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанныеИзТаблицы.Номенклатура,
	|	НовыеДанныеИзТаблицы.Характеристика,
	|	НовыеДанныеИзТаблицы.Содержание,
	|	НовыеДанныеИзТаблицы.Упаковка,
	|	НовыеДанныеИзТаблицы.ЗаказПоставщику,
	|	НовыеДанныеИзТаблицы.КодСтроки,
	|	НовыеДанныеИзТаблицы.Склад,
	|	НовыеДанныеИзТаблицы.Сделка,
	|	НовыеДанныеИзТаблицы.СтатьяРасходов,
	|	НовыеДанныеИзТаблицы.АналитикаРасходов,
	|	НовыеДанныеИзТаблицы.НомерГТД,
	|	НовыеДанныеИзТаблицы.СтавкаНДС,
	|	СУММА(НовыеДанныеИзТаблицы.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(НовыеДанныеИзТаблицы.Количество) КАК Количество,
	|	СУММА(НовыеДанныеИзТаблицы.Сумма) КАК Сумма,
	|	СУММА(НовыеДанныеИзТаблицы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(НовыеДанныеИзТаблицы.СуммаСНДС) КАК СуммаСНДС
	|ПОМЕСТИТЬ НовыеДанные
	|ИЗ
	|	НовыеДанныеИзТаблицы КАК НовыеДанныеИзТаблицы
	|
	|СГРУППИРОВАТЬ ПО
	|	НовыеДанныеИзТаблицы.Номенклатура,
	|	НовыеДанныеИзТаблицы.Характеристика,
	|	НовыеДанныеИзТаблицы.Содержание,
	|	НовыеДанныеИзТаблицы.Упаковка,
	|	НовыеДанныеИзТаблицы.ЗаказПоставщику,
	|	НовыеДанныеИзТаблицы.КодСтроки,
	|	НовыеДанныеИзТаблицы.Склад,
	|	НовыеДанныеИзТаблицы.Сделка,
	|	НовыеДанныеИзТаблицы.СтатьяРасходов,
	|	НовыеДанныеИзТаблицы.АналитикаРасходов,
	|	НовыеДанныеИзТаблицы.НомерГТД,
	|	НовыеДанныеИзТаблицы.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НовыеДанныеИзТаблицы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ИсходныеДанные.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(ИсходныеДанные.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	
	|	ВЫБОР КОГДА ИсходныеДанные.Содержание <> Неопределено ТОГДА
	|		ИсходныеДанные.Содержание
	|	ИНАЧЕ
	|		""""
	|	КОНЕЦ КАК Содержание,
	|	
	|	ВЫРАЗИТЬ(ИсходныеДанные.Упаковка КАК Справочник.УпаковкиНоменклатуры) КАК Упаковка,
	|	ВЫРАЗИТЬ(ИсходныеДанные.ЗаказПоставщику КАК Документ.ЗаказПоставщику) КАК ЗаказПоставщику,
	|	
	|	ВЫБОР КОГДА ИсходныеДанные.КодСтроки <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.КодСтроки КАК Число(10))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК КодСтроки,
	|	
	|	ВЫРАЗИТЬ(ИсходныеДанные.Склад КАК Справочник.Склады) КАК Склад,
	|	ВЫРАЗИТЬ(ИсходныеДанные.Сделка КАК Справочник.СделкиСКлиентами) КАК Сделка,
	|	ВЫРАЗИТЬ(ИсходныеДанные.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов) КАК СтатьяРасходов,
	|	ИсходныеДанные.АналитикаРасходов КАК АналитикаРасходов,
	|	ВЫРАЗИТЬ(ИсходныеДанные.НомерГТД КАК Справочник.НомераГТД) КАК НомерГТД,
	|	ВЫРАЗИТЬ(ИсходныеДанные.СтавкаНДС КАК Перечисление.СтавкиНДС) КАК СтавкаНДС,
	|	
	|	СУММА(ВЫБОР КОГДА ИсходныеДанные.КоличествоУпаковок <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.КоличествоУпаковок КАК Число(15, 3))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК КоличествоУпаковок,
	|	
	|	СУММА(ВЫБОР КОГДА ИсходныеДанные.Количество <> Неопределено ТОГДА
	|		ВЫБОР КОГДА ИсходныеДанные.Количество = 0 ТОГДА
	|			1
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(ИсходныеДанные.Количество КАК Число(15, 3))
	|		КОНЕЦ
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК Количество,
	|	
	|	СУММА(ВЫБОР КОГДА ИсходныеДанные.Сумма <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.Сумма КАК Число(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК Сумма,
	|	
	|	СУММА(ВЫБОР КОГДА ИсходныеДанные.СуммаНДС <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.СуммаНДС КАК Число(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК СуммаНДС,
	|	
	|	СУММА(ВЫБОР КОГДА ИсходныеДанные.СуммаСНДС <> Неопределено ТОГДА
	|		ВЫРАЗИТЬ(ИсходныеДанные.СуммаСНДС КАК Число(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК СуммаСНДС
	|
	|ПОМЕСТИТЬ ИсходныеДанныеДляРасхождений
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсходныеДанные.Характеристика,
	|	ИсходныеДанные.Содержание,
	|	ИсходныеДанные.Упаковка,
	|	ИсходныеДанные.ЗаказПоставщику,
	|	ИсходныеДанные.КодСтроки,
	|	ИсходныеДанные.Склад,
	|	ИсходныеДанные.Номенклатура,
	|	ИсходныеДанные.Сделка,
	|	ИсходныеДанные.СтатьяРасходов,
	|	ИсходныеДанные.АналитикаРасходов,
	|	ИсходныеДанные.НомерГТД,
	|	ИсходныеДанные.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИсходныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанные.Номенклатура КАК Номенклатура,
	|	НовыеДанные.Характеристика КАК Характеристика,
	|	НовыеДанные.Содержание КАК Содержание,
	|	НовыеДанные.Упаковка КАК Упаковка,
	|	НовыеДанные.ЗаказПоставщику КАК ЗаказПоставщику,
	|	НовыеДанные.КодСтроки КАК КодСтроки,
	|	НовыеДанные.Склад КАК Склад,
	|	НовыеДанные.Сделка КАК Сделка,
	|	НовыеДанные.СтатьяРасходов КАК СтатьяРасходов,
	|	НовыеДанные.АналитикаРасходов КАК АналитикаРасходов,
	|	НовыеДанные.НомерГТД КАК НомерГТД,
	|	НовыеДанные.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА НовыеДанные.Количество = ИсходныеДанные.Количество
	|			ТОГДА НовыеДанные.Сумма
	|		ИНАЧЕ НовыеДанные.Сумма / НовыеДанные.Количество * ИсходныеДанные.Количество
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ДанныеРасхождений
	|ИЗ
	|	НовыеДанные КАК НовыеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИсходныеДанныеДляРасхождений КАК ИсходныеДанные
	|		ПО НовыеДанные.Номенклатура = ИсходныеДанные.Номенклатура
	|			И НовыеДанные.Характеристика = ИсходныеДанные.Характеристика
	|			И НовыеДанные.Содержание = ИсходныеДанные.Содержание
	|			И НовыеДанные.Упаковка = ИсходныеДанные.Упаковка
	|			И НовыеДанные.ЗаказПоставщику = ИсходныеДанные.ЗаказПоставщику
	|			И НовыеДанные.КодСтроки = ИсходныеДанные.КодСтроки
	|			И НовыеДанные.Склад = ИсходныеДанные.Склад
	|			И НовыеДанные.Сделка = ИсходныеДанные.Сделка
	|			И НовыеДанные.СтатьяРасходов = ИсходныеДанные.СтатьяРасходов
	|			И НовыеДанные.АналитикаРасходов = ИсходныеДанные.АналитикаРасходов
	|			И НовыеДанные.НомерГТД = ИсходныеДанные.НомерГТД
	|			И НовыеДанные.СтавкаНДС = ИсходныеДанные.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсходныеДанные.Номенклатура,
	|	ИсходныеДанные.Характеристика,
	|	ИсходныеДанные.Содержание,
	|	ИсходныеДанные.Упаковка,
	|	ИсходныеДанные.ЗаказПоставщику,
	|	ИсходныеДанные.КодСтроки,
	|	ИсходныеДанные.Склад,
	|	ИсходныеДанные.Сделка,
	|	ИсходныеДанные.СтатьяРасходов,
	|	ИсходныеДанные.АналитикаРасходов,
	|	ИсходныеДанные.НомерГТД,
	|	ИсходныеДанные.СтавкаНДС,
	|	-ИсходныеДанные.Сумма
	|ИЗ
	|	ИсходныеДанныеДляРасхождений КАК ИсходныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НовыеДанные КАК НовыеДанные
	|		ПО ИсходныеДанные.Номенклатура = НовыеДанные.Номенклатура
	|			И ИсходныеДанные.Характеристика = НовыеДанные.Характеристика
	|			И ИсходныеДанные.Содержание = НовыеДанные.Содержание
	|			И ИсходныеДанные.Упаковка = НовыеДанные.Упаковка
	|			И ИсходныеДанные.ЗаказПоставщику = НовыеДанные.ЗаказПоставщику
	|			И ИсходныеДанные.КодСтроки = НовыеДанные.КодСтроки
	|			И ИсходныеДанные.Склад = НовыеДанные.Склад
	|			И ИсходныеДанные.Сделка = НовыеДанные.Сделка
	|			И ИсходныеДанные.СтатьяРасходов = НовыеДанные.СтатьяРасходов
	|			И ИсходныеДанные.АналитикаРасходов = НовыеДанные.АналитикаРасходов
	|			И ИсходныеДанные.НомерГТД = НовыеДанные.НомерГТД
	|			И ИсходныеДанные.СтавкаНДС = НовыеДанные.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказПоставщику,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.Сделка,
	|	ДанныеРасхождений.СтатьяРасходов,
	|	ДанныеРасхождений.АналитикаРасходов,
	|	ДанныеРасхождений.НомерГТД,
	|	ДанныеРасхождений.СтавкаНДС,
	|	СУММА(ДанныеРасхождений.Сумма),
	|	ВЫРАЗИТЬ(ДанныеРасхождений.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ДанныеРасхождений КАК ДанныеРасхождений
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказПоставщику,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Сделка,
	|	ДанныеРасхождений.СтатьяРасходов,
	|	ДанныеРасхождений.АналитикаРасходов,
	|	ДанныеРасхождений.НомерГТД,
	|	ДанныеРасхождений.СтавкаНДС
	|
	|ИМЕЮЩИЕ
	|	ВЫРАЗИТЬ(СУММА(ДанныеРасхождений.Сумма) КАК ЧИСЛО(15,2)) <> 0
	|";
	ТаблицаРасхожденийВСуммах = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ДанныеРасхождения Из ТаблицаРасхожденийВСуммах Цикл
		
		НовоеРасхождение = Объект.Расхождения.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеРасхождение, ДанныеРасхождения);
		
		ТекПроцентНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(НовоеРасхождение.СтавкаНДС);
		НовоеРасхождение.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(НовоеРасхождение.Сумма, ТекПроцентНДС, Объект.ЦенаВключаетНДС);
		
		НовоеРасхождение.СуммаСНДС = НовоеРасхождение.Сумма + ?(Объект.ЦенаВключаетНДС, 0, НовоеРасхождение.СуммаНДС);
		НовоеРасхождение.ВариантОтражения = ВариантОтраженияПоСтроке(НовоеРасхождение, ДанныеРасхождения.ТипНоменклатуры, КорректировкаПрошлогоПериода);
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("УчтенныеРасхождения", Объект.Расхождения.Выгрузить());
	Запрос.Текст = "
	|УНИЧТОЖИТЬ ДанныеРасхождений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчтенныеРасхождения.Номенклатура КАК Номенклатура,
	|	УчтенныеРасхождения.Характеристика КАК Характеристика,
	|	УчтенныеРасхождения.Содержание КАК Содержание,
	|	УчтенныеРасхождения.Упаковка КАК Упаковка,
	|	УчтенныеРасхождения.ЗаказПоставщику КАК ЗаказПоставщику,
	|	УчтенныеРасхождения.КодСтроки КАК КодСтроки,
	|	УчтенныеРасхождения.Склад КАК Склад,
	|	УчтенныеРасхождения.Сделка КАК Сделка,
	|	УчтенныеРасхождения.СтатьяРасходов КАК СтатьяРасходов,
	|	УчтенныеРасхождения.АналитикаРасходов КАК АналитикаРасходов,
	|	УчтенныеРасхождения.НомерГТД КАК НомерГТД,
	|	УчтенныеРасхождения.СтавкаНДС КАК СтавкаНДС,
	|	УчтенныеРасхождения.СуммаНДС КАК СуммаНДС,
	|	УчтенныеРасхождения.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ УчтенныеРасхождения
	|ИЗ
	|	&УчтенныеРасхождения КАК УчтенныеРасхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанные.Номенклатура КАК Номенклатура,
	|	НовыеДанные.Характеристика КАК Характеристика,
	|	НовыеДанные.Содержание КАК Содержание,
	|	НовыеДанные.Упаковка КАК Упаковка,
	|	НовыеДанные.ЗаказПоставщику КАК ЗаказПоставщику,
	|	НовыеДанные.КодСтроки КАК КодСтроки,
	|	НовыеДанные.Склад КАК Склад,
	|	НовыеДанные.Сделка КАК Сделка,
	|	НовыеДанные.СтатьяРасходов КАК СтатьяРасходов,
	|	НовыеДанные.АналитикаРасходов КАК АналитикаРасходов,
	|	НовыеДанные.НомерГТД КАК НомерГТД,
	|	НовыеДанные.СтавкаНДС КАК СтавкаНДС,
	|	НовыеДанные.КоличествоУпаковок КАК КоличествоУпаковок,
	|	НовыеДанные.Количество КАК Количество,
	|	НовыеДанные.СуммаНДС КАК СуммаНДС,
	|	НовыеДанные.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ ДанныеРасхождений
	|ИЗ
	|	НовыеДанные КАК НовыеДанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсходныеДанные.Номенклатура,
	|	ИсходныеДанные.Характеристика,
	|	ИсходныеДанные.Содержание,
	|	ИсходныеДанные.Упаковка,
	|	ИсходныеДанные.ЗаказПоставщику,
	|	ИсходныеДанные.КодСтроки,
	|	ИсходныеДанные.Склад,
	|	ИсходныеДанные.Сделка,
	|	ИсходныеДанные.СтатьяРасходов,
	|	ИсходныеДанные.АналитикаРасходов,
	|	ИсходныеДанные.НомерГТД,
	|	ИсходныеДанные.СтавкаНДС,
	|	-ИсходныеДанные.КоличествоУпаковок,
	|	-ИсходныеДанные.Количество,
	|	-ИсходныеДанные.СуммаНДС,
	|	-ИсходныеДанные.СуммаСНДС
	|ИЗ
	|	ИсходныеДанныеДляРасхождений КАК ИсходныеДанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УчтенныеРасхождения.Номенклатура,
	|	УчтенныеРасхождения.Характеристика,
	|	УчтенныеРасхождения.Содержание,
	|	УчтенныеРасхождения.Упаковка,
	|	УчтенныеРасхождения.ЗаказПоставщику,
	|	УчтенныеРасхождения.КодСтроки,
	|	УчтенныеРасхождения.Склад,
	|	УчтенныеРасхождения.Сделка,
	|	УчтенныеРасхождения.СтатьяРасходов,
	|	УчтенныеРасхождения.АналитикаРасходов,
	|	УчтенныеРасхождения.НомерГТД,
	|	УчтенныеРасхождения.СтавкаНДС,
	|	0,
	|	0,
	|	-УчтенныеРасхождения.СуммаНДС,
	|	-УчтенныеРасхождения.СуммаСНДС
	|ИЗ
	|	УчтенныеРасхождения КАК УчтенныеРасхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказПоставщику,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.Сделка,
	|	ДанныеРасхождений.СтатьяРасходов,
	|	ДанныеРасхождений.АналитикаРасходов,
	|	ДанныеРасхождений.НомерГТД,
	|	ДанныеРасхождений.СтавкаНДС,
	|	СУММА(ДанныеРасхождений.КоличествоУпаковок),
	|	СУММА(ДанныеРасхождений.Количество),
	|	СУММА(ДанныеРасхождений.СуммаНДС),
	|	СУММА(ДанныеРасхождений.СуммаСНДС),
	|	ВЫРАЗИТЬ(ДанныеРасхождений.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ДанныеРасхождений КАК ДанныеРасхождений
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказПоставщику,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Сделка,
	|	ДанныеРасхождений.СтатьяРасходов,
	|	ДанныеРасхождений.АналитикаРасходов,
	|	ДанныеРасхождений.НомерГТД,
	|	ДанныеРасхождений.СтавкаНДС
	|
	|ИМЕЮЩИЕ
	|	ВЫРАЗИТЬ(СУММА(ДанныеРасхождений.КоличествоУпаковок) КАК ЧИСЛО(15,3)) <> 0
	|	ИЛИ ВЫРАЗИТЬ(СУММА(ДанныеРасхождений.Количество) КАК ЧИСЛО(15,3)) <> 0
	|";
	ДанныеРасхождения = Запрос.Выполнить().Выбрать();
	Пока ДанныеРасхождения.Следующий() Цикл
		
		НовоеРасхождение = Объект.Расхождения.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеРасхождение, ДанныеРасхождения);
		
		ПараметрыПоиска = Новый Структура(КлючевыеПоля);
		ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ДанныеРасхождения);
		
		НовоеРасхождение.Сумма = НовоеРасхождение.СуммаСНДС - ?(Объект.ЦенаВключаетНДС, 0, НовоеРасхождение.СуммаНДС);
		НовоеРасхождение.ВариантОтражения = ВариантОтраженияПоСтроке(НовоеРасхождение, ДанныеРасхождения.ТипНоменклатуры, КорректировкаПрошлогоПериода);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВариантОтраженияПоСтроке(СтрокаРасхождений, ТипНоменклатуры, КорректировкаПрошлогоПериода)
	
	ВариантОтражения = Неопределено;
	
	Если (ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Товар
		И ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.МногооборотнаяТара)
		Или СтрокаРасхождений.КоличествоУпаковок = 0 Тогда
		
		ОтразитьНаПрочихДоходахРасходах = (КорректировкаПрошлогоПериода И
			(ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар
			Или ТипНоменклатуры = Перечисления.ТипыНоменклатуры.МногооборотнаяТара));
		
		Если (СтрокаРасхождений.СуммаСНДС < 0 ИЛИ СтрокаРасхождений.СуммаСНДС - СтрокаРасхождений.СуммаНДС < 0)
			И ОтразитьНаПрочихДоходахРасходах Тогда
			ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах;
		ИначеЕсли (СтрокаРасхождений.СуммаСНДС < 0 ИЛИ СтрокаРасхождений.СуммаСНДС - СтрокаРасхождений.СуммаНДС < 0) Тогда
			ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара;
		ИначеЕсли ОтразитьНаПрочихДоходахРасходах Тогда
			ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы;
		Иначе
			ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВариантОтражения;
	
КонецФункции

Процедура СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, ДокументОснование)
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымПоступлениеТоваровУслуг();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеУслугПрочихАктивов") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымПоступлениеУслугПрочихАктивов();
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	КорректировкаПоступления.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДанныеПоследнейКорректировки
	|ИЗ
	|	Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|ГДЕ
	|	КорректировкаПоступления.Проведен
	|	И КорректировкаПоступления.ДокументОснование = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаПоступления.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки		 	 КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураПоставщика КАК НоменклатураПоставщика,
	|	ТаблицаТовары.Номенклатура		 	 КАК Номенклатура,
	|	ТаблицаТовары.Характеристика	 	 КАК Характеристика,
	|	ТаблицаТовары.Содержание		 	 КАК Содержание,
	|	ТаблицаТовары.Упаковка			 	 КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок 	 КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество		 	 КАК Количество,
	|	ТаблицаТовары.Цена				 	 КАК Цена,
	|	ТаблицаТовары.Сумма				 	 КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС			 	 КАК СтавкаНДС,
	|	ТаблицаТовары.СуммаНДС			 	 КАК СуммаНДС,
	|	ТаблицаТовары.СуммаСНДС			 	 КАК СуммаСНДС,
	|	ТаблицаТовары.ЗаказПоставщику	 	 КАК ЗаказПоставщику,
	|	ТаблицаТовары.КодСтроки			 	 КАК КодСтроки,
	|	ТаблицаТовары.Склад				 	 КАК Склад,
	|	ТаблицаТовары.НомерГТД			 	 КАК НомерГТД,
	|	ТаблицаТовары.Сделка			 	 КАК Сделка,
	|	ТаблицаТовары.СтатьяРасходов		 КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаРасходов		 КАК АналитикаРасходов
	|
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ТаблицаТовары.Ссылка = ДанныеПоследнейКорректировки.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|" + ТекстЗапросаПоОснованию + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеПоследнейКорректировки
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстЗапросаПоИсходнымДаннымПоступлениеТоваровУслуг()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки			 КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураПоставщика КАК НоменклатураПоставщика,
	|	ТаблицаТовары.Номенклатура		 	 КАК Номенклатура,
	|	ТаблицаТовары.Характеристика	 	 КАК Характеристика,
	|	Неопределено					 	 КАК Содержание,
	|	ТаблицаТовары.Упаковка			 	 КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок 	 КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество		 	 КАК Количество,
	|	
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаТовары.Цена * ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(15,2)) = ТаблицаТовары.Сумма
	|			ТОГДА ТаблицаТовары.Цена
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.Сумма / ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК Цена,
	|	
	|	ТаблицаТовары.Сумма				 	 КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС			 	 КАК СтавкаНДС,
	|	ТаблицаТовары.СуммаНДС			 	 КАК СуммаНДС,
	|	ТаблицаТовары.СуммаСНДС			 	 КАК СуммаСНДС,
	|	ТаблицаТовары.ЗаказПоставщику	 	 КАК ЗаказПоставщику,
	|	ТаблицаТовары.КодСтроки			 	 КАК КодСтроки,
	|	ТаблицаТовары.Склад				 	 КАК Склад,
	|	ТаблицаТовары.НомерГТД			 	 КАК НомерГТД,
	|	ТаблицаТовары.Сделка			 	 КАК Сделка,
	|	ТаблицаТовары.СтатьяРасходов		 КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаРасходов		 КАК АналитикаРасходов
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаТовары.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПоИсходнымДаннымПоступлениеУслугПрочихАктивов()
	
	ТекстЗапроса = "	
	|ВЫБРАТЬ
	|	ТаблицаРасходы.НомерСтроки 		 КАК НомерСтроки,
	|	Неопределено			   		 КАК НоменклатураПоставщика,
	|	Неопределено			   		 КАК Номенклатура,
	|	Неопределено			  		 КАК Характеристика,
	|	ТаблицаРасходы.Содержание  		 КАК Содержание,
	|	Неопределено			   		 КАК Упаковка,
	|	Неопределено			   		 КАК КоличествоУпаковок,
	|	ТаблицаРасходы.Количество  		 КАК Количество,
	|	ТаблицаРасходы.Цена		   		 КАК Цена,
	|	ТаблицаРасходы.Сумма	   		 КАК Сумма,
	|	ТаблицаРасходы.СтавкаНДС   		 КАК СтавкаНДС,
	|	ТаблицаРасходы.СуммаНДС	   		 КАК СуммаНДС,
	|	ТаблицаРасходы.СуммаСНДС   		 КАК СуммаСНДС,
	|	Неопределено			   		 КАК ЗаказПоставщику,
	|	Неопределено			   		 КАК КодСтроки,
	|	Неопределено			   		 КАК Склад,
	|	Неопределено			   		 КАК НомерГТД,
	|	Неопределено			   		 КАК Сделка,
	|	ТаблицаРасходы.СтатьяРасходов	 КАК СтатьяРасходов,
	|	ТаблицаРасходы.АналитикаРасходов КАК АналитикаРасходов
	|ИЗ
	|	Документ.ПоступлениеУслугПрочихАктивов.Расходы КАК ТаблицаРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаРасходы.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
//Обновление информационной базы

// Обработчик обновления УТ 11.1.0.0
// Заполняет новый реквизит ХозяйственнаяОперация в существующих документах КорректировкаПоступления
//
Процедура ЗаполнитьХозяйственнуюОперацию() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаПоступления.Ссылка                КАК Ссылка,
	|	КорректировкаПоступления.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	КорректировкаПоступления.МоментВремени         КАК МоментВремени,
	|	КорректировкаПоступления.ДокументОснование     КАК ДокументОснование
	|	
	|ПОМЕСТИТЬ КорректировкиПоступлений
	|ИЗ
	|	Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|	
	|ГДЕ
	|	НЕ КорректировкаПоступления.ПометкаУдаления
	|;
	|
	|/////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкиПоступлений.Ссылка               КАК Ссылка,
	|	КорректировкиПоступлений.ДокументОснование    КАК ДокументОснование,
	|	КорректировкиПоступлений.МоментВремени        КАК МоментВремени
	|	
	|ПОМЕСТИТЬ ДокументыДляОбработки
	|ИЗ
	|	КорректировкиПоступлений КАК КорректировкиПоступлений
	|ГДЕ
	|	КорректировкиПоступлений.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ДокументОснование
	|;
	|
	|/////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкиПоступлений.Ссылка                КАК Ссылка,
	|	КорректировкиПоступлений.МоментВремени         КАК МоментВремени,
	|	КорректировкиПоступлений.ДокументОснование     КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА СчетФактураПолученный.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                         КАК Корректировочный
	|
	|ПОМЕСТИТЬ ДанныеПоСчетамФактурам
	|ИЗ
	|	КорректировкиПоступлений КАК КорректировкиПоступлений
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|		ПО КорректировкиПоступлений.Ссылка = СчетФактураПолученный.ДокументОснование
	|			И (СчетФактураПолученный.Ссылка.Корректировочный)
	|			И НЕ (СчетФактураПолученный.Ссылка.ПометкаУдаления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ДокументОснование
	|;
	|
	|/////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыДляОбработки.Ссылка                  КАК Ссылка,
	|	МИНИМУМ(ВЫБОР
	|				КОГДА СчетаФактурыДокументовДляОбработки.Корректировочный
	|			 			И НЕ ЕСТЬNULL(СчетаФактурыОснований.Корректировочный, ЛОЖЬ)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)                                КАК ИсходныйКорректировочный
	|	
	|ИЗ
	|	ДокументыДляОбработки КАК ДокументыДляОбработки
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ДанныеПоСчетамФактурам КАК СчетаФактурыДокументовДляОбработки
	|	ПО 
	|		ДокументыДляОбработки.Ссылка = СчетаФактурыДокументовДляОбработки.Ссылка
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ДанныеПоСчетамФактурам КАК СчетаФактурыОснований
	|	ПО 
	|		ДокументыДляОбработки.Ссылка <> СчетаФактурыОснований.Ссылка
	|		И ДокументыДляОбработки.ДокументОснование = СчетаФактурыОснований.ДокументОснование
	|		И ДокументыДляОбработки.МоментВремени > СчетаФактурыОснований.МоментВремени
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДокументыДляОбработки.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		ДокументОбъект.ХозяйственнаяОперация = ?(Выборка.ИсходныйКорректировочный,
				Перечисления.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон,
				Перечисления.ХозяйственныеОперации.ИсправлениеОшибок);
				
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли