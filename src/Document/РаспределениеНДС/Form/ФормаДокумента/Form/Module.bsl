////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Обработчик подсистемы "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);

	// Обработчик подсистемы "Дополнительные отчеты и обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначенияУТ.ПолучитьКартинкуКомментария(Объект.Комментарий);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ОбновитьАктуальностьБазыРаспределения(Объект.Дата, Объект.Организация);
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ОткрытьФормуРедактированияКомментария(Элемент.ТекстРедактирования, Объект.Комментарий, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура СписатьНДСКакЦенностиПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииСервер();
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНД

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьБазуРаспределения();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

////////////////////////////////////////////////////////////////////////////////
//Инициализация и заполнение

&НаСервере
Процедура ОбновитьАктуальностьБазыРаспределения(Период, Организация)
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Период",			Объект.Дата);
	Реквизиты.Вставить("Организация",		Объект.Организация);
	Реквизиты.Вставить("ВыручкаНДС",		Объект.ВыручкаНДС);
	Реквизиты.Вставить("ВыручкаНеНДС",		Объект.ВыручкаНеНДС);
	Реквизиты.Вставить("ВыручкаЕНВД",		Объект.ВыручкаЕНВД);
	Реквизиты.Вставить("ВыручкаНДС0",		Объект.ВыручкаНДС0);
	
	БазаРаспределенияАктуальна = Документы.РаспределениеНДС.ВычислитьАктуальностьБазыРаспределения(Реквизиты, Период, Организация);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()

	Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначенияУТ.ПолучитьКартинкуКомментария(Объект.Комментарий);
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ОбновитьАктуальностьБазыРаспределения(Объект.Дата, Объект.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭлементамиФормы()
	
	Элементы.ГруппаСписаниеРасходов.Доступность = НЕ Объект.СписатьНДСКакЦенности;
	Элементы.ДекорацияАктуальностьВыручки.Видимость = НЕ БазаРаспределенияАктуальна;
	НадписьПериод = ПредставлениеПериода(НачалоКвартала(Объект.Дата), КонецКвартала(Объект.Дата), "ДЛФ=D");
	
КонецПроцедуры	

&НаСервере
Процедура ОрганизацияПриИзмененииСервер()
	
	ПравилаСписания = Документы.РаспределениеНДС.ЗаполнитьПравилаСписанияНДС(Объект.Организация);
		
	Если ПравилаСписания <> Неопределено Тогда
		Объект.СписатьНДСКакЦенности	= ПравилаСписания.СписатьНДСКакЦенности;
		Объект.СтатьяРасходовНеНДС		= ПравилаСписания.СписатьНДСКакЦенности;
		Объект.АналитикаРасходовНеНДС	= ПравилаСписания.АналитикаРасходовНеНДС;
		Объект.СтатьяРасходовЕНВД		= ПравилаСписания.СписатьНДСКакЦенности;
		Объект.АналитикаРасходовЕНВД	= ПравилаСписания.АналитикаРасходовЕНВД;
	КонецЕсли;
	
	ОбновитьАктуальностьБазыРаспределения(Объект.Дата, Объект.Организация);
	
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
//Автозаполнение

&НаКлиенте
Процедура ЗаполнитьБазуРаспределения()
	
	СтруктураНаПроверку = Новый Структура("Дата,Организация",Объект.Дата,Объект.Организация);
	
	Если ОбщегоНазначенияУТКлиент.ВозможноЗаполнениеТабличнойЧасти(ЭтаФорма, Неопределено, СтруктураНаПроверку) Тогда
		ЗаполнитьБазуРаспределенияСервер();
		БазаРаспределенияАктуальна = Истина;
		УправлениеЭлементамиФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьБазуРаспределенияСервер()
	
	БазаРаспределения = Документы.РаспределениеНДС.ПолучитьБазуРаспределения(Объект.Дата,Объект.Организация);
	
	Объект.ВыручкаНДС	= БазаРаспределения.ТаблицаВыручка.Итог("ВыручкаНДС");
	Объект.ВыручкаНеНДС	= БазаРаспределения.ТаблицаВыручка.Итог("ВыручкаНеНДС");
	Объект.ВыручкаЕНВД	= БазаРаспределения.ТаблицаВыручка.Итог("ВыручкаЕНВД");
	Объект.ВыручкаНДС0	= БазаРаспределения.ТаблицаДокументыЭкспорт.Итог("Сумма");
	
	Объект.ДокументыЭкспорт.Загрузить(БазаРаспределения.ТаблицаДокументыЭкспорт);
	
КонецПроцедуры
