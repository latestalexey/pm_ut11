#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ВыручкаНДС,
	|	ДанныеДокумента.ВыручкаНеНДС,
	|	ДанныеДокумента.ВыручкаЕНВД,
	|	ДанныеДокумента.ВыручкаНДС0,
	|	ДанныеДокумента.СтатьяРасходовНеНДС,
	|	ДанныеДокумента.АналитикаРасходовНеНДС,
	|	ДанныеДокумента.СтатьяРасходовЕНВД,
	|	ДанныеДокумента.АналитикаРасходовЕНВД,
	|	ДанныеДокумента.СписатьНДСКакЦенности
	|ИЗ
	|	Документ.РаспределениеНДС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Результат = СформироватьДвижения(ДокументСсылка,Реквизиты);
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаНДСЗаписиКнигиПродаж",	Результат[0]);
	ТаблицыДляДвижений.Вставить("ТаблицаНДСЗаписиКнигиПокупок",	Результат[1]);
	ТаблицыДляДвижений.Вставить("ТаблицаПрочиеРасходы",			Результат[2]);
	ТаблицыДляДвижений.Вставить("ТаблицаПартииПрочихРасходов",	Результат[3]);
	
КонецПроцедуры

//Движения
Функция СформироватьДвижения(ДокументСсылка, Реквизиты)
	
	Организация	= Реквизиты.Организация;
	Период 		= Реквизиты.Дата;
	
	БазаВыручка = БазаРаспределенияВыручка(ДокументСсылка);
	БазаДокументыЭкспорт = БазаРаспределенияДокументыЭкспорт(ДокументСсылка);
	
	ТаблицаДанных = ДанныеДляРаспределения(Организация, Период, ДокументСсылка);
	
	ТаблицаДокументыЭкспорт = ТаблицаДанных.СкопироватьКолонки();
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	МассивТипов.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
	ДопустимыеТипы = Новый ОписаниеТипов(МассивТипов);
	
	ТаблицаДокументыЭкспорт.Колонки.Добавить("ДокументЭкспорт",ДопустимыеТипы);
	
	ВыполнитьРаспределение(ТаблицаДанных, ТаблицаДокументыЭкспорт, БазаВыручка, БазаДокументыЭкспорт);
	
	ТаблицаНДСЗаписиКнигиПродаж		= РегистрыНакопления.НДСЗаписиКнигиПродаж.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТаблицаНДСЗаписиКнигиПокупок	= РегистрыНакопления.НДСЗаписиКнигиПокупок.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТаблицаПрочиеРасходы			= РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	Налогообложения = Перечисления.ТипыНалогообложенияНДС;
	
	Для Каждого Данные Из ТаблицаДанных Цикл
		
		Если Данные.НалогообложениеНДС = Налогообложения.ПродажаОблагаетсяНДС Тогда
			
			Если Данные.НДСНеНДС <> 0 Тогда
				//Восстановление по "Не облагается НДС"
				Если Данные.СчетФактура <> Неопределено Тогда
					ВосстановлениеНДС = НоваяСтрокаДанныхНДСПродажи(ТаблицаНДСЗаписиКнигиПродаж, Организация, Период, Данные);
					ВосстановлениеНДС.СуммаБезНДС = Данные.СуммаНеНДС;
					ВосстановлениеНДС.НДС = Данные.НДСНеНДС;
				КонецЕсли;
				//В расходы по "Не облагается НДС"
				ВключениеВСтоимость = НоваяСтрокаДанныхПрочиеРасходы(ТаблицаПрочиеРасходы, Реквизиты, Данные, Ложь);
				ВключениеВСтоимость.СуммаРегл = Данные.НДСНеНДС;
			КонецЕсли;
			
			Если Данные.НДСЕНВД <> 0 Тогда
				//Восстановление по "Облагается ЕНВД"
				Если Данные.СчетФактура <> Неопределено Тогда
					ВосстановлениеНДС = НоваяСтрокаДанныхНДСПродажи(ТаблицаНДСЗаписиКнигиПродаж, Организация, Период, Данные);
					ВосстановлениеНДС.СуммаБезНДС = Данные.СуммаЕНВД;
					ВосстановлениеНДС.НДС = Данные.НДСЕНВД;
				КонецЕсли;
				//В расходы по "Облагается ЕНВД"
				ВключениеВСтоимость = НоваяСтрокаДанныхПрочиеРасходы(ТаблицаПрочиеРасходы, Реквизиты, Данные, Истина);
				ВключениеВСтоимость.СуммаРегл = Данные.НДСЕНВД;
			КонецЕсли;
			
			Если Данные.НДС0 <> 0 Тогда
				//Восстановление по "Экспорт"
				Если Данные.СчетФактура <> Неопределено Тогда
					ВосстановлениеНДС = НоваяСтрокаДанныхНДСПродажи(ТаблицаНДСЗаписиКнигиПродаж, Организация, Период, Данные);
					ВосстановлениеНДС.СуммаБезНДС = Данные.СуммаНДС0;
					ВосстановлениеНДС.НДС = Данные.НДС0;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли Данные.НалогообложениеНДС = Налогообложения.ПродажаНеОблагаетсяНДС Тогда
			
			Если Данные.НДСНДС <> 0 Тогда
				//Из расходов по "Облагается НДС"
				ИсключениеИзСтоимости = НоваяСтрокаДанныхПрочиеРасходы (ТаблицаПрочиеРасходы, Реквизиты, Данные, Ложь);
				ИсключениеИзСтоимости.СуммаРегл = -Данные.НДСНДС;
				//Принятие к вычету по "Облагается НДС"
				Если Данные.СчетФактура <> Неопределено Тогда
					ПринятиеКВычету = НоваяСтрокаДанныхНДСПокупки(ТаблицаНДСЗаписиКнигиПокупок, Организация, Период, Данные);
					ПринятиеКВычету.СуммаБезНДС = Данные.СуммаНДС;
					ПринятиеКВычету.НДС = Данные.НДСНДС;
				КонецЕсли;
			КонецЕсли;
			
			Если Данные.НДС0 <> 0 Тогда
				//Из расходов по "Экспорт"
				ИсключениеИзСтоимости = НоваяСтрокаДанныхПрочиеРасходы (ТаблицаПрочиеРасходы, Реквизиты, Данные, Ложь);
				ИсключениеИзСтоимости.СуммаРегл = -Данные.НДС0;
			КонецЕсли;

		ИначеЕсли Данные.НалогообложениеНДС = Налогообложения.ПродажаОблагаетсяЕНВД Тогда
			
			Если Данные.НДСНДС <> 0 Тогда
				//Из расходов по "Облагается НДС"
				ИсключениеИзСтоимости = НоваяСтрокаДанныхПрочиеРасходы (ТаблицаПрочиеРасходы, Реквизиты, Данные, Истина);
				ИсключениеИзСтоимости.СуммаРегл = -Данные.НДСНДС;
				//Принятие к вычету по "Облагается НДС"
				Если Данные.СчетФактура <> Неопределено Тогда
					ПринятиеКВычету = НоваяСтрокаДанныхНДСПокупки(ТаблицаНДСЗаписиКнигиПокупок, Организация, Период, Данные);
					ПринятиеКВычету.СуммаБезНДС = Данные.СуммаНДС;
					ПринятиеКВычету.НДС = Данные.НДСНДС;
				КонецЕсли;
			КонецЕсли;
			
			Если Данные.НДС0 <> 0 Тогда
				//Из расходов по "Экспорт"
				ИсключениеИзСтоимости = НоваяСтрокаДанныхПрочиеРасходы (ТаблицаПрочиеРасходы,Реквизиты, Данные, Истина);
				ИсключениеИзСтоимости.СуммаРегл = -Данные.НДС0;
			КонецЕсли;
		
		ИначеЕсли Данные.НалогообложениеНДС = Налогообложения.ПродажаНаЭкспорт Тогда
			//Принятие к вычету по "Облагается НДС"
			Если Данные.НДСНДС <> 0 Тогда
				Если Данные.СчетФактура <> Неопределено Тогда
					ПринятиеКВычету = НоваяСтрокаДанныхНДСПокупки(ТаблицаНДСЗаписиКнигиПокупок, Организация, Период, Данные);
					ПринятиеКВычету.СуммаБезНДС = Данные.СуммаНДС;
					ПринятиеКВычету.НДС = Данные.НДСНДС;
				КонецЕсли;
			КонецЕсли;
			
			//В расходы по "Не облагается НДС"
			Если Данные.НДСНеНДС <> 0 Тогда
				ВключитьВСтоимость = НоваяСтрокаДанныхПрочиеРасходы(ТаблицаПрочиеРасходы, Реквизиты, Данные, Ложь);
				ВключитьВСтоимость.СуммаРегл = Данные.НДСНеНДС;
			КонецЕсли;
			
			//В расходы по "Облагается ЕНВД"
			Если Данные.НДСЕНВД <> 0 Тогда
				ВключитьВСтоимость = НоваяСтрокаДанныхПрочиеРасходы(ТаблицаПрочиеРасходы, Реквизиты, Данные, Истина);
				ВключитьВСтоимость.СуммаРегл = Данные.НДСЕНВД;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаПартииПрочихРасходов = СформироватьТаблицуПартииПрочихРасходов(ТаблицаДанных, ТаблицаДокументыЭкспорт, Период, Организация);
	
	Выборка = ВыборкаДляПодтвержденияНДС(ТаблицаДокументыЭкспорт, Период, Организация, ДокументСсылка);
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаНДСЗаписиКнигиПокупок.Добавить();
		НоваяСтрока.Период			= Период;
		НоваяСтрока.Организация		= Организация;
		НоваяСтрока.Поставщик		= Выборка.Контрагент;
		НоваяСтрока.СчетФактура		= Выборка.ДокументПоступления;
		НоваяСтрока.ВидЦенности		= Перечисления.ВидыЦенностей.Товары;
		НоваяСтрока.СтавкаНДС		= Выборка.СтавкаНДС;
		НоваяСтрока.Событие			= Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету0;
		НоваяСтрока.ДатаСобытия		= Период;
		Если Выборка.Состояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0
			И НачалоКвартала(Выборка.ДатаРеализации) < НачалоКвартала(Период) Тогда
				НоваяСтрока.ЗаписьДополнительногоЛиста = Истина
		КонецЕсли;
		Если Выборка.Состояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0
			И НачалоКвартала(Выборка.ДатаРеализации) < НачалоКвартала(Период)Тогда
				НоваяСтрока.КорректируемыйПериод = Выборка.ДатаРеализации
		КонецЕсли;
		НоваяСтрока.СуммаБезНДС		= Выборка.СуммаБезНДС;
		НоваяСтрока.НДС				= Выборка.НДС;
	
	КонецЦикла;
	
	Результат = Новый Массив;
	Результат.Добавить(ТаблицаНДСЗаписиКнигиПродаж);
	Результат.Добавить(ТаблицаНДСЗаписиКнигиПокупок);
	Результат.Добавить(ТаблицаПрочиеРасходы);
	Результат.Добавить(ТаблицаПартииПрочихРасходов);
	
	Возврат Результат
	
КонецФункции

// Данные для распределения
Функция ДанныеДляРаспределения(Организация, Период, ДокументСсылка)
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДанных.Подразделение КАК Подразделение,
	|	ТаблицаДанных.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДанных.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДанных.ДокументПоступления КАК ДокументПоступления,
	|	ТаблицаДанных.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаДанных.Контрагент КАК Контрагент,
	|	ТаблицаДанных.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаДанных.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаДанных.СуммаВсего) КАК СуммаВсего,
	|	СУММА(ТаблицаДанных.НДСВсего) КАК НДСВсего
	|ПОМЕСТИТЬ ВтТаблицаОстатков
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПартииПрочихРасходовОстатки.Подразделение КАК Подразделение,
	|		ПартииПрочихРасходовОстатки.СтатьяРасходов КАК СтатьяРасходов,
	|		ПартииПрочихРасходовОстатки.АналитикаРасходов КАК АналитикаРасходов,
	|		ПартииПрочихРасходовОстатки.ДокументПоступленияРасходов КАК ДокументПоступления,
	|		ПартииПрочихРасходовОстатки.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|		АналитикаПартий.Контрагент КАК Контрагент,
	|		АналитикаПартий.НалогообложениеНДС КАК НалогообложениеНДС,
	|		АналитикаПартий.СтавкаНДС КАК СтавкаНДС,
	|		ПартииПрочихРасходовОстатки.СтоимостьРеглОстаток КАК СуммаВсего,
	|		ПартииПрочихРасходовОстатки.НДСРеглОстаток КАК НДСВсего
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов.Остатки(
	|				&Период,
	|				Организация = &Организация
	|					И СтатьяРасходов.ВариантРаспределенияРасходов 
	|						= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)) КАК ПартииПрочихРасходовОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО ПартииПрочихРасходовОстатки.АналитикаУчетаПартий = АналитикаПартий.КлючАналитики
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПартииПрочихРасходовОбороты.Подразделение,
	|		ПартииПрочихРасходовОбороты.СтатьяРасходов,
	|		ПартииПрочихРасходовОбороты.АналитикаРасходов,
	|		ПартииПрочихРасходовОбороты.ДокументПоступленияРасходов,
	|		ПартииПрочихРасходовОбороты.АналитикаУчетаПартий,
	|		АналитикаПартий.Контрагент,
	|		АналитикаПартий.НалогообложениеНДС,
	|		АналитикаПартий.СтавкаНДС,
	|		-ПартииПрочихРасходовОбороты.СтоимостьРеглОборот,
	|		-ПартииПрочихРасходовОбороты.НДСРеглОборот
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов.Обороты(
	|				,
	|				&Период,
	|				Регистратор,
	|				Организация = &Организация
	|					И СтатьяРасходов.ВариантРаспределенияРасходов 
	|						= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)) КАК ПартииПрочихРасходовОбороты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО ПартииПрочихРасходовОбороты.АналитикаУчетаПартий = АналитикаПартий.КлючАналитики
	|	ГДЕ
	|		ПартииПрочихРасходовОбороты.Регистратор = &ДокументСсылка
	|	
	|	) КАК ТаблицаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.Подразделение,
	|	ТаблицаДанных.СтатьяРасходов,
	|	ТаблицаДанных.АналитикаРасходов,
	|	ТаблицаДанных.ДокументПоступления,
	|	ТаблицаДанных.АналитикаУчетаПартий,
	|	ТаблицаДанных.Контрагент,
	|	ТаблицаДанных.НалогообложениеНДС,
	|	ТаблицаДанных.СтавкаНДС
	|
	|;
	|////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Подразделение КАК Подразделение,
	|	ТаблицаОстатков.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаОстатков.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаОстатков.ДокументПоступления КАК ДокументПоступления,
	|	ЕстьNULL(СчетФактураПолученный.Ссылка,Неопределено) КАК СчетФактура,
	|	ТаблицаОстатков.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаОстатков.Контрагент КАК Контрагент,
	|	ТаблицаОстатков.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаОстатков.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаОстатков.СуммаВсего КАК СуммаВсего,
	|	ТаблицаОстатков.НДСВсего КАК НДСВсего,
	|	0 КАК СуммаНДС,
	|	0 КАК НДСНДС,
	|	0 КАК СуммаНеНДС,
	|	0 КАК НДСНеНДС,
	|	0 КАК СуммаЕНВД,
	|	0 КАК НДСЕНВД,
	|	0 КАК СуммаНДС0,
	|	0 КАК НДС0
	|	ИЗ
	|		ВтТаблицаОстатков КАК ТаблицаОстатков
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|			ПО ТаблицаОстатков.ДокументПоступления = СчетФактураПолученный.ДокументОснование
	|				И СчетФактураПолученный.Ссылка.Проведен
	|				И НЕ СчетФактураПолученный.Ссылка.Исправление
	|				И НЕ СчетФактураПолученный.Ссылка.Корректировочный
	|				И СчетФактураПолученный.Ссылка.ДатаПолучения <> ДАТАВРЕМЯ(1,1,1,0,0,0)
	|";
	
	Запрос.УстановитьПараметр("Организация",	Организация);
	Запрос.УстановитьПараметр("Период",			Новый Граница(КонецКвартала(Период),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДокументСсылка",	ДокументСсылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция БазаРаспределенияВыручка(ДокументСсылка)
	
	ИтогоВыручка = ДокументСсылка.ВыручкаНДС + ДокументСсылка.ВыручкаНеНДС
					+ ДокументСсылка.ВыручкаЕНВД + ДокументСсылка.ВыручкаНДС0;
	
	База = Новый Структура;
	База.Вставить("НДС",	?(ИтогоВыручка = 0,0,ДокументСсылка.ВыручкаНДС		/ ИтогоВыручка));
	База.Вставить("НеНДС",	?(ИтогоВыручка = 0,0,ДокументСсылка.ВыручкаНеНДС	/ ИтогоВыручка));
	База.Вставить("ЕНВД",	?(ИтогоВыручка = 0,0,ДокументСсылка.ВыручкаЕНВД		/ ИтогоВыручка));
	База.Вставить("НДС0",	?(ИтогоВыручка = 0,0,ДокументСсылка.ВыручкаНДС0		/ ИтогоВыручка));
	
	Возврат База
	
КонецФункции

Функция БазаРаспределенияДокументыЭкспорт(ДокументСсылка)
	
	ТаблицаДокументы = ДокументСсылка.ДокументыЭкспорт.Выгрузить();
	ТаблицаДокументы.Колонки.Добавить("Процент");
	
	ИтогоПоТаблице = ТаблицаДокументы.Итог("Сумма");
	
	Если ИтогоПоТаблице <> 0 Тогда
	
		Для Каждого Строка Из ТаблицаДокументы Цикл
			Строка.Процент = Строка.Сумма / ИтогоПоТаблице;
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат ТаблицаДокументы
	
КонецФункции

// Распределение
Процедура ВыполнитьРаспределение(ТаблицаДанных, ТаблицаДокументыЭкспорт, БазаВыручка, БазаДокументыЭкспорт)
	
	Для каждого Строка Из ТаблицаДанных Цикл
		
		//Распределение Суммы
		Строка.СуммаНДС		= Окр(БазаВыручка.НДС	* Строка.СуммаВсего,2);
		Строка.СуммаНеНДС	= Окр(БазаВыручка.НеНДС	* Строка.СуммаВсего,2);
		Строка.СуммаЕНВД	= Окр(БазаВыручка.ЕНВД	* Строка.СуммаВсего,2);
		Строка.СуммаНДС0	= Окр(БазаВыручка.НДС0	* Строка.СуммаВсего,2);
		
		РазницаСумма = Строка.СуммаВсего - Строка.СуммаНДС
			- Строка.СуммаНеНДС- Строка.СуммаЕНВД - Строка.СуммаНДС0;
		
		Если Строка.СуммаНДС <> 0 Тогда
			Строка.СуммаНДС = Строка.СуммаНДС + РазницаСумма;
		ИначеЕсли Строка.СуммаНеНДС <> 0 Тогда
			Строка.СуммаНеНДС = Строка.СуммаНеНДС + РазницаСумма
		ИначеЕсли Строка.СуммаЕНВД <> 0 Тогда
			Строка.СуммаЕНВД = Строка.СуммаЕНВД + РазницаСумма
		ИначеЕсли Строка.СуммаНДС0 <> 0 Тогда
			Строка.СуммаНДС0 = Строка.СуммаНДС0 + РазницаСумма
		КонецЕсли;
		
		//Распределение НДС
		Строка.НДСНДС	= Окр(БазаВыручка.НДС	* Строка.НДСВсего,2);
		Строка.НДСНеНДС	= Окр(БазаВыручка.НеНДС	* Строка.НДСВсего,2);
		Строка.НДСЕНВД	= Окр(БазаВыручка.ЕНВД	* Строка.НДСВсего,2);
		Строка.НДС0		= Окр(БазаВыручка.НДС0	* Строка.НДСВсего,2);
		
		РазницаНДС = Строка.НДСВсего - Строка.НДСНДС
			- Строка.НДСНеНДС- Строка.НДСЕНВД - Строка.НДС0;
		
		Если Строка.НДСНДС <> 0 Тогда
			Строка.НДСНДС = Строка.НДСНДС + РазницаНДС;
		ИначеЕсли Строка.НДСНеНДС <> 0 Тогда
			Строка.НДСНеНДС = Строка.НДСНеНДС + РазницаНДС
		ИначеЕсли Строка.НДСЕНВД <> 0 Тогда
			Строка.НДСЕНВД = Строка.НДСаЕНВД + РазницаНДС
		ИначеЕсли Строка.НДС0 <> 0 Тогда
			Строка.НДС0 = Строка.НДС0 + РазницаНДС
		КонецЕсли;
		
		//Распределение по документам на экспорт
		Если Строка.СуммаНДС0 <> 0 Тогда
			
			УчтеноСумма	= 0;
			УчтеноНДС	= 0;
			
			Для Каждого СтрокаДокументыЭкспорт Из БазаДокументыЭкспорт Цикл
				НоваяСтрока = ТаблицаДокументыЭкспорт.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
				НоваяСтрока.ДокументЭкспорт	= СтрокаДокументыЭкспорт.Документ;
				НоваяСтрока.СуммаНДС0		= Окр(СтрокаДокументыЭкспорт.Процент * Строка.СуммаНДС0,2);
				НоваяСтрока.НДС0			= Окр(СтрокаДокументыЭкспорт.Процент * Строка.НДС0,2);
				
				УчтеноСумма	= УчтеноСумма	+ НоваяСтрока.СуммаНДС0;
				УчтеноНДС 	= УчтеноНДС		+ НоваяСтрока.НДС0;
			КонецЦикла;
			
			//Погрешность расчетов отнесем на последний документ
			РазницаСумма	= Строка.СуммаНДС0 - УчтеноСумма;
			РазницаНДС		= Строка.НДС0 - УчтеноНДС;
			
			Если РазницаСумма <> 0 ИЛИ РазницаНДС <> 0 Тогда
				НомерСтроки = ТаблицаДокументыЭкспорт.Количество()-1;
				ТаблицаДокументыЭкспорт[НомерСтроки].СуммаНДС0	= ТаблицаДокументыЭкспорт[НомерСтроки].СуммаНДС0 + РазницаСумма;
				ТаблицаДокументыЭкспорт[НомерСтроки].НДС0		= ТаблицаДокументыЭкспорт[НомерСтроки].НДС0 + РазницаНДС;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция СформироватьТаблицуПартииПрочихРасходов(ТаблицаДанных, ТаблицаДокументыЭкспорт, Период, Организация);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДанных.Подразделение КАК Подразделение,
	|	ТаблицаДанных.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДанных.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДанных.ДокументПоступления КАК ДокументПоступления,
	|	ТаблицаДанных.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаДанных.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДанных.НДСНДС КАК НДСНДС,
	|	ТаблицаДанных.СуммаНеНДС КАК СуммаНеНДС,
	|	ТаблицаДанных.НДСНеНДС КАК НДСНеНДС,
	|	ТаблицаДанных.СуммаЕНВД КАК СуммаЕНВД,
	|	ТаблицаДанных.НДСЕНВД КАК НДСЕНВД
	|ПОМЕСТИТЬ ВтТаблицаДанных
	|ИЗ
	|	&ТаблицаДанных КАК ТаблицаДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументыЭкспорт.Подразделение КАК Подразделение,
	|	ТаблицаДокументыЭкспорт.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДокументыЭкспорт.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДокументыЭкспорт.ДокументПоступления КАК ДокументПоступления,
	|	ТаблицаДокументыЭкспорт.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаДокументыЭкспорт.СуммаНДС0 КАК СуммаНДС0,
	|	ТаблицаДокументыЭкспорт.НДС0 КАК НДС0,
	|	ТаблицаДокументыЭкспорт.ДокументЭкспорт КАК ДокументЭкспорт
	|ПОМЕСТИТЬ ВтТаблицаДокументыЭкспорт
	|ИЗ
	|	&ТаблицаДокументыЭкспорт КАК ТаблицаДокументыЭкспорт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаДанных.Подразделение КАК Подразделение,
	|	ТаблицаДанных.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДанных.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДанных.ДокументПоступления КАК ДокументПоступленияРасходов,
	|	ТаблицаДанных.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаДанных.СуммаНДС КАК СтоимостьРегл,
	|	ТаблицаДанных.НДСНДС КАК НДСРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС) КАК НалогообложениеНДС,
	|	NULL КАК ДокументРеализации
	|ИЗ
	|	ВтТаблицаДанных КАК ТаблицаДанных
	|ГДЕ
	|	ТаблицаДанных.СуммаНДС <> 0 
	|		ИЛИ ТаблицаДанных.НДСНДС <>0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация КАК Организация,
	|	ТаблицаДанных.Подразделение,
	|	ТаблицаДанных.СтатьяРасходов,
	|	ТаблицаДанных.АналитикаРасходов,
	|	ТаблицаДанных.ДокументПоступления,
	|	ТаблицаДанных.АналитикаУчетаПартий,
	|	ТаблицаДанных.СуммаНеНДС,
	|	ТаблицаДанных.НДСНеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|	NULL
	|ИЗ
	|	ВтТаблицаДанных КАК ТаблицаДанных
	|ГДЕ
	|	ТаблицаДанных.СуммаНеНДС <> 0 
	|		ИЛИ ТаблицаДанных.НДСНеНДС <>0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация,
	|	ТаблицаДанных.Подразделение,
	|	ТаблицаДанных.СтатьяРасходов,
	|	ТаблицаДанных.АналитикаРасходов,
	|	ТаблицаДанных.ДокументПоступления,
	|	ТаблицаДанных.АналитикаУчетаПартий,
	|	ТаблицаДанных.СуммаЕНВД КАК СуммаРегл,
	|	ТаблицаДанных.НДСЕНВД КАК НДСРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД),
	|	NULL
	|ИЗ
	|	ВтТаблицаДанных КАК ТаблицаДанных
	|ГДЕ
	|	ТаблицаДанных.СуммаЕНВД <> 0 
	|		ИЛИ ТаблицаДанных.НДСЕНВД <>0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация,
	|	ТаблицаДокументыЭкспорт.Подразделение,
	|	ТаблицаДокументыЭкспорт.СтатьяРасходов,
	|	ТаблицаДокументыЭкспорт.АналитикаРасходов,
	|	ТаблицаДокументыЭкспорт.ДокументПоступления,
	|	ТаблицаДокументыЭкспорт.АналитикаУчетаПартий,
	|	ТаблицаДокументыЭкспорт.СуммаНДС0 КАК СуммаРегл,
	|	ТаблицаДокументыЭкспорт.НДС0 КАК НДСРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|	ТаблицаДокументыЭкспорт.ДокументЭкспорт
	|ИЗ
	|	ВтТаблицаДокументыЭкспорт КАК ТаблицаДокументыЭкспорт
	|ГДЕ
	|	ТаблицаДокументыЭкспорт.СуммаНДС0 <> 0 
	|		ИЛИ ТаблицаДокументыЭкспорт.НДС0 <>0
	|"
	;
	
	Запрос.УстановитьПараметр("ТаблицаДанных",ТаблицаДанных);
	Запрос.УстановитьПараметр("ТаблицаДокументыЭкспорт",ТаблицаДокументыЭкспорт);
	Запрос.УстановитьПараметр("Период",Период);
	Запрос.УстановитьПараметр("Организация",Организация);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ВыборкаДляПодтвержденияНДС(ТаблицаДокументыЭкспорт, Период, Организация, ДокументСсылка)

	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДССостояниеРеализации0.ДокументРеализации КАК ДокументРеализации,
	|	НДССостояниеРеализации0.ДатаРеализации КАК ДатаРеализации,
	|	НДССостояниеРеализации0.Состояние КАК Состояние
	|ПОМЕСТИТЬ ВтДокументыРеализации
	|ИЗ
	|	РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|ГДЕ
	|	НДССостояниеРеализации0.Организация = &Организация
	|	И НДССостояниеРеализации0.ДатаРеализации МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И НДССостояниеРеализации0.Состояние <> ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыЭкспорт.ДокументПоступления КАК ДокументПоступления,
	|	ДокументыЭкспорт.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ДокументыЭкспорт.ДокументЭкспорт КАК ДокументЭкспорт,
	|	ДокументыЭкспорт.СуммаНДС0 КАК Стоимость,
	|	ДокументыЭкспорт.НДС0 КАК НДС
	|ПОМЕСТИТЬ ВтДокументыЭкспорт
	|ИЗ
	|	&ДокументыЭкспорт КАК ДокументыЭкспорт
	|ГДЕ
	|	(ДокументыЭкспорт.СуммаНДС0 <> 0
	|			ИЛИ ДокументыЭкспорт.НДС0 <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыРеализации.ДокументРеализации КАК ДокументРеализации,
	|	ДокументыРеализации.ДатаРеализации КАК ДатаРеализации,
	|	ДокументыРеализации.Состояние КАК Состояние,
	|	ПартииТоваровОрганизаций.ДокументПоступления КАК ДокументПоступления,
	|	ПартииТоваровОрганизаций.АналитикаУчетаПартий,
	|	ПартииТоваровОрганизаций.СтоимостьРеглРасход КАК Стоимость,
	|	ПартииТоваровОрганизаций.НДСРеглРасход КАК НДС
	|ПОМЕСТИТЬ ВтДокументыПоступления
	|ИЗ
	|	ВтДокументыРеализации КАК ДокументыРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций.Обороты(, , Регистратор, Организация = &Организация) КАК ПартииТоваровОрганизаций
	|		ПО ДокументыРеализации.ДокументРеализации = ПартииТоваровОрганизаций.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыРеализации.ДокументРеализации,
	|	ДокументыРеализации.ДатаРеализации,
	|	ДокументыРеализации.Состояние,
	|	ПартииРасходовНаСебестоимостьТоваров.ДокументПоступленияРасходов,
	|	ПартииРасходовНаСебестоимостьТоваров.АналитикаУчетаПартий,
	|	ПартииРасходовНаСебестоимостьТоваров.СтоимостьРеглРасход,
	|	ПартииРасходовНаСебестоимостьТоваров.НДСРеглРасход
	|ИЗ
	|	ВтДокументыРеализации КАК ДокументыРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров.Обороты(
	|			,
	|			,
	|			Регистратор,
	|			Организация = &Организация) КАК ПартииРасходовНаСебестоимостьТоваров
	|		ПО ДокументыРеализации.ДокументРеализации = ПартииРасходовНаСебестоимостьТоваров.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыРеализации.ДокументРеализации,
	|	ДокументыРеализации.ДатаРеализации,
	|	ДокументыРеализации.Состояние,
	|	ПартииЗатратНаВыпуск.ДокументПоступления,
	|	ПартииЗатратНаВыпуск.АналитикаУчетаПартий,
	|	ПартииЗатратНаВыпуск.СтоимостьРеглРасход,
	|	ПартииЗатратНаВыпуск.НДСРеглРасход
	|ИЗ
	|	ВтДокументыРеализации КАК ДокументыРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииЗатратНаВыпуск.Обороты(, , Регистратор, Организация = &Организация) КАК ПартииЗатратНаВыпуск
	|		ПО ДокументыРеализации.ДокументРеализации = ПартииЗатратНаВыпуск.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыРеализации.ДокументРеализации,
	|	ДокументыРеализации.ДатаРеализации,
	|	ДокументыРеализации.Состояние,
	|	ПартииПрочихРасходов.ДокументПоступленияРасходов,
	|	ПартииПрочихРасходов.АналитикаУчетаПартий,
	|	ПартииПрочихРасходов.СтоимостьРегл,
	|	ПартииПрочихРасходов.НДСРегл
	|ИЗ
	|	ВтДокументыРеализации КАК ДокументыРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|		ПО ДокументыРеализации.ДокументРеализации = ПартииПрочихРасходов.ДокументРеализации
	|ГДЕ
	|	ПартииПрочихРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ПартииПрочихРасходов.Организация = &Организация
	|	И ПартииПрочихРасходов.Регистратор <> &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыРеализации.ДокументРеализации,
	|	ДокументыРеализации.ДатаРеализации,
	|	ДокументыРеализации.Состояние,
	|	ДокументыЭкспорт.ДокументПоступления,
	|	ДокументыЭкспорт.АналитикаУчетаПартий,
	|	ДокументыЭкспорт.Стоимость,
	|	ДокументыЭкспорт.НДС
	|ИЗ
	|	ВтДокументыРеализации КАК ДокументыРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДокументыЭкспорт КАК ДокументыЭкспорт
	|		ПО ДокументыРеализации.ДокументРеализации = ДокументыЭкспорт.ДокументЭкспорт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПоступления.ДокументРеализации КАК ДокументРеализации,
	|	ДокументыПоступления.ДатаРеализации,
	|	ДокументыПоступления.Состояние КАК Состояние,
	|	ДокументыПоступления.ДокументПоступления КАК ДокументПоступления,
	|	АналитикаПартий.Контрагент КАК Контрагент,
	|	АналитикаПартий.НалогообложениеНДС КАК НалогообложениеНДС,
	|	АналитикаПартий.СтавкаНДС КАК СтавкаНДС,
	|	ДокументыПоступления.Стоимость КАК СуммаБезНДС,
	|	ДокументыПоступления.НДС КАК НДС
	|ИЗ
	|	ВтДокументыПоступления КАК ДокументыПоступления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|		ПО ДокументыПоступления.ДокументПоступления = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|		ПО ДокументыПоступления.АналитикаУчетаПартий = АналитикаПартий.КлючАналитики
	|ГДЕ
	|	СчетФактураПолученныйДокументыОснования.Ссылка.Организация = &Организация
	|	И СчетФактураПолученныйДокументыОснования.Ссылка.Проведен
	|	И СчетФактураПолученныйДокументыОснования.Ссылка.ТипСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ТипыПолученныхСчетовФактур.НаПоступление)
	|	И (ДокументыПоступления.Стоимость <> 0
	|			ИЛИ ДокументыПоступления.НДС <> 0)"
	;
	
	Запрос.УстановитьПараметр("ПериодНачало",		НачалоКвартала(Период));
	Запрос.УстановитьПараметр("ПериодОкончание",	КонецКвартала(Период));
	Запрос.УстановитьПараметр("Организация",		Организация);
	Запрос.УстановитьПараметр("ДокументыЭкспорт",	ТаблицаДокументыЭкспорт);
	Запрос.УстановитьПараметр("ДокументСсылка",		ДокументСсылка);
	
	Возврат Запрос.Выполнить().Выбрать()
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Инициализация и заполнение

//Автозаполнение
Функция ЗаполнитьПравилаСписанияНДС(Организация = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ДанныеДокумента.СписатьНДСКакЦенности КАК СписатьНДСКакЦенности,
	|	ДанныеДокумента.СтатьяРасходовНеНДС КАК СтатьяРасходовНеНДС,
	|	ДанныеДокумента.АналитикаРасходовНеНДС КАК АналитикаРасходовНеНДС,
	|	ДанныеДокумента.СтатьяРасходовЕНВД КАК СтатьяРасходовЕНВД,
	|	ДанныеДокумента.АналитикаРасходовЕНВД КАК АналитикаРасходовЕНВД
	|ИЗ
	|	Документ.РаспределениеНДС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И (ВЫБОР
	|		КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ДанныеДокумента.Организация = &Организация
	|	КОНЕЦ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.Дата УБЫВ
	|";
	
	Запрос.УстановитьПараметр("Организация",?(Организация = Неопределено,Справочники.Организации.ПустаяСсылка(),Организация));
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПравилаСписания = Новый Структура("СписатьНДСКакЦенности,СтатьяРасходовНеНДС,АналитикаРасходовНеНДС,СтатьяРасходовЕНВД,АналитикаРасходовЕНВД");
	
	Если Выборка.Следующий() Тогда
		ПравилаСписания.СписатьНДСКакЦенности	= Выборка.СписатьНДСКакЦенности;
		ПравилаСписания.СтатьяРасходовНеНДС		= Выборка.СтатьяРасходовНеНДС;
		ПравилаСписания.АналитикаРасходовНеНДС	= Выборка.АналитикаРасходовНеНДС;
		ПравилаСписания.СтатьяРасходовЕНВД		= Выборка.СтатьяРасходовЕНВД;
		ПравилаСписания.АналитикаРасходовЕНВД	= Выборка.АналитикаРасходовЕНВД;
	КонецЕсли;
	
	Возврат ПравилаСписания
	
КонецФункции

Функция ПолучитьБазуРаспределения(Дата, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДанных.Организация КАК Организация,
	|	СУММА(ТаблицаДанных.ВыручкаНДС) КАК ВыручкаНДС,
	|	СУММА(ТаблицаДанных.ВыручкаНеНДС) КАК ВыручкаНеНДС,
	|	СУММА(ТаблицаДанных.ВыручкаЕНВД) КАК ВыручкаЕНВД
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДСЗаписиКнигиПродажОбороты.Организация КАК Организация,
	|		ЕстьNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,0) КАК ВыручкаНДС,
	|		0 КАК ВыручкаНеНДС,
	|		0 КАК ВыручкаЕНВД
	|	ИЗ
	|		РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|				&ПериодНачало,
	|				&ПериодОкончание,
	|				Квартал,
	|				Организация В (&Организация)
	|					И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)
	|					И НЕ СтавкаНДС В (ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС))) КАК НДСЗаписиКнигиПродажОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДСЗаписиКнигиПродажОбороты.Организация,
	|		0,
	|		ЕстьNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,0),
	|		0
	|	ИЗ
	|		РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|				&ПериодНачало,
	|				&ПериодОкончание,
	|				Квартал,
	|				Организация В (&Организация)
	|					И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)
	|					И СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)) КАК НДСЗаписиКнигиПродажОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаПоПартнерам.Организация,
	|		0,
	|		0,
	|		ЕстьNULL(ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл,0)
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|			ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = АналитикаПоПартнерам.КлючАналитики
	|	ГДЕ
	|		АналитикаПоПартнерам.Организация В (&Организация)
	|		И ВыручкаИСебестоимостьПродаж.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|		И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &ПериодНачало И &ПериодОкончание
	|		И ВыручкаИСебестоимостьПродаж.Активность
	|	
	|	) КАК ТаблицаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	
	|ВЫБРАТЬ
	|	ТаблицаДанных.Организация КАК Организация,
	|	ТаблицаДанных.Ссылка КАК Документ,
	|	СУММА(ТаблицаДанных.СуммаБезНДС) КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДССостояниеРеализации0.Организация КАК Организация,
	|		ДанныеДокумента.Ссылка КАК Ссылка,
	|		ВЫБОР
	|			КОГДА СуммыВВалютеРегл.СуммаБезНДСРегл ЕСТЬ NULL 
	|					ИЛИ СуммыВВалютеРегл.СуммаБезНДСРегл = 0
	|				ТОГДА ДанныеДокумента.СуммаСНДС
	|			ИНАЧЕ СуммыВВалютеРегл.СуммаБезНДСРегл
	|		КОНЕЦ КАК СуммаБезНДС
	|	ИЗ
	|		Документ.АктВыполненныхРабот.Услуги КАК ДанныеДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|			ПО ДанныеДокумента.Ссылка = НДССостояниеРеализации0.ДокументРеализации
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыВВалютеРегл
	|			ПО ДанныеДокумента.Ссылка = СуммыВВалютеРегл.Регистратор
	|				И ДанныеДокумента.ИдентификаторСтроки = СуммыВВалютеРегл.ИдентификаторСтроки
	|	ГДЕ
	|		НДССостояниеРеализации0.Организация В (&Организация)
	|		И НДССостояниеРеализации0.ДатаРеализации МЕЖДУ &ПериодНачало И &ПериодОкончание
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НДССостояниеРеализации0.Организация,
	|		ДанныеДокумента.Ссылка,
	|		ВЫБОР
	|			КОГДА СуммыВВалютеРегл.СуммаБезНДСРегл ЕСТЬ NULL 
	|					ИЛИ СуммыВВалютеРегл.СуммаБезНДСРегл = 0
	|				ТОГДА ДанныеДокумента.СуммаСНДС
	|			ИНАЧЕ СуммыВВалютеРегл.СуммаБезНДСРегл
	|		КОНЕЦ
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ДанныеДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|			ПО ДанныеДокумента.Ссылка = НДССостояниеРеализации0.ДокументРеализации
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыВВалютеРегл
	|			ПО ДанныеДокумента.Ссылка = СуммыВВалютеРегл.Регистратор
	|				И ДанныеДокумента.ИдентификаторСтроки = СуммыВВалютеРегл.ИдентификаторСтроки
	|	ГДЕ
	|		НДССостояниеРеализации0.Организация В (&Организация)
	|		И НДССостояниеРеализации0.ДатаРеализации МЕЖДУ &ПериодНачало И &ПериодОкончание
	|		И ДанныеДокумента.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НДССостояниеРеализации0.Организация,
	|		ДанныеДокумента.Ссылка,
	|		ВЫБОР
	|			КОГДА СуммыВВалютеРегл.СуммаБезНДСРегл ЕСТЬ NULL 
	|					ИЛИ СуммыВВалютеРегл.СуммаБезНДСРегл = 0
	|				ТОГДА ДанныеДокумента.СуммаСНДС
	|			ИНАЧЕ СуммыВВалютеРегл.СуммаБезНДСРегл
	|		КОНЕЦ
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.Товары КАК ДанныеДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|			ПО ДанныеДокумента.Ссылка = НДССостояниеРеализации0.ДокументРеализации
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыВВалютеРегл
	|			ПО ДанныеДокумента.Ссылка = СуммыВВалютеРегл.Регистратор
	|				И ДанныеДокумента.ИдентификаторСтроки = СуммыВВалютеРегл.ИдентификаторСтроки
	|	ГДЕ
	|		НДССостояниеРеализации0.Организация В (&Организация)
	|		И НДССостояниеРеализации0.ДатаРеализации МЕЖДУ &ПериодНачало И &ПериодОкончание
	|		И ДанныеДокумента.Номенклатура.ТипНоменклатуры НЕ В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|	) КАК ТаблицаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.Организация,
	|	ТаблицаДанных.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДанных.Ссылка.Дата
	|";
	
	Запрос.УстановитьПараметр("ПериодНачало",НачалоКвартала(Дата));
	Запрос.УстановитьПараметр("ПериодОкончание", КонецКвартала(Дата));
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") Тогда
		МассивОрганизаций = Новый Массив();
		МассивОрганизаций.Добавить(Организация);
		Запрос.УстановитьПараметр("Организация", МассивОрганизаций);
	Иначе
		Запрос.УстановитьПараметр("Организация", Организация)
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	БазаРаспределения = Новый Структура();
	
	БазаРаспределения.Вставить("ТаблицаВыручка", 			Результат[0].Выгрузить());
	БазаРаспределения.Вставить("ТаблицаДокументыЭкспорт",	Результат[1].Выгрузить());
	
	Возврат БазаРаспределения
	
КонецФункции	

Функция ВычислитьАктуальностьБазыРаспределения (Реквизиты,Период, Организация) Экспорт
	
	БазаРаспределения = ПолучитьБазуРаспределения(Период,Организация);
	
	Если Реквизиты.ВыручкаНДС	<> БазаРаспределения.ТаблицаВыручка.Итог("ВыручкаНДС")
		ИЛИ Реквизиты.ВыручкаНеНДС <> БазаРаспределения.ТаблицаВыручка.Итог("ВыручкаНеНДС")
		ИЛИ Реквизиты.ВыручкаЕНВД <> БазаРаспределения.ТаблицаВыручка.Итог("ВыручкаЕНВД")
		ИЛИ Реквизиты.ВыручкаНДС0 <> БазаРаспределения.ТаблицаДокументыЭкспорт.Итог("Сумма") Тогда
			Возврат Ложь
	Иначе
		Возврат Истина
	КонецЕсли;

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Прочее

Функция НоваяСтрокаДанныхНДСПродажи(ТаблицаНДСПродажи, Организация, Период, Данные)
	
	НоваяСтрока = ТаблицаНДСПродажи.Добавить();
	НоваяСтрока.Период = Период;
	НоваяСтрока.Организация = Организация;
	НоваяСтрока.Покупатель = Данные.Контрагент;
	НоваяСтрока.СчетФактура = Данные.ДокументПоступления;
	НоваяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.Товары;
	НоваяСтрока.СтавкаНДС = Данные.СтавкаНДС;
	НоваяСтрока.Событие = Перечисления.СобытияПоНДСПродажи.ВосстановлениеНДС;
	НоваяСтрока.ДатаСобытия = Период;
	
	Возврат НоваяСтрока;
	
КонецФункции

Функция НоваяСтрокаДанныхПрочиеРасходы (ТаблицаПрочиеРасходы, Реквизиты, Данные, ЕНВД)
	
	НоваяСтрока = ТаблицаПрочиеРасходы.Добавить();
	НоваяСтрока.Период					= Реквизиты.Дата;
	НоваяСтрока.Активность				= Истина;
	НоваяСтрока.Организация				= Реквизиты.Организация;
	НоваяСтрока.Подразделение			= Данные.Подразделение;
	НоваяСтрока.ХозяйственнаяОперация	= Перечисления.ХозяйственныеОперации.РаспределениеНДС;
	Если Реквизиты.СписатьНДСКакЦенности Тогда
		НоваяСтрока.СтатьяРасходов		= Данные.СтатьяРасходов;
		НоваяСтрока.АналитикаРасходов	= Данные.АналитикаРасходов;
	ИначеЕсли ЕНВД Тогда
		НоваяСтрока.СтатьяРасходов		= Реквизиты.СтатьяРасходовЕНВД;
		НоваяСтрока.АналитикаРасходов	= Реквизиты.АналитикаРасходовЕНВД;
	Иначе
		НоваяСтрока.СтатьяРасходов		= Реквизиты.СтатьяРасходовНеНДС;
		НоваяСтрока.АналитикаРасходов	= Реквизиты.АналитикаРасходовНеНДС;
	КонецЕсли;
	
	Возврат НоваяСтрока
	
КонецФункции

Функция НоваяСтрокаДанныхНДСПокупки(ТаблицаНДСПокупки, Организация, Период, Данные)
	
	НоваяСтрока = ТаблицаНДСПокупки.Добавить();
	НоваяСтрока.Период = Период;
	НоваяСтрока.Организация = Организация;
	НоваяСтрока.Поставщик = Данные.Контрагент;
	НоваяСтрока.СчетФактура = Данные.ДокументПоступления;
	НоваяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.Товары;
	НоваяСтрока.СтавкаНДС = Данные.СтавкаНДС;
	НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету;
	НоваяСтрока.ДатаСобытия = Период;

	Возврат НоваяСтрока
	
КонецФункции

#КонецЕсли