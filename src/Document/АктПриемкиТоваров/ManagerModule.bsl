#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Печать

// Формирует печатные формы объектов
//
// Параметры:
//   ИменаМакетов - Строка - Имена макетов, перечисленные через запятую
//   МассивОбъектов - Массив - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода - Структура - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ИмяМакета = "";
	СинонимМакета = "";
	ТабличныйДокумент = Неопределено;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ1") Тогда
		
		ИмяМакета = "ТОРГ1";
		СинонимМакета = НСтр("ru='ТОРГ-1'");
		ТабличныйДокумент = ПолучитьПечатнуюФормуТОРГ1(ПараметрыПечати["ПечатьВНоменклатуреПоставщика"], МассивОбъектов, ОбъектыПечати);
		
	КонецЕсли;
	
	Если ТабличныйДокумент <> Неопределено Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			ИмяМакета,
			СинонимМакета,
			ТабличныйДокумент
		);
		
	КонецЕсли;
	
	ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Функция формирует и возвращает печатную форму ТОРГ1
//
// Параметры:
// 	ПечатьВНоменклатуреПоставщика - Булево
// 	МассивОбъектов - Массив - массив документов для печати
// 	ОбъектыПечати - СписокЗначений
//
// Возвращаемое значение:
// 	ТабличныйДокумент
//
Функция ПолучитьПечатнуюФормуТОРГ1(ПечатьВНоменклатуреПоставщика, МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ПечатьВНоменклатуреПоставщика", ПечатьВНоменклатуреПоставщика);
	СформироватьТекстЗапросаПоДокументам(Запрос.Текст);
	
	ЗаполнитьТабличныйДокументТОРГ1(ПечатьВНоменклатуреПоставщика, ТабличныйДокумент, Запрос, ОбъектыПечати);
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Производит пересчет ценовых показателей таблицы товаров в валюте регламентированного учета
//
// Параметры:
// 	ТаблицаТоваров - ТаблицаЗначений - Таблица по товарам
// 	ТаблицаВалют - ТаблицаЗначений - Таблица значений курсов валют
//
Процедура ПересчитатьСуммыВВалютеРегламентированногоУчета(ТаблицаТоваров, ТаблицаВалют)
	
	СоответствиеСсылок = Новый Соответствие;	// Ключ - <ДокументСсылка.АктПриемкиТоваров>
												// Значение - <Структура>
	
	Для Каждого Строка Из ТаблицаТоваров Цикл
		
		ТекущаяСсылка = СоответствиеСсылок[Строка.Ссылка];
		
		Если ТекущаяСсылка = Неопределено Тогда
			
			ТекущаяСсылка = Новый Структура;
			
			ОписаниеКурсовВалют = ТаблицаВалют.Найти(Строка.Ссылка, "Ссылка");
			Если ОписаниеКурсовВалют = Неопределено ИЛИ ОписаниеКурсовВалют.ПересчетНеТребуется Тогда
				ТекущаяСсылка.Вставить("ТребуетсяПересчет", Ложь);
			Иначе
				ТекущаяСсылка.Вставить("ТребуетсяПересчет", Истина);
				
				ТекущаяСсылка.Вставить("КоэффициентПересчета",
					ОписаниеКурсовВалют.КурсВалютыДокумента * ОписаниеКурсовВалют.КратностьВалютыРегламентированногоУчета
					/ (ОписаниеКурсовВалют.КратностьВалютыДокумента * ОписаниеКурсовВалют.КурсВалютыРегламентированногоУчета)
				);
				
				ТекущаяСсылка.Вставить("ЦенаВключаетНДСПоДокументам", Строка.ЦенаВключаетНДСПоДокументам);
				ТекущаяСсылка.Вставить("ЦенаВключаетНДСПоФакту", Строка.ЦенаВключаетНДСПоФакту);
				
				ТекущаяСсылка.Вставить("ИтогПоДокументам", 0);
				ТекущаяСсылка.Вставить("МассивПоДокументам", Новый Массив);
				ТекущаяСсылка.Вставить("ИтогПоФакту", 0);
				ТекущаяСсылка.Вставить("МассивПоФакту", Новый Массив);
				
			КонецЕсли;
			
			СоответствиеСсылок.Вставить(Строка.Ссылка, ТекущаяСсылка);
			
		КонецЕсли;
		
		Если ТекущаяСсылка.ТребуетсяПересчет Тогда
			
			Если Строка.СуммаПоДокументам <> 0 Тогда
				СуммаПоДокументам = ?(ТекущаяСсылка.ЦенаВключаетНДСПоДокументам,
					Строка.СуммаПоДокументам,
					Строка.СуммаПоДокументам+Строка.СуммаНДСПоДокументам)
					* ТекущаяСсылка.КоэффициентПересчета;
				ТекущаяСсылка.ИтогПоДокументам = ТекущаяСсылка.ИтогПоДокументам + СуммаПоДокументам;
				ТекущаяСсылка.МассивПоДокументам.Добавить(СуммаПоДокументам);
			КонецЕсли;
			Если Строка.СуммаПоФакту <> 0 Тогда
				СуммаПоФакту = ?(ТекущаяСсылка.ЦенаВключаетНДСПоФакту,
					Строка.СуммаПоФакту,
					Строка.СуммаПоФакту+Строка.СуммаНДСПоФакту)
					* ТекущаяСсылка.КоэффициентПересчета;
				ТекущаяСсылка.ИтогПоФакту = ТекущаяСсылка.ИтогПоФакту + СуммаПоФакту;
				ТекущаяСсылка.МассивПоФакту.Добавить(СуммаПоФакту);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ТекущаяСсылка Из СоответствиеСсылок Цикл
		
		Если НЕ ТекущаяСсылка.Значение.ТребуетсяПересчет Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяСсылка.Значение.МассивПоДокументам = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
			ТекущаяСсылка.Значение.ИтогПоДокументам, ТекущаяСсылка.Значение.МассивПоДокументам
		);
		ТекущаяСсылка.Значение.МассивПоФакту = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
			ТекущаяСсылка.Значение.ИтогПоФакту, ТекущаяСсылка.Значение.МассивПоФакту
		);
		
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаТоваров Цикл
		
		ТекущаяСсылка = СоответствиеСсылок[Строка.Ссылка];
		Если ТекущаяСсылка = Неопределено ИЛИ НЕ ТекущаяСсылка.ТребуетсяПересчет Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяСсылка.МассивПоДокументам <> Неопределено И Строка.СуммаПоДокументам<>0 Тогда
			СтавкаНДСЧислом = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(Строка.СтавкаНДСПоДокументам);
			СуммаСНДС = ТекущаяСсылка.МассивПоДокументам[0];
			Строка.СуммаНДСПоДокументам = Окр(СуммаСНДС / (1 + СтавкаНДСЧислом) * СтавкаНДСЧислом,2);
			Строка.СуммаПоДокументам = ?(ТекущаяСсылка.ЦенаВключаетНДСПоДокументам, СуммаСНДС, СуммаСНДС - Строка.СуммаНДСПоДокументам);
			ТекущаяСсылка.МассивПоДокументам.Удалить(0);
			Если Строка.КоличествоПоДокументам > 0 Тогда
				Строка.ЦенаПоДокументам = Строка.СуммаПоДокументам / Строка.КоличествоПоДокументам;
			КонецЕсли;
		КонецЕсли;
		
		Если ТекущаяСсылка.МассивПоФакту <> Неопределено И Строка.СуммаПоФакту <> 0 Тогда
			СтавкаНДСЧислом = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(Строка.СтавкаНДСПоФакту);
			Строка.СуммаСНДСПоФакту = ТекущаяСсылка.МассивПоФакту[0];
			Строка.СуммаНДСПоФакту = Окр(Строка.СуммаСНДСПоФакту / (1 + СтавкаНДСЧислом) * СтавкаНДСЧислом,2);
			Строка.СуммаПоФакту = ?(ТекущаяСсылка.ЦенаВключаетНДСПоФакту, Строка.СуммаСНДСПоФакту, Строка.СуммаСНДСПоФакту - Строка.СуммаНДСПоФакту);
			ТекущаяСсылка.МассивПоФакту.Удалить(0);
			Если Строка.КоличествоПоФакту > 0 Тогда
				Строка.ЦенаПоФакту = Строка.СуммаПоФакту / Строка.КоличествоПоФакту;
			КонецЕсли;
		Иначе
			Строка.ЦенаПоФакту = Строка.ЦенаПоДокументам;
		КонецЕсли;
		
		Строка.СуммаОтклонение = Строка.СуммаПоФакту-Строка.СуммаПоДокументам;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает соответствие строки сертификата по строкам таблицы товаров
//
// Параметры:
// 	ТаблицаТоваров - ТаблицаЗначений - Таблица с товарами
// 	ТаблицаСертификатов - ТаблицаЗначений - Таблица с сертификатами
//
// Возвращаемое значение:
// 	Соответствие - Соответствие с ключами типа "СтрокаТаблицыЗначений" и значениями типа "Строка"
//
Функция ПолучитьСоответствиеСертификатов(ТаблицаТоваров, ТаблицаСертификатов)
	
	СоответствиеСертификатов = Новый Соответствие;
	
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		СтрокаСертификатов = "";
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Ссылка", СтрокаТовара.Ссылка);
		СтруктураОтбора.Вставить("НоменклатураПоставщика", СтрокаТовара.НоменклатураПоставщика);
		СтруктураОтбора.Вставить("Номенклатура", СтрокаТовара.Номенклатура);
		СтруктураОтбора.Вставить("Характеристика", СтрокаТовара.Характеристика);
		СтруктураОтбора.Вставить("ЕдиницаИзмерения", СтрокаТовара.ЕдиницаИзмерения);
		СтруктураОтбора.Вставить("Упаковка", СтрокаТовара.Упаковка);
		СтруктураОтбора.Вставить("Цена", СтрокаТовара.ЦенаПоФакту);
		
		НайденныеСтроки = ТаблицаСертификатов.НайтиСтроки(СтруктураОтбора);
		
		ПерваяСтрока = Истина;
		Для Каждого Строка Из НайденныеСтроки Цикл
			СтрокаСертификатов = СтрокаСертификатов + ?(ПерваяСтрока, "", ", ") + Строка.Сертификат;
			ПерваяСтрока = Ложь;
		КонецЦикла;
		
		СоответствиеСертификатов.Вставить(СтрокаТовара, СтрокаСертификатов);
		
	КонецЦикла;
	
	Возврат СоответствиеСертификатов;
	
КонецФункции

//Процедура заполняет табличный документ ТОРГ1
//
// Параметры:
// 	ПечатьВНоменклатуреПоставщика - Булево
// 	ТабличныйДокумент - ТабличныйДокумент - Табличный документ для вывода печатной формы
// 	Запрос - Запрос - Запрос по данным документов
// 	ОбъектыПечати - СписокЗначений
//
Процедура ЗаполнитьТабличныйДокументТОРГ1(ПечатьВНоменклатуреПоставщика, ТабличныйДокумент, Запрос, ОбъектыПечати)
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АктПриемкиТоваров_ТОРГ1";
	
	Макет = УправлениеПечатью.ПолучитьМакет("Документ.АктПриемкиТоваров.ПФ_MXL_ТОРГ1");
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетРезультатов[0].Выбрать();
	ТаблицаТоваров = ПакетРезультатов[3].Выгрузить();
	ТаблицаСертификатов = ПакетРезультатов[4].Выгрузить();
	ТаблицаКурсовВалют = ПакетРезультатов[5].Выгрузить();
	
	СоответствиеСертификатов = ПолучитьСоответствиеСертификатов(ТаблицаТоваров, ТаблицаСертификатов);
	ПересчитатьСуммыВВалютеРегламентированногоУчета(ТаблицаТоваров, ТаблицаКурсовВалют);
	
	ПервыйДокумент = Истина;
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	
	Если НЕ ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если ПервыйДокумент Тогда
			ПервыйДокумент = Ложь;
		Иначе
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Страница №1 ////////////////////////////////////////////////////////////////////////////////////////////////////
		// Вывод шапки
		Область = Макет.ПолучитьОбласть("ШапкаПервойСтраницы");
		Область.Параметры.Заполнить(ДанныеПечати);
		Область.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДанныеПечати.НомерДокумента, Ложь, Истина);
		Область.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.ДатаДокумента), "ПолноеНаименование,ЮридическийАдрес,Телефоны,");
		Область.Параметры.Грузоотправитель = ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Грузоотправитель, ДанныеПечати.ДатаДокумента), "ПолноеНаименование,ЮридическийАдрес,Телефоны,");
		Область.Параметры.Производитель = ДанныеПечати.Производитель;
		Область.Параметры.Поставщик = ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Поставщик, ДанныеПечати.ДатаДокумента), "ПолноеНаименование,ЮридическийАдрес,Телефоны,");
		Область.Параметры.СтраховаяКомпания = ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.СтраховаяКомпания, ДанныеПечати.ДатаДокумента), "ПолноеНаименование,ЮридическийАдрес,Телефоны,");
		Область.Параметры.Заполнить(ПолучитьСтруктуруПоДате("ДатаДоставкиТоваров", ДанныеПечати.ДатаДоставкиТоваров));
		Область.Параметры.Заполнить(ПолучитьСтруктуруПоДате("ДатаОтправленияТоваров", ДанныеПечати.ДатаОтправленияТоваров));
		Область.Параметры.Заполнить(ПолучитьСтруктуруПоДате("ДатаДоговораПоставки", ДанныеПечати.ДатаДоговораПоставки));
		Область.Параметры.Заполнить(ПолучитьСтруктуруПоДате("ДатаСчетаФактуры", ДанныеПечати.ДатаСчетаФактуры));
		Область.Параметры.Заполнить(ПолучитьСтруктуруПоДате("ДатаКоммерческогоАкта", ДанныеПечати.ДатаКоммерческогоАкта));
		Область.Параметры.Заполнить(ПолучитьСтруктуруПоДате("ДатаВетеринарногоСвидетельства", ДанныеПечати.ДатаВетеринарногоСвидетельства));
		Область.Параметры.Заполнить(ПолучитьСтруктуруПоДате("ДатаЖелезнодорожнойНакладной", ДанныеПечати.ДатаЖелезнодорожнойНакладной));
		Область.Параметры.Заполнить(ПолучитьСтруктуруПоДате("ДатаДокументаОВызовеПредставителяПартнера", ДанныеПечати.ДатаДокументаОВызовеПредставителяПартнера));
		Область.Параметры.ВызваемыйПредставительПартнера = ПолучитьЗначениеРеквизитаПоШаблону(ДанныеПечати.ВызваемыйПредставительПартнера, НСтр("ru='грузоотправителя, поставщика, производителя'"));
		Область.Параметры.ВидДокументаОВызовеПредставителяПартнера = ПолучитьЗначениеРеквизитаПоШаблону(ДанныеПечати.ВидДокументаОВызовеПредставителяПартнера, НСтр("ru='телеграмма, факс, телефонограмма, радиограмма'"));
		Область.Параметры.ОснованиеДляСоставленияАкта = ПолучитьЗначениеРеквизитаПоШаблону(ДанныеПечати.ОснованиеДляСоставленияАкта, НСтр("ru='приказ, распоряжение'"));
		Область.Параметры.РуководительФИО = ДанныеПечати.Руководитель;
		
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, Область, ДанныеПечати.Ссылка);
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод таблицы приемки товаров
		Область = Макет.ПолучитьОбласть("ТаблицаПриемкиТоваровШапка");
		ТабличныйДокумент.Вывести(Область);
		ТаблицаПриемкиТоваров = ДанныеПечати.ТаблицаПриемкиТоваров.Выгрузить();
		Для Каждого Строка Из ТаблицаПриемкиТоваров Цикл
			Область = Макет.ПолучитьОбласть("ТаблицаПриемкиТоваровСтрока");
			Область.Параметры.Заполнить(Строка);
			ТабличныйДокумент.Вывести(Область);
		КонецЦикла;
		Область = Макет.ПолучитьОбласть("ТаблицаПриемкиТоваровПодвал");
		ТабличныйДокумент.Вывести(Область);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Страница №2 ////////////////////////////////////////////////////////////////////////////////////////////////////
		
		// Вывод шапки
		Область = Макет.ПолучитьОбласть("ШапкаВторойСтраницы");
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод первой части таблицы товаров
		СтруктураИтогов = Новый Структура("КоличествоПоДокументам, СуммаПоДокументам", 0, 0);
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров1Шапка");
		ТабличныйДокумент.Вывести(Область);
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров1СтрокаТовара");
		ОбластьСертификат = Макет.ПолучитьОбласть("ТаблицаТоваров1СтрокаСертификата");
		Товары = ТаблицаТоваров.НайтиСтроки(Новый Структура("Ссылка", ДанныеПечати.Ссылка));
		НомерСтроки = 1;
		Для Каждого Товар Из Товары Цикл
			Область.Параметры.Заполнить(Товар);
			Если ПечатьВНоменклатуреПоставщика Тогда
				Область.Параметры.Товар = Товар.НоменклатураПоставщика;
			Иначе
				Область.Параметры.Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					Товар.НоменклатураНаименование,
					Товар.ХарактеристикаНаименование
				);
			КонецЕсли;
			Область.Параметры.НомерСтроки = НомерСтроки;
			Область.Параметры.ЗначениеКода = Товар[КолонкаКодов];
			ТабличныйДокумент.Вывести(Область);
			ОбластьСертификат.Параметры.Заполнить(Товар);
			ОбластьСертификат.Параметры.Сертификат = СоответствиеСертификатов[Товар];
			ТабличныйДокумент.Вывести(ОбластьСертификат);
			
			НомерСтроки = НомерСтроки + 1;
			СтруктураИтогов.КоличествоПоДокументам = СтруктураИтогов.КоличествоПоДокументам + Товар.КоличествоПоДокументам;
			СтруктураИтогов.СуммаПоДокументам = СтруктураИтогов.СуммаПоДокументам + Товар.СуммаПоДокументам;
		КонецЦикла;
		// Вывод строк с тарой и тарифом
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров1СтрокаТараТариф");
		Область.Параметры.Наименование = НСтр("ru='Тара'");
		ТабличныйДокумент.Вывести(Область);
		Область.Параметры.Наименование = НСтр("ru='Тариф'");
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров1Подвал");
		Область.Параметры.Заполнить(СтруктураИтогов);
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод Состояния Товара В Момент Осмотра
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "СостояниеТовараВМоментОсмотра", 4);
		// Вывод Способа Определения Количества Товара
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "СпособОпределенияКоличестваТовара", 4);
		// Вывод Заключения О Причинах Образования Недостачи
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "ЗаключениеОПричинахОбразованияНедостачи", 4);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Страница №3 ////////////////////////////////////////////////////////////////////////////////////////////////////
		
		// Вывод шапки
		Область = Макет.ПолучитьОбласть("ШапкаТретьейСтраницы");
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод второй части таблицы товаров
		СтруктураИтогов = Новый Структура("КоличествоПоФакту, СуммаПоФакту, СуммаСНДСПоФакту, СуммаНДСПоФакту, КоличествоОтклонение, СуммаОтклонение", 0, 0, 0, 0, 0, 0);
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров2Шапка");
		ТабличныйДокумент.Вывести(Область);
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров2СтрокаТовара");
		ОбластьСертификат = Макет.ПолучитьОбласть("ТаблицаТоваров2СтрокаСертификата");
		Для Каждого Товар Из Товары Цикл
			Область.Параметры.Заполнить(Товар);
			ТабличныйДокумент.Вывести(Область);
			ТабличныйДокумент.Вывести(ОбластьСертификат);
			
			Для Каждого Итог Из СтруктураИтогов Цикл
				СтруктураИтогов[Итог.Ключ] = Итог.Значение + Товар[Итог.Ключ];
			КонецЦикла;
		КонецЦикла;
		// Вывод строк с тарой и тарифом
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров2СтрокаТовара");
		ТабличныйДокумент.Вывести(Область);
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров2Подвал");
		Область.Параметры.Заполнить(СтруктураИтогов);
		ТабличныйДокумент.Вывести(Область);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Страница №4 ////////////////////////////////////////////////////////////////////////////////////////////////////
		
		// Вывод шапки
		Область = Макет.ПолучитьОбласть("ШапкаЧетвертойСтраницы");
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод членов комиссии
		Область = Макет.ПолучитьОбласть("ЧленыКомиссии");
		Область.Параметры.Заполнить(ДанныеПечати);
		Область.Параметры.ПредседательКомиссииФИО = ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.ПредседательКомиссии);
		Область.Параметры.ЧленКомиссии1ФИО = ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.ЧленКомиссии1);
		Область.Параметры.ЧленКомиссии2ФИО = ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.ЧленКомиссии2);
		Область.Параметры.ЧленКомиссии3ФИО = ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.ЧленКомиссии3);
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод заключения комиссии
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "ЗаключениеКомиссии", 4);
		
		Область = Макет.ПолучитьОбласть("Представитель");
		Область.Параметры.Заполнить(ДанныеПечати);
		Область.Параметры.Заполнить(ПолучитьСтруктуруПоДате("ДатаВыдачиДокументаУдостоверяющегоПолномочияПредставителяПартнера", ДанныеПечати.ДатаВыдачиДокументаУдостоверяющегоПолномочияПредставителяПартнера));
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("ГлавныйБухгалтер");
		Область.Параметры.Заполнить(ДанныеПечати);
		Область.Параметры.ГлавныйБухгалтерФИО = ДанныеПечати.ГлавныйБухгалтер;
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод решения руководителя
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "РешениеРуководителя", 4);
		
		// Вывод кладовщика
		Область = Макет.ПолучитьОбласть("Кладовщик");
		Область.Параметры.КладовщикПринявшийТоварФИО = ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.КладовщикПринявшийТовар);
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
КонецПроцедуры

// Выводит многострочное поле в табличный документ
//
// Параметры:
// 	ТабличныйДокумент - ТабличныйДокумент
// 	Макет - ТабличныйДокумент - Макет табличного документа
// 	ДанныеПечати - ВыборкаИзРезультатаЗапроса
// 	ИмяПоля - Строка - Имя реквизита результата запроса, имя области макета, имя параметра области макета
// 	КоличествоСтрокРучногоВвода - Число - Количество строк ручного заполнения
//
Процедура ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, ИмяПоля, КоличествоСтрокРучногоВвода=4)
	
	Область = Макет.ПолучитьОбласть(ИмяПоля);
	ТабличныйДокумент.Вывести(Область);
	Если ЗначениеЗаполнено(ДанныеПечати[ИмяПоля]) Тогда
		
		Область = Макет.ПолучитьОбласть("МногострочноеПоле");
		Область.Параметры.Значение = ДанныеПечати[ИмяПоля];
		ТабличныйДокумент.Вывести(Область);
		
	Иначе
		ОбластьСтрокиРучногоЗаполнения = Макет.ПолучитьОбласть("СтрокаРучногоЗаполнения");
		Для Счет =1 По КоличествоСтрокРучногоВвода Цикл
			ТабличныйДокумент.Вывести(ОбластьСтрокиРучногоЗаполнения);
		КонецЦикла;
		
	КонецЕсли;
	
	ОбластьСтрокиПробела = Макет.ПолучитьОбласть("СтрокаПробела");
	ТабличныйДокумент.Вывести(ОбластьСтрокиПробела);
	
КонецПроцедуры

// Получает заполненную структуру по дате и имени параметра
//
// Параметры:
// 	ИмяПараметра - Строка - Строка префикса имён параметров структуры
// 	Дата - Дата - Дата, которую необходимо разбить на параметры
//
// Возвращаемое значение:
// 	СтруктураДаты - Структура, содержащая данные даты
//
Функция ПолучитьСтруктуруПоДате(ИмяПараметра,Дата)
	
	СтруктураДаты = Новый Структура;
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Возврат СтруктураДаты;
	КонецЕсли;
	СтруктураДаты.Вставить(ИмяПараметра+"День", День(Дата));
	СтруктураДаты.Вставить(ИмяПараметра+"Месяц", Сред(Формат(Дата,"ДФ=dd.MMMM"), 4));
	СтруктураДаты.Вставить(ИмяПараметра+"Год", Формат(Год(Дата), "ЧГ=0"));
	
	Возврат СтруктураДаты;
	
КонецФункции

// Возвращает реквизит или шаблонную строку
//
// Параметры:
// 	Значение - ПроизвольноеЗначение - Значение реквизита документа для преобразования
// 	ШаблонПустогоЗначения - Строка - Строка, возвращаемая в случае если параметр "Значение" на заполнен
//
// Возвращаемое значение:
// 	Строка
//
Функция ПолучитьЗначениеРеквизитаПоШаблону(Значение, ШаблонПустогоЗначения)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		Возврат Строка(Значение);
	Иначе
		Возврат ШаблонПустогоЗначения;
	КонецЕсли;
	
КонецФункции

// Процедура формирует текст запроса по данным для заполнения форм ТОРГ-1
//
// Параметры:
// 	ТекстЗапроса - Строка - Строка для записи текста запроса
//
Процедура СформироватьТекстЗапросаПоДокументам(ТекстЗапроса)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АктПриемкиТоваров.Ссылка КАК Ссылка,
	|	АктПриемкиТоваров.Номер КАК НомерДокумента,
	|	АктПриемкиТоваров.Дата КАК ДатаДокумента,
	|	АктПриемкиТоваров.Организация КАК Организация,
	|	АктПриемкиТоваров.Организация.Префикс КАК Префикс,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	АктПриемкиТоваров.Подразделение КАК ПредставлениеПодразделения,
	|	АктПриемкиТоваров.МестоПриемкиТовара КАК МестоПриемкиТовара,
	|	АктПриемкиТоваров.ОснованиеДляСоставленияАкта КАК ОснованиеДляСоставленияАкта,
	|	АктПриемкиТоваров.ДатаДоставкиТоваров КАК ДатаДоставкиТоваров,
	|	АктПриемкиТоваров.ДатаОтправленияТоваров КАК ДатаОтправленияТоваров,
	|	АктПриемкиТоваров.ТемператураВТолщеМясаРыбы КАК ТемператураВТолщеМясаРыбы,
	|	АктПриемкиТоваров.ПереченьСопроводительныхДокументов КАК ПереченьСопроводительныхДокументов,
	|	АктПриемкиТоваров.ВызываемыйПредставительПартнера КАК ВызваемыйПредставительПартнера,
	|	АктПриемкиТоваров.ВидДокументаВызоваПредставителяПартнера КАК ВидДокументаОВызовеПредставителяПартнера,
	|	АктПриемкиТоваров.НомерДокументаОВызовеПредставителяПартнера КАК НомерДокументаОВызовеПредставителяПартнера,
	|	АктПриемкиТоваров.ДатаДокументаОВызовеПредставителяПартнера КАК ДатаДокументаОВызовеПредставителяПартнера,
	|	АктПриемкиТоваров.Грузоотправитель КАК Грузоотправитель,
	|	АктПриемкиТоваров.Производитель КАК Производитель,
	|	АктПриемкиТоваров.Контрагент КАК Поставщик,
	|	АктПриемкиТоваров.КонтрагентСтраховойКомпании КАК СтраховаяКомпания,
	|	АктПриемкиТоваров.НомерДоговораПоставки КАК НомерДоговораПоставки,
	|	АктПриемкиТоваров.ДатаДоговораПоставки КАК ДатаДоговораПоставки,
	|	АктПриемкиТоваров.ДокументОснование.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	АктПриемкиТоваров.ДокументОснование.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	АктПриемкиТоваров.НомерКоммерческогоАкта КАК НомерКоммерческогоАкта,
	|	АктПриемкиТоваров.ДатаКоммерческогоАкта КАК ДатаКоммерческогоАкта,
	|	АктПриемкиТоваров.НомерВетеринарногоСвидетельства КАК НомерВетеринарногоСвидетельства,
	|	АктПриемкиТоваров.ДатаВетеринарногоСвидетельства КАК ДатаВетеринарногоСвидетельства,
	|	АктПриемкиТоваров.НомерЖелезнодорожнойНакладной КАК НомерЖелезнодорожнойНакладной,
	|	АктПриемкиТоваров.ДатаЖелезнодорожнойНакладной КАК ДатаЖелезнодорожнойНакладной,
	|	АктПриемкиТоваров.СпособДоставки КАК СпособДоставки,
	|	АктПриемкиТоваров.НомерТранспортногоСредства КАК НомерТранспортногоСредства,
	|	АктПриемкиТоваров.ДатаОтправленияТоваров КАК ДатаОтправленияТовара,
	|	АктПриемкиТоваров.ПунктОтправления КАК ПунктОтправления,
	|	АктПриемкиТоваров.СкладОтправителяТовара КАК СкладОтправителяТовара,
	|	АктПриемкиТоваров.СпособОпределенияКоличества КАК СпособОпределенияКоличестваТовара,
	|	АктПриемкиТоваров.ЗаключениеКомиссии КАК ЗаключениеКомиссии,
	|	АктПриемкиТоваров.Приложение КАК Приложение,
	|	АктПриемкиТоваров.ДокументУдостоверяющийПолномочияПредставителяПартнера КАК ДокументУдостоверяющийПолномочияПредставителяПартнера,
	|	АктПриемкиТоваров.НомерДокументаУдостоверяющегоПолномочияПредставителяПартнера КАК НомерДокументаУдостоверяющегоПолномочияПредставителяПартнера,
	|	АктПриемкиТоваров.ДатаВыдачиДокументаУдостоверяющегоПолномочияПредставителяПартнера КАК ДатаВыдачиДокументаУдостоверяющегоПолномочияПредставителяПартнера,
	|	АктПриемкиТоваров.КоличествоЛистовПриложения КАК КоличествоЛистовПриложения,
	|	АктПриемкиТоваров.РешениеРуководителя КАК РешениеРуководителя,
	|	АктПриемкиТоваров.КладовщикПринявшийТовар КАК КладовщикПринявшийТовар,
	|	АктПриемкиТоваров.ПредседательКомиссии КАК ПредседательКомиссии,
	|	АктПриемкиТоваров.ДолжностьПредседателяКомиссии КАК ДолжностьПредседателяКомиссии,
	|	АктПриемкиТоваров.ЧленКомиссии1 КАК ЧленКомиссии1,
	|	АктПриемкиТоваров.ДолжностьЧленаКомиссии1 КАК ДолжностьЧленаКомиссии1,
	|	АктПриемкиТоваров.ЧленКомиссии2 КАК ЧленКомиссии2,
	|	АктПриемкиТоваров.ДолжностьЧленаКомиссии2 КАК ДолжностьЧленаКомиссии2,
	|	АктПриемкиТоваров.ЧленКомиссии3 КАК ЧленКомиссии3,
	|	АктПриемкиТоваров.ДолжностьЧленаКомиссии3 КАК ДолжностьЧленаКомиссии3,
	|	АктПриемкиТоваров.ПриемкаТоваров.(
	|		НомерСтроки КАК НомерСтроки,
	|		ДатаПрибытия КАК ДатаПрибытия,
	|		ДатаНачалаПриемки КАК ДатаНачалаПриемки,
	|		ДатаПриостановленияПриемки КАК ДатаПриостановленияПриемки,
	|		ДатаВозобновленияПриемки КАК ДатаВозабновленияПриемки,
	|		ДатаОкончанияПриемки КАК ДатаОкончанияПриемки
	|	) КАК ТаблицаПриемкиТоваров,
	|	АктПриемкиТоваров.СостояниеТовараТарыИУпаковкиВМоментОсмотра КАК СостояниеТовараВМоментОсмотра,
	|	АктПриемкиТоваров.ЗаключениеОПричинахИМестеОбразованияНедостачиИзлишковТовара КАК ЗаключениеОПричинахОбразованияНедостачи
	|ИЗ
	|	Документ.АктПриемкиТоваров КАК АктПриемкиТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО АктПриемкиТоваров.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	АктПриемкиТоваров.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктПриемкиТоваров.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслугТовары.НоменклатураПоставщика КАК НоменклатураПоставщика,
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслугТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровУслугТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровУслугТовары.КоличествоУпаковок, 0)) КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТовары.СуммаРучнойСкидки, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Сумма, 0) / ЕСТЬNULL(ПоступлениеТоваровУслугТовары.КоличествоУпаковок, 1)
	|		ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Цена, 0)
	|	КОНЕЦ КАК Цена,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Сумма, 0)) КАК Сумма,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровУслугТовары.СуммаНДС, 0)) КАК СуммаНДС,
	|	ПоступлениеТоваровУслугТовары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ПоступлениеТоваровУслугТовары.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ВЫБОР
	|		КОГДА &ПечатьВНоменклатуреПоставщика = ИСТИНА
	|			ТОГДА ПоступлениеТоваровУслугТовары.НоменклатураПоставщика.Код
	|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Номенклатура.Код
	|	КОНЕЦ КАК Код,
	|	ВЫБОР
	|		КОГДА &ПечатьВНоменклатуреПоставщика = ИСТИНА
	|			ТОГДА ПоступлениеТоваровУслугТовары.НоменклатураПоставщика.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Номенклатура.Артикул
	|	КОНЕЦ КАК Артикул,
	|	ПоступлениеТоваровУслугТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	МИНИМУМ(ПоступлениеТоваровУслугТовары.НомерСтроки + 1000) КАК НомерСтроки
	|ПОМЕСТИТЬ ТоварыПоФакту
	|ИЗ
	|	Документ.АктПриемкиТоваров КАК АктПриемкиТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|		ПО АктПриемкиТоваров.ДокументОснование = ПоступлениеТоваровУслугТовары.Ссылка
	|ГДЕ
	|	АктПриемкиТоваров.Ссылка В(&МассивОбъектов)
	|	И ПоступлениеТоваровУслугТовары.Номенклатура.ТипНоменклатуры В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|СГРУППИРОВАТЬ ПО
	|	АктПриемкиТоваров.Ссылка,
	|	ПоступлениеТоваровУслугТовары.НоменклатураПоставщика,
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТовары.СуммаРучнойСкидки, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Сумма, 0) / ЕСТЬNULL(ПоступлениеТоваровУслугТовары.КоличествоУпаковок, 1)
	|		ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Цена, 0)
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС,
	|	ПоступлениеТоваровУслугТовары.Номенклатура.НаименованиеПолное,
	|	ПоступлениеТоваровУслугТовары.Характеристика.НаименованиеПолное,
	|	ПоступлениеТоваровУслугТовары.Ссылка.ЦенаВключаетНДС,
	|	ВЫБОР
	|		КОГДА &ПечатьВНоменклатуреПоставщика = ИСТИНА
	|			ТОГДА ПоступлениеТоваровУслугТовары.НоменклатураПоставщика.Код
	|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Номенклатура.Код
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ПечатьВНоменклатуреПоставщика = ИСТИНА
	|			ТОГДА ПоступлениеТоваровУслугТовары.НоменклатураПоставщика.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Номенклатура.Артикул
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслугТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровУслугТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Упаковка
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктПриемкиТоваров.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.НоменклатураПоставщика, ПоступлениеТоваровУслугТовары.НоменклатураПоставщика) КАК НоменклатураПоставщика,
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура, ПоступлениеТоваровУслугТовары.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Характеристика, ПоступлениеТоваровУслугТовары.Характеристика) КАК Характеристика,
	|	ВЫБОР
	|		КОГДА (НЕ ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка ЕСТЬ NULL )
	|				И (НЕ ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка.Коэффициент, 1) = 1)
	|			ТОГДА ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка
	|		КОГДА (НЕ ПоступлениеТоваровУслугТовары.Упаковка ЕСТЬ NULL )
	|				И (НЕ ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Упаковка.Коэффициент, 1) = 1)
	|			ТОГДА ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка, ПоступлениеТоваровУслугТовары.Упаковка) = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура.ЕдиницаИзмерения, ПоступлениеТоваровУслугТовары.Номенклатура.ЕдиницаИзмерения)
	|		ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка.ЕдиницаИзмерения, ПоступлениеТоваровУслугТовары.Упаковка.ЕдиницаИзмерения)
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	СУММА(ЕСТЬNULL(ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.КоличествоУпаковок, ПоступлениеТоваровУслугТовары.КоличествоУпаковок), 0)) КАК Количество,
	|	ЕСТЬNULL(ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Цена, ПоступлениеТоваровУслугТовары.Цена), 0) КАК Цена,
	|	СУММА(ЕСТЬNULL(ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Сумма, ПоступлениеТоваровУслугТовары.Сумма), 0)) КАК Сумма,
	|	ЕСТЬNULL(ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.СтавкаНДС, ПоступлениеТоваровУслугТовары.СтавкаНДС), 0) КАК СтавкаНДС,
	|	СУММА(ЕСТЬNULL(ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.СуммаНДС, ПоступлениеТоваровУслугТовары.СуммаНДС), 0)) КАК СуммаНДС,
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура.НаименованиеПолное, ПоступлениеТоваровУслугТовары.Номенклатура.НаименованиеПолное) КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Характеристика.НаименованиеПолное, ПоступлениеТоваровУслугТовары.Характеристика.НаименованиеПолное) КАК ХарактеристикаНаименование,
	|	ВЫБОР
	|		КОГДА &ПечатьВНоменклатуреПоставщика = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.НоменклатураПоставщика.Код, ПоступлениеТоваровУслугТовары.НоменклатураПоставщика.Код)
	|		ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура.Код, ПоступлениеТоваровУслугТовары.Номенклатура.Код)
	|	КОНЕЦ КАК Код,
	|	ВЫБОР
	|		КОГДА &ПечатьВНоменклатуреПоставщика = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.НоменклатураПоставщика.Артикул, ПоступлениеТоваровУслугТовары.НоменклатураПоставщика.Артикул)
	|		ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура.Артикул, ПоступлениеТоваровУслугТовары.Номенклатура.Артикул)
	|	КОНЕЦ КАК Артикул,
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Ссылка.ЦенаВключаетНДС, ПоступлениеТоваровУслугТовары.Ссылка.ЦенаВключаетНДС) КАК ЦенаВключаетНДС,
	|	МИНИМУМ(ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.НомерСтроки, ПоступлениеТоваровУслугТовары.НомерСтроки)) КАК НомерСтроки
	|ПОМЕСТИТЬ ТоварыПоДокументам
	|ИЗ
	|	Документ.АктПриемкиТоваров КАК АктПриемкиТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.ТоварыПоДаннымПоставщика КАК ПоступлениеТоваровУслугТоварыПоДаннымПоставщика
	|		ПО АктПриемкиТоваров.ДокументОснование = ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Ссылка
	|			И (АктПриемкиТоваров.ДокументОснование.ЕстьРасхождения = ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|		ПО АктПриемкиТоваров.ДокументОснование = ПоступлениеТоваровУслугТовары.Ссылка
	|			И (АктПриемкиТоваров.ДокументОснование.ЕстьРасхождения = ЛОЖЬ)
	|ГДЕ
	|	АктПриемкиТоваров.Ссылка В(&МассивОбъектов)
	|	И (ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура.ТипНоменклатуры В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ИЛИ ПоступлениеТоваровУслугТовары.Номенклатура.ТипНоменклатуры В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)))
	|
	|СГРУППИРОВАТЬ ПО
	|	АктПриемкиТоваров.Ссылка,
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.НоменклатураПоставщика, ПоступлениеТоваровУслугТовары.НоменклатураПоставщика),
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура, ПоступлениеТоваровУслугТовары.Номенклатура),
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Характеристика, ПоступлениеТоваровУслугТовары.Характеристика),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка, ПоступлениеТоваровУслугТовары.Упаковка) = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура.ЕдиницаИзмерения, ПоступлениеТоваровУслугТовары.Номенклатура.ЕдиницаИзмерения)
	|		ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка.ЕдиницаИзмерения, ПоступлениеТоваровУслугТовары.Упаковка.ЕдиницаИзмерения)
	|	КОНЕЦ,
	|	ЕСТЬNULL(ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Цена, ПоступлениеТоваровУслугТовары.Цена), 0),
	|	ЕСТЬNULL(ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.СтавкаНДС, ПоступлениеТоваровУслугТовары.СтавкаНДС), 0),
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура.НаименованиеПолное, ПоступлениеТоваровУслугТовары.Номенклатура.НаименованиеПолное),
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Характеристика.НаименованиеПолное, ПоступлениеТоваровУслугТовары.Характеристика.НаименованиеПолное),
	|	ВЫБОР
	|		КОГДА &ПечатьВНоменклатуреПоставщика = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.НоменклатураПоставщика.Код, ПоступлениеТоваровУслугТовары.НоменклатураПоставщика.Код)
	|		ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура.Код, ПоступлениеТоваровУслугТовары.Номенклатура.Код)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ПечатьВНоменклатуреПоставщика = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.НоменклатураПоставщика.Артикул, ПоступлениеТоваровУслугТовары.НоменклатураПоставщика.Артикул)
	|		ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Номенклатура.Артикул, ПоступлениеТоваровУслугТовары.Номенклатура.Артикул)
	|	КОНЕЦ,
	|	ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Ссылка.ЦенаВключаетНДС, ПоступлениеТоваровУслугТовары.Ссылка.ЦенаВключаетНДС),
	|	ВЫБОР
	|		КОГДА (НЕ ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка ЕСТЬ NULL )
	|				И (НЕ ЕСТЬNULL(ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка.Коэффициент, 1) = 1)
	|			ТОГДА ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка
	|		КОГДА (НЕ ПоступлениеТоваровУслугТовары.Упаковка ЕСТЬ NULL )
	|				И (НЕ ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Упаковка.Коэффициент, 1) = 1)
	|			ТОГДА ПоступлениеТоваровУслугТоварыПоДаннымПоставщика.Упаковка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТоварыПоДокументам.Ссылка, ТоварыПоФакту.Ссылка) КАК Ссылка,
	|	ЕСТЬNULL(ТоварыПоДокументам.НоменклатураПоставщика, ТоварыПоФакту.НоменклатураПоставщика) КАК НоменклатураПоставщика,
	|	ЕСТЬNULL(ТоварыПоДокументам.Номенклатура, ТоварыПоФакту.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ТоварыПоДокументам.Характеристика, ТоварыПоФакту.Характеристика) КАК Характеристика,
	|	ЕСТЬNULL(ТоварыПоДокументам.Упаковка, ТоварыПоФакту.Упаковка) КАК Упаковка,
	|	ЕСТЬNULL(ТоварыПоДокументам.Цена, ТоварыПоФакту.Цена) КАК ЦенаПоДокументам,
	|	ЕСТЬNULL(ТоварыПоФакту.Цена, ТоварыПоДокументам.Цена) КАК ЦенаПоФакту,
	|	ЕСТЬNULL(ТоварыПоДокументам.Количество, 0) КАК КоличествоПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.Сумма, 0) КАК СуммаПоДокументам,
	|	ТоварыПоДокументам.СтавкаНДС КАК СтавкаНДСПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.СуммаНДС, 0) КАК СуммаНДСПоДокументам,
	|	ЕСТЬNULL(ТоварыПоФакту.Количество, 0) КАК КоличествоПоФакту,
	|	ЕСТЬNULL(ТоварыПоФакту.Сумма, 0) КАК СуммаПоФакту,
	|	ТоварыПоФакту.СтавкаНДС КАК СтавкаНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоФакту.СуммаНДС, 0) КАК СуммаНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоДокументам.Код, ТоварыПоФакту.Код) КАК Код,
	|	ЕСТЬNULL(ТоварыПоДокументам.Артикул, ТоварыПоФакту.Артикул) КАК Артикул,
	|	ЕСТЬNULL(ТоварыПоДокументам.ЕдиницаИзмерения, ТоварыПоФакту.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ТоварыПоДокументам.ЕдиницаИзмерения.Код, ТоварыПоФакту.ЕдиницаИзмерения.Код) КАК КодПоОКЕИ,
	|	ЕСТЬNULL(ТоварыПоФакту.Количество, 0) - ЕСТЬNULL(ТоварыПоДокументам.Количество, 0) КАК КоличествоОтклонение,
	|	ЕСТЬNULL(ТоварыПоФакту.Сумма, 0) - ЕСТЬNULL(ТоварыПоДокументам.Сумма, 0) КАК СуммаОтклонение,
	|	ЕСТЬNULL(ТоварыПоДокументам.НоменклатураНаименование, ТоварыПоФакту.НоменклатураНаименование) КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТоварыПоДокументам.ХарактеристикаНаименование, ТоварыПоФакту.ХарактеристикаНаименование) КАК ХарактеристикаНаименование,
	|	ВЫБОР
	|		КОГДА (НЕ ТоварыПоФакту.ЦенаВключаетНДС ЕСТЬ NULL )
	|				И ТоварыПоФакту.ЦенаВключаетНДС
	|			ТОГДА ЕСТЬNULL(ТоварыПоФакту.Сумма, 0)
	|		ИНАЧЕ ЕСТЬNULL(ТоварыПоФакту.Сумма, 0) + ЕСТЬNULL(ТоварыПоФакту.СуммаНДС, 0)
	|	КОНЕЦ КАК СуммаСНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоДокументам.ЦенаВключаетНДС, ЛОЖЬ) КАК ЦенаВключаетНДСПоДокументам,
	|	ЕСТЬNULL(ТоварыПоФакту.ЦенаВключаетНДС, ЛОЖЬ) КАК ЦенаВключаетНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоДокументам.НомерСтроки, ТоварыПоФакту.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	ТоварыПоДокументам КАК ТоварыПоДокументам
	|		ПОЛНОЕ СОЕДИНЕНИЕ ТоварыПоФакту КАК ТоварыПоФакту
	|		ПО ТоварыПоДокументам.Ссылка = ТоварыПоФакту.Ссылка
	|			И ТоварыПоДокументам.НоменклатураПоставщика = ТоварыПоФакту.НоменклатураПоставщика
	|			И ТоварыПоДокументам.Номенклатура = ТоварыПоФакту.Номенклатура
	|			И ТоварыПоДокументам.Характеристика = ТоварыПоФакту.Характеристика
	|			И ТоварыПоДокументам.Упаковка = ТоварыПоФакту.Упаковка
	|			И ТоварыПоДокументам.Цена = ТоварыПоФакту.Цена
	|			И ТоварыПоДокументам.ЕдиницаИзмерения = ТоварыПоФакту.ЕдиницаИзмерения
	|			И ТоварыПоДокументам.СтавкаНДС = ТоварыПоФакту.СтавкаНДС
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктПриемкиТоваров.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслугТовары.НоменклатураПоставщика КАК НоменклатураПоставщика,
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Характеристика КАК Характеристика,
	|	ПоступлениеТоваровУслугТовары.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслугТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровУслугТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТовары.СуммаРучнойСкидки, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Сумма, 0) / ЕСТЬNULL(ПоступлениеТоваровУслугТовары.КоличествоУпаковок, 1)
	|		ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Цена, 0)
	|	КОНЕЦ КАК Цена,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
	|	ПоступлениеТоваровУслугТовары.Сертификат КАК Сертификат
	|ИЗ
	|	Документ.АктПриемкиТоваров КАК АктПриемкиТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|		ПО АктПриемкиТоваров.ДокументОснование = ПоступлениеТоваровУслугТовары.Ссылка
	|ГДЕ
	|	АктПриемкиТоваров.Ссылка В(&МассивОбъектов)
	|	И ПоступлениеТоваровУслугТовары.Номенклатура.ТипНоменклатуры В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ПоступлениеТоваровУслугТовары.Сертификат <> """"
	|
	|СГРУППИРОВАТЬ ПО
	|	АктПриемкиТоваров.Ссылка,
	|	ПоступлениеТоваровУслугТовары.НоменклатураПоставщика,
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Характеристика,
	|	ПоступлениеТоваровУслугТовары.Упаковка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТовары.СуммаРучнойСкидки, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Сумма, 0) / ЕСТЬNULL(ПоступлениеТоваровУслугТовары.КоличествоУпаковок, 1)
	|		ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугТовары.Цена, 0)
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС,
	|	ПоступлениеТоваровУслугТовары.Сертификат,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслугТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровУслугТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыКурсовВалютДокументов.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ПериодыКурсовВалютДокументов.Ссылка.ДокументОснование.Валюта = КурсыВалют.Валюта
	|				ТОГДА ЕСТЬNULL(КурсыВалют.Курс, 1)
	|		КОНЕЦ) КАК КурсВалютыДокумента,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ПериодыКурсовВалютДокументов.Ссылка.ДокументОснование.Валюта = КурсыВалют.Валюта
	|				ТОГДА ЕСТЬNULL(КурсыВалют.Кратность, 1)
	|		КОНЕЦ) КАК КратностьВалютыДокумента,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА &ВалютаРегламентированногоУчета = КурсыВалют.Валюта
	|				ТОГДА ЕСТЬNULL(КурсыВалют.Курс, 1)
	|		КОНЕЦ) КАК КурсВалютыРегламентированногоУчета,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА &ВалютаРегламентированногоУчета = КурсыВалют.Валюта
	|				ТОГДА ЕСТЬNULL(КурсыВалют.Кратность, 1)
	|		КОНЕЦ) КАК КратностьВалютыРегламентированногоУчета,
	|	ВЫБОР
	|		КОГДА &ВалютаРегламентированногоУчета = ПериодыКурсовВалютДокументов.Ссылка.ДокументОснование.Валюта
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПересчетНеТребуется
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеДокумента.Ссылка КАК Ссылка,
	|		МАКСИМУМ(ВЫБОР
	|				КОГДА ДанныеДокумента.ДокументОснование.Валюта = КурсыВалют.Валюта
	|					ТОГДА КурсыВалют.Период
	|			КОНЕЦ) КАК ПериодВалютаДокумента,
	|		МАКСИМУМ(ВЫБОР
	|				КОГДА &ВалютаРегламентированногоУчета = КурсыВалют.Валюта
	|					ТОГДА КурсыВалют.Период
	|			КОНЕЦ) КАК ПериодВалютаРегламентированногоУчета
	|	ИЗ
	|		Документ.АктПриемкиТоваров КАК ДанныеДокумента
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО ДанныеДокумента.Дата >= КурсыВалют.Период
	|				И (ДанныеДокумента.ДокументОснование.Валюта = КурсыВалют.Валюта
	|					ИЛИ &ВалютаРегламентированногоУчета = КурсыВалют.Валюта)
	|	ГДЕ
	|		ДанныеДокумента.Ссылка В(&МассивОбъектов)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДанныеДокумента.Ссылка) КАК ПериодыКурсовВалютДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО (ПериодыКурсовВалютДокументов.Ссылка.ДокументОснование.Валюта = КурсыВалют.Валюта
	|					И ПериодыКурсовВалютДокументов.ПериодВалютаДокумента = КурсыВалют.Период
	|				ИЛИ &ВалютаРегламентированногоУчета = КурсыВалют.Валюта
	|					И ПериодыКурсовВалютДокументов.ПериодВалютаРегламентированногоУчета = КурсыВалют.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПериодыКурсовВалютДокументов.Ссылка,
	|	ВЫБОР
	|		КОГДА &ВалютаРегламентированногоУчета = ПериодыКурсовВалютДокументов.Ссылка.ДокументОснование.Валюта
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	
КонецПроцедуры

Процедура ЗаполнитьСтруктуруПолучателейПечатныхФорм(СтруктураДанныхОбъектаПечати) Экспорт
	
	СтруктураДанныхОбъектаПечати.ОсновнойПолучатель = "Партнер";
	
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Партнер");
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов") Тогда 
		СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Контрагент");
	КонецЕсли;
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Грузоотправитель");
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("СтраховаяКомпания");
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("КонтрагентСтраховойКомпании");
	
КонецПроцедуры

#КонецЕсли