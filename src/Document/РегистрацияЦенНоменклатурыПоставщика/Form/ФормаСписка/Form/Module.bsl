////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Обработчик подсистемы "Внешние обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	ДоступноДляПродажиКлиентам = Параметры.ДоступноДляПродажиКлиентам;
	ДоступноДляЗакупки         = Параметры.ДоступноДляЗакупки;
	
	Если ДоступноДляПродажиКлиентам Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ДоступноДляПродажиКлиентам", Истина, ВидСравненияКомпоновкиДанных.Равно,, Истина);
		Заголовок = НСтр("ru='Цены конкурентов'");
	КонецЕсли;
	
	Если ДоступноДляЗакупки Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ДоступноДляЗакупки", Истина, ВидСравненияКомпоновкиДанных.Равно,, Истина);
		Заголовок = НСтр("ru='Цены поставщиков'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Подсистема "ЭлектронныеДокументы"
	Если ИмяСобытия = "ОбновитьСостояниеЭД" ИЛИ ИмяСобытия = "Запись_РегистрацияЦенНоменклатурыПоставщика" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не Копирование Тогда
		Отказ = Истина;
		
		ПараметрыФормы = Новый Структура();
		
		НайденныеЭлементы = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.Отбор, "Соглашение");
		Если НайденныеЭлементы.Количество() = 1 И НайденныеЭлементы[0].Использование Тогда
			ПараметрыФормы.Вставить("Основание", НайденныеЭлементы[0].ПравоеЗначение);
		Иначе
			НайденныеЭлементы = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.Отбор, "Партнер");
			Если НайденныеЭлементы.Количество() = 1 И НайденныеЭлементы[0].Использование Тогда
				ПараметрыФормы.Вставить("Основание", НайденныеЭлементы[0].ПравоеЗначение);
			КонецЕсли;
		КонецЕсли;
			
		ОткрытьФорму("Обработка.ПомощникРегистрацииЦенПоставщика.Форма.Форма", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СоздатьНовый(Команда)
	
	ПараметрыОтбора = Новый Структура;
	
	Для Каждого ЭлементОтбора из Список.Отбор.Элементы Цикл
		ПараметрыОтбора.Вставить("Соглашение", ЭлементОтбора.ПравоеЗначение);
	КонецЦикла;
	
	ОткрытьФорму("Документ.РегистрацияЦенНоменклатурыПоставщика.Форма.ФормаДокумента", Новый Структура("ЗначенияЗаполнения", ПараметрыОтбора) );
	
КонецПроцедуры

