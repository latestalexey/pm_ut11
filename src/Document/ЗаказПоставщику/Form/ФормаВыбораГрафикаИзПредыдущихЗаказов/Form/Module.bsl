////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.АдресТаблицыШаблонов) Или
		Не ЗначениеЗаполнено(Параметры.КоличествоДокументов)Тогда
		
		ТекстСообщения = НСтр("ru='Непосредственное открытие этой формы не предусмотрено. Для открытия формы можно воспользоваться командой ""Заполнить этапы по предыдущим заказам"" в форме документа ""Заказ поставщику""'");
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
	КоличествоДокументов = Параметры.КоличествоДокументов;
	
	ТаблицаШаблонов = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицыШаблонов);
	
	ШаблоныГрафиковДанные = РеквизитФормыВЗначение("ШаблоныГрафиков");
	
	Для Каждого Шаблон Из ТаблицаШаблонов Цикл
		ЗаполнитьЗначенияСвойств(ШаблоныГрафиковДанные.Добавить(), Шаблон);
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ШаблоныГрафиковДанные, "ШаблоныГрафиков");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТаблицаГрафиковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаГрафиков.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КодГрафика = ТекущиеДанные.КодГрафика;
	
	Закрыть(КодГрафика);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ТОВАРЫ

&НаКлиенте
Процедура ТаблицаГрафиковПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаГрафиков.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаИнформации = НСтр("ru='Используется в %1 из %2 %3, дата последнего использования %4'");
	СтрокаИнформации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		СтрокаИнформации, 
		ТекущиеДанные.ЧастотаИспользования,
		КоличествоДокументов,
		?(КоличествоДокументов%10 = 1, НСтр("ru='заказа'"),НСтр("ru='заказов'")),
		Формат(ТекущиеДанные.ДатаПоследнегоИспользования, "ДЛФ=DD")
	);
	
	Информация = СтрокаИнформации;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаГрафиков.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КодГрафика = ТекущиеДанные.КодГрафика;
	
	Закрыть(КодГрафика);
	
КонецПроцедуры

&НаКлиенте
Процедура УпорядочитьПоЧастоте(Команда)
	
	ШаблоныГрафиков.Сортировать("ЧастотаИспользования Убыв, ДатаПоследнегоИспользования Убыв");
	
	Элементы.УпорядочитьПоЧастоте.Пометка = Истина;
	Элементы.УпорядочитьПоДате.Пометка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УпорядочитьПоДате(Команда)
	
	ШаблоныГрафиков.Сортировать("ДатаПоследнегоИспользования Убыв,ЧастотаИспользования Убыв");
	
	Элементы.УпорядочитьПоЧастоте.Пометка = Ложь;
	Элементы.УпорядочитьПоДате.Пометка = Истина;
	
КонецПроцедуры