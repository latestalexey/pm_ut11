
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТабНоменклатуры = ПолучитьИзВременногоХранилища(Параметры.АдресНоменклатуры);
    ТабНоменклатуры.Колонки.Добавить("Артикул", Новый ОписаниеТипов("Строка"));
	
	// Формируем "маски артикулов"
	Для каждого ТекСтрока Из ТабНоменклатуры Цикл
		ТекСтрока.Артикул = Лев(ТекСтрока.Номенклатура.Артикул, 3) + "_" + Сред(ТекСтрока.Номенклатура.Артикул, 5);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВТ.НомерСтроки,
		|	ВТ.Номенклатура,
		|	ВТ.Артикул
		|ПОМЕСТИТЬ ВремТаб
		|ИЗ
		|	&ВТ КАК ВТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВремТаб.Номенклатура КАК Номенклатура,
		|	Номенклатуры.Ссылка,
		|	ВремТаб.НомерСтроки КАК НомерСтроки,
		|	Номенклатуры.Артикул КАК Артикул
		|ИЗ
		|	ВремТаб КАК ВремТаб
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатуры
		|		ПО ВремТаб.Номенклатура <> Номенклатуры.Ссылка
		|			И (Номенклатуры.Артикул ПОДОБНО ВремТаб.Артикул)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки,
		|	Артикул
		|ИТОГИ
		|	МАКСИМУМ(НомерСтроки)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("ВТ", ТабНоменклатуры);
	
	ВыборкаДок = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    Дерево = РеквизитФормыВЗначение("ВариантыЗамены");
	Пока ВыборкаДок.Следующий() Цикл
		НовСтрока = Дерево.Строки.Добавить();	
		НовСтрока.Выбрана = Истина;
		НовСтрока.Номенклатура = ВыборкаДок.Номенклатура;
		НовСтрока.НомерСтроки = ВыборкаДок.НомерСтроки;
		НовСтрока.Группа = Сред(ВыборкаДок.Номенклатура.Артикул, 4, 1);
		
		Выборка = ВыборкаДок.Выбрать();
		Пока Выборка.Следующий() Цикл
			НовСтрокаП = НовСтрока.Строки.Добавить();	
			НовСтрокаП.Выбрана = Ложь;
			НовСтрокаП.Номенклатура = Выборка.Ссылка;
			НовСтрокаП.Группа = Сред(Выборка.Артикул, 4, 1);
		КонецЦикла;
	КонецЦикла;

	ЗначениеВРеквизитФормы(Дерево, "ВариантыЗамены");
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыЗаменыВыбранаПриИзменении(Элемент)
	
	ТекДанные = Элементы.ВариантыЗамены.ТекущиеДанные;
	ТекРодитель = ТекДанные.ПолучитьРодителя();
	Если ТекРодитель = Неопределено Тогда 
		Если ТекДанные.Выбрана Тогда
			// Установили "флаг" у корневого элемента - снимаем у подчиненных
			СброситьПометку(ТекДанные);
			НоменклатурКИзменению = НоменклатурКИзменению - 1;
		Иначе
			// Сняли "флаг" у корневого элемента - возвращаем его
			ТекДанные.Выбрана = Истина;
		КонецЕсли;	
	Иначе
		Если ТекДанные.Выбрана Тогда
			Если ТекРодитель.Выбрана Тогда
				ТекРодитель.Выбрана = Ложь;
				НоменклатурКИзменению = НоменклатурКИзменению + 1;
			Иначе
				СброситьПометку(ТекРодитель);
				ТекДанные.Выбрана = Истина;
			КонецЕсли;
		Иначе
			// Сняли "флаг" у подчиненного элемента - устанавливаем родителю
			ТекРодитель.Выбрана = Истина;
			НоменклатурКИзменению = НоменклатурКИзменению - 1;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьПометку(ТекДанные)

	Для каждого Дочерний Из ТекДанные.ПолучитьЭлементы() Цикл
		Дочерний.Выбрана = Ложь;			
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВДокументе(Команда)
	
	ЭтаФорма.Закрыть(АдресНоменклатурыКЗамене());
	
КонецПроцедуры

// Функция помещает во временное хранилище перечень номенклатуры для замены
//
&НаСервере
Функция АдресНоменклатурыКЗамене()

	ТабЗамены = Новый Массив;

	Для каждого ТекСтрока Из ВариантыЗамены.ПолучитьЭлементы() Цикл
		Если Не ТекСтрока.Выбрана Тогда
			Для каждого ТекВариант Из ТекСтрока.ПолучитьЭлементы() Цикл
				Если ТекВариант.Выбрана Тогда
					ТабЗамены.Добавить(Новый Структура("НомерСтроки, Номенклатура", ТекСтрока.НомерСтроки, ТекВариант.Номенклатура));

					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат ?(ТабЗамены = Неопределено, Неопределено, ПоместитьВоВременноеХранилище(ТабЗамены));	

КонецФункции // АдресТоварыПредварительно()

// Выделяем все по текущему варианту
//
&НаКлиенте
Процедура ВыделитьПоКритерию(Команда)
	
	Если ПустаяСтрока(Выделить) Тогда
		Возврат;	
	КонецЕсли;
	
	НоменклатурКИзменению = 0;
	
	Для каждого ТекСтрока Из ВариантыЗамены.ПолучитьЭлементы() Цикл
		Если Не ТекСтрока.Выбрана И ТекСтрока.Группа = Выделить Тогда
			ТекСтрока.Выбрана = Истина;
			СброситьПометку(ТекСтрока);
		ИначеЕсли Не ТекСтрока.Группа = Выделить Тогда
			Для каждого ТекВариант Из ТекСтрока.ПолучитьЭлементы() Цикл
				Если ТекВариант.Группа = Выделить И ТекВариант.Выбрана Тогда
					Прервать;
				ИначеЕсли ТекВариант.Группа = Выделить И Не ТекВариант.Выбрана Тогда
					ТекСтрока.Выбрана = Ложь;
					СброситьПометку(ТекСтрока);
					ТекВариант.Выбрана = Истина;
				    Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		НоменклатурКИзменению = НоменклатурКИзменению + ?(ТекСтрока.Выбрана, 0, 1);
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//Элементы.ВариантыЗамены.Развернут(0);
КонецПроцедуры

