&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если НЕ УправлениеПечатьюКлиент.ПроверитьДокументыПроведены(ПараметрКоманды, ПараметрыВыполненияКоманды.Источник) Тогда
		Возврат;
	КонецЕсли;
	
	СоставДокументов = ПараметрКоманды;
	
	Состояние(НСтр("ru = 'Выполняется формирование печатных форм'"));
	
	ИмяМакета = "ПФ_DOC_КоммерческоеПредложение";
	ТипМакета = "doc";
	
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.ПолучитьМакетыИДанныеОбъектов("Документ.КоммерческоеПредложениеКлиенту",
		ИмяМакета,
		СоставДокументов);
		
	ДвоичныеДанныеМакетов = МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	
	Секции = МакетИДанныеОбъекта.Макеты.ОписаниеСекций;
	
	Для Каждого ДокументСсылка Из СоставДокументов Цикл
		
		ДанныеОбъекта = МакетИДанныеОбъекта.Данные[ДокументСсылка][ИмяМакета];
		
		Попытка
			
			ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета);
			Макет = УправлениеПечатьюКлиент.ИнициализироватьМакет(
				ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета
			);
			
			Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Секции[ИмяМакета]["ВерхнийКолонтитул"]);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта);
			
			Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет,Секции[ИмяМакета].Заголовок);
			УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
			УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.КонтактноеЛицо) Тогда
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет,Секции[ИмяМакета].КонтактноеЛицо);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет,Секции[ИмяМакета].Предложение);
			УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
			УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			
			Если ДанныеОбъекта.ЕстьСкидки Тогда
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет,Секции[ИмяМакета].ШапкаТаблицаТовары);
			Иначе
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет,Секции[ИмяМакета].ШапкаТаблицаТоварыБезСкидки);
			КонецЕсли;
			УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
			УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			
			Если ДанныеОбъекта.ЕстьСкидки Тогда
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Секции[ИмяМакета].СтрокаТаблицаТовары);
			Иначе
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Секции[ИмяМакета].СтрокаТаблицаТоварыБезСкидки);
			КонецЕсли;
			УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма,Область, ДанныеОбъекта.Товары, Ложь);
			
			Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет,Секции[ИмяМакета].ИтоговаяСтрока);
			УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
			УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.СрокДействия) Тогда
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет,Секции[ИмяМакета].СрокДействия);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ГрафикОплаты)
				ИЛИ ЗначениеЗаполнено(ДанныеОбъекта.ФормаОплаты)
				ИЛИ ЗначениеЗаполнено(ДанныеОбъекта.Склад)
				ИЛИ ЗначениеЗаполнено(ДанныеОбъекта.ДополнительнаяИнформация) Тогда
				
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет,Секции[ИмяМакета].ЗаголовокДопИнформации);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ГрафикОплаты) Тогда
				
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Секции[ИмяМакета].ГрафикОплаты);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ФормаОплаты) Тогда
				
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Секции[ИмяМакета].ФормаОплаты);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.Склад) Тогда
				
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Секции[ИмяМакета].Склад);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ДополнительнаяИнформация) Тогда
				
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Секции[ИмяМакета].ДополнительнаяИнформация);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
				
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Секции[ИмяМакета].Подвал);
			УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
			УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ТелефонОрганизации) Тогда
				
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Секции[ИмяМакета].ТелефонОрганизации);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.АдресЭлектроннойПочтыМенеджера) Тогда
				
				Область = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Секции[ИмяМакета].АдресЭлектроннойПочтыМенеджера);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
				
			КонецЕсли;
			
			УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);
			
		Исключение
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма);
			УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
			
			Возврат;
			
		КонецПопытки;
		
		УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, Ложь);
		УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
		
	КонецЦикла;
	
	Состояние(НСтр("ru = 'Формирование печатных форм завершено'"));
	
КонецПроцедуры
