#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ 

Процедура ПередЗаписью(Отказ, Замещение)
	
	Организация = ЭтотОбъект.Отбор.Организация.Значение;
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ОбособленноеПодразделение") = Истина
		И ЭтотОбъект.ДополнительныеСвойства.Свойство("СинхронизацияНастроек") <> Истина Тогда
		
		ТекстОшибки = НСтр("ru='Изменение настроек системы налогообложения необходимо выполнять из головной организации.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	СинхронизироватьНастройкиОбособленныхПодразделений(ЭтотОбъект, Отказ);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

Процедура СинхронизироватьНастройкиОбособленныхПодразделений(НастройкиГоловнойОрганизации, Отказ)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеПодразделенияВыделенныеНаБаланс")
		Или ЭтотОбъект.ДополнительныеСвойства.Свойство("СинхронизацияНастроек") = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ОбособленноеПодразделение
		|	И Организации.ГоловнаяОрганизация = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", НастройкиГоловнойОрганизации.Отбор.Организация.Значение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НастройкиОбособленногоПодразделения = РегистрыСведений.СистемыНалогообложенияОрганизаций.СоздатьНаборЗаписей();
		НастройкиОбособленногоПодразделения.Отбор.Организация.Установить(Выборка.Ссылка);
		
		ОтборПериод = НастройкиГоловнойОрганизации.Отбор.Период;
		Если ОтборПериод.Использование Тогда
			НастройкиОбособленногоПодразделения.Отбор.Период.Установить(ОтборПериод.Значение);
		КонецЕсли;
		
		Для Каждого НастройкаГоловнойОрганизации Из НастройкиГоловнойОрганизации Цикл
			
			НастройкаОбособленногоПодразделения = НастройкиОбособленногоПодразделения.Добавить();
			ЗаполнитьЗначенияСвойств(НастройкаОбособленногоПодразделения, НастройкаГоловнойОрганизации);
			НастройкаОбособленногоПодразделения.Организация = Выборка.Ссылка;
			
		КонецЦикла;
		
		Попытка
			
			НастройкиОбособленногоПодразделения.ДополнительныеСвойства.Вставить("СинхронизацияНастроек", Истина);
			НастройкиОбособленногоПодразделения.Записать();
			
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось изменить настройки системы налогообложения. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			
			Возврат;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры
#КонецЕсли