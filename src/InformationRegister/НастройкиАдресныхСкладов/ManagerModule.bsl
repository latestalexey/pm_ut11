#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

//Процедура добавляет в регистр настройку подпитки по умолчанию, если
//еще нет настроек для склада (помещения) с адресным хранением
//	Параметры:
//		Склад - СправочникСсылка.Склады
//		Помещение - СправочникСсылка.СкладскиеПомещения
//
Процедура УстановитьНастройкиПоУмолчанию(Склад, Помещение, ИспользоватьАдресноеХранение, ИспользоватьАдресноеХранениеСправочно, ИспользоватьРабочиеУчастки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиАдресныхСкладов.Склад,
	|	НастройкиАдресныхСкладов.Помещение,
	|	НастройкиАдресныхСкладов.ГлубинаАнализа,
	|	НастройкиАдресныхСкладов.МинимальнаяВероятностьОтгрузки,
	|	НастройкиАдресныхСкладов.УровеньОбслуживанияУпаковокКлассаX,
	|	НастройкиАдресныхСкладов.УровеньОбслуживанияУпаковокКлассаY,
	|	НастройкиАдресныхСкладов.УровеньОбслуживанияУпаковокКлассаZ,
	|	НастройкиАдресныхСкладов.ИспользоватьПодпитку,
	|	НастройкиАдресныхСкладов.РегламентноеЗадание,
	|	НастройкиАдресныхСкладов.ИспользоватьАдресноеХранение,
	|	НастройкиАдресныхСкладов.ИспользоватьАдресноеХранениеСправочно,
	|	НастройкиАдресныхСкладов.ИспользоватьРабочиеУчастки
	|ИЗ
	|	РегистрСведений.НастройкиАдресныхСкладов КАК НастройкиАдресныхСкладов
	|ГДЕ
	|	НастройкиАдресныхСкладов.Склад = &Склад
	|	И НастройкиАдресныхСкладов.Помещение = &Помещение";
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Помещение", Помещение);
	
	Настройки = Запрос.Выполнить().Выбрать();
		
	Набор = РегистрыСведений.НастройкиАдресныхСкладов.СоздатьНаборЗаписей();
	Набор.Отбор.Склад.Установить(Склад);
	Набор.Отбор.Помещение.Установить(Помещение);
	
	СтрокаНастроек = Набор.Добавить();
	Если Настройки.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтрокаНастроек, Настройки);
	Иначе
		СтрокаНастроек.Склад = Склад;
		СтрокаНастроек.Помещение = Помещение;
		Набор.Заполнить(Неопределено);
	КонецЕсли;
	
	СтрокаНастроек.ИспользоватьАдресноеХранение = ИспользоватьАдресноеХранение;
	СтрокаНастроек.ИспользоватьАдресноеХранениеСправочно = ИспользоватьАдресноеХранениеСправочно;
	СтрокаНастроек.ИспользоватьРабочиеУчастки = ИспользоватьРабочиеУчастки;
	
	Набор.Записать();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы

// Обработчик обновления УТ 11.1.0.0
// Заполняет регистр настроек по значениям реквизивто справочников
Процедура ЗаполнитьНастройкиАдресныхСкладов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РабочиеУчастки.Ссылка) КАК Количество,
	|	РабочиеУчастки.Владелец КАК Склад,
	|	РабочиеУчастки.Помещение КАК Помещение
	|ПОМЕСТИТЬ РабочиеУчастки
	|ИЗ
	|	Справочник.РабочиеУчастки КАК РабочиеУчастки
	|
	|СГРУППИРОВАТЬ ПО
	|	РабочиеУчастки.Владелец,
	|	РабочиеУчастки.Помещение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка) КАК Помещение,
	|	Склады.ИспользоватьАдресноеХранение КАК ИспользоватьАдресноеХранение,
	|	Склады.ИспользоватьАдресноеХранениеСправочно КАК ИспользоватьАдресноеХранениеСправочно,
	|	ВЫБОР
	|		КОГДА РабочиеУчастки.Количество ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ИспользоватьРабочиеУчастки
	|ИЗ
	|	Справочник.Склады КАК Склады
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиАдресныхСкладов КАК НастройкиАдресныхСкладов
	|		ПО (НастройкиАдресныхСкладов.Склад = Склады.Ссылка)
	|			И (ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка) = НастройкиАдресныхСкладов.Помещение)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РабочиеУчастки КАК РабочиеУчастки
	|		ПО Склады.Ссылка = РабочиеУчастки.Склад
	|			И (ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка) = РабочиеУчастки.Помещение)
	|ГДЕ
	|	НЕ Склады.ЭтоГруппа
	|	И ВЫБОР
	|			КОГДА НастройкиАдресныхСкладов.Склад ЕСТЬ NULL 
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ НастройкиАдресныхСкладов.ИспользоватьАдресноеХранение <> Склады.ИспользоватьАдресноеХранение
	|					ИЛИ НастройкиАдресныхСкладов.Склад.ИспользоватьАдресноеХранениеСправочно <> Склады.ИспользоватьАдресноеХранениеСправочно
	|					ИЛИ ВЫБОР
	|						КОГДА РабочиеУчастки.Количество ЕСТЬ NULL 
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ИСТИНА
	|					КОНЕЦ <> НастройкиАдресныхСкладов.ИспользоватьРабочиеУчастки
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СкладскиеПомещения.Владелец,
	|	СкладскиеПомещения.Ссылка,
	|	СкладскиеПомещения.ИспользоватьАдресноеХранение,
	|	СкладскиеПомещения.ИспользоватьАдресноеХранениеСправочно,
	|	ВЫБОР
	|		КОГДА РабочиеУчастки.Количество ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	Справочник.СкладскиеПомещения КАК СкладскиеПомещения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиАдресныхСкладов КАК НастройкиАдресныхСкладов
	|		ПО (НастройкиАдресныхСкладов.Склад = СкладскиеПомещения.Владелец)
	|			И (НастройкиАдресныхСкладов.Помещение = СкладскиеПомещения.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РабочиеУчастки КАК РабочиеУчастки
	|		ПО (РабочиеУчастки.Склад = СкладскиеПомещения.Владелец)
	|			И (РабочиеУчастки.Помещение = СкладскиеПомещения.Ссылка)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НастройкиАдресныхСкладов.Склад ЕСТЬ NULL 
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ НастройкиАдресныхСкладов.ИспользоватьАдресноеХранение <> СкладскиеПомещения.ИспользоватьАдресноеХранение
	|					ИЛИ НастройкиАдресныхСкладов.Склад.ИспользоватьАдресноеХранениеСправочно <> СкладскиеПомещения.ИспользоватьАдресноеХранениеСправочно
	|					ИЛИ ВЫБОР
	|						КОГДА РабочиеУчастки.Количество ЕСТЬ NULL 
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ИСТИНА
	|					КОНЕЦ <> НастройкиАдресныхСкладов.ИспользоватьРабочиеУчастки
	|		КОНЕЦ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		РегистрыСведений.НастройкиАдресныхСкладов.УстановитьНастройкиПоУмолчанию(Выборка.Склад,
																				Выборка.Помещение,
																				Выборка.ИспользоватьАдресноеХранение,
																				Выборка.ИспользоватьАдресноеХранениеСправочно,
																				Выборка.ИспользоватьРабочиеУчастки);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли