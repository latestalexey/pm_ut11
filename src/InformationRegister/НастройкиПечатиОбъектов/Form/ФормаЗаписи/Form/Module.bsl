////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаОбъектовДляПечатиКомплектно = РегистрыСведений.НастройкиПечатиОбъектов.ТаблицаОбъектовДляПечатиКомплектно();
	Для Каждого ТекСтрока Из ТаблицаОбъектовДляПечатиКомплектно Цикл
		Элементы.ТипОбъекта.СписокВыбора.Добавить(ТекСтрока.ТипОбъекта, ТекСтрока.Представление);
	КонецЦикла;
	
	ПриИзмененииТипаОбъектаСервер();
	
	Если Не Пользователи.РолиДоступны("СохранениеНастроекПечатиОбъектов,СохранениеНастроекПечатиОбъектовПоПартнерам") Тогда
		
		ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	Если Не Пользователи.РолиДоступны("СохранениеНастроекПечатиОбъектов") Тогда
		
		Элементы.Организация.ТолькоПросмотр = Истина;
		
		Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
			Запись.Организация = Неопределено;
		ИначеЕсли ЗначениеЗаполнено(Запись.Организация) Тогда
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУдалить", "Видимость", Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Пользователи.РолиДоступны("СохранениеНастроекПечатиОбъектовПоПартнерам") Тогда
		
		Элементы.Партнер.ТолькоПросмотр = Истина;
		
		Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
			Запись.Партнер = Неопределено;
		ИначеЕсли ЗначениеЗаполнено(Запись.Партнер) Тогда
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУдалить", "Видимость", Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не Пользователи.РолиДоступны("СохранениеНастроекПечатиОбъектов")
	 И ЗначениеЗаполнено(ТекущийОбъект.Организация) Тогда
		
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нет прав на запись настройки печати по организации'"));
		
	КонецЕсли;
	
	Если Не Пользователи.РолиДоступны("СохранениеНастроекПечатиОбъектовПоПартнерам")
	 И ЗначениеЗаполнено(ТекущийОбъект.Партнер) Тогда
		
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нет прав на запись настройки печати по партнеру'"));
		
	КонецЕсли;
	
	ТекущийОбъект.Настройки = Новый ХранилищеЗначения(КомплектПечатныхФорм.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_НастройкиПечатиОбъектов");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТипОбъектаПриИзменении(Элемент)
	
	ПриИзмененииТипаОбъектаСервер();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого ТекСтрока Из КомплектПечатныхФорм Цикл
		ТекСтрока.Печатать = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого ТекСтрока Из КомплектПечатныхФорм Цикл
		ТекСтрока.Печатать = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтандартныеНастройки(Команда)
	
	УстановитьСтандартныеНастройкиСервер();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьСтандартныеНастройкиСервер()
	
	КомплектПечатныхФорм.Очистить();
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Запись.ТипОбъекта);
	КоллекцияПечатныхФорм = МенеджерОбъекта.КомплектПечатныхФорм();
	РегистрыСведений.НастройкиПечатиОбъектов.ДополнитьКомплектВнешнимиПечатнымиФормами(Запись.ТипОбъекта, КоллекцияПечатныхФорм);
	
	Для Каждого ТекСтрока Из КоллекцияПечатныхФорм Цикл
		
		НоваяСтрока = КомплектПечатныхФорм.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииТипаОбъектаСервер()
	
	ПараметрыОбъекта = РегистрыСведений.НастройкиПечатиОбъектов.ПараметрыОбъектаДляПечатиКомплектно(Запись.ТипОбъекта);
	
	Элементы.Организация.Доступность = ПараметрыОбъекта.ЕстьОрганизация;
	Элементы.Партнер.Доступность = ПараметрыОбъекта.ЕстьПартнер;
	Элементы.КомплектПечатныхФорм.Доступность = ПараметрыОбъекта.ДоступнаПечатьКомплекта;
	
	Если Не ПараметрыОбъекта.ЕстьОрганизация Тогда
		Запись.Организация = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыОбъекта.ЕстьПартнер Тогда
		Запись.Партнер = Неопределено;
	КонецЕсли;
	
	Если ПараметрыОбъекта.ДоступнаПечатьКомплекта Тогда
		
		ЗаписьОбъект = РеквизитФормыВЗначение("Запись");
		ЗаписьОбъект.Прочитать();
		СохраненноеЗначение = ЗаписьОбъект.Настройки.Получить();
		
		// Настройки по умолчанию
		Если СохраненноеЗначение = Неопределено Тогда
			УстановитьСтандартныеНастройкиСервер();
		Иначе
			КомплектПечатныхФорм.Загрузить(СохраненноеЗначение);
		КонецЕсли;
		
	Иначе
		
		КомплектПечатныхФорм.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры