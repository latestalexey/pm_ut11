
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы

Процедура ЗаполнитьПоДаннымПодтвержденияНулевойСтавкиНДС() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Подтверждение.Ссылка.Дата,РеализацияТоваровУслуг.Дата) КАК Период,
	|	ИСТИНА КАК Активность,
	|	РеализацияТоваровУслуг.Ссылка КАК Регистратор,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.Ссылка КАК ДокументРеализации,
	|	РеализацияТоваровУслуг.Дата КАК ДатаРеализации,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Подтверждение.Ссылка.ПодтвержденаСтавкаНДС0,НЕОПРЕДЕЛЕНО) = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0)
	|		КОГДА ЕСТЬNULL(Подтверждение.Ссылка.ПодтвержденаСтавкаНДС0,НЕОПРЕДЕЛЕНО) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеподтвержденаРеализация0)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение)
	|	КОНЕЦ КАК Состояние,
	|	ЕСТЬNULL(Подтверждение.СтавкаНДС,ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)) КАК СтавкаНДС,
	|	ЕСТЬNULL(Подтверждение.Ссылка.Дата,ДАТАВРЕМЯ(1,1,1)) КАК ДатаПодтверждения
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УдалитьПодтверждениеНулевойСтавкиНДС.ДокументыРеализации КАК Подтверждение
	|			ПО РеализацияТоваровУслуг.Ссылка = Подтверждение.СчетФактура
	|				И Подтверждение.Ссылка.Проведен
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|			ПО РеализацияТоваровУслуг.Ссылка = НДССостояниеРеализации0.ДокументРеализации
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|	И РеализацияТоваровУслуг.Ссылка.Проведен
	|	И НДССостояниеРеализации0.ДокументРеализации ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Подтверждение.Ссылка.Дата,АктВыполненныхРабот.Дата),
	|	ИСТИНА,
	|	АктВыполненныхРабот.Ссылка,
	|	АктВыполненныхРабот.Организация,
	|	АктВыполненныхРабот.Ссылка,
	|	АктВыполненныхРабот.Дата,
	|	АктВыполненныхРабот.Контрагент,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Подтверждение.Ссылка.ПодтвержденаСтавкаНДС0,НЕОПРЕДЕЛЕНО) = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0)
	|		КОГДА ЕСТЬNULL(Подтверждение.Ссылка.ПодтвержденаСтавкаНДС0,НЕОПРЕДЕЛЕНО) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеподтвержденаРеализация0)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение)
	|	КОНЕЦ,
	|	ЕСТЬNULL(Подтверждение.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)),
	|	ЕСТЬNULL(Подтверждение.Ссылка.Дата,ДАТАВРЕМЯ(1,1,1))
	|ИЗ
	|	Документ.АктВыполненныхРабот КАК АктВыполненныхРабот
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УдалитьПодтверждениеНулевойСтавкиНДС.ДокументыРеализации КАК Подтверждение
	|			ПО АктВыполненныхРабот.Ссылка = Подтверждение.СчетФактура
	|				И Подтверждение.Ссылка.Проведен
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|			ПО АктВыполненныхРабот.Ссылка = НДССостояниеРеализации0.ДокументРеализации
	|ГДЕ
	|	АктВыполненныхРабот.Ссылка.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|	И АктВыполненныхРабот.Ссылка.Проведен
	|	И НДССостояниеРеализации0.ДокументРеализации ЕСТЬ NULL
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
	
		Набор = РегистрыСведений.НДССостояниеРеализации0.СоздатьНаборЗаписей();
		СтрокаНабора = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНабора,Выборка);
		Если СтрокаНабора.СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда
			СтрокаНабора.СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка()
		ИначеЕсли СтрокаНабора.СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110 Тогда
			СтрокаНабора.СтавкаНДС = Перечисления.СтавкиНДС.НДС10
		ИначеЕсли СтрокаНабора.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118 Тогда
			СтрокаНабора.СтавкаНДС = Перечисления.СтавкиНДС.НДС18
		КонецЕсли;
		
		Попытка
			Набор.Записать();
		Исключение
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

