////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОтчетныйПериод = КонецКвартала(ТекущаяДата());
	
	КварталСтрока = УчетНДСКлиент.ДатаКакКварталПредставление(ОтчетныйПериод);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьОтборПоПериоду();
	УстановитьОтборПоОрганизации();
	УстановитьОтборПоСостоянию();
	УстановитьОтборПоКонтрагенту();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура КварталСтрокаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РезультатВыбора = УчетНДСКлиент.НачалоВыбораИзСпискаПредставленияПериодаРегистрации(Элемент, СтандартнаяОбработка, ОтчетныйПериод, ЭтаФорма);
	Если РезультатВыбора <> Неопределено Тогда
		КварталСтрока = РезультатВыбора;
		УстановитьОтборПоПериоду();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КварталСтрокаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОтчетныйПериод = ДобавитьМесяц(ОтчетныйПериод, 3*Направление);
	КварталСтрока = УчетНДСКлиент.ДатаКакКварталПредставление(ОтчетныйПериод);
	
	УстановитьОтборПоПериоду();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОтборПриИзменении(Элемент)
	
	УстановитьОтборПоСостоянию();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОтборПриИзменении(Элемент)
	
	УстановитьОтборПоКонтрагенту();
	
КонецПроцедуры
 
&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)

	УстановитьОтборПоОрганизации();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ СПИСОК

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле = Элементы.СписокДокументРеализации Тогда
		ОткрытьТекущийДокументРеализации();	
	Иначе
		ОткрытьТекущуюФормуЗаписи();
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение <> Неопределено Тогда 
		ЗаполнитьИзФормыРучногоВвода(ВыбранноеЗначение)
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНД

&НаКлиенте
Процедура ПодтвержденаСтавкаНДС(Команда)
	
	ТекущееСостояние = ПредопределенноеЗначение("Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0");
	ПодтверждениеСтавкиНДС(ТекущееСостояние);
	
КонецПроцедуры

&НаКлиенте
Процедура НеподтвержденаСтавкаНДС(Команда)
	
	ТекущееСостояние = ПредопределенноеЗначение("Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0");
	ПодтверждениеСтавкиНДС(ТекущееСостояние);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидаетсяПодтверждение(Команда)
	
	ТекущееСостояние = ПредопределенноеЗначение("Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение");
	ПодтверждениеСтавкиНДС(ТекущееСостояние);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗаписи(Команда)
	
	ОткрытьТекущуюФормуЗаписи();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументРеализации(Команда)
	
	ОткрытьТекущийДокументРеализации();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаСервере
Процедура УстановитьОтборПоПериоду()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор, 
		"ДатаРеализации", 
		КонецКвартала(ОтчетныйПериод), 
		ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
		, // Представление - автоматически
		ЗначениеЗаполнено(ОтчетныйПериод));
		
КонецПроцедуры	

&НаСервере
Процедура УстановитьОтборПоСостоянию()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор, 
		"Состояние", 
		Состояние, 
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(Состояние));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоОрганизации()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор, 
		"Организация", 
		Организация, 
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(Организация));
		
	Элементы.СписокОрганизация.Видимость = НЕ ЗначениеЗаполнено(Организация);	
		
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоКонтрагенту()

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор, 
		"Контрагент", 
		Контрагент, 
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(Контрагент));

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// При регистрации подтверждения/неподтверждения

&НаКлиенте
Процедура ПодтверждениеСтавкиНДС(ТекущееСостояние)

	СписокДокументов = Элементы.Список.ВыделенныеСтроки;
	
	Если СписокДокументов = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Для Каждого Строка Из СписокДокументов Цикл
		Регистратор = Элементы.Список.ДанныеСтроки(Строка).Регистратор;
		Комментарий = Элементы.Список.ДанныеСтроки(Строка).Комментарий;
		ПодтверждениеСтавкиНДССервер(Регистратор, ТекущееСостояние,,Комментарий);
	КонецЦикла;	
	
	Элементы.Список.Обновить();
	
КонецПроцедуры	

&НаСервере
Процедура ПодтверждениеСтавкиНДССервер(Регистратор, ТекущееСостояние, СтавкаНДС = Неопределено, Комментарий = "")
	
	НаборЗаписей = РегистрыСведений.НДССостояниеРеализации0.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Значение = Регистратор;
	НаборЗаписей.Прочитать();
	
	Если ТекущееСостояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 Тогда
		СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
		ДатаПодтверждения = НачалоДня(ОтчетныйПериод);
	ИначеЕсли ТекущееСостояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0 Тогда
		СтавкаНДС = ?(СтавкаНДС = Неопределено,Перечисления.СтавкиНДС.НДС18,СтавкаНДС);
		ДатаПодтверждения = НачалоДня(ОтчетныйПериод);
	Иначе
		СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
		ДатаПодтверждения = '00010101000000';
	КонецЕсли;	
	
	ПерезаписатьДокумент = Ложь;
	
	Для Каждого	СтрокаНабора Из НаборЗаписей Цикл
		
		Если СтрокаНабора.Состояние <> ТекущееСостояние Тогда
			СтрокаНабора.Состояние = ТекущееСостояние;
			ПерезаписатьДокумент = Истина;
		КонецЕсли;	
		
		Если СтрокаНабора.ДатаПодтверждения <> ДатаПодтверждения Тогда
			СтрокаНабора.ДатаПодтверждения = ДатаПодтверждения;
			ПерезаписатьДокумент = Истина;
		КонецЕсли;
		
		Если СтрокаНабора.СтавкаНДС <> СтавкаНДС Тогда
			СтрокаНабора.СтавкаНДС = СтавкаНДС;
			ПерезаписатьДокумент = Истина;
		КонецЕсли;
		
		Если СтрокаНабора.Комментарий <> Комментарий Тогда
			СтрокаНабора.Комментарий = Комментарий;
		КонецЕсли;
		
	КонецЦикла;	
	
	Попытка
		НаборЗаписей.Записать();
		Если ПерезаписатьДокумент Тогда
			ДокументОбъект = Регистратор.ПолучитьОбъект();	
			Попытка	
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЕсли;	
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры	
	
////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Процедура ЗаполнитьИзФормыРучногоВвода(ВыбранноеЗначение)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;	
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПодтверждениеСтавкиНДССервер(ТекущаяСтрока.Регистратор, ВыбранноеЗначение.Состояние, ВыбранноеЗначение.СтавкаНДС, ВыбранноеЗначение.Комментарий);
	Элементы.Список.Обновить();
	
КонецПроцедуры	

&НаКлиенте
Процедура ОткрытьТекущуюФормуЗаписи()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;	
	
	ПараметрыПодбора = Новый Структура();
	ПараметрыПодбора.Вставить("Документ",			ТекущиеДанные.ДокументРеализации);
	ПараметрыПодбора.Вставить("Состояние",			ТекущиеДанные.Состояние);
	ПараметрыПодбора.Вставить("ДатаПодтверждения",	ТекущиеДанные.ДатаПодтверждения);
	ПараметрыПодбора.Вставить("ТекущийПериод",		ОтчетныйПериод);
	ПараметрыПодбора.Вставить("СтавкаНДС",			ТекущиеДанные.СтавкаНДС);
	ПараметрыПодбора.Вставить("Комментарий",		ТекущиеДанные.Комментарий);
	
	ОткрытьФорму("РегистрСведений.НДССостояниеРеализации0.Форма.ФормаРучногоВвода", ПараметрыПодбора, Элементы.Список);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОткрытьТекущийДокументРеализации()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ОткрытьЗначение(ТекущиеДанные.ДокументРеализации);
	
КонецПроцедуры	
	