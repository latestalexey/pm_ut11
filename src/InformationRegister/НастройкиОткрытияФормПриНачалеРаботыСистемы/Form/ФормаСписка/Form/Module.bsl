////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьТаблицуНастроекФорм();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ВРег(ИмяСобытия) = ВРег("Запись_НастройкиОткрытияФормПриНачалеРаботыСистемы") Тогда
		Если ТипЗнч(Источник) = Тип("Структура") Тогда
			ОбновитьЗаписьТаблицыНастроекФорм(Источник);			
		Иначе
			ОбновитьТаблицуНастроекФорм();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ НАСТРОЙКИ ФОРМ

&НаКлиенте
Процедура НастройкиФормВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьФормуЗаписиРегистра();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ИзменитьНастройку(Команда)
	ОткрытьФормуЗаписиРегистра();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОткрытьФормуЗаписиРегистра()
	ТекущиеДанные = Элементы.НастройкиФорм.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрываемойФормы = Новый Структура;
	
	Если ТекущиеДанные.ОткрыватьПоУмолчанию
		Или ТекущиеДанные.НастройкиУстановлены Тогда
		КлючЗаписиПараметровФорм = КлючЗаписиПараметровФорм(ТекущиеДанные.Пользователь, ТекущиеДанные.ОткрываемаяФорма);
		
		Если Не КлючЗаписиПараметровФорм.Пустой() Тогда
			ПараметрыОткрываемойФормы.Вставить("Ключ", КлючЗаписиПараметровФорм);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОткрываемойФормы.Вставить("Пользователь", ТекущиеДанные.Пользователь);
	ПараметрыОткрываемойФормы.Вставить("ОткрываемаяФорма", ТекущиеДанные.ОткрываемаяФорма);
	
	Если ТекущиеДанные.НеобходимыНастройки Тогда
		ИмяОткрываемойФормы = ТекущиеДанные.ИмяФормыНастроек;
	Иначе
		ИмяОткрываемойФормы = "РегистрСведений.НастройкиОткрытияФормПриНачалеРаботыСистемы.ФормаЗаписи";
	КонецЕсли;
	
	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыОткрываемойФормы, ЭтаФорма);
		
КонецПроцедуры

&НаСервере
Функция КлючЗаписиПараметровФорм(Пользователь, ОткрываемаяФорма)
	
	СтруктураИзмерений = Новый Структура;
	СтруктураИзмерений.Вставить("Пользователь", Пользователь);
	СтруктураИзмерений.Вставить("ОткрываемаяФорма", ОткрываемаяФорма);
	
	Возврат РегистрыСведений.НастройкиОткрытияФормПриНачалеРаботыСистемы.СоздатьКлючЗаписи(СтруктураИзмерений);
	
КонецФункции

&НаСервере
Процедура ОбновитьТаблицуНастроекФорм()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|
	|УПОРЯДОЧИТЬ ПО
	|	Пользователи.Наименование";
	
	ПользователиИБ = Запрос.Выполнить().Выбрать();
	
	Для Каждого ЗначениеПеречисления из Перечисления.ФормыОткрываемыеПриНачалеРаботыСистемы Цикл
		
		СтруктураПараметровФормы = ОткрытиеФормПриНачалеРаботыСистемыКлиентСерверПереопределяемый.НастройкиФормы(ЗначениеПеречисления);
				
		Пока ПользователиИБ.Следующий() Цикл
			
			Если УправлениеДоступом.ЕстьРоль(СтруктураПараметровФормы.Роль,Неопределено,ПользователиИБ.Пользователь)
				Или УправлениеДоступом.ЕстьРоль("ПолныеПрава" ,Неопределено,ПользователиИБ.Пользователь) Тогда
				
				НоваяСтрока = НастройкиФорм.Добавить();
				
				НоваяСтрока.Пользователь        = ПользователиИБ.Пользователь;
				НоваяСтрока.ОткрываемаяФорма    = ЗначениеПеречисления;
				НоваяСтрока.ПараметрЗапуска     = СтруктураПараметровФормы.ПараметрЗапуска;
				НоваяСтрока.НеобходимыНастройки = СтруктураПараметровФормы.НеобходимыНастройки;
				НоваяСтрока.ИмяФормыНастроек    = СтруктураПараметровФормы.ИмяФормыНастроек;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ПользователиИБ.Сбросить();
		
	КонецЦикла;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиФорм.Пользователь,
	|	НастройкиФорм.НеобходимыНастройки,
	|	НастройкиФорм.ОткрываемаяФорма,
	|	НастройкиФорм.ПараметрЗапуска,
	|	НастройкиФорм.ИмяФормыНастроек
	|ПОМЕСТИТЬ НастройкиФорм
	|ИЗ
	|	&НастройкиФорм КАК НастройкиФорм
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиФорм.Пользователь,
	|	НастройкиФорм.ОткрываемаяФорма,
	|	НастройкиФорм.НеобходимыНастройки,
	|	НастройкиФорм.ПараметрЗапуска,
	|	НастройкиФорм.ИмяФормыНастроек,
	|	ЕСТЬNULL(НастройкиОткрытияФормПриНачалеРаботыСистемы.ТекстовоеПредставлениеПараметров, """") КАК НастройкиФормы,
	|	ЕСТЬNULL(НастройкиОткрытияФормПриНачалеРаботыСистемы.ОткрыватьПоУмолчанию, ЛОЖЬ) КАК ОткрыватьПоУмолчанию,
	|	ВЫБОР
	|		КОГДА НастройкиОткрытияФормПриНачалеРаботыСистемы.Пользователь ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК НастройкиУстановлены
	|ИЗ
	|	НастройкиФорм КАК НастройкиФорм
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОткрытияФормПриНачалеРаботыСистемы КАК НастройкиОткрытияФормПриНачалеРаботыСистемы
	|		ПО НастройкиФорм.Пользователь = НастройкиОткрытияФормПриНачалеРаботыСистемы.Пользователь
	|			И НастройкиФорм.ОткрываемаяФорма = НастройкиОткрытияФормПриНачалеРаботыСистемы.ОткрываемаяФорма";
	
	Запрос.УстановитьПараметр("НастройкиФорм", РеквизитФормыВЗначение("НастройкиФорм"));
	
	НастройкиФорм.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаписьТаблицыНастроекФорм(КлючЗаписиРегистра)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиОткрытияФормПриНачалеРаботыСистемы.ТекстовоеПредставлениеПараметров,
	|	НастройкиОткрытияФормПриНачалеРаботыСистемы.ОткрыватьПоУмолчанию
	|ИЗ
	|	РегистрСведений.НастройкиОткрытияФормПриНачалеРаботыСистемы КАК НастройкиОткрытияФормПриНачалеРаботыСистемы
	|ГДЕ
	|	НастройкиОткрытияФормПриНачалеРаботыСистемы.Пользователь = &Пользователь
	|	И НастройкиОткрытияФормПриНачалеРаботыСистемы.ОткрываемаяФорма = &ОткрываемаяФорма";
	
	Запрос.УстановитьПараметр("Пользователь", КлючЗаписиРегистра.Пользователь);
	Запрос.УстановитьПараметр("ОткрываемаяФорма", КлючЗаписиРегистра.ОткрываемаяФорма);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Пользователь", КлючЗаписиРегистра.Пользователь);
	ПараметрыОтбора.Вставить("ОткрываемаяФорма", КлючЗаписиРегистра.ОткрываемаяФорма);
	
	СтрокаТаблицыНастроекФорм = НастройкиФорм.НайтиСтроки(ПараметрыОтбора)[0];
	
	Если Выборка.Следующий() Тогда
		СтрокаТаблицыНастроекФорм.НастройкиФормы       = Выборка.ТекстовоеПредставлениеПараметров;
		СтрокаТаблицыНастроекФорм.ОткрыватьПоУмолчанию = Выборка.ОткрыватьПоУмолчанию;
		СтрокаТаблицыНастроекФорм.НастройкиУстановлены = Истина;
	Иначе
		СтрокаТаблицыНастроекФорм.НастройкиФормы       = "";
		СтрокаТаблицыНастроекФорм.ОткрыватьПоУмолчанию = Ложь;
		СтрокаТаблицыНастроекФорм.НастройкиУстановлены = Ложь;
	КонецЕсли;
	
КонецПроцедуры
