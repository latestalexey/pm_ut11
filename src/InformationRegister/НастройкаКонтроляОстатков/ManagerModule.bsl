#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Возвращает настройку контроля остатков склада
//
//	Параметры:
//		Склад - СправочникСсылка.Склады - склад, для которого необходимо получить настройку контроля остатков
//	Возвращаемое значение:
//		Структура - настройка контроля остатков склада
//			ВариантКонтроля - ПеречислениеСсылка.ВариантыКонтроля - вариант контроля
//			СрокПоставки - Число - срок поставки
//			ГраницаГрафикаДоступности - Дата - граница графика доступности
//
Функция ПолучитьНастройкуКонтроляОстатковСклада(Склад) Экспорт
	
	НастройкиКонтроляОстатков = Новый Структура("ВариантКонтроля, СрокПоставки, ГраницаГрафикаДоступности");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкаКонтроляОстатков.ВариантКонтроля КАК ВариантКонтроля,
	|	НастройкаКонтроляОстатков.СрокПоставки КАК СрокПоставки,
	|	НастройкаКонтроляОстатков.ГраницаГрафикаДоступности КАК ГраницаГрафикаДоступности
	|ИЗ
	|	РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаКонтроляОстатков
	|ГДЕ
	|	НастройкаКонтроляОстатков.Склад = &Склад
	|	И НастройкаКонтроляОстатков.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И НастройкаКонтроляОстатков.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)");
	
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(НастройкиКонтроляОстатков, Выборка);
		
	КонецЕсли;
		
	Возврат НастройкиКонтроляОстатков;
	
КонецФункции

// Сохраняет настройку контроля остатков склада
//
//	Параметры:
//		Склад - СправочникСсылка.Склады - склад, для которого необходимо сохранить настройку контроля остатков
//		ВариантКонтроля - ПеречислениеСсылка.ВариантыКонтроля - вариант контроля
//		СрокПоставки - Число - срок поставки
//		ГраницаГрафикаДоступности - Дата - граница графика доступности
//
Процедура УстановитьНастройкуКонтроляОстатковСклада(Склад, ВариантКонтроля, СрокПоставки, ГраницаГрафикаДоступности) Экспорт
	
	НастройкиКонтроляОстатков = ПолучитьНастройкуКонтроляОстатковСклада(Склад);
	
	Если НастройкиКонтроляОстатков.ВариантКонтроля <> ВариантКонтроля
		ИЛИ НастройкиКонтроляОстатков.СрокПоставки <> СрокПоставки
		ИЛИ НастройкиКонтроляОстатков.ГраницаГрафикаДоступности <> ГраницаГрафикаДоступности Тогда
		
		МенеджерЗаписи = РегистрыСведений.НастройкаКонтроляОстатков.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Склад = Склад;
		МенеджерЗаписи.ВариантКонтроля = ВариантКонтроля;
		
		Если ВариантКонтроля = Перечисления.ВариантыКонтроля.ОстаткиСУчетомГрафика Тогда
			
			МенеджерЗаписи.СрокПоставки = СрокПоставки;
			МенеджерЗаписи.ГраницаГрафикаДоступности = ГраницаГрафикаДоступности;
			
		КонецЕсли;
		
		МенеджерЗаписи.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли