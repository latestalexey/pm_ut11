#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура ЗаписатьПроцентнуюСтавкуКомисиию(Договор, НоваяСтавка, НоваяКомиссия, Период = Неопределено) Экспорт
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДата();
	КонецЕсли;
	
	ИсторияСтавок = СрезПоследних(Период,Новый Структура("Договор",Договор));
	Если ИсторияСтавок.Количество() = 0 Тогда		
		НоваяЗапись = СоздатьМенеджерЗаписи();
		ЗаписатьЗапись(НоваяЗапись, Договор, НоваяСтавка, НоваяКомиссия, Период);
		
	Иначе
		
		ЕстьИзменения = НоваяСтавка <> ИсторияСтавок[0].Процент ИЛИ НоваяКомиссия <> ИсторияСтавок[0].Комиссия;
		Если ИсторияСтавок[0].Период = НачалоДня(Период) И ЕстьИзменения Тогда
			НоваяЗапись = СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ИсторияСтавок[0]);
			НоваяЗапись.Прочитать();
			ЗаписатьЗапись(НоваяЗапись, Договор, НоваяСтавка, НоваяКомиссия, Период);
			
		ИначеЕсли ЕстьИзменения Тогда
			НоваяЗапись = СоздатьМенеджерЗаписи();
			ЗаписатьЗапись(НоваяЗапись, Договор, НоваяСтавка, НоваяКомиссия, Период);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьПроцентнуюСтавкуКомисиию(Договор, НоваяСтавка, НоваяКомиссия, Период = Неопределено) Экспорт
	
	НоваяСтавка = 0;
	НоваяКомиссия = 0;
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДата();
	КонецЕсли;
	
	ИсторияСтавок = СрезПоследних(Период, Новый Структура("Договор",Договор));
	Если ИсторияСтавок.Количество() > 0 Тогда
		
		НоваяСтавка = ИсторияСтавок[0].Процент;
		НоваяКомиссия = ИсторияСтавок[0].Комиссия;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗаписатьЗапись(НоваяЗапись, Договор, НоваяСтавка, НоваяКомиссия, Период = Неопределено, АвторИзменения = Неопределено, ДатаИзменения = Неопределено)
	
	Если Период = Неопределено Тогда		
		Период = ТекущаяДата();		
	КонецЕсли;
	
	Если АвторИзменения = Неопределено Тогда
		АвторИзменения = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	Если ДатаИзменения = Неопределено Тогда
		ДатаИзменения = ТекущаяДата();
	КонецЕсли;
	
	НоваяЗапись.Период			 = Период;
	НоваяЗапись.Договор			 = Договор;
	НоваяЗапись.Процент			 = НоваяСтавка;
	НоваяЗапись.Комиссия		 = НоваяКомиссия;
	НоваяЗапись.АвторИзменения	 = АвторИзменения;
	НоваяЗапись.ДатаИзменения	 = ДатаИзменения;
	НоваяЗапись.Записать();	
	
КонецПроцедуры
#КонецЕсли