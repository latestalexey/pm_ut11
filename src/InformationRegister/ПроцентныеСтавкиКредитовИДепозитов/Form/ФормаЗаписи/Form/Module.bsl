
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.Договор) Тогда
		УстановитьДоступностьПоДоговору();
	КонецЕсли;
		
	Запись.ДатаИзменения = ТекущаяДата();
	Запись.АвторИзменения = ПараметрыСеанса.ТекущийПользователь;
	Модифицированность = Ложь;
			
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УстановитьДоступностьПоДоговору();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Запись.ДатаИзменения = ТекущаяДата();
	Запись.АвторИзменения = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
	УстановитьДоступностьПоДоговору();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПоГрафикуНажатие(Элемент)
	
	ТекущийГрафик = ТекущийГрафикСервер(Запись.Договор);
	Если ЗначениеЗаполнено(ТекущийГрафик) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущийГрафик);
		ОткрытьФорму("Справочник.ВариантыГрафиковКредитовИДепозитов.Форма.ФормаЭлемента",
				ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура УстановитьДоступностьПоДоговору()
	
	ДоговорЗакрыт = Запись.Договор.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Закрыт;
	Элементы.НадписьДоговорЗакрыт.Видимость = ДоговорЗакрыт;
	
	Кнопки = Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы;
	Кнопки.ФормаЗаписатьИЗакрыть.Доступность = НЕ ДоговорЗакрыт;
	Кнопки.ФормаЗаписать.Доступность = НЕ ДоговорЗакрыт;
	
	Если Запись.Договор.ТипКомиссии = Перечисления.ТипКомиссииКредитовИДепозитов.Нет Тогда
		Элементы.СтраницыКомиссия.ТекущаяСтраница = Элементы["БезКомиссии"];
	ИначеЕсли Запись.Договор.ТипКомиссии = Перечисления.ТипКомиссииКредитовИДепозитов.Сумма Тогда
		Элементы.СтраницыКомиссия.ТекущаяСтраница = Элементы["ВРублях"];
	ИначеЕсли Запись.Договор.ТипКомиссии = Перечисления.ТипКомиссииКредитовИДепозитов.Процент Тогда
		Элементы.СтраницыКомиссия.ТекущаяСтраница = Элементы["Процентом"];
	ИначеЕсли Запись.Договор.ТипКомиссии = Перечисления.ТипКомиссииКредитовИДепозитов.ПроизвольныйГрафик Тогда
		Элементы.СтраницыКомиссия.ТекущаяСтраница = Элементы["ПоГрафику"];
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТекущийГрафикСервер(Договор)
	
	Возврат Справочники.ВариантыГрафиковКредитовИДепозитов.ТекущийВариантГрафика(Договор);
	
КонецФункции