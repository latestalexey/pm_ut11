////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьЗаголовокФормы(Параметры.Уровень);
	
	Если ПустаяСтрока(Параметры.Регион) и Параметры.Уровень > 1 Тогда 
		Параметры.Уровень = 0;
	КонецЕсли;	
	Уровень = Параметры.Уровень;
	
	// Восстанавливаем значение флага "Скрывать неактуальные адреса"
	Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ВводАдреса", "СкрыватьНеактуальныеАдреса");
	Если Значение = Неопределено Тогда
		СкрыватьНеактуальныеАдреса = Ложь;
	Иначе
		СкрыватьНеактуальныеАдреса = Значение;
	КонецЕсли;
	
	// Установим отбор
	Параметры.Отбор = ПолучитьОграниченияПоУровню(Параметры.Уровень);
	
	// Установим текущую строку
	Поля = ПолучитьПоляПоУровню(Параметры.Уровень + 1);
	СтруктураОтбора = АдресныйКлассификатор.ВернутьСтруктуруАдресногоКлассификатораПоАдреснымЭлементам(
		Поля.Регион, Поля.Район, Поля.Город, Поля.НаселенныйПункт, Поля.Улица);
	Если СтруктураОтбора.ТипАдресногоЭлемента = Параметры.Уровень Тогда
		Параметры.ТекущаяСтрока = РегистрыСведений.АдресныйКлассификатор.СоздатьКлючЗаписи(СтруктураОтбора);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.СкрыватьНеактуальныеАдреса.Пометка = СкрыватьНеактуальныеАдреса;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОсуществитьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОсуществитьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрыватьНеактуальныеАдреса(Команда)
	
	СкрыватьНеактуальныеАдреса = Не СкрыватьНеактуальныеАдреса;
	Элементы.СкрыватьНеактуальныеАдреса.Пометка = СкрыватьНеактуальныеАдреса;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ПризнакАктуальности", 
	0, ВидСравненияКомпоновкиДанных.Равно, "Признак актуальности", СкрыватьНеактуальныеАдреса);
	ЭтаФорма.ОбновитьОтображениеДанных();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОсуществитьВыбор()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Наименование", ТекущиеДанные.Наименование);
	Результат.Вставить("Сокращение", ТекущиеДанные.Сокращение);
	Результат.Вставить("ПризнакАктуальности", ТекущиеДанные.ПризнакАктуальности);
	Результат.Вставить("КодРегиона", ТекущиеДанные.КодАдресногоОбъектаВКоде);
	Результат.Вставить("Код", ТекущиеДанные.Код);
	
	Если ТекущиеДанные.ПризнакАктуальности <> 0 Тогда
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить("Выбрать");
		СписокКнопок.Добавить("Отмена");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1 %2 не актуален.
				|Выбрать его?'"), 
			ТекущиеДанные.Сокращение, 
			ТекущиеДанные.Наименование);
		Ответ = Вопрос(ТекстВопроса, СписокКнопок);
		Если Ответ = "Отмена" Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Результат.Вставить("СкрыватьНеактуальныеАдреса", СкрыватьНеактуальныеАдреса);
	Результат.Вставить("Уровень", Уровень);
	Результат.Вставить("СтруктураАдреса", ПолучитьСтруктуруАдреса(Результат.Код));
	
	ПослеВыбораАдресногоЭлемента(Результат, Результат.СтруктураАдреса, Уровень); 
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПоляПоУровню(Уровень)
	
	Поля = Новый Структура;
	Поля.Вставить("Регион",          ?(Уровень <= 1, "", Параметры.Регион));
	Поля.Вставить("Район",           ?(Уровень <= 2, "", Параметры.Район));
	Поля.Вставить("Город",           ?(Уровень <= 3, "", Параметры.Город));
	Поля.Вставить("НаселенныйПункт", ?(Уровень <= 4, "", Параметры.НаселенныйПункт));
	Поля.Вставить("Улица",           ?(Уровень <= 5, "", Параметры.Улица));
	
	Возврат Поля;
	
КонецФункции

&НаСервере
Функция ПолучитьОграниченияПоУровню(Уровень)
	
	Поля = ПолучитьПоляПоУровню(Уровень);
	Ограничения = АдресныйКлассификатор.ВернутьСтруктуруОграниченийПоРодителю(
		Поля.Регион, Поля.Район, Поля.Город, Поля.НаселенныйПункт, Поля.Улица, 0, Уровень);
		
	Если СкрыватьНеактуальныеАдреса Тогда		
		Ограничения.Вставить("ПризнакАктуальности", 0);
	КонецЕсли;
	
	Возврат Ограничения;
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовокФормы(Уровень)
	Если Уровень = 1 Тогда
		Заголовок = НСтр("ru = 'Выберите регион или область'");
	ИначеЕсли Уровень = 2 Тогда
		Заголовок = НСтр("ru = 'Выберите район'");
	ИначеЕсли Уровень = 3 Тогда
		Заголовок = НСтр("ru = 'Выберите город'");
	ИначеЕсли Уровень = 4 Тогда
		Заголовок = НСтр("ru = 'Выберите населенный пункт'");
	ИначеЕсли Уровень = 5 Тогда
		Заголовок = НСтр("ru = 'Выберите улицу'");
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПослеВыбораАдресногоЭлемента(СтруктураРезультата, СтруктураАдреса, Уровень)
	
	// Восстанавливаем значение флага "Скрывать неактуальные адреса"
	Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ВводАдреса", "СкрыватьНеактуальныеАдреса");
	Если Значение = Неопределено Тогда
		СтруктураРезультата.Вставить("СкрыватьНеактуальныеАдреса", Ложь);
	Иначе
		СтруктураРезультата.Вставить("СкрыватьНеактуальныеАдреса", Значение);
	КонецЕсли;
	
	// Заполнение дополнительных свойств структуры результата
	СтруктураПолей = ИсправитьПоля(СтруктураАдреса, Уровень);
	СтруктураРезультата.Вставить("СтруктураОшибок", СтруктураПолей.СтруктураОшибок);
	СтруктураРезультата.Вставить("СтруктураЗагруженных", ЗагруженныеПоляПоРегиону(СтруктураАдреса));
	СтруктураРезультата.Вставить("МожноЗагружатьРегион", ЗагружатьРегион(СтруктураАдреса.Регион));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИсправитьПоля(СтруктураАдреса, Уровень)
	
	// Получаем поля из структуры адреса
	Индекс = СтруктураАдреса.Индекс;
	Регион = СтруктураАдреса.Регион;
	Район = СтруктураАдреса.Район;
	Город = СтруктураАдреса.Город;
	НаселенныйПункт = СтруктураАдреса.НаселенныйПункт;
	Улица = СтруктураАдреса.Улица;
	Дом = СтруктураАдреса.Дом;
	Корпус = СтруктураАдреса.Корпус;
	Квартира = СтруктураАдреса.Квартира;
	
	Если Не АдресныйКлассификатор.АдресныйЭлементЗагружен(Регион) Тогда
		СтруктураПолей = Новый Структура("СтруктураОшибок,СтруктураАдреса", Новый Структура, СтруктураАдреса);
		Возврат СтруктураПолей;
	КонецЕсли;
	
	СтруктураПроверки = АдресныйКлассификатор.ПроверитьСоответствиеАдресаКЛАДРу(""
	, Регион, Район, Город, НаселенныйПункт, Улица, Дом, Корпус);
	
	// Очищаем не подходящие поля
	Если СтруктураПроверки.ЕстьОшибки Тогда
		
		АдресныйКлассификатор.ОчиститьПотомковПоУровнюАдресногоЭлемента(Регион, Район
		, Город, НаселенныйПункт, Улица, Дом, Корпус, Квартира, Уровень); 
		
	КонецЕсли;	
	
	// Заполняем или исправляем промежуточные поля
	Если Уровень > 2 И (ПустаяСтрока(Район) Или СтруктураПроверки.СтруктураОшибок.Свойство("Район")) Тогда
		Район = СокрЛП(СтруктураПроверки.Район.Наименование + " " + СтруктураПроверки.Район.Сокращение);	
	КонецЕсли;
	
	Если Уровень > 3 И (ПустаяСтрока(Город) Или СтруктураПроверки.СтруктураОшибок.Свойство("Город")) Тогда
		Город = СокрЛП(СтруктураПроверки.Город.Наименование + " " + СтруктураПроверки.Город.Сокращение);	
	КонецЕсли;
	
	Если Уровень > 4 И (ПустаяСтрока(НаселенныйПункт) Или СтруктураПроверки.СтруктураОшибок.Свойство("НаселенныйПункт")) Тогда
		НаселенныйПункт = СокрЛП(СтруктураПроверки.НаселенныйПункт.Наименование + " " 
		+ СтруктураПроверки.НаселенныйПункт.Сокращение);
	КонецЕсли;
	
	// Исправляем индекс
	НовыйИндекс = АдресныйКлассификатор.ПолучитьИндекс(Регион, 
		Район, Город, НаселенныйПункт, Улица, Дом, Корпус);
	
	Если Не ПустаяСтрока(НовыйИндекс) Тогда
		Индекс = НовыйИндекс;
	КонецЕсли;
	
	// Обновляем структуру адреса
	СтруктураАдреса.Индекс = Индекс;
	СтруктураАдреса.Регион = Регион;
	СтруктураАдреса.Район = Район;
	СтруктураАдреса.Город = Город;
	СтруктураАдреса.НаселенныйПункт = НаселенныйПункт;
	СтруктураАдреса.Улица = Улица;
	СтруктураАдреса.Дом = Дом;
	СтруктураАдреса.Корпус = Корпус;
	СтруктураАдреса.Квартира = Квартира;
	
	// Проверяем ещё раз и возвращаем структуру ошибок
	СтруктураПроверки = АдресныйКлассификатор.ПроверитьСоответствиеАдресаКЛАДРу(""
	, Регион, Район, Город, НаселенныйПункт, Улица, Дом, Корпус);
	
	СтруктураПроверки.Вставить("СтруктураАдреса", СтруктураАдреса);
	
	Возврат СтруктураПроверки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗагружатьРегион(Регион)
	
	// Загрузить регион могут только администраторы или пользователи с правом добавления/изменения базовой НСИ
	Если НЕ Пользователи.РолиДоступны("ДобавлениеИзменениеОбщейБазовойНСИ") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Разбиваем регион на наименование и сокращение
	АдресноеСокращение = "";
	НаименованиеРегиона = АдресныйКлассификатор.ПолучитьИмяИАдресноеСокращение(Регион, АдресноеСокращение);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
					|	АдресныйКлассификатор.Код КАК Код
					|ИЗ
					|	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
					|ГДЕ
					|	АдресныйКлассификатор.ТипАдресногоЭлемента = 1
					|	И АдресныйКлассификатор.Наименование = &НаименованиеРегиона";
	Запрос.УстановитьПараметр("НаименованиеРегиона", НаименованиеРегиона);
	
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат Ложь; // Загружаются только регионы, которые есть в списке регионов.
	Иначе
		РегионЗагружен = АдресныйКлассификатор.АдресныйЭлементЗагружен(Регион);
		Возврат Не РегионЗагружен; // Загружаем регион, если он не загружен.
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗагруженныеПоляПоРегиону(СтруктураАдреса)
	
	Если АдресныйКлассификатор.АдресныйЭлементЗагружен(СтруктураАдреса.Регион) Тогда
		
		СтруктураЗагруженных = АдресныйКлассификатор.СтруктураЗагруженныхЭлементовАдреса(
		СтруктураАдреса.Регион, СтруктураАдреса.Район, СтруктураАдреса.Город, СтруктураАдреса.НаселенныйПункт, СтруктураАдреса.Улица);
		
		Возврат СтруктураЗагруженных;
		
	Иначе
		
		Возврат Новый Структура("Регион", Ложь);
		
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруАдреса(Код)
	
	Возврат АдресныйКлассификатор.ПолучитьСтруктуруАдреса(Код);
	
КонецФункции