#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗарегистрироватьОшибкуПроверкиОбъекта(Ссылка, УзелИнформационнойБазы, ТипСтрокой, Причина, ТипПроблемы) Экспорт
	
	НаборЗаписейКонфликта = РегистрыСведений.РезультатыОбменаДанными.СоздатьНаборЗаписей();
	НаборЗаписейКонфликта.Отбор.Ссылка.Установить(Ссылка);
	НаборЗаписейКонфликта.Отбор.ТипПроблемы.Установить(ТипПроблемы);
	
	НаборЗаписейКонфликта.Прочитать();
	НаборЗаписейКонфликта.Очистить();
	
	ЗаписьКонфликта = НаборЗаписейКонфликта.Добавить();
	ЗаписьКонфликта.Ссылка = Ссылка;
	ЗаписьКонфликта.ТипПроблемы = ТипПроблемы;
	ЗаписьКонфликта.УзелИнформационнойБазы = УзелИнформационнойБазы;
	ЗаписьКонфликта.ДатаВозникновения = ТекущаяДатаСеанса();
	ЗаписьКонфликта.Причина = СокрЛП(Причина);
	ЗаписьКонфликта.Пропущена = Ложь;
	ЗаписьКонфликта.ПометкаУдаления = Ссылка.ПометкаУдаления;
	
	Если ТипПроблемы = Перечисления.ТипыПроблемОбменаДанными.НепроведенныйДокумент Тогда
		
		ЗаписьКонфликта.НомерДокумента = Ссылка.Номер;
		ЗаписьКонфликта.ДатаДокумента = Ссылка.Дата;
		
	КонецЕсли;
	
	НаборЗаписейКонфликта.Записать();
	
КонецПроцедуры

Процедура Игнорировать(Ссылка, ТипПроблемы, Игнорировать) Экспорт
	
	НаборЗаписейКонфликта = РегистрыСведений.РезультатыОбменаДанными.СоздатьНаборЗаписей();
	НаборЗаписейКонфликта.Отбор.Ссылка.Установить(Ссылка);
	НаборЗаписейКонфликта.Отбор.ТипПроблемы.Установить(ТипПроблемы);
	НаборЗаписейКонфликта.Прочитать();
	НаборЗаписейКонфликта[0].Пропущена = Игнорировать;
	НаборЗаписейКонфликта.Записать();
	
КонецПроцедуры

Функция КоличествоПроблем(ТипПроблемы = Неопределено, УзелИнформационнойБазы = Неопределено, УчитыватьПроигнорированные = Ложь, Период = Неопределено, СтрокаПоиска = "") Экспорт
	
	Количество = 0;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РезультатыОбменаДанными.Ссылка) КАК КоличествоПроблем
	|ИЗ
	|	РегистрСведений.РезультатыОбменаДанными КАК РезультатыОбменаДанными
	|ГДЕ
	|	РезультатыОбменаДанными.Пропущена <> &ОтборПоПропущенным
	|	[ОтборПоУзлу]
	|	[ОтборПоТипу]
	|	[ОтборПоПериоду]
	|	[ОтборПоПричине]";
	
	Запрос = Новый Запрос;
	
	ОтборПоПропущенным = ?(УчитыватьПроигнорированные, Неопределено, Истина);
	Запрос.УстановитьПараметр("ОтборПоПропущенным", ОтборПоПропущенным);
	
	Если ТипПроблемы = Неопределено Тогда
		СтрокаОтбора = "";
	Иначе
		СтрокаОтбора = "И РезультатыОбменаДанными.ТипПроблемы = &ТипПроблемы";
		Запрос.УстановитьПараметр("ТипПроблемы", ТипПроблемы);
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ОтборПоТипу]", СтрокаОтбора);
	
	Если УзелИнформационнойБазы = Неопределено Тогда
		СтрокаОтбора = "";
	Иначе
		СтрокаОтбора = "И РезультатыОбменаДанными.УзелИнформационнойБазы = &УзелИнформационнойБазы";
		Запрос.УстановитьПараметр("УзелИнформационнойБазы", УзелИнформационнойБазы);
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ОтборПоУзлу]", СтрокаОтбора);
	
	Если ЗначениеЗаполнено(Период) Тогда
		
		СтрокаОтбора = "И (РезультатыОбменаДанными.ДатаВозникновения >= &ДатаНачала
		| И РезультатыОбменаДанными.ДатаВозникновения <= &ДатаОкончания)";
		Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
		
	Иначе
		
		СтрокаОтбора = "";
		
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ОтборПоПериоду]", СтрокаОтбора);
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		
		СтрокаОтбора = "И РезультатыОбменаДанными.Причина ПОДОБНО &Причина";
		Запрос.УстановитьПараметр("Причина", "%" + СтрокаПоиска + "%");
		
	Иначе
		
		СтрокаОтбора = "";
		
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ОтборПоПричине]", СтрокаОтбора);
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Количество = Выборка.КоличествоПроблем;
		
	КонецЕсли;
	
	Возврат Количество;
	
КонецФункции

Процедура ЗарегистрироватьУстранениеПроблемы(Источник, ТипПроблемы) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		ОбменДаннымиСервер.УдалитьНаборЗаписейВРегистреСведений(Новый Структура("Ссылка, ТипПроблемы", Источник.Ссылка, ТипПроблемы), "РезультатыОбменаДанными");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалениемУзлаПланаОбмена(СсылкаНаУзел) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РезультатыОбменаДанными.Ссылка КАК Ссылка,
	|	РезультатыОбменаДанными.ТипПроблемы КАК ТипПроблемы,
	|	РезультатыОбменаДанными.УзелИнформационнойБазы КАК УзелИнформационнойБазы
	|ИЗ
	|	РегистрСведений.РезультатыОбменаДанными КАК РезультатыОбменаДанными
	|ГДЕ
	|	РезультатыОбменаДанными.УзелИнформационнойБазы = &УзелИнформационнойБазы";
	
	Запрос.УстановитьПараметр("УзелИнформационнойБазы", СсылкаНаУзел);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	Запись = РегистрыСведений.РезультатыОбменаДанными.СоздатьМенеджерЗаписи();
	ПустаяСсылкаНаУзел = ПланыОбмена[СсылкаНаУзел.Метаданные().Имя].ПустаяСсылка();
	
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Прочитать();
		
		Если Запись.Выбран() Тогда
			
			Запись.УзелИнформационнойБазы = ПустаяСсылкаНаУзел;
			Запись.Записать();
			
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецЕсли