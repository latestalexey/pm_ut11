
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьОрганизацию();
	
	УстановитьОтборДинамическихСписковСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если Элементы.ОрганизацияОтбор.ТолькоПросмотр Тогда
		Настройки.Удалить("Организация");
	Иначе
		Организация = Настройки.Получить("Организация");
		УстановитьОтборДинамическихСписковСервер();
	КонецЕсли;
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)
	
	УстановитьОтборДинамическихСписковСервер();
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦ ФОРМЫ

&НаКлиенте
Процедура СписокРегистрацииПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрокаРегистрации = Элементы.СписокРегистрации.ТекущаяСтрока;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРегистрацииПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Подразделение = ПараметрыПеретаскивания.Значение.Получить(0);
	СтандартнаяОбработка = ТипЗнч(Подразделение) <> Тип("СправочникСсылка.СтруктураПредприятия");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРегистрацииПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Подразделение = ПараметрыПеретаскивания.Значение.Получить(0);
	Если ТипЗнч(Подразделение) <> Тип("СправочникСсылка.СтруктураПредприятия") ИЛИ Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьРегистрациюПодразделений(ПараметрыПеретаскивания.Значение, Строка, Организация);
	
	ОбновитьДинамическиеСписки();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПодразделенияПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Регистрация = ПараметрыПеретаскивания.Значение.Получить(0);
	Если ТипЗнч(Регистрация) <> Тип("СправочникСсылка.РегистрацииВНалоговомОргане") ИЛИ Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьРегистрациюПодразделений(Строка, Регистрация, Организация);
	
	ОбновитьДинамическиеСписки();
	
КонецПроцедуры
 

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура НазначитьОсновнуюРегистрациюОрганизации(Команда)
	
	НазначитьОсновнуюРегистрациюОрганизацииСервер(Организация, ТекущаяСтрокаРегистрации);
	
	ОсновнаяРегистрацияОрганизации = ТекущаяСтрокаРегистрации;
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьРегистрациюПодразделения(Команда)
	
	ЗаписатьРегистрациюПодразделений(Элементы.СписокПодразделения.ТекущаяСтрока, ТекущаяСтрокаРегистрации, Организация);
	
	ОбновитьДинамическиеСписки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРегистрациюПодразделения(Команда)
	
	УдалитьРегистрациюПодразделения(Элементы.СписокПодразделения.ТекущаяСтрока, Организация);
	
	ОбновитьДинамическиеСписки();
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Запись данных

&НаСервереБезКонтекста
Процедура НазначитьОсновнуюРегистрациюОрганизацииСервер(Организация, Регистрация)
	
	Если НЕ ЗначениеЗаполнено(Организация)
	 ИЛИ НЕ ЗначениеЗаполнено(Регистрация) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектОрганизация = Организация.ПолучитьОбъект();
	ОбъектОрганизация.РегистрацияВНалоговомОргане = Регистрация;
	
	Попытка
		ОбъектОрганизация.Записать();
	Исключение
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьРегистрациюПодразделений(Подразделение, Регистрация, Организация)
	Перем МассивПодразделений;
	
	Если НЕ ЗначениеЗаполнено(Организация)
	 ИЛИ НЕ ЗначениеЗаполнено(Регистрация)
	 ИЛИ НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.РегистрацииВНалоговомОргане) Тогда
		Возврат;
	КонецЕсли;
		
	Если ТипЗнч(Подразделение) = Тип("СправочникСсылка.СтруктураПредприятия")
	 И ЗначениеЗаполнено(Подразделение) Тогда
		МассивПодразделений = Новый Массив;
		МассивПодразделений.Добавить(Подразделение);
	ИначеЕсли ТипЗнч(Подразделение) = Тип("Массив") Тогда
		МассивПодразделений = Подразделение;
	Иначе
		Возврат;
	КонецЕсли;
	
	Попытка
		
		НачатьТранзакцию();
		
		Для Каждого ТекущееПодразделение Из МассивПодразделений Цикл
			
			Запись = РегистрыСведений.РегистрацииВНалоговомОргане.СоздатьМенеджерЗаписи();
			
			Запись.Организация 					= Организация;
			Запись.Подразделение 				= ТекущееПодразделение;
			Запись.РегистрацияВНалоговомОргане 	= Регистрация;
			
			Запись.Записать(Истина);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьРегистрациюПодразделения(Подразделение, Организация)
	
	Если НЕ ЗначениеЗаполнено(Организация)
	 ИЛИ НЕ ЗначениеЗаполнено(Подразделение)
	 ИЛИ НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.РегистрацииВНалоговомОргане) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		Запись = РегистрыСведений.РегистрацииВНалоговомОргане.СоздатьМенеджерЗаписи();
		
		Запись.Организация 	 = Организация;
		Запись.Подразделение = Подразделение;
		
		Запись.Удалить();
		
	Исключение
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура УстановитьОрганизацию()
	
	Если Параметры.Свойство("Отбор")
	 И Параметры.Отбор.Свойство("Организация") Тогда
		Организация = Параметры.Отбор.Организация;
		Элементы.ОрганизацияОтбор.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ИзменениеОрганизаций = ПравоДоступа("Изменение", Метаданные.Справочники.Организации);
	
	Элементы.СписокРегистрацииНазначитьОсновнуюРегистрациюОрганизации.Видимость 			   = ИзменениеОрганизаций;
	Элементы.СписокРегистрацииКонтекстноеМенюНазначитьОсновнуюРегистрациюОрганизации.Видимость = ИзменениеОрганизаций;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборДинамическихСписковСервер()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокРегистрации.Отбор, "Владелец", Организация, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокПодразделения, "Организация", Организация);
	
	ОсновнаяРегистрацияОрганизации = Организация.РегистрацияВНалоговомОргане;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДинамическиеСписки()
	
	Элементы.СписокРегистрации.Обновить();
	Элементы.СписокПодразделения.Обновить();
	
КонецПроцедуры
