////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ОтборОбластьХранения;
	
	Параметры.Отбор.Свойство("Склад",			ОтборСклад);
	Параметры.Отбор.Свойство("Помещение",		ОтборПомещение);
	Параметры.Отбор.Свойство("ОбластьХранения",	ОтборОбластьХранения);
	
	Если ЗначениеЗаполнено(ОтборОбластьХранения) Тогда
		ОтборСклад     = ОтборОбластьХранения.Владелец;
		ОтборПомещение = ОтборОбластьХранения.Помещение;
	ИначеЕсли ЗначениеЗаполнено(ОтборПомещение) Тогда
		ОтборСклад 	   = ОтборПомещение.Владелец;
	КонецЕсли;
	
	Элементы.ОтборСклад.Видимость 	  = НЕ ЗначениеЗаполнено(ОтборСклад);
	Элементы.ОтборПомещение.Видимость = НЕ ЗначениеЗаполнено(ОтборПомещение);
	
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Склад", ОтборСклад));
	УстановитьОтборыВСписке();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Не ЗначениеЗаполнено(ОтборСклад)
	 И Не ЗначениеЗаполнено(ОтборПомещение) Тогда
		ОтборСклад = Настройки.Получить("ОтборСклад");
		ОтборПомещение = Настройки.Получить("ОтборПомещение");
		
		УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Склад", ОтборСклад));
		УстановитьОтборыВСписке();
	Иначе
	    Настройки.Удалить("ОтборСклад");
		Настройки.Удалить("ОтборПомещение");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОтборСкладПриИзменении(Элемент)
	ОтборСкладПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ОтборПомещениеПриИзменении(Элемент)
	УстановитьОтборыВСписке();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура УстановитьОтборыВСписке()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Склад", ОтборСклад, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Помещение", ОтборПомещение, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ОтборСкладПриИзмененииСервер()
	
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Склад", ОтборСклад));
	УстановитьОтборыВСписке();
	
КонецПроцедуры
