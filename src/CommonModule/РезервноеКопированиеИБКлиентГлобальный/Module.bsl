////////////////////////////////////////////////////////////////////////////////
// Подсистема "Резервное копирование ИБ".
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Выполняет обработчик ожидания старта автоматического резервного копирования
// в процессе работы пользователя, а также повторного оповещения после игнорировании первоначального.
//
Процедура ОбработчикДействийРезервногоКопирования() Экспорт 
	
	ПараметрыРезервногоКопированияИБНаКлиенте = РезервноеКопированиеИБКлиент.ПолучитьПараметрыРезервногоКопированияИБ();
	ПроводитьАвтоматическоеРезервноеКопирование = РезервноеКопированиеИБКлиент.НеобходимостьАвтоматическогоРезервногоКопирования();
	
	Если ПроводитьАвтоматическоеРезервноеКопирование Тогда
		РезервноеКопированиеИБКлиент.ПровестиРезервноеКопирование();
	КонецЕсли;
	
	Если ПараметрыРезервногоКопированияИБНаКлиенте.ОтложенноеРезервноеКопирование Тогда
		ОтложенноеИнтерактивноеРезервноеКопирование(ПараметрыРезервногоКопированияИБНаКлиенте);
	КонецЕсли;
	
	ВариантОповещения = ПараметрыРезервногоКопированияИБНаКлиенте.ПараметрОповещения;
	Если ВариантОповещения = "Просрочено" Или ВариантОповещения = "Напомнить" Или ВариантОповещения = "ЕщеНеНастроено" Тогда     
		РезервноеКопированиеИБКлиент.ОповеститьПользователяОРезервномКопировании(ПараметрыРезервногоКопированияИБНаКлиенте.ПараметрОповещения);
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Обработчик ожидания выполнения резервного копирования.
//
// Параметры:
//	ПараметрыКопирования - Структура - параметры резервного копирования.
//
Процедура ОтложенноеИнтерактивноеРезервноеКопирование(ПараметрыКопирования) Экспорт
	
	ДатаКопирования = РезервноеКопированиеИБКлиент.ПолучитьДатуКопирования();
	ТекущаяДата = ОбщегоНазначенияКлиент.ДатаСеанса();
	Если ТекущаяДата > ДатаКопирования Тогда
		
		ПараметрыРезервногоКопированияИБНаКлиенте = РезервноеКопированиеИБКлиент.ПолучитьПараметрыРезервногоКопированияИБ();
		ПараметрыРезервногоКопированияИБНаКлиенте.ОтложенноеРезервноеКопирование = Ложь;
		РезервноеКопированиеИБКлиент.УстановитьПараметрыРезервногоКопирования(ПараметрыРезервногоКопированияИБНаКлиенте);
		
		НачатьРезервноеКопирование();
		
	Иначе
		
		Если РезервноеКопированиеИБВызовСервера.ПолучитьКоличествоАктивныхПользователей() = 1 Тогда
			
			ТекстВопроса = НСтр("ru = 'В данный момент с базой больше никто не работает. Выполнить резервное копирование?'");
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Да, 
				Нстр("ru = 'Провести резервное копирование прямо сейчас'"), КодВозвратаДиалога.Да);
			
			Если Ответ = КодВозвратаДиалога.Да ИЛИ Ответ = КодВозвратаДиалога.Таймаут Тогда
				ПараметрыРезервногоКопированияИБНаКлиенте = РезервноеКопированиеИБКлиент.ПолучитьПараметрыРезервногоКопированияИБ();
				ПараметрыРезервногоКопированияИБНаКлиенте.ОтложенноеРезервноеКопирование = Ложь;
				РезервноеКопированиеИБКлиент.УстановитьПараметрыРезервногоКопирования(ПараметрыРезервногоКопированияИБНаКлиенте);
				
				НачатьРезервноеКопирование();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Открывает форму выполнения резервного копирования.
//
Процедура НачатьРезервноеКопирование()
	
	ПараметрыФормы = Новый Структура("ТипВызова, ТекущаяСтраница, ЗаголовокНадписи", 1, "СтраницаИнформацииИВыполненияРезервногоКопирования", "");
	ФормаОбработки = ОткрытьФорму("Обработка.РезервноеКопированиеИБ.Форма.РезервноеКопированиеИнформационнойБазы", ПараметрыФормы);
	
КонецПроцедуры


