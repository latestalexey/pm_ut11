////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции, вызываемые из обработчиков событий.

// Заполняет значения ответственных лиц документа значениями по умолчанию.
//
// Параметры
//	Объект - ДокументОбъект - документ, реквизиты которого необходимо заполнить.
//	АвтозаполнениеСвойств - Булево - заполнять реквизиты с использованием механизма автозаполнения по статистике
//
Процедура ЗаполнитьОтветственныхЛицДокумента(Объект, АвтозаполнениеСвойств = Истина) Экспорт
	
	ПараметрыУказанияОтветственныхЛиц = ПараметрыУказанияОтветственныхЛиц(Объект);
	
	Если АвтозаполнениеСвойств Тогда
		
		// Автозаполнение реквизитов ответственных лиц по статистике.
		РеквизитыОтветственныеЛица = ПараметрыУказанияОтветственныхЛиц.РеквизитыОтветственныеЛица;
		СвойстваАвтозаполнения = Новый Структура("Организация", ПараметрыУказанияОтветственныхЛиц.Организация);
		
		ОбщегоНазначенияУТКлиентСервер.ДополнитьСтруктуру(СвойстваАвтозаполнения, РеквизитыОтветственныеЛица, Ложь);
		ОбщегоНазначенияУТ.ЗаполнитьЗначенияСвойствАвтозаполнения(ПараметрыУказанияОтветственныхЛиц.Ссылка, СвойстваАвтозаполнения);
		
		Для Каждого КлючИЗначение Из РеквизитыОтветственныеЛица Цикл
			// Если по статистике реквизит заполнился, то запишем его в объект.
			Если ЗначениеЗаполнено(СвойстваАвтозаполнения[КлючИЗначение.Ключ]) Тогда
				Объект[КлючИЗначение.Ключ] 							  = СвойстваАвтозаполнения[КлючИЗначение.Ключ];
				ПараметрыУказанияОтветственныхЛиц[КлючИЗначение.Ключ] = СвойстваАвтозаполнения[КлючИЗначение.Ключ];
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// Проверим соответстие ответственных лиц дате и организации документа.
	// Если не соответствует - заполним ответственными лицами по умолчанию.
	ИзмененныеРеквизиты = СкорректироватьОтветственныхЛицДокумента(ПараметрыУказанияОтветственныхЛиц);
	Если ИзмененныеРеквизиты.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(Объект, ИзмененныеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет корректность заполнения реквизитов ответственных лиц документа.
//
// Параметры
//	Объект - ДокументОбъект - документ, реквизиты которого необходимо проверить
//	Отказ  - Булево - признак корректности заполнения реквизитов
//
Процедура ПроверитьЗаполнениеОтветственныхЛицДокумента(Объект, Отказ) Экспорт
	
	МетаданныеОбъекта = Объект.Метаданные();
	
	ПараметрыУказанияОтветственныхЛиц = ПараметрыУказанияОтветственныхЛиц(Объект);
	
	РеквизитыОтветственныеЛица = ПараметрыУказанияОтветственныхЛиц.РеквизитыОтветственныеЛица;
	
	Для Каждого КлючИЗначение Из РеквизитыОтветственныеЛица Цикл
		
		ЗначениеОтветственногоЛица = ПараметрыУказанияОтветственныхЛиц[КлючИЗначение.Ключ];
		Если ЗначениеЗаполнено(ЗначениеОтветственногоЛица) Тогда
			
			РеквизитыДляПроверки =
				Новый Структура(
					"Дата, Организация, ОтветственноеЛицо",
					ПараметрыУказанияОтветственныхЛиц.Дата, ПараметрыУказанияОтветственныхЛиц.Организация, КлючИЗначение.Значение);
			
			// Если реквизит заполнен некорректно - сообщим пользователю и установим параметр Отказ = Истина
			Если НЕ ПроверитьСоответствиеРеквизитовИОтветственногоЛица(РеквизитыДляПроверки, ЗначениеОтветственногоЛица) Тогда
				
				ПредставлениеРеквизита = МетаданныеОбъекта.Реквизиты.Найти(КлючИЗначение.Ключ).Синоним;
				
				СообщениеОбОшибке = НСтр("ru = 'Ответственное лицо ""%ПредставлениеРеквизита%"" не имеет права подписи этого документа.'");
				СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%ПредставлениеРеквизита%", ПредставлениеРеквизита); 

				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СообщениеОбОшибке,
					,
					"Объект." + КлючИЗначение.Ключ,
					,
					Отказ);
				
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик события "ПриИзменении" ключевых реквизитов документа (Дата, Организация).
// Проверяет соответствие ответственных лиц документа и ключевых реквизитов.
//
// Параметры:
//	РеквизитыДокумента - ДанныеФормыСтруктура - свойство "Объект" формы документа
//
Процедура ПриИзмененииСвязанныхРеквизитовДокумента(Объект) Экспорт
	
	ЗаполнитьОтветственныхЛицДокумента(Объект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для получения информации об ответственных лицах.

// Возвращает структуру, содержащую информацию об ответственном лице организации.
//
// Параметры:
//	Организация - СправочникСсылка.Организации - отбор по организации
//	Дата - Дата - отбор по дате
//  ОтветственноеЛицо - Перечисления.ОтветственныеЛицаОрганизаций - "вид" ответственного лица.
//
// Возвращаемое значение:
// Структура с ключами
//	- СтруктурнаяЕдиница
//	- ОтветственноеЛицо
//	- ФизическоеЛицо
//	- Должность
//
Функция ПолучитьДанныеОтветственногоЛица(Организация, Дата = Неопределено, ОтветственноеЛицо) Экспорт
	
	СтруктураОтветственных = ПолучитьОтветственныеЛицаОрганизации(Организация, Дата);
	Результат 			   = Новый Структура("СтруктурнаяЕдиница, ОтветственноеЛицо", Организация, ОтветственноеЛицо);
	
	Для Каждого МетаВидОтветственного Из Метаданные.Перечисления.ОтветственныеЛицаОрганизаций.ЗначенияПеречисления Цикл
		Если ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций[МетаВидОтветственного.Имя] Тогда
			Результат.Вставить("ФизическоеЛицо", СтруктураОтветственных[МетаВидОтветственного.Имя]);
			Результат.Вставить("Должность", 	 СтруктураОтветственных[МетаВидОтветственного.Имя + "Должность"]);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру, содержащую информацию об ответственных лицах организации.
//
// Параметры:
//	Организация - СправочникСсылка.Организации - отбор по организации
//	Дата - Дата - отбор по дате
//
// Возвращаемое значение:
// Структура с ключами
//	- %ВидОтветственного% - СправочникСсылка.ФизическиеЛица - физическое лицо, соответствующее ответственному лицу
//	- %ВидОтветственного%Должность - Строка - должность ответственного лица
// Для полей, начинающихся с %ВидОтветственного%
// 	- вместо %ВидОтветственного% подставляется имя значения перечисления ОтветственныеЛицаОрганизаций как оно задано в конфигураторе
//	- количество каждого из этих ключей соответствует количеству значений перечисления ОтветственныеЛицаОрганизаций
//
Функция ПолучитьОтветственныеЛицаОрганизации(Организация, Дата = Неопределено) Экспорт
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СтруктураОтветственных = Новый Структура;
	
	СтруктураОтбора 	 = Новый Структура("Дата, Владелец, ПравоПодписиПоДоверенности", Дата, Организация, Ложь);
	ТаблицаОтветственных = ПолучитьТаблицуОтветственныхЛицПоОтбору(СтруктураОтбора);
	
	Для Каждого МетаВидОтветственного Из Метаданные.Перечисления.ОтветственныеЛицаОрганизаций.ЗначенияПеречисления Цикл
		
		ВидОтветственного = Перечисления.ОтветственныеЛицаОрганизаций[МетаВидОтветственного.Имя];
		
		СтрокаТаблицы = ТаблицаОтветственных.Найти(ВидОтветственного, "ОтветственноеЛицо");
		
		Если СтрокаТаблицы = Неопределено Тогда
			
			// Основное ответственное лицо этого вида не задано
			СтруктураОтветственных.Вставить(МетаВидОтветственного.Имя, Справочники.ФизическиеЛица.ПустаяСсылка());
			СтруктураОтветственных.Вставить(МетаВидОтветственного.Имя + "Должность", "");
			
		Иначе
			
			// Основное ответственное лицо этого вида найдено
			СтруктураОтветственных.Вставить(МетаВидОтветственного.Имя, СтрокаТаблицы.ФизическоеЛицо);
			СтруктураОтветственных.Вставить(МетаВидОтветственного.Имя + "Должность", СтрокаТаблицы.Должность);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтруктураОтветственных;
	
КонецФункции

// Возвращает таблицу ответственных лиц, сформированную в соответствии с произвольным отбором.
//
// Параметры
//  Отбор - Структура - структура, содержащая параметры отбора данных справочника ОтветственныеЛицаОрганизаций.
//		Ключ - имя реквизита справочника. Возможны дополнительные ключи:
//			- Дата 		  - для отбора по периоду ДатаНачала - ДатаОкончания
//			- Организация - синоним ключа Владелец
//		Значение - значение для отбора по ключу
//	ДопустимыПомеченныеНаУдаление - Булево - выводить в результирующую таблицу помеченные на удаление элементы.
//	
// Возвращаемое значение:
//  Таблица значений - таблица, содержащая данные справочника ОтветственныеЛицаОрганизаций.
//		Имена колонок таблицы совпадают с именами реквизитов справочника.
//
Функция ПолучитьТаблицуОтветственныхЛицПоОтбору(Знач Отбор, ДопустимыПомеченныеНаУдаление = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	
	ТекстВыбираемыеПоля = "";
	ТекстОтбора = "";
	НомерОтбора = 0;
	
	ПоляОтбора = ВозможныеПоляОтбораОтветственныхЛиц(); // реквизиты справочника
	
	Если Отбор.Свойство("Дата") Тогда
		
		// Отбор по периоду действия записи об ответственном лице
		ТекстОтбора =
			ТекстОтбора
			+ ?(ТекстОтбора = "", "", " И ") + "ОтветственныеЛицаОрганизаций.ДатаНачала <= &ОтборПоДате
			| И (ОтветственныеЛицаОрганизаций.ДатаОкончания >= &ОтборПоДате 
			|	 ИЛИ ОтветственныеЛицаОрганизаций.ДатаОкончания = ДАТАВРЕМЯ(1,1,1,0,0,0))
			|";
		Запрос.УстановитьПараметр("ОтборПоДате", НачалоДня(Отбор.Дата));
		
		Отбор.Удалить("Дата");
		
	КонецЕсли;
	
	Если Отбор.Свойство("Организация") Тогда
		Отбор.Вставить("Владелец", Отбор.Организация);
		Отбор.Удалить("Организация");
	КонецЕсли;
	
	Если НЕ ДопустимыПомеченныеНаУдаление Тогда
		Отбор.Вставить("ПометкаУдаление", Ложь);
	КонецЕсли;
	
	// Формируем секцию запроса "ГДЕ", устанавливаем параметры запроса
	Для Каждого ТекущийОтбор Из Отбор Цикл
		
		Если НЕ ПоляОтбора.Свойство(ТекущийОтбор.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерОтбора = НомерОтбора + 1;
		
		ТекстОтбора =
			ТекстОтбора
			+ ?(ТекстОтбора = "", "", " И ") + "ОтветственныеЛицаОрганизаций." + ТекущийОтбор.Ключ 
				+ ?(ТипЗнч(ТекущийОтбор.Значение) = Тип("Массив"), " В ", " = ") + "&ЗначениеОтбора" + СокрЛП(НомерОтбора) + "
			|";
		
		Запрос.УстановитьПараметр("ЗначениеОтбора" + СокрЛП(НомерОтбора), ТекущийОтбор.Значение);
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТекстОтбора) Тогда
		ТекстОтбора =
			"ГДЕ
			| " + ТекстОтбора;
	КонецЕсли;
	
	Для Каждого Поле Из ПоляОтбора Цикл
		ТекстВыбираемыеПоля = ТекстВыбираемыеПоля
			+ ?(ТекстВыбираемыеПоля = "", "", ",") + "
			|ОтветственныеЛицаОрганизаций." + Поле.Ключ + " КАК " + Поле.Ключ;
	КонецЦикла;
	
	// Формируем запрос, получаем данные
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	%ТекстВыбираемыеПоля%
	|ИЗ
	|	Справочник.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
	|%ТекстОтбора%
	|УПОРЯДОЧИТЬ ПО
	|	Владелец,
	|	ОтветственноеЛицо,
	|	ПравоПодписиПоДоверенности,
	|	ДатаНачала,
	|	ДатаОкончания,
	|	ФизическоеЛицо";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ТекстВыбираемыеПоля%", ТекстВыбираемыеПоля);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ТекстОтбора%", 		  ТекстОтбора);
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Формирует временную таблицу, содержащую информацию об ответственных лицах в разрезе документов.
// Используется для формирования печатных форм документов подсистемой печати
//	- перед выборкой данных из шапки документа вызвать эту процедуру
//	- при выборке данных создать левое соединение таблицы документов с временной таблицей ТаблицаОтветственныеЛица по полю Ссылка
//
// Параметры
//	ОтборДокументы - Массив, ДокументСсылка - ссылки на документы, по которым необходимо получить ответственных лиц.
//		Важно: если передан массив, то все его элементы должны иметь одинаковый тип.
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - в него помещается сформированная временная таблица ТаблицаОтветственныеЛица
//  ИмяРеквизитаОрганизация - имя поля документа для получения организации-владельца ответственного лица
//	РеквизитыОтветственныеЛица - Структура, Соответствие
//		Ключ - Строка - имя поля в запросе для получения значения ответственного лица
//		Значение - ПеречислениеСсылка.ОтветственныеЛицаОрганизаций - "вид" ответственного лица
// Количество строк в таблице "ТаблицаОтветственныеЛица" соответствует количеству элементов в ОтборДокументы.
//
// Структура временной таблицы "ТаблицаОтветственныеЛица"
//	- Ссылка - ссылка на документ из ОтборДокументы
//	- %Реквизит% - СправочникСсылка.ОтветственныеЛицаОрганизаций - значение реквизита ответственного лица из документа
//	- %Реквизит%ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - физическое лицо, соответствующее ответственному лицу
//	- %Реквизит%Наименование - Строка - наименование для печати ответственного лица
//	- %Реквизит%Должность - Строка - должность ответственного лица
//	- %Реквизит%ОснованиеПраваПодписи - Строка - основание права подписи для неосновного ответственного лица
// Для полей, начинающихся с %Реквизит%
// 	- вместо %Реквизит% подставляется имя реквизита ответственного лица как оно задано в конфигураторе
//	- количество каждого из этих полей соответствует количеству реквизитов ответственных лиц документа
//
// Например, если в метаданных документа есть два реквизита ответственных лиц - Руководитель и Бухгалтер,
// то таблица ТаблицаОтветственныеЛица будет иметь следующую структуру:
//	- Ссылка
//	- Руководитель
//	- РуководительФизическоеЛицо
//	- РуководительНаименование
//	- РуководительДолжность
//	- РуководительОснованиеПраваПодписи
//	- Бухгалтер
//	- БухгалтерФизическоеЛицо
//	- БухгалтерНаименование
//	- БухгалтерДолжность
//	- БухгалтерОснованиеПраваПодписи
//
Процедура СформироватьВременнуюТаблицуОтветственныхЛицДокументов(ОтборДокументы, МенеджерВременныхТаблиц,
			ИмяРеквизитаОрганизация = "Организация", РеквизитыОтветственныеЛица = Неопределено) Экспорт
	
	Если ТипЗнч(ОтборДокументы) = Тип("Массив") И ОтборДокументы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ОтборДокументы) <> Тип("Массив") Тогда // это ссылка на документ
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(ОтборДокументы);
	Иначе // это массив ссылок на документы
		МассивОбъектов = ОтборДокументы;
	КонецЕсли;
	
	МетаданныеДокумента = МассивОбъектов[0].Метаданные();
	
	ИменаПолейПереданыВПараметрах = (РеквизитыОтветственныеЛица <> Неопределено);
	Если НЕ ИменаПолейПереданыВПараметрах Тогда
		РеквизитыОтветственныеЛица = РеквизитыОтветственныхЛицДокумента(МетаданныеДокумента);
	КонецЕслИ;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыОтветственныеЛица) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизитаОрганизация = "ДокументДляПечати." + ИмяРеквизитаОрганизация;
	
	Если МенеджерВременныхТаблиц = Неопределено Тогда
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	// Получим данные из документов
	ШаблонЗапроса = 
	"ВЫБРАТЬ
	|	ДокументДляПечати.Ссылка 					КАК Ссылка,
	|	НАЧАЛОПЕРИОДА(ДокументДляПечати.Дата, ДЕНЬ) КАК Дата,
	|	%Организация%								КАК Организация,
	|	%ИмяРеквизита% 								КАК ВыбранноеОтветственноеЛицо,
	|	ЗНАЧЕНИЕ(%ОтветственноеЛицо%) 				КАК ОтветственноеЛицо
	|%СозданиеВременнойТаблицы%
	|ИЗ
	|	Документ." + МетаданныеДокумента.Имя + " КАК ДокументДляПечати
	|ГДЕ
	|	ДокументДляПечати.Ссылка В(&МассивОбъектов)";
	
	Индекс = 0;
	Для Каждого КлючИЗначение Из РеквизитыОтветственныеЛица Цикл
		
		Индекс 		 = Индекс + 1;
		ТекстЗапроса = ШаблонЗапроса;
		
		Если Индекс > 1 Тогда
			ТекстЗапроса = "
			|ОБЪЕДИНИТЬ ВСЕ
			|" + ТекстЗапроса;
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса , "%Организация%", 			 	ИмяРеквизитаОрганизация);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса , "%ИмяРеквизита%", 			
			?(ИменаПолейПереданыВПараметрах, 
				"ЗНАЧЕНИЕ(Справочник.ОтветственныеЛицаОрганизаций.ПустаяСсылка)", // будут выбраны значения по умолчанию
				"ДокументДляПечати." + КлючИЗначение.Ключ));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса , "%ОтветственноеЛицо%", 		ПолучитьПолноеИмяПредопределенногоЗначения(КлючИЗначение.Значение));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса , "%СозданиеВременнойТаблицы%", ?(Индекс = 1, "ПОМЕСТИТЬ ВТДанныеДокументов", ""));
		
		Запрос.Текст = Запрос.Текст + ТекстЗапроса;
		
	КонецЦикла;
	
	Запрос.Текст = Запрос.Текст + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОтветственноеЛицо,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	// Заполним пустые поля ответственных лиц значениями по умолчанию
	Запрос.Текст = Запрос.Текст +
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТДанныеДокументов.Организация КАК Организация,
	|	ВТДанныеДокументов.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	ВТДанныеДокументов.Дата КАК Дата
	|ПОМЕСТИТЬ ВТНезаполненныеОтветственные
	|ИЗ
	|	ВТДанныеДокументов КАК ВТДанныеДокументов
	|ГДЕ
	|	ВТДанныеДокументов.ВыбранноеОтветственноеЛицо = ЗНАЧЕНИЕ(Справочник.ОтветственныеЛицаОрганизаций.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОтветственноеЛицо,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНезаполненныеОтветственные.Организация КАК Организация,
	|	ВТНезаполненныеОтветственные.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	ВТНезаполненныеОтветственные.Дата КАК Дата,
	|	ОтветственныеЛицаОрганизаций.Ссылка КАК ОтветственноеЛицоПоУмолчанию
	|ПОМЕСТИТЬ ВТОтветственныеПоУмолчанию
	|ИЗ
	|	ВТНезаполненныеОтветственные КАК ВТНезаполненныеОтветственные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
	|		ПО ВТНезаполненныеОтветственные.Организация = ОтветственныеЛицаОрганизаций.Владелец
	|			И ВТНезаполненныеОтветственные.ОтветственноеЛицо = ОтветственныеЛицаОрганизаций.ОтветственноеЛицо
	|			И ВТНезаполненныеОтветственные.Дата >= ОтветственныеЛицаОрганизаций.ДатаНачала
	|			И (ВТНезаполненныеОтветственные.Дата <= ОтветственныеЛицаОрганизаций.ДатаОкончания
	|				ИЛИ ОтветственныеЛицаОрганизаций.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1))
	|			И (ОтветственныеЛицаОрганизаций.ПравоПодписиПоДоверенности = ЛОЖЬ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОтветственноеЛицо,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанныеДокументов.Ссылка,
	|	ВТДанныеДокументов.ОтветственноеЛицо,
	|	ВЫБОР
	|		КОГДА ВТДанныеДокументов.ВыбранноеОтветственноеЛицо = ЗНАЧЕНИЕ(Справочник.ОтветственныеЛицаОрганизаций.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ВТОтветственныеПоУмолчанию.ОтветственноеЛицоПоУмолчанию, ЗНАЧЕНИЕ(Справочник.ОтветственныеЛицаОрганизаций.ПустаяСсылка))
	|		ИНАЧЕ ВТДанныеДокументов.ВыбранноеОтветственноеЛицо
	|	КОНЕЦ КАК ОтветственноеЛицоПоУмолчанию
	|ПОМЕСТИТЬ ВТДокументыСОтветственными
	|ИЗ
	|	ВТДанныеДокументов КАК ВТДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтветственныеПоУмолчанию КАК ВТОтветственныеПоУмолчанию
	|		ПО ВТДанныеДокументов.Организация = ВТОтветственныеПоУмолчанию.Организация
	|			И ВТДанныеДокументов.ОтветственноеЛицо = ВТОтветственныеПоУмолчанию.ОтветственноеЛицо
	|			И ВТДанныеДокументов.Дата = ВТОтветственныеПоУмолчанию.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТДокументыСОтветственными.ОтветственноеЛицоПоУмолчанию.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА (1000)) КАК ФИО
	|ИЗ
	|	ВТДокументыСОтветственными КАК ВТДокументыСОтветственными
	|ГДЕ
	|	ВТДокументыСОтветственными.ОтветственноеЛицоПоУмолчанию.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)";
	
	// Для всех выбранных физ. лиц получим временную таблицу с их ФИО
	ТаблицаФизЛиц = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекСтр Из ТаблицаФизЛиц Цикл
		ТекСтр.ФИО = СокрЛП(ФизическиеЛица.ФамилияИнициалыФизЛица(ТекСтр.ФизическоеЛицо));
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТаблицаФизЛиц", ТаблицаФизЛиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаФизЛиц.ФизическоеЛицо,
	|	ТаблицаФизЛиц.ФИО
	|ПОМЕСТИТЬ ВТФИОФизЛиц
	|ИЗ
	|	&ТаблицаФизЛиц КАК ТаблицаФизЛиц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДокументыСОтветственными.Ссылка,
	|	ВТДокументыСОтветственными.ОтветственноеЛицо,
	|	ВТДокументыСОтветственными.ОтветственноеЛицоПоУмолчанию,
	|	ЕСТЬNULL(ВТФИОФизЛиц.ФИО, """") КАК ФИО
	|ПОМЕСТИТЬ ВТДокументыСФИООтветственных
	|ИЗ
	|	ВТДокументыСОтветственными КАК ВТДокументыСОтветственными
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИОФизЛиц КАК ВТФИОФизЛиц
	|		ПО ВТДокументыСОтветственными.ОтветственноеЛицоПоУмолчанию.ФизическоеЛицо = ВТФИОФизЛиц.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	// Свернем результат - поместим всех ответственных в одну запись выборки запроса
	ШаблонЗапроса = "
	|ВЫБРАТЬ
	|	ВТДокументыСФИООтветственных.Ссылка%СтрокаВыборкаЗапроса%
	|ПОМЕСТИТЬ ВТДокументыСФИООтветственныхСвернутая
	|ИЗ
	|(%ВложенныйЗапрос%) КАК ВТДокументыСФИООтветственных
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДокументыСФИООтветственных.Ссылка
	|";
	
	ШаблонВложенногоЗапроса = "
	|ВЫБРАТЬ
	|	ВТДокументыСФИООтветственных.Ссылка%СтрокаВыборкаВложенногоЗапроса%
	|ИЗ
	|	ВТДокументыСФИООтветственных КАК ВТДокументыСФИООтветственных
	|ГДЕ
	|	ВТДокументыСФИООтветственных.ОтветственноеЛицо = ЗНАЧЕНИЕ(%ОтветственноеЛицо%)
	|";
	
	Индекс = 0;
	СтрокаВыборкаЗапроса = "";
	ТекстВложенногоЗапроса = "";
	
	Для Каждого КлючИЗначение Из РеквизитыОтветственныеЛица Цикл
		
		Индекс = Индекс + 1;
		
		СтрокаВыборкаВложенногоЗапроса = "";
		Для Каждого КлючИЗначение2 Из РеквизитыОтветственныеЛица Цикл
			
			Если КлючИЗначение.Ключ = КлючИЗначение2.Ключ Тогда
				СтрокаВыборкаВложенногоЗапроса = СтрокаВыборкаВложенногоЗапроса + ",
				|	ВТДокументыСФИООтветственных.ОтветственноеЛицоПоУмолчанию КАК " + КлючИЗначение2.Ключ + ",
				|	ВТДокументыСФИООтветственных.ФИО КАК " + КлючИЗначение2.Ключ + "ФИО";
			Иначе
				СтрокаВыборкаВложенногоЗапроса = СтрокаВыборкаВложенногоЗапроса + ",
				|	ЗНАЧЕНИЕ(Справочник.ОтветственныеЛицаОрганизаций.ПустаяСсылка) КАК " + КлючИЗначение2.Ключ + ",
				|	"""" КАК " + КлючИЗначение2.Ключ + "ФИО";
			КонецЕсли;
			
		КонецЦикла;
		
		Если Индекс > 1 Тогда
			ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + ШаблонВложенногоЗапроса;
		
		ТекстВложенногоЗапроса = СтрЗаменить(ТекстВложенногоЗапроса , "%СтрокаВыборкаВложенногоЗапроса%", СтрокаВыборкаВложенногоЗапроса);
		ТекстВложенногоЗапроса = СтрЗаменить(ТекстВложенногоЗапроса , "%ОтветственноеЛицо%", 			  ПолучитьПолноеИмяПредопределенногоЗначения(КлючИЗначение.Значение));
		
		СтрокаВыборкаЗапроса = СтрокаВыборкаЗапроса + ",
		|	МАКСИМУМ(ВТДокументыСФИООтветственных." + КлючИЗначение.Ключ + ") КАК " + КлючИЗначение.Ключ + ",
		|	МАКСИМУМ(ВТДокументыСФИООтветственных." + КлючИЗначение.Ключ + "ФИО) КАК " + КлючИЗначение.Ключ + "ФИО";

	КонецЦикла;
	
	ТекстЗапроса = СтрЗаменить(ШаблонЗапроса , "%СтрокаВыборкаЗапроса%", СтрокаВыборкаЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса ,  "%ВложенныйЗапрос%", 	 ТекстВложенногоЗапроса);
	
	Запрос.Текст = Запрос.Текст + ТекстЗапроса;
	
	// Расшифруем данные - выберем нужные реквизиты ответственных лиц в отдельные поля
	ШаблонЗапроса = "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСвернутая.Ссылка КАК Ссылка%СтрокаВыборка%
	|ПОМЕСТИТЬ ТаблицаОтветственныеЛица
	|ИЗ
	|	ВТДокументыСФИООтветственныхСвернутая КАК ВТСвернутая
	|";
	
	ШаблонСтрокиВыборки = ",
	|	ВТСвернутая.%Реквизит% КАК %Реквизит%,
	|	ЕСТЬNULL(ВТСвернутая.%Реквизит%.ФизическоеЛицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК %Реквизит%ФизическоеЛицо,
	|	ВТСвернутая.%Реквизит%ФИО 
	|		+ ВЫБОР
	|			КОГДА ВТСвернутая.%Реквизит%.ОснованиеПраваПодписи ЕСТь NULL
	|				ИЛИ ВТСвернутая.%Реквизит%.ОснованиеПраваПодписи = """"
	|			ТОГДА """"
	|			ИНАЧЕ "", "" + ВТСвернутая.%Реквизит%.ОснованиеПраваПодписи
	|		  КОНЕЦ КАК %Реквизит%Наименование,
	|	ЕСТЬNULL(ВТСвернутая.%Реквизит%.Должность, """") КАК %Реквизит%Должность,
	|	ЕСТЬNULL(ВТСвернутая.%Реквизит%.ОснованиеПраваПодписи, """") КАК %Реквизит%ОснованиеПраваПодписи";

	СтрокаВыборка = "";
	Для Каждого КлючИЗначение Из РеквизитыОтветственныеЛица Цикл
		СтрокаВыборка = СтрокаВыборка + СтрЗаменить(ШаблонСтрокиВыборки , "%Реквизит%", КлючИЗначение.Ключ);
	КонецЦикла;
	
	ТекстЗапроса = СтрЗаменить(ШаблонЗапроса , "%СтрокаВыборка%", СтрокаВыборка);
	
	// Удалим ненужные временные таблицы
	Запрос.Текст = Запрос.Текст + ТекстЗапроса + "
	|;
	|УНИЧТОЖИТЬ ВТДанныеДокументов
	|;
	|УНИЧТОЖИТЬ ВТНезаполненныеОтветственные
	|;
	|УНИЧТОЖИТЬ ВТОтветственныеПоУмолчанию
	|;
	|УНИЧТОЖИТЬ ВТДокументыСОтветственными
	|;
	|УНИЧТОЖИТЬ ВТДокументыСФИООтветственных
	|;
	|УНИЧТОЖИТЬ ВТДокументыСФИООтветственныхСвернутая
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//Формирует параметры указания отвественных лиц документа
//	
//	Параметры:
//		Объект - ДокументОбъект, ДаныеФормыСтруктура - объект, для которого нужно получить параметры
//	Возвращаемое значение:
//		Структура:
//			РеквизитыОтветственныеЛица - структура - ключ - имя ревизита документа, хранящего информацию об ответсвенном лице
//													- значение - ПеречислениеСсылка.ОтветственныеЛицаОрганизаций - "вид" ответственного лица
//			Ссылка - ДокументСсылка
//			Дата   - Дата - дата документа
//          Организация - СправочникСсылка.Организации - организация, для которой нужно получаять отвественых лиц
//          <ИмяРеквизитаОтвественногоЛица> - СправочникСсылка.ОтвественныеЛицаОрганизаций - текущие отвественные лица, указанные в документе
//			
Функция ПараметрыУказанияОтветственныхЛиц(Объект)
	
	ПараметрыУказанияОтветственныхЛиц = Новый Структура("Ссылка, Дата, Организация");
	ПараметрыУказанияОтветственныхЛиц.Вставить("РеквизитыОтветственныеЛица", РеквизитыОтветственныхЛицДокумента(Объект.Ссылка.Метаданные()));
	Для Каждого ЭлементСтруктуры из ПараметрыУказанияОтветственныхЛиц.РеквизитыОтветственныеЛица Цикл
		ПараметрыУказанияОтветственныхЛиц.Вставить(ЭлементСтруктуры.Ключ);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ПараметрыУказанияОтветственныхЛиц, Объект);
	
	Возврат ПараметрыУказанияОтветственныхЛиц;
	
КонецФункции

// Возвращает структуру реквизитов справочника ОтветственныеЛицаОрганизаций.
//
// Параметры
// 	Нет
//
// Возвращаемое значение:
// 	Структура - структура, ключами которой являются реквизиты (в т.ч. и стандартные реквизиты) справочника.
//
Функция ВозможныеПоляОтбораОтветственныхЛиц()
	
	ПоляОтбора 			  = Новый Структура;
	МетаданныеСправочника = Метаданные.Справочники.ОтветственныеЛицаОрганизаций;
	
	Для Каждого Элемент Из МетаданныеСправочника.Реквизиты Цикл
		ПоляОтбора.Вставить(Элемент.Имя);
	КонецЦикла;
	Для Каждого Элемент Из МетаданныеСправочника.СтандартныеРеквизиты Цикл
		ПоляОтбора.Вставить(Элемент.Имя);
	КонецЦикла;
	
	Возврат ПоляОтбора;
	
КонецФункции

// Возвращает структуру реквизитов документа, предназначенных для хранения информации об ответственных лицах.
// Реквизит ответственного лица документа должен
//	- иметь тип СправочникСсылка.ОтветственныеЛицаОрганизаций
// 	- содержать параметр выбора с типом ПеречислениеСсылка.ОтветственныеЛицаОрганизаций ("вид" ответственного)
//
// Параметры
//  ИмяДокумента - Строка - имя документа в метаданных конфигурации.
//
// Возвращаемое значение:
// 	Структура - структура ответственных лиц документа
// 		Ключ 	 - имя реквизита документа
// 		Значение - "вид" ответственного лица документа
//
Функция РеквизитыОтветственныхЛицДокумента(МетаданныеДокумента)
	
	СтруктураОтветственных  = Новый Структура;
	
	ТипРеквизита 	  = Тип("СправочникСсылка.ОтветственныеЛицаОрганизаций");
	ТипЗначенияВыбора = Тип("ПеречислениеСсылка.ОтветственныеЛицаОрганизаций");
	
	Для Каждого Реквизит Из МетаданныеДокумента.Реквизиты Цикл
		
		Если НЕ Реквизит.Тип.СодержитТип(ТипРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		
		// Нашли в документе реквизит ответственного лица.
		Для Индекс = 0 По Реквизит.ПараметрыВыбора.Количество() - 1 Цикл
			
			ЗначениеВыбора = Реквизит.ПараметрыВыбора.Получить(Индекс).Значение;
			
			Если ТипЗнч(ЗначениеВыбора) = ТипЗначенияВыбора Тогда
				// Нашли "вид" реквизита ответственного лица.
				СтруктураОтветственных.Вставить(Реквизит.Имя, ЗначениеВыбора);
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат СтруктураОтветственных;
	
КонецФункции

// Проверяет соответствие ответственного лица и ключевых реквизитов документа.
//
// Параметры
//	РеквизитыДляПроверки - Структура - структура реквизитов документа с ключами
//		- Дата
//		- Организация
//		- ОтветственноеЛицо - "вид" ответственного лица
//		- ПометкаУдаления - необязательный, проверять пометку удаления ответственного лица
//	ОтветственноеЛицоДляПроверки - СправочникСсылка.ОтветственныеЛицаОрганизаций - ответственное лицо
//
// Возвращаемое значение:
//	Булево - результат проверки
//
Функция ПроверитьСоответствиеРеквизитовИОтветственногоЛица(РеквизитыДляПроверки, ОтветственноеЛицоДляПроверки)
	
	Если НЕ ЗначениеЗаполнено(ОтветственноеЛицоДляПроверки) Тогда
		Возврат Истина;
	КонецЕсли;
	
	РеквизитыОтветственногоЛица =
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ОтветственноеЛицоДляПроверки,
			"Владелец, ОтветственноеЛицо, ДатаНачала, ДатаОкончания, ПометкаУдаления");
	
	Дата = НачалоДня(РеквизитыДляПроверки.Дата);
	
	Если РеквизитыОтветственногоЛица.Владелец <> РеквизитыДляПроверки.Организация
	 Или РеквизитыОтветственногоЛица.ОтветственноеЛицо <> РеквизитыДляПроверки.ОтветственноеЛицо
	 Или РеквизитыОтветственногоЛица.ДатаНачала > Дата
	 Или (ЗначениеЗаполнено(РеквизитыОтветственногоЛица.ДатаОкончания) И РеквизитыОтветственногоЛица.ДатаОкончания < Дата)
	 Или (РеквизитыДляПроверки.Свойство("ПометкаУдаления")
	 		И РеквизитыОтветственногоЛица.ПометкаУдаления <> РеквизитыДляПроверки.ПометкаУдаления) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Проверяет соответствие значений реквизитов ответственных лиц документа и ключевых реквизитов документа.
// Если значения не соответствуют то процедура подбирает подходящее значение для ответственного лица.
// Параметры
// 	РеквизитыДокумента - ДокументОбъект, Структура - данные документа
//		- ключевые реквизиты (Дата, Организация, Ссылка)
//		- реквизиты ответственных лиц
//
// Возвращаемое значение:
//  Структура - структура измененных реквизитов ответственных лиц документа
//		Ключ 	 - имя измененного реквизита документа
//		Значение - новое значение реквизита
//
Функция СкорректироватьОтветственныхЛицДокумента(ПараметрыУказанияОтветственныхЛиц)
	
	ИзмененныеРеквизиты = Новый Структура;
	
	РеквизитыОтветственныеЛица = ПараметрыУказанияОтветственныхЛиц.РеквизитыОтветственныеЛица;
	Дата = ?(ЗначениеЗаполнено(ПараметрыУказанияОтветственныхЛиц.Дата), ПараметрыУказанияОтветственныхЛиц.Дата, ТекущаяДатаСеанса());
	
	ТаблицаОтветственных = ПолучитьТаблицуОтветственныхЛицПоОтбору(
		Новый Структура("Дата, Организация", Дата, ПараметрыУказанияОтветственныхЛиц.Организация));
	
	Для Каждого КлючИЗначение Из РеквизитыОтветственныеЛица Цикл
		
		ЗначениеОтветственногоЛица = ПараметрыУказанияОтветственныхЛиц[КлючИЗначение.Ключ];
		РеквизитыДляПроверки = 
			Новый Структура(
				"Дата, Организация, ОтветственноеЛицо, ПометкаУдаления",
				Дата, ПараметрыУказанияОтветственныхЛиц.Организация, КлючИЗначение.Значение, Ложь);
		
		// Если выбрано неподходящее ответственное лицо, то очистим его
		Если ЗначениеЗаполнено(ЗначениеОтветственногоЛица)
		 И Не ПроверитьСоответствиеРеквизитовИОтветственногоЛица(РеквизитыДляПроверки, ЗначениеОтветственногоЛица) Тогда
			ЗначениеОтветственногоЛица = Справочники.ОтветственныеЛицаОрганизаций.ПустаяСсылка();
		КонецЕсли;
		
		// Если ответственное лицо не выбрано, то попытаемся подобрать его
		Если Не ЗначениеЗаполнено(ЗначениеОтветственногоЛица) Тогда
			СтрокиТаблицы = ТаблицаОтветственных.НайтиСтроки(Новый Структура("ОтветственноеЛицо", КлючИЗначение.Значение));
			Если СтрокиТаблицы.Количество() = 1 Тогда
				ЗначениеОтветственногоЛица = СтрокиТаблицы[0].Ссылка; // можно однозначно определить ответственного
			КонецЕсли;
		КонецЕсли;
		
		// Запомним изменение реквизита
		Если ПараметрыУказанияОтветственныхЛиц[КлючИЗначение.Ключ] <> ЗначениеОтветственногоЛица Тогда
			ИзмененныеРеквизиты.Вставить(КлючИЗначение.Ключ, ЗначениеОтветственногоЛица);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИзмененныеРеквизиты;
	
КонецФункции
