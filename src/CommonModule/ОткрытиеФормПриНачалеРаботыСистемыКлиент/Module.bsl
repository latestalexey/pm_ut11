////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Обработчик события ПриНачалеРаботыСистемы вызывается
// для выполнения действий, требуемых для подсистемы ОткрытиеФормПриНачалеРаботыСистемы.
//
Процедура ПриНачалеРаботыСистемы() Экспорт
	
	МассивФорм = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске().ФормыОткрываемыеПриНачалеРаботыСистемы;
	
	Для Каждого ТекущаяФорма из МассивФорм Цикл
		
		Если ТекущаяФорма.ОткрыватьПоУмолчанию Тогда
			
			ОткрытьТекущуюФорму(ТекущаяФорма);
			
		Иначе
			
			Если Найти(ВРег(ПараметрЗапуска), ВРег(ТекущаяФорма.ПараметрЗапуска)) = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ОткрытьТекущуюФорму(ТекущаяФорма);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ОткрытьТекущуюФорму(ТекущаяФорма)
	
	Если ТекущаяФорма.НеобходимыНастройки И Не ТекущаяФорма.НастройкиУстановлены Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При попытке открытия формы ""%1"" произошла ошибка - не заданы настройки формы. Обратитесь к администратору.'"),
			ТекущаяФорма.ИмяЗапускаемойФормы);
		
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
	Если ВРег(ТекущаяФорма.ПараметрЗапуска) = ВРег("WarehouseMobileWorkplace") Тогда
		
		Если Не ТекущаяФорма.Параметры.Свойство("РазрешениеЭкрана") Тогда
			
			ИмяЗапускаемойФормы = "Обработка.РабочееМестоРаботникаСклада.Форма.ФормаРабочегоМеста_320х320";
			
		Иначе
			
			Если ТекущаяФорма.Параметры.РазрешениеЭкрана = ПредопределенноеЗначение("Перечисление.РазрешенияЭкрана._320х320") Тогда
				
				ИмяЗапускаемойФормы = "Обработка.РабочееМестоРаботникаСклада.Форма.ФормаРабочегоМеста_320х320";
				
			ИначеЕсли ТекущаяФорма.Параметры.РазрешениеЭкрана = ПредопределенноеЗначение("Перечисление.РазрешенияЭкрана._240х320") Тогда
				
				ИмяЗапускаемойФормы = "Обработка.РабочееМестоРаботникаСклада.Форма.ФормаРабочегоМеста_240х320";
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ИмяЗапускаемойФормы = ТекущаяФорма.ИмяЗапускаемойФормы;
		
	КонецЕсли;
	
	ОткрытьФорму(ИмяЗапускаемойФормы, ТекущаяФорма.Параметры);
	
КонецПроцедуры