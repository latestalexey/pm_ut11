////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Определяет разделы, в которых доступна панель отчетов.
//
// Параметры:
//   Разделы (Массив) из (ОбъектМетаданных)
//
// Описание:
//   В Разделы необходимо добавить метаданные подсистем тех разделов,
//   в которых размещены команды вызова панелей отчетов.
//
// Например:
//	Разделы.Добавить(Метаданные.Подсистемы.ИмяПодсистемы);
//
Процедура ОпределитьРазделыСВариантамиОтчетов(Разделы) Экспорт
	
	Разделы.Добавить(Метаданные.Подсистемы.Администрирование);
	Разделы.Добавить(Метаданные.Подсистемы.ЗапасыИЗакупки);
	Разделы.Добавить(Метаданные.Подсистемы.Маркетинг);
	Разделы.Добавить(Метаданные.Подсистемы.Органайзер);
	Разделы.Добавить(Метаданные.Подсистемы.Продажи);
	Разделы.Добавить(Метаданные.Подсистемы.Склад);
	Разделы.Добавить(Метаданные.Подсистемы.Финансы);
	Разделы.Добавить(Метаданные.Подсистемы.РегламентированныйУчет);
	
КонецПроцедуры

// Содержит настройки размещения вариантов отчетов в панели отчетов.
//
// Параметры:
//   Настройки (ДеревоЗначений) Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации()
//
// Описание:
//   В данной процедуре необходимо указать каким именно образом предопределенные варианты отчетов
//   будут регистрироваться в системе и показываться в панели отчетов.
//
// Вспомогательные функции:
//   Отчет = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.<ИмяОтчета>);
//   Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "<ИмяВарианта>");
//
//   Данные функции получают описание отчета или варианта отчета следующей структуры:
//       |- Включен (Булево)
//            Если Ложь, то вариант отчета не регистрируется в подсистеме.
//              Используется для удаления технических и контекстных вариантов отчетов из всех интерфейсов.
//              Эти варианты отчета по прежнему можно открывать в форме отчета программно при помощи
//              параметров открытия (см. справку по "Расширение управляемой формы для отчета.КлючВарианта").
//       |- ВидимостьПоУмолчанию (Булево)
//            Если Ложь, то вариант отчета по умолчанию скрыт в панели отчетов.
//              Пользователь может "включить" его в режиме настройки панели отчетов
//              или открыть через форму "Все отчеты".
//       |- Описание (Строка)
//            Дополнительная информация по варианту отчета.
//              В панели отчетов выводится в качестве подсказки.
//              Должно расшифровывать для пользователя содержимое варианта отчета
//              и не должно дублировать наименование варианта отчета.
//       |- Размещение (Соответствие) Настройки размещения варианта отчета в разделах
//           |- Ключ     (ОбъектМетаданных) Подсистема, в которой размещается отчет или вариант отчета
//           |- Значение (Строка)           Необязательный. Настройки размещения в подсистеме.
//               |- ""        - Выводить отчет в своей группе обычным шрифтом.
//               |- "Важный"  - Выводить отчет в своей группе жирным шрифтом.
//               |- "СмТакже" - Выводить отчет в группе "См. также".
//
// Например:
//
//  (1) Оставить в подсистеме только один из вариантов отчета
//	Отчет = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ИмяОтчета);
//	Отчет.Размещение.Удалить(Метаданные.Подсистемы.ИмяРаздела);
//	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "ИмяВарианта1");
//	Отчет.Размещение.Вставить(Метаданные.Подсистемы.ИмяРаздела);
//
//  (2) Отключить вариант отчета
//	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.ИмяОтчета, "ИмяВарианта1");
//	Вариант.Включен = Ложь;
//
//  (3) Отключить все варианты отчета, кроме требуемого
//	Отчет = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ИмяОтчета);
//	Отчет.Включен = Ложь;
//	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "ИмяВарианта");
//	Вариант.Включен = Истина;
//
//  (4) Результат исполнения любого из двух фрагментов кода будет одинаковым:
//	Отчет = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ИмяОтчета);
//	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "ИмяВарианта1");
//	Вариант.Размещение.Удалить(Метаданные.Подсистемы.ИмяРаздела.Подсистемы.ИмяПодсистемы);
//	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "ИмяВарианта2");
//	Вариант.Размещение.Удалить(Метаданные.Подсистемы.ИмяРаздела.Подсистемы.ИмяПодсистемы);
//	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "ИмяВарианта3");
//	Вариант.Размещение.Удалить(Метаданные.Подсистемы.ИмяРаздела.Подсистемы.ИмяПодсистемы);
//
//	Отчет = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ИмяОтчета);
//	Отчет.Размещение.Удалить(Метаданные.Подсистемы.ИмяРаздела.Подсистемы.ИмяПодсистемы);
//	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "ИмяВарианта1");
//	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "ИмяВарианта2");
//	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "ИмяВарианта3");
//	Отчет.Размещение.Вставить(Метаданные.Подсистемы.ИмяРаздела.Подсистемы.ИмяПодсистемы);
//
//
// Важно:
//   Начальная настройка размещения отчетов по разделам зачитывается из метаданных,
//   ее дублирование в коде не требуется.
//   
//   Настройки варианта имеют приоритет над настройками отчета.
//   
//   Настройки варианта при получении формируются из настроек отчета
//   и после получения не зависят от настроек отчета (становятся самостоятельными, см. примеры 3 и 4).
//
Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	ОтчетыСНетиповымиНастройками = ПолучитьИменаОтчетовСНетиповымиНастройками();
	Для Каждого ИмяОтчета Из ОтчетыСНетиповымиНастройками Цикл
		Отчеты[ИмяОтчета].НастроитьВариантыОтчета(
			Настройки,
 			ВариантыОтчетов.ОписаниеОтчета(
				Настройки, 
				Метаданные.Отчеты[ИмяОтчета]
			)
		);
	КонецЦикла;
	
КонецПроцедуры 

// Содержит описания изменений имен вариантов отчетов. Используется
//   при обновлении информационной базы, в целях контроля ссылочной целостности
//   и для сохранения настроек варианта, сделанных администратором.
//
// Параметры:
//   Изменения (ТаблицаЗначений) Таблица изменений имен вариантов
//       |- Отчет (ОбъектМетаданных)
//       |- СтароеИмяВарианта (Строка)
//       |- АктуальноеИмяВарианта (Строка)
//
// Описание:
//   В Изменения необходимо добавить описания изменений имен вариантов
//   отчетов, подключенных к подсистеме.
//
// Например:
//	ДобавитьИзменениеКлючей(Изменения, "<ИмяОтчета>", "<СтароеИмяВарианта>", "<АктуальноеИмяВарианта>");
//
// Важно:
//   Старое имя варианта резервируется и не может быть использовано в дальнейшем.
//   Если изменений было несколько, то каждое изменение необходимо зарегистрировать,
//   указывая в актуальном имени варианта последнее (текущее) имя варианта отчета.
//   Поскольку имена вариантов отчетов не выводятся в пользовательском интерфейсе,
//   то рекомендуется задавать их таким образом, что бы затем не менять.
//
Процедура ЗарегистрироватьИзмененияКлючейВариантовОтчетов(Изменения) Экспорт
	
	ДобавитьИзменениеКлючей(Изменения, "АнализПоступленийРасходовТоваров", "АнализПоступленийТоваров", "СебестоимостьПоступившихТоваров");
	ДобавитьИзменениеКлючей(Изменения, "АнализПоступленийРасходовТоваров", "АнализРасходовТоваров", "СебестоимостьВыбывшихТоваров");
	ДобавитьИзменениеКлючей(Изменения, "АнализПоступленийРасходовТоваров", "АнализСписанийТоваров", "СебестоимостьСписанныхТоваров");
	ДобавитьИзменениеКлючей(Изменения, "АнализРасчетовСКлиентами", "ИнтервалыГоризонтально", "ДебиторскаяЗадолженность");
	ДобавитьИзменениеКлючей(Изменения, "АнализРасчетовСКлиентами", "ПоРасчетнымДокументам", "ДебиторскаяЗадолженностьПоРасчетнымДокументам");
	ДобавитьИзменениеКлючей(Изменения, "АнализРасчетовСПоставщиками", "ИнтервалыГоризонтально", "КредиторскаяЗадолженность");
	ДобавитьИзменениеКлючей(Изменения, "АнализРасчетовСПоставщиками", "ПоРасчетнымДокументам", "КредиторскаяЗадолженностьПоРасчетнымДокументам");
	ДобавитьИзменениеКлючей(Изменения, "ВедомостьПоТоварамОрганизацийВЦенахНоменклатуры", "Основной", "ВедомостьПоТоварамОрганизацийВЦенахНоменклатуры");
	ДобавитьИзменениеКлючей(Изменения, "ВыручкаИСебестоимостьПродаж", "ПродажиСписок", "ПродажиПоПартнерам");
	ДобавитьИзменениеКлючей(Изменения, "ВыручкаИСебестоимостьПродаж", "АнализПродажПоПоставщикам", "ПродажиПоПоставщикам");
	ДобавитьИзменениеКлючей(Изменения, "ВыручкаИСебестоимостьПродаж", "АнализПродажПоБизнесРегионамДиаграмма", "АнализПродажПоБизнесРегионам");
	ДобавитьИзменениеКлючей(Изменения, "ДинамикаПоказателейРаботыТорговыхПредставителей", "Основной", "ДинамикаПоказателейТорговыхПредставителей");
	ДобавитьИзменениеКлючей(Изменения, "КарточкаРасчетовСКлиентами", "Основной", "КарточкаРасчетовСКлиентами");
	ДобавитьИзменениеКлючей(Изменения, "КарточкаРасчетовСПоставщиками", "Основной", "КарточкаРасчетовСПоставщиками");
	ДобавитьИзменениеКлючей(Изменения, "ПланФактныйАнализРаботыТорговыхПредставителейПоКоличеству", 
		"ВыполнениеЗаданийПоКоличествуВРазрезеТорговыхПредставителей", "ВыполнениеЗаданийПоПредставителям");
	ДобавитьИзменениеКлючей(Изменения, "ПланФактныйАнализРаботыТорговыхПредставителейПоКоличеству", 
		"ВыполнениеЗаданийПоКоличествуВРазрезеНоменклатуры", "ВыполнениеЗаданийПоНоменклатуре");
	ДобавитьИзменениеКлючей(Изменения, "РаспределениеТорговыхПредставителей", "Основной", "КалендарьВизитовТорговыхПредставителей");
	ДобавитьИзменениеКлючей(Изменения, "СостояниеВыполненияЗаказовКлиентов", "Основной", "СостояниеВыполненияЗаказовКлиентов");
	ДобавитьИзменениеКлючей(Изменения, "СостояниеВыполненияЗаказовПоставщикам", "Основной", "СостояниеВыполненияЗаказовПоставщикам");
	ДобавитьИзменениеКлючей(Изменения, "СостояниеРасчетовСКлиентами", "Основной", "СостояниеРасчетовСКлиентами");
	ДобавитьИзменениеКлючей(Изменения, "СостояниеРасчетовСПоставщиками", "Основной", "СостояниеРасчетовСПоставщиками");
	ДобавитьИзменениеКлючей(Изменения, "ТоварныйКалендарь", "Основной", "ТоварныйКалендарь");
	
КонецПроцедуры

// Функция возвращает имена отчетов, для которых необходима нетиповая настройка размещения вариантов в панели отчетов.
// В модуле менеджера отчета должна быть объявлена экспортная процедура НастроитьВариантыОтчета.
//
// Возвращаемое значение:
//	Массив
//	  |- Строка - имя отчета
//
Функция ПолучитьИменаОтчетовСНетиповымиНастройками(ОтчетыСНетиповымиНастройками = Неопределено) Экспорт

	Если ОтчетыСНетиповымиНастройками = Неопределено Тогда
		ОтчетыСНетиповымиНастройками = Новый Массив;
	КонецЕсли;
	
	// Отчеты, полностью отключенные от механизма "Варианты отчетов".
	ОтчетыСНетиповымиНастройками.Добавить("АнализПричинОтменыЗаказовПоставщикамПоДокументу");
	ОтчетыСНетиповымиНастройками.Добавить("АнализОстатковТоваровОрганизаций");
	ОтчетыСНетиповымиНастройками.Добавить("ДанныеАссортиментаНоменклатуры");
	ОтчетыСНетиповымиНастройками.Добавить("ДинамикаИзмененияЦенНоменклатуры");
	ОтчетыСНетиповымиНастройками.Добавить("ДинамикаИзмененияЦенНоменклатурыПоставщика");
	ОтчетыСНетиповымиНастройками.Добавить("КарточкаПартииПоВидамНалогообложения");
	ОтчетыСНетиповымиНастройками.Добавить("ОстаткиТоваровОрганизаций");
	ОтчетыСНетиповымиНастройками.Добавить("ПримененныеСкидкиВДокументе");
	
	// Отчеты с индивидуальными настройками.
	ОтчетыСНетиповымиНастройками.Добавить("АнализДвиженийДенежныхСредств");
	ОтчетыСНетиповымиНастройками.Добавить("АнализДоступностиТоваров");
	ОтчетыСНетиповымиНастройками.Добавить("АнализДоходовРасходов");
	ОтчетыСНетиповымиНастройками.Добавить("АнализИсполненияАссортимента");
	ОтчетыСНетиповымиНастройками.Добавить("АнализРасчетовСКлиентами");
	ОтчетыСНетиповымиНастройками.Добавить("АнализРасчетовСПоставщиками");
	ОтчетыСНетиповымиНастройками.Добавить("АнализСебестоимостиТоваровПоПоставщикам");
	ОтчетыСНетиповымиНастройками.Добавить("АнализФинансовыхРезультатов");
	ОтчетыСНетиповымиНастройками.Добавить("ВедомостьПоТоварамНаСкладах");
	ОтчетыСНетиповымиНастройками.Добавить("ВедомостьПоТоварамОрганизаций");
	ОтчетыСНетиповымиНастройками.Добавить("ВыручкаИСебестоимостьПродаж");
	ОтчетыСНетиповымиНастройками.Добавить("ДвиженияСерийТоваров");
	ОтчетыСНетиповымиНастройками.Добавить("КарточкаРасчетовСКлиентами");
	ОтчетыСНетиповымиНастройками.Добавить("КарточкаРасчетовСПоставщиками");
	ОтчетыСНетиповымиНастройками.Добавить("КонтактнаяИнформация");
	ОтчетыСНетиповымиНастройками.Добавить("НаличныеДенежныеСредства");
	ОтчетыСНетиповымиНастройками.Добавить("ОстаткиИДвиженияДенежныхСредств");
	ОтчетыСНетиповымиНастройками.Добавить("ОформлениеИзлишковНедостачТоваров");
	ОтчетыСНетиповымиНастройками.Добавить("ПрайсЛист");
	ОтчетыСНетиповымиНастройками.Добавить("ПричиныЗапретаОтгрузки");
	ОтчетыСНетиповымиНастройками.Добавить("РасчетыСПартнерами");
	ОтчетыСНетиповымиНастройками.Добавить("СостояниеРасчетовСКлиентами");
	ОтчетыСНетиповымиНастройками.Добавить("СостояниеРасчетовСПоставщиками");
	ОтчетыСНетиповымиНастройками.Добавить("СправочноеРазмещениеНоменклатуры");
	ОтчетыСНетиповымиНастройками.Добавить("СравнительныйАнализДинамикиАссортиментаИПродаж");
	ОтчетыСНетиповымиНастройками.Добавить("ТоварыВЯчейках");
	ОтчетыСНетиповымиНастройками.Добавить("ТоварыКОтгрузке");
	ОтчетыСНетиповымиНастройками.Добавить("ТоварыНаСкладахПоСрокамГодности");
	ОтчетыСНетиповымиНастройками.Добавить("ТоварныйКалендарь");

	Возврат ОтчетыСНетиповымиНастройками;
	
КонецФункции

// Функция возвращает имена отчетов, для которых необходима нетиповая настройка размещения вариантов в панели отчетов.
// В модуле менеджера отчета должна быть объявлена экспортная процедура НастроитьВариантыОтчета.
//
// Параметры:
//   ЗависимыеОтчеты (Массив) Т- имена отчетов, зависимых от функциональных опций
//
// Возвращаемое значение:
//	Массив
//	  |- Строка - имя отчета
//
Функция ПолучитьИменаОтчетовЗависимыхОтФункциональныхОпций(ЗависимыеОтчеты = Неопределено) Экспорт

	Если ЗависимыеОтчеты = Неопределено Тогда
		ЗависимыеОтчеты = Новый Массив;
	КонецЕсли;
	
	ЗависимыеОтчеты.Добавить("АнализДвиженийДенежныхСредств");
	ЗависимыеОтчеты.Добавить("АнализДоходовРасходов");
	ЗависимыеОтчеты.Добавить("АнализПричинОтменыЗаказовКлиентов");
	ЗависимыеОтчеты.Добавить("ВыручкаИСебестоимостьПродаж");
	ЗависимыеОтчеты.Добавить("ДвиженияСерийТоваров");
	ЗависимыеОтчеты.Добавить("КонтрольДенежныхСредств");
	ЗависимыеОтчеты.Добавить("НаличныеДенежныеСредства");
	ЗависимыеОтчеты.Добавить("ОстаткиИДвиженияДенежныхСредств");
	ЗависимыеОтчеты.Добавить("ПлатежныйКалендарь");
	ЗависимыеОтчеты.Добавить("ЭффективностьСделокСКлиентами");
	
	Возврат ЗависимыеОтчеты;
	
КонецФункции

// Содержит настройки размещения вариантов отчетов в панели отчетов.
//
// Параметры:
//   КлючОбъекта (Строка) Имя отчета в виде "Отчет.<ИмяОтчета>"
//
// Возвращаемое значение:
//	Строка - имя отчета без "Отчет."
//
Функция ИмяОтчетаПоКлючуОбъекта(КлючОбъекта) Экспорт
	Возврат СтрЗаменить(КлючОбъекта, "Отчет.", "");
КонецФункции

// Функция пустую таблицу зависимостей вариантов отчетов от функц. опций
//
// Возвращаемое значение:
//	ТаблицаЗначений
//	  |- ФункциональнаяОпция - имя функциональной опции, влияющей на отчет
//	  |- ВариантОтчета - имя варианта отчета, зависимого от функциональной опции
//
Функция ИнициализироватьФункциональныеОпцииВариантовОтчетов() Экспорт
	ФункциональныеОпцииВариантовОтчетов = Новый ТаблицаЗначений;
	ФункциональныеОпцииВариантовОтчетов.Колонки.Добавить("ФункциональнаяОпция", МониторингЦелевыхПоказателей.ПолучитьОписаниеТиповСтроки(0));
	ФункциональныеОпцииВариантовОтчетов.Колонки.Добавить("ВариантОтчета", МониторингЦелевыхПоказателей.ПолучитьОписаниеТиповСтроки(0));
	
	Возврат ФункциональныеОпцииВариантовОтчетов;
КонецФункции

// Обработчик обновления. Включает или отключает варианты отчетов в зависимости от функциональных опций
//
Процедура НастроитьВариантыОтчетовПоФункциональнымОпциям() Экспорт 
	ОтчетыЗависимыеОтФункциональныхОпций = ПолучитьИменаОтчетовЗависимыхОтФункциональныхОпций();
	
	Для Каждого ИмяОтчета Из ОтчетыЗависимыеОтФункциональныхОпций Цикл
		НастраиваемыеВариантыОтчетов = Отчеты[ИмяОтчета].ФункциональныеОпцииВариантовОтчетов();
		
		ИнформацияОбОтчете = ВариантыОтчетов.СформироватьИнформациюОбОтчетеПоПолномуИмени("Отчет."+ИмяОтчета);
		
		НастроенныеВариантыОтчетов = Новый Массив;
		
		Для Каждого НастраиваемыйВариантОтчета Из НастраиваемыеВариантыОтчетов Цикл
			Если Не НастроенныеВариантыОтчетов.Найти(НастраиваемыйВариантОтчета.ВариантОтчета) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ВариантОтчета = ВариантыОтчетов.ПолучитьСсылку(ИнформацияОбОтчете.Отчет, НастраиваемыйВариантОтчета.ВариантОтчета);
			
			// Вариант отчета может быть не зарегистрирован в справочнике "ВариантыОтчетов"
			Если ВариантОтчета = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ОтборПоВариантуОтчета = Новый Структура("ВариантОтчета", НастраиваемыйВариантОтчета.ВариантОтчета);
			НайденныеОпции = НастраиваемыеВариантыОтчетов.НайтиСтроки(ОтборПоВариантуОтчета);
			
			Если НайденныеОпции.Количество() = 1 Тогда
				ЗначениеФункциональнойОпции = ПолучитьФункциональнуюОпцию(НастраиваемыйВариантОтчета.ФункциональнаяОпция);
			Иначе
				ЕстьВыключенные = Ложь;
				Для Каждого НайденнаяОпция Из НайденныеОпции Цикл 
					Если Не ПолучитьФункциональнуюОпцию(НайденнаяОпция.ФункциональнаяОпция) Тогда
						ЕстьВыключенные = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				ЗначениеФункциональнойОпции = Не ЕстьВыключенные;
			КонецЕсли;
			
			Если ВариантОтчета.ПометкаУдаления <> (Не ЗначениеФункциональнойОпции) Тогда
				ВариантОтчетаОбъект = ВариантОтчета.ПолучитьОбъект();
				ВариантОтчетаОбъект.ПометкаУдаления = Не ЗначениеФункциональнойОпции;
				ВариантОтчетаОбъект.ДополнительныеСвойства.Вставить("ЗаполнениеПредопределенных", Истина);
				ВариантОтчетаОбъект.Записать();
			КонецЕсли;
			
			НастроенныеВариантыОтчетов.Добавить(НастраиваемыйВариантОтчета.ВариантОтчета);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

// Функциональные опции складываются по И
//
// Параметры:
//   ИмяФункциональнойОпции (Строка) Используется для передачи имени измененной функциональной опции
//
Процедура НастроитьВариантыОтчетовПоФункциональнойОпции(ИмяФункциональнойОпции, Знач ЗначениеФункциональнойОпции) Экспорт
	ОтчетыЗависимыеОтФункциональныхОпций = ПолучитьИменаОтчетовЗависимыхОтФункциональныхОпций();
	Для Каждого ИмяОтчета Из ОтчетыЗависимыеОтФункциональныхОпций Цикл
		НастраиваемыеВариантыОтчетов = Отчеты[ИмяОтчета].ФункциональныеОпцииВариантовОтчетов();
		
		ОтборПоФункциональнойОпции = Новый Структура("ФункциональнаяОпция", ИмяФункциональнойОпции); 
		Найденные = НастраиваемыеВариантыОтчетов.НайтиСтроки(ОтборПоФункциональнойОпции);
		ОбрабатываемыеПоОпцииВариантыОтчетов = НастраиваемыеВариантыОтчетов.Скопировать(Найденные);
		
		ИнформацияОбОтчете = ВариантыОтчетов.СформироватьИнформациюОбОтчетеПоПолномуИмени("Отчет."+ИмяОтчета);
		
		НастроенныеВариантыОтчетов = Новый Массив;

		Для Каждого ОбрабатываемыйВариантОтчета Из ОбрабатываемыеПоОпцииВариантыОтчетов Цикл
			// Учтем зависимость варианта отчета от других опций
			ВариантОтчета = ВариантыОтчетов.ПолучитьСсылку(ИнформацияОбОтчете.Отчет, ОбрабатываемыйВариантОтчета.ВариантОтчета);
			
			// Вариант отчета может быть не зарегистрирован в справочнике "ВариантыОтчетов", 
			// т.к. на момент обновления функциональная опция была выключена.
			// Зарегистрируем вариант отчета.
			Если ВариантОтчета = Неопределено Тогда
				СтрокиДереваНастроек = ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().Строки;
				НайденныеСтроки = СтрокиДереваНастроек.НайтиСтроки(Новый Структура("Отчет", ИнформацияОбОтчете.Отчет));
				Если Не НайденныеСтроки = Неопределено Тогда
					ВариантыОтчета = НайденныеСтроки[0].Строки;
					НайденныеСтроки = ВариантыОтчета.НайтиСтроки(Новый Структура("КлючВарианта", ОбрабатываемыйВариантОтчета.ВариантОтчета));
					
					Если Не НайденныеСтроки = Неопределено Тогда
						ОписаниеВарианта = НайденныеСтроки[0];
						
						ВариантыОтчетов.ЗаполнитьОписаниеСтрокиВарианта(ОписаниеВарианта);
						
						ВариантОбъект = Справочники.ВариантыОтчетов.СоздатьЭлемент();
						ВариантОбъект.Наименование         = ОписаниеВарианта.Наименование;
						ВариантОбъект.Отчет                = ОписаниеВарианта.Отчет;
						ВариантОбъект.ТипОтчета            = Перечисления.ТипыОтчетов.Внутренний;
						ВариантОбъект.КлючВарианта         = ОписаниеВарианта.КлючВарианта;
						ВариантОбъект.Пользовательский     = Ложь;
						ВариантОбъект.Описание             = ОписаниеВарианта.Описание;
						ВариантОбъект.ВидимостьПоУмолчанию = ОписаниеВарианта.ВидимостьПоУмолчанию;
						
						ВариантыОтчетов.ВосстановитьРазмещениеПредопределенногоВариантаОтчета(ВариантОбъект, ОписаниеВарианта);
						
						ВариантОбъект.Записать();
					КонецЕсли;
				КонецЕсли;
				
				Продолжить;
			КонецЕсли;
			
			ОтборПоВариантуОтчета = Новый Структура("ВариантОтчета", ОбрабатываемыйВариантОтчета.ВариантОтчета);
			НайденныеОпции = НастраиваемыеВариантыОтчетов.НайтиСтроки(ОтборПоВариантуОтчета);
			
			Если НайденныеОпции.Количество() = 1 Тогда
				ЗначениеФункциональнойОпции = ПолучитьФункциональнуюОпцию(ОбрабатываемыйВариантОтчета.ФункциональнаяОпция);
			Иначе
				ЕстьВыключенные = Ложь;
				Для Каждого НайденнаяОпция Из НайденныеОпции Цикл 
					Если Не ПолучитьФункциональнуюОпцию(НайденнаяОпция.ФункциональнаяОпция) Тогда
						ЕстьВыключенные = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				ЗначениеФункциональнойОпции = Не ЕстьВыключенные;
			КонецЕсли;
			
			Если ВариантОтчета.ПометкаУдаления <> (Не ЗначениеФункциональнойОпции) Тогда
				ВариантОтчетаОбъект = ВариантОтчета.ПолучитьОбъект();
				ВариантОтчетаОбъект.ПометкаУдаления = Не ЗначениеФункциональнойОпции;
				ВариантОтчетаОбъект.ДополнительныеСвойства.Вставить("ЗаполнениеПредопределенных", Истина);
				ВариантОтчетаОбъект.Записать();
			КонецЕсли;
			
			НастроенныеВариантыОтчетов.Добавить(ОбрабатываемыйВариантОтчета.ВариантОтчета);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

// Функция пустую таблицу зависимостей вариантов отчетов от функц. опций
//
// Параметры:
//	ФункциональныеОпцииВариантовОтчетов (ТаблицаЗначений) - зависимости вариантов отчетов от функц. опций
//	ИмяВариантОтчета (Строка) - добавляемый зависимый вариант отчета
//	ИмяФункциональнойОпции (Строка) - добавляемый влияющая функциональная опция
//
Процедура ДобавитьФункциональнуюОпциюВариантаОтчета(ФункциональныеОпцииВариантовОтчетов, ИмяВариантОтчета, ИмяФункциональнойОпции) Экспорт
	
	НоваяФОВариантаОтчета = ФункциональныеОпцииВариантовОтчетов.Добавить();
	НоваяФОВариантаОтчета.ВариантОтчета = ИмяВариантОтчета;
	НоваяФОВариантаОтчета.ФункциональнаяОпция = ИмяФункциональнойОпции;
	
КонецПроцедуры

// Устанавливает важность для указанного варианта отчета в указанной подсистеме
//
// Параметры:
// 	ОписаниеОтчета - СтрокаДереваЗначений
//  Важность   - Строка
//               |- ""        - Вывод отчета в своей группе обычным шрифтом.
//               |- "Важный"  - Вывод отчета в своей группе жирным шрифтом.
//               |- "СмТакже" - Вывод отчета в группе "См. также".
//  Подсистема   - Метаданные.Подсистема - если подсистема не указана, то для всех подсистем варианта
//
Процедура УстановитьВажностьВариантаОтчета(ОписаниеВарианта, Важность, Подсистема = Неопределено) Экспорт
	Если ВажностьУказанаВерно(Важность) Тогда
		Размещение = ОписаниеВарианта.Размещение.Получить(Подсистема);
	
		Если Не Размещение = Неопределено Тогда
			ОписаниеВарианта.Размещение.Вставить(Подсистема, Важность);
		Иначе
			Для Каждого Размещение Из ОписаниеВарианта.Размещение Цикл 
				ОписаниеВарианта.Размещение.Вставить(Размещение.Ключ, Важность);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Отключает указанный отчет со всеми его вариантами от механизма "Варианты отчетов".
//
// Параметры:
// 	ОписаниеОтчета - СтрокаДереваЗначений
//
Процедура ОтключитьОтчет(ОписаниеОтчета) Экспорт
	
	ОписаниеОтчета.Включен = Ложь;
	
КонецПроцедуры

// Отключает указанный вариант отчета от механизма "Варианты отчетов".
//
// Параметры:
//	Настройки 	   - ДеревоЗначений
// 	ОписаниеОтчета - СтрокаДереваЗначений
//  КлючВарианта   - Строка
//
Процедура ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, КлючВарианта) Экспорт
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, КлючВарианта);
	
	ОписаниеВарианта.Включен = Ложь;
	
КонецПроцедуры

// Удаляет отбор из настроек и пользовательских настроек отчета
//
// Параметры
//  КомпоновщикНастроек - КомпоновщикНастроек 
//  ИмяЭлемента  - Строка - имя элемента, который будет удален.
//
Процедура УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроек, ИмяЭлемента) Экспорт
	Если ТипЗнч(КомпоновщикНастроек) <> Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Возврат
	КонецЕсли;
	
	ЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(КомпоновщикНастроек.Настройки.Отбор, ИмяЭлемента);
	Для Каждого ЭлементОтбора ИЗ ЭлементыОтбора Цикл
		ИдентификаторПользовательскойНастройки = ЭлементОтбора.ИдентификаторПользовательскойНастройки;
		КомпоновщикНастроек.Настройки.Отбор.Элементы.Удалить(ЭлементОтбора);
		
		ЭлементПользовательскихНастроек = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
		Если ЭлементПользовательскихНастроек <> Неопределено Тогда
			КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Удалить(ИдентификаторПользовательскойНастройки);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Удаляет параметр из пользовательских настроек отчета
//
// Параметры
//  КомпоновщикНастроек - КомпоновщикНастроек 
//  ИмяПараметра  - Строка - имя параметра, который будет удален.
//
Процедура УдалитьПараметрИзПользовательскихНастроекОтчета(КомпоновщикНастроек, ИмяПараметра) Экспорт
	Если ТипЗнч(КомпоновщикНастроек) <> Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Возврат
	КонецЕсли;

	ЗначениеПоиска = Новый ПараметрКомпоновкиДанных(ИмяПараметра);
	ЭлементыПараметыДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных;
	
	ЭлементПараметрыДанных = ЭлементыПараметыДанных.НайтиЗначениеПараметра(ЗначениеПоиска);
	Если ЭлементПараметрыДанных <> Неопределено Тогда
		ЭлементПараметрыДанных.ИдентификаторПользовательскойНастройки = "";
	КонецЕсли; 
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Проверяет строку на содержание параметров важности
//
// Параметры:
//  Важность   - (Строка)
//
// Возвращаемое значение: 
//  Булево   - Истина, если переданная строка равна одному из значений:
//               |- ""        - Вывод отчета в своей группе обычным шрифтом.
//               |- "Важный"  - Вывод отчета в своей группе жирным шрифтом.
//               |- "СмТакже" - Вывод отчета в группе "См. также".
//             Ложь, в ином случае
//
Функция ВажностьУказанаВерно(Важность)
	Если ПустаяСтрока(Важность) Или Важность = "Важный" Или Важность = "СмТакже" Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

Процедура ДобавитьИзменениеКлючей(Изменения, ИмяОтчета, СтароеИмяВарианта, АктуальноеИмяВарианта)
	Изменение = Изменения.Добавить();
	
	Изменение.Отчет = Метаданные.Отчеты[ИмяОтчета];
	Изменение.СтароеИмяВарианта = СтароеИмяВарианта;
	Изменение.АктуальноеИмяВарианта = АктуальноеИмяВарианта;
КонецПроцедуры

