////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Функция обрабатывает расшифровку в отчете, отображаемом на форме. Если расшифровка предполагает открытие значение, то
// дальшейшую обработку в форме делать не нужно, если нужно сформировать отчет-расшифровки, то фукнция вернет описание расшифровки
// которое нужно передать в соотвествующую серверную фукнцию
//
// Параметры:
//		Форма - Форма - форма, в которой расположен отчет
//		Расшифровка - параметр события ОбработкаРасшифровки
//		СтандартнаяОбработка - параметр события ОбработкаРасшифровки
// Возвращаемое значение:
//		ОписаниеОбработкиРасшифровкиКомпоновкиДанных, Неопределено - если необходимо сформировать отчет-расшифровку возвращается значение
//			для передачи в соотвествующую серверную фукнцию, иначе возвращается Неопределено
//
Функция ОбработатьРасшифровку(Форма, Расшифровка, СтандартнаяОбработка) Экспорт
	
	Перем ВыполненноеДействие, ПараметрВыполненногоДействия;
	
	СтандартнаяОбработка = Ложь;
	
	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(Форма.АдресДанныхРасшифровки,
								Новый ИсточникДоступныхНастроекКомпоновкиДанных(Форма.АдресСхемы));
	
	ОбработкаРасшифровки.ВыбратьДействие(Расшифровка, ВыполненноеДействие, ПараметрВыполненногоДействия);
	
	ОписаниеОбработкиРасшифровки = Неопределено;
	
	Если Не (ПараметрВыполненногоДействия = Неопределено) Тогда
		
		Если ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
			
			ОткрытьЗначение(ПараметрВыполненногоДействия);
			
		Иначе
			
			ОписаниеОбработкиРасшифровки = Новый ОписаниеОбработкиРасшифровкиКомпоновкиДанных(Форма.АдресДанныхРасшифровки, Расшифровка, ПараметрВыполненногоДействия);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОписаниеОбработкиРасшифровки;
	
КонецФункции
