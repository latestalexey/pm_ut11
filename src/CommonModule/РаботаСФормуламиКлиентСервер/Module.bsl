////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Получает текст операнда для вставки в формулу
//
// Параметры:
// Операнд - Строка - имя операнда
//
// Возвращаемое значение:
// Строка
//
Функция ПолучитьТекстОперандаДляВставки(Операнд) Экспорт
	
	Возврат "[" + Операнд + "]";
	
КонецФункции // ПолучитьТекстОперандаДляВставки()

// Осуществляет проверку корректности формулы
//
// Параметры:
// Формула              - Строка - текст формулы
// Операнды             - Массив - операнды формулы
// Поле                 - Строка - имя поля, к которому необходимо привязать сообщение
// СообщениеОбОшибке    - Строка - текст сообщения об ошибке
// СтроковаяФормула     - Булево - признак строковой формулы
// ПутьКДанным          - Строка - путь к данным, для выдачи сообщения об ошибке
//
// Возвращаемое значение:
// Булево
// Ложь, если есть ошибки, иначе Истина
//
Функция ПроверитьФормулу(Формула, Операнды, Знач Поле = "", Знач СообщениеОбОшибке = "", СтроковаяФормула = Ложь, ПутьКДанным = "") Экспорт
	
	Результат = Истина;
	
	Если ЗначениеЗаполнено(Формула) Тогда
		
		Если СтроковаяФормула Тогда
			ТекстРасчета = """Строка"" + " + Формула;
			ЗначениеЗамены = """1""";
		Иначе
			ЗначениеЗамены = 1;
			ТекстРасчета = Формула;
		КонецЕсли;
		
		Для Каждого Операнд Из Операнды Цикл
			ТекстРасчета = СтрЗаменить(ТекстРасчета, ПолучитьТекстОперандаДляВставки(Операнд), ЗначениеЗамены);
		КонецЦикла;
		
		Попытка
			
			РезультатРасчета = Вычислить(ТекстРасчета);
			
			Если СтроковаяФормула Тогда
				ТекстПроверки = СтрЗаменить(Формула, " ", "");
				ОтсутствиеРазделителей = Найти(ТекстПроверки, "][");
				Если ОтсутствиеРазделителей > 0 Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru='В формуле обнаружены ошибки. Между операндами должен присутствовать оператор или разделитель'"),
					,
					Поле,
					ПутьКДанным,
					);
					Результат = Ложь;
				КонецЕсли;
			КонецЕсли;
		
		Исключение
			
			Результат = Ложь;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			?(ЗначениеЗаполнено(СообщениеОбОшибке) ,СообщениеОбОшибке, НСтр("ru='В формуле обнаружены ошибки. Проверьте формулу. Формулы должны составляться по правилам написания выражений на встроенном языке 1С:Предприятия.'")),
			,
			Поле,
			ПутьКДанным,
			);
			
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПроверитьФормулу()

// Извлекает операнды из текстовой формулы
//
// Параметры:
// Формула - Строка - текст формулы
//
// Возвращаемое значение:
// Массив
// Операнды из текстовой формулы
//
Функция ПолучитьМассивОперандовТекстовойФормулы(Формула) Экспорт
	
	МассивОперандов = Новый Массив();
	
	ТекстФормулы = СокрЛП(Формула);
	Если СтрЧислоВхождений(ТекстФормулы, "[") <> СтрЧислоВхождений(ТекстФормулы, "]") Тогда
		ЕстьОперанды = Ложь;
	Иначе
		ЕстьОперанды = Истина;
	КонецЕсли;
	
	Пока ЕстьОперанды = Истина Цикл
		НачалоОперанда = Найти(ТекстФормулы, "[");
		КонецОперанда = Найти(ТекстФормулы, "]");
		
		Если НачалоОперанда = 0
			Или КонецОперанда = 0
			Или НачалоОперанда > КонецОперанда Тогда
			ЕстьОперанды = Ложь;
			Прервать;
			
		КонецЕсли;
		
		ИмяОперанда = Сред(ТекстФормулы, НачалоОперанда + 1, КонецОперанда - НачалоОперанда - 1);
		МассивОперандов.Добавить(ИмяОперанда);
		ТекстФормулы = СтрЗаменить(ТекстФормулы, "[" + ИмяОперанда + "]", "");
		КонецПрошлогоОперанда = КонецОперанда;
		
	КонецЦикла;
	
	Возврат МассивОперандов
	
КонецФункции // ПолучитьМассивОперандовТекстовойФормулы()

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
