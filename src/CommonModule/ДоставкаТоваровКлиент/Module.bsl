////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

//  Открывает форму ввода адреса с заполненными из параметра значениями полей адреса,
//
//  Параметры:
//		АдресПредставление - в этот параметр возвращается представление адреса
//		АдресЗначенияПолей - строка, из этого параметра заполняются значения полей адреса,
//								сюда же возвращаются измененные значения полей
//		РедактироватьАдресаДоставкиТолькоВДиалоге - в этот параметр передается значение одноименной 
//										функциональной опции для использования в форме ввода адреса
//		АдресаДоставкиТолькоРоссийские - в этот параметр передается значение одноименной функциональной опции
//										для использования в форме ввода адреса
//
Процедура ОткрытьФормуВыбораАдресаИОбработатьРезультат(АдресПредставление,АдресЗначенияПолей,РедактироватьАдресаДоставкиТолькоВДиалоге,АдресаДоставкиТолькоРоссийские) Экспорт
	
	СписокЗначенияПолей = Новый СписокЗначений;
	СписокЗначенияПолей = ДоставкаТоваровКлиентСервер.ПреобразоватьСтрокуВСписокПолей(АдресЗначенияПолей);
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЗначенияПолей",                СписокЗначенияПолей);
	СтруктураПараметров.Вставить("Вид",                          ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресПартнера"));
	СтруктураПараметров.Вставить("Представление",                АдресПредставление);
	СтруктураПараметров.Вставить("РедактированиеТолькоВДиалоге", РедактироватьАдресаДоставкиТолькоВДиалоге);
	СтруктураПараметров.Вставить("АдресТолькоРоссийский",        АдресаДоставкиТолькоРоссийские);
	
	Результат = ОткрытьФормуМодально("ОбщаяФорма.ВводАдреса",СтруктураПараметров);
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Свойство("Представление") Тогда
			АдресПредставление = Результат.Представление;
		КонецЕсли;
		Если Результат.Свойство("ЗначенияПолей") Тогда
			АдресЗначенияПолей = УправлениеКонтактнойИнформациейКлиентСервер.ПреобразоватьСписокПолейВСтроку(Результат.ЗначенияПолей);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет реквизиты доставки из структуры в соответствии с выбранным адресом
//
// Параметры
//		ЭлементыФормы - все элементы формы, в которой происходит выбор адреса (для получения списков выбора)
//		ДокОбъект - объект, в котором происходит выбор адреса, реквизиты этого объекта заполняются реквизитами доставки
//		ИмяЭлементаФормы - имя элемента формы, в котором происходит выбор адреса
//		ВыбранноеЗначение - структура с реквизитами доставки, соответствующая выбранному адресу
//
Процедура АдресДоставкиОбработкаВыбора(ЭлементыФормы,ДокОбъект,ИмяЭлементаФормы,ВыбранноеЗначение) Экспорт
	
	Если ИмяЭлементаФормы = "АдресДоставкиПолучателя" Тогда
		ДопИнфоИзмененоПользователем = ЗначениеЗаполнено(ДокОбъект.ДополнительнаяИнформацияПоДоставке)
			И ДоставкаТоваровКлиентСервер.НайтиВСпискеСтруктур(ЭлементыФормы.АдресДоставкиПолучателя.СписокВыбора,
				"ДополнительнаяИнформацияПоДоставке", ДокОбъект.ДополнительнаяИнформацияПоДоставке) = Неопределено;
		Если ДопИнфоИзмененоПользователем
			И ВыбранноеЗначение.Свойство("ДополнительнаяИнформацияПоДоставке") Тогда
			ЗаполнитьЗначенияСвойств(ДокОбъект, ВыбранноеЗначение, , "ДополнительнаяИнформацияПоДоставке");
		Иначе
			ЗаполнитьЗначенияСвойств(ДокОбъект, ВыбранноеЗначение);
		КонецЕсли;
	ИначеЕсли ИмяЭлементаФормы = "АдресДоставкиПеревозчика" Тогда
		ДопИнфоИзмененоПользователем = ЗначениеЗаполнено(ДокОбъект.ДополнительнаяИнформацияПоДоставке)
			И ДоставкаТоваровКлиентСервер.НайтиВСпискеСтруктур(ЭлементыФормы.АдресДоставкиПеревозчика.СписокВыбора,
				"ДополнительнаяИнформацияПоДоставке", ДокОбъект.ДополнительнаяИнформацияПоДоставке) = Неопределено
			И ДоставкаТоваровКлиентСервер.НайтиВСпискеСтруктур(ЭлементыФормы.АдресДоставкиПолучателя2.СписокВыбора,
				"ДополнительнаяИнформацияПоДоставке", ДокОбъект.ДополнительнаяИнформацияПоДоставке) = Неопределено;
		Если ДопИнфоИзмененоПользователем 
			И ВыбранноеЗначение.Свойство("ДополнительнаяИнформацияПоДоставке") Тогда
			ЗаполнитьЗначенияСвойств(ДокОбъект, ВыбранноеЗначение,,
									"АдресДоставки, АдресДоставкиЗначенияПолей, ДополнительнаяИнформацияПоДоставке");
			ДокОбъект.АдресДоставкиПеревозчика = ВыбранноеЗначение.АдресДоставки;
			ДокОбъект.АдресДоставкиПеревозчикаЗначенияПолей = ВыбранноеЗначение.АдресДоставкиЗначенияПолей;
		Иначе
			ЗаполнитьЗначенияСвойств(ДокОбъект, ВыбранноеЗначение,,
									"АдресДоставки, АдресДоставкиЗначенияПолей");
			ДокОбъект.АдресДоставкиПеревозчика = ВыбранноеЗначение.АдресДоставки;
			ДокОбъект.АдресДоставкиПеревозчикаЗначенияПолей = ВыбранноеЗначение.АдресДоставкиЗначенияПолей;
		КонецЕсли;
	ИначеЕсли ИмяЭлементаФормы = "АдресДоставкиПолучателя1" Тогда
		ДопИнфоИзмененоПользователем = ЗначениеЗаполнено(ДокОбъект.ДополнительнаяИнформацияПоДоставке)
			И ДоставкаТоваровКлиентСервер.НайтиВСпискеСтруктур(ЭлементыФормы.АдресДоставкиПолучателя1.СписокВыбора,
				"ДополнительнаяИнформацияПоДоставке", ДокОбъект.ДополнительнаяИнформацияПоДоставке) = Неопределено;
		Если ДопИнфоИзмененоПользователем
			И ВыбранноеЗначение.Свойство("ДополнительнаяИнформацияПоДоставке") Тогда
			ЗаполнитьЗначенияСвойств(ДокОбъект, ВыбранноеЗначение, , "ДополнительнаяИнформацияПоДоставке");
		Иначе
			ЗаполнитьЗначенияСвойств(ДокОбъект, ВыбранноеЗначение);
		КонецЕсли;
	ИначеЕсли ИмяЭлементаФормы = "АдресДоставкиПолучателя2" Тогда
		ДопИнфоИзмененоПользователем = ЗначениеЗаполнено(ДокОбъект.ДополнительнаяИнформацияПоДоставке)
			И ДоставкаТоваровКлиентСервер.НайтиВСпискеСтруктур(ЭлементыФормы.АдресДоставкиПолучателя2.СписокВыбора,
				"ДополнительнаяИнформацияПоДоставке", ДокОбъект.ДополнительнаяИнформацияПоДоставке) = Неопределено
			И ДоставкаТоваровКлиентСервер.НайтиВСпискеСтруктур(ЭлементыФормы.АдресДоставкиПеревозчика.СписокВыбора,
				"ДополнительнаяИнформацияПоДоставке", ДокОбъект.ДополнительнаяИнформацияПоДоставке) = Неопределено;
		Если ВыбранноеЗначение.Свойство("ДополнительнаяИнформацияПоДоставке")
			И (ДопИнфоИзмененоПользователем
				ИЛИ ЗначениеЗаполнено(ВыбранноеЗначение.ДополнительнаяИнформацияПоДоставке)) Тогда
			ЗаполнитьЗначенияСвойств(ДокОбъект, ВыбранноеЗначение, , "ДополнительнаяИнформацияПоДоставке");
		Иначе
			ЗаполнитьЗначенияСвойств(ДокОбъект, ВыбранноеЗначение);
		КонецЕсли;
	ИначеЕсли ИмяЭлементаФормы = "АдресДоставкиСамовывоз" Тогда
		ДопИнфоИзмененоПользователем = ЗначениеЗаполнено(ДокОбъект.ДополнительнаяИнформацияПоДоставке)
			И ДоставкаТоваровКлиентСервер.НайтиВСпискеСтруктур(ЭлементыФормы.АдресДоставкиСамовывоз.СписокВыбора,
				"ДополнительнаяИнформацияПоДоставке", ДокОбъект.ДополнительнаяИнформацияПоДоставке) = Неопределено;
		Если ДопИнфоИзмененоПользователем
			И ВыбранноеЗначение.Свойство("ДополнительнаяИнформацияПоДоставке") Тогда
			ЗаполнитьЗначенияСвойств(ДокОбъект, ВыбранноеЗначение, , "ДополнительнаяИнформацияПоДоставке");
		Иначе
			ЗаполнитьЗначенияСвойств(ДокОбъект, ВыбранноеЗначение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
