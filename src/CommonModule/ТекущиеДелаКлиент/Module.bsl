////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// Формы расшифровки показателей.

// Возвращает имя формы, которую требуется открывать для указанного показателя
//
// Параметры:
//  ИмяПоказателя - имя показателя, для которого требуется получить имя формы
//  Группа - группа показателя
//
// Возвращаемое значение:
//  Имя формы - имя формы, соответствующей показателю
//
Функция ПолучитьИмяФормы(ИмяПоказателя, ИмяГруппы) Экспорт
	Перем ИмяОткрываемойФормы;
	
	СтруктураФорм = ТекущиеДелаКлиентПереопределяемый.ПолучитьФормыПоказателей();
	
	СтруктураФорм.Свойство(ИмяГруппы, ИмяОткрываемойФормы); // форма для группы показателей
	
	Если ТипЗнч(ИмяОткрываемойФормы) = Тип("Структура") Тогда
		ИмяОткрываемойФормы.Свойство(ИмяПоказателя, ИмяОткрываемойФормы); // форма для конкретного показателя
	КонецЕсли;
	
	Возврат ИмяОткрываемойФормы;
	
КонецФункции

// Формирует и возвращает структуру параметров формы,
// которая будет открыта с этими параметрами для указанного показателя
//
// Параметры:
//  ИмяПоказателя 		- имя показателя, для которого требуется получить структуру
//  ДанныеСтрокиТаблицы - данные строки таблицы-показателя, для расшифровки которой требуется получить параметры
//
// Возвращаемое значение:
//  Структура параметров - структура, содержащая параметры формы
//
Функция ПолучитьСтруктуруПараметровФормы(ИмяПоказателя, ДанныеСтрокиТаблицы = Неопределено) Экспорт

	СтруктураПараметров		= Новый Структура;
	СтруктураБыстрогоОтбора = Новый Структура;
	
	ТекущиеДелаКлиентПереопределяемый.ПолучитьСтруктуруПараметровФормы(
		ИмяПоказателя,
		ДанныеСтрокиТаблицы,
		СтруктураПараметров,
		СтруктураБыстрогоОтбора);
	
	СтруктураПараметров.Вставить("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	Возврат СтруктураПараметров;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Возвращает строку-элемент дерева, содержащую указанный показатель
//
// Параметры:
//  ИмяПоказателя - имя показателя, для которого требуется получить строку
//  ИмяГруппы 	  - имя группы, содержащей указанный показатель
//
Функция ПолучитьСтрокуДереваПоказателей(ДеревоПоказателей, ИмяПоказателя, ИмяГруппы) Экспорт
	
	ЭлементыДереваПоказателей = ДеревоПоказателей.ПолучитьЭлементы();
	
	Для Каждого СтрокаГруппы Из ЭлементыДереваПоказателей Цикл
		
		Если СтрокаГруппы.Показатель = ИмяГруппы Тогда
			
			ЭлементыГруппы = СтрокаГруппы.ПолучитьЭлементы();
			
			Для Каждого СтрокаПоказателя из ЭлементыГруппы Цикл
				Если СтрокаПоказателя.Показатель = ИмяПоказателя Тогда
					Возврат СтрокаПоказателя;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции
