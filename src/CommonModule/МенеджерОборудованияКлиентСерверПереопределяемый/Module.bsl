
///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Возвращает список доступных типов оборудования
//
&НаСервере
Функция ПолучитьДоступныеТипыОборудования() Экспорт
	
	СписокОборудования = Новый Массив;

	// Сканеры штрихкода
	СписокОборудования.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.СканерШтрихкода"));
	// Конец Сканеры штрихкода

	// Считыватели магнитных карт
	СписокОборудования.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.СчитывательМагнитныхКарт"));
	// Конец Считыватели магнитных карт

	// Фискальные регистраторы
	СписокОборудования.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ФискальныйРегистратор"));
	// Конец Фискальные регистраторы

	// Дисплеи покупателя
	СписокОборудования.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ДисплейПокупателя"));
	// Конец Дисплеи покупателя

	// Терминалы сбора данных
	СписокОборудования.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ТерминалСбораДанных"));
	// Конец Терминалы сбора данных

	// Эквайринговые терминалы
	СписокОборудования.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ЭквайринговыйТерминал"));
	// Конец Эквайринговые терминалы
	
	// Электронные весы
	СписокОборудования.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ЭлектронныеВесы"));
	// Конец Электронные весы

	Если ПолучитьФункциональнуюОпцию("ИспользоватьОбменСПодключаемымОборудованиемOffline") Тогда
	
		// Весы с печатью этикеток
		СписокОборудования.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток"));
		// Конец Весы с печатью этикеток

		// ККМ offline
		СписокОборудования.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ККМOffline"));
		// Конец ККМ offline
	
	КонецЕсли;
	
	Возврат СписокОборудования;
	
КонецФункции

// Функция возвращает объект обработчика драйвера по его наименованию
//
&НаКлиенте
Функция ПолучитьОбработчикаДрайвера(ОбработчикДрайвера) Экспорт
	
	Результат = Неопределено;

	// Вызов метода выполнения команды у обработчика
	Если ОбработчикДрайвера <> Неопределено Тогда
		
		// Сканеры штрихкода
		Если ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.Обработчик1ССканерыШтрихкода") Тогда
			Результат = ПодключаемоеОборудование1ССканерыШтрихкода;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикСканкодСканерыШтрихкода") Тогда
			Результат = ПодключаемоеОборудованиеСканкодСканерыШтрихкода;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикАтолСканерыШтрихкода") Тогда
			Результат = ПодключаемоеОборудованиеАтолСканерыШтрихкода;
		КонецЕсли;
		// Конец Сканеры штрихкода
		
		// Считыватели магнитных карт
		Если ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.Обработчик1ССчитывателиМагнитныхКарт") Тогда
			Результат = ПодключаемоеОборудование1ССчитывателиМагнитныхКарт;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикАтолСчитывателиМагнитныхКарт") Тогда
			Результат = ПодключаемоеОборудованиеАтолСчитывателиМагнитныхКарт;
		КонецЕсли;
		// Конец Считыватели магнитных карт

		// Фискальные регистраторы
		Если ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.Обработчик1СФискальныйРегистраторЭмулятор") Тогда
			Результат = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикАтолФискальныеРегистраторы") Тогда
			Результат = ПодключаемоеОборудованиеАтолФискальныеРегистраторы;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикВерсияТКасбиФР01К") Тогда
			Результат = ПодключаемоеОборудованиеВерсияТКасбиФР01К;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикККСФискальныеРегистраторыСпарк") Тогда
			Результат = ПодключаемоеОборудованиеККСФискальныеРегистраторыСпарк;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикШтрихМФискальныеРегистраторы") Тогда
			Результат = ПодключаемоеОборудованиеШтрихМФискальныеРегистраторы;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикОРИОНФискальныйРегистраторФР01К") Тогда
			Результат = ПодключаемоеОборудованиеОРИОНФР01К;
		КонецЕсли;
		// Конец Фискальные регистраторы

		// Дисплеи покупателя
		Если ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикАтолДисплеиПокупателя") Тогда
			Результат = ПодключаемоеОборудованиеАтолДисплеиПокупателя;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикСканкодДисплеиПокупателя") Тогда
			Результат = ПодключаемоеОборудованиеСканкодДисплеиПокупателя;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикШтрихМДисплеиПокупателя") Тогда
			Результат = ПодключаемоеОборудованиеШтрихМДисплеиПокупателя;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикККСДисплеиПокупателя") Тогда
			Результат = ПодключаемоеОборудованиеККСДисплеиПокупателя;
		КонецЕсли;                 
		// Конец Дисплеи покупателя
		
		// Терминалы сбора данных
		Если ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикАтолТерминалыСбораДанных") Тогда
			Результат = ПодключаемоеОборудованиеАтолТерминалыСбораДанных;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикШтрихМТерминалыСбораДанных") Тогда
			Результат = ПодключаемоеОборудованиеШтрихМТерминалыСбораДанных;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикСканкодТерминалыСбораДанных") Тогда
			Результат = ПодключаемоеОборудованиеСканкодТерминалыСбораДанных;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикСканситиТерминалыСбораДанных") Тогда
			Результат = ПодключаемоеОборудованиеСканситиТерминалыСбораДанных;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикКлеверенсТерминалыСбораДанных") Тогда
			Результат = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент;
		КонецЕсли;
		
		// Конец Терминалы сбора данных
		
		// Эквайринговые терминалы
		Если ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикСБРФЭквайринговыеТерминалы") Тогда
			Результат = ПодключаемоеОборудованиеСБРФЭквайринговыеТерминалы;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикИНПАСЭквайринговыеТерминалы") Тогда
			Результат = ПодключаемоеОборудованиеИНПАСЭквайринговыеТерминалы;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикИНПАСЭквайринговыеТерминалыSmart") Тогда
			Результат = ПодключаемоеОборудованиеИНПАСЭквайринговыеТерминалыSmart;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикСофткейсЭквайринговыеТерминалы") Тогда
			Результат = ПодключаемоеОборудованиеСофтКейсЭквайринговыеТерминалы;
		КонецЕсли;
		// Конец Эквайринговые терминалы
		                     
		// Электронные весы
		Если ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикАтолЭлектронныеВесы") Тогда
			Результат = ПодключаемоеОборудованиеАтолЭлектронныеВесы;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикШтрихМЭлектронныеВесы") Тогда
			Результат = ПодключаемоеОборудованиеШтрихМЭлектронныеВесы;	
		КонецЕсли;
		// Конец Электронные весы
		
		// Весы с печатью этикеток
		Если ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикACOMВесыСПечатьюЭтикеток") Тогда
			Результат = ПодключаемоеОборудованиеACOMВесыСПечатьюЭтикеток;	
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикScaleCASВесыСПечатьюЭтикеток") Тогда
			Результат = ПодключаемоеОборудованиеScaleCASВесыСПечатьюЭтикеток
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикШтрихМВесыСПечатьюЭтикеток") Тогда
			Результат = ПодключаемоеОборудованиеШтрихМВесыСПечатьюЭтикеток
		КонецЕсли;
		// Конец Весы с печатью этикеток
		
		// ККМ offline
		Если ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикАтолККМOffline") Тогда
			Результат = ПодключаемоеОборудованиеАтолККМOffline;
		ИначеЕсли ОбработчикДрайвера = ПредопределенноеЗначение("Перечисление.ОбработчикиДрайверовПодключаемогоОборудования.ОбработчикШтрихМККМOffline") Тогда
			Результат = ПодключаемоеОборудованиеШтрихМККМOffline;
		КонецЕсли;
		// Конец ККМ offline
		
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Осуществляет печать фискального чека
//
&НаКлиенте
Функция ПечатьЧека(ОбщийМодульОборудования, ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры, ВыходныеПараметры, ВывестиСообщениеПользователю = Ложь) Экспорт
	
	ТаблицаНоменклатуры = ВходныеПараметры[0];
	ТаблицаОплат        = ВходныеПараметры[1];
	ОбщиеПараметры      = ВходныеПараметры[2];
		                 
	Результат  = Истина;
	// Открываем чек
	Результат = ОбщийМодульОборудования.ОткрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения,
	                       ОбщиеПараметры[0] = 1, ОбщиеПараметры[1], ВыходныеПараметры);

	// Печатаем строки чека   
	Если Результат Тогда
		ОшибкаПриПечатиСтроки = Ложь;
		// Печатаем строки чека
		Для ИндексМассива = 0 По ТаблицаНоменклатуры.Количество() - 1 Цикл
			Наименование  = ТаблицаНоменклатуры[ИндексМассива][0].Значение;
			Количество    = ТаблицаНоменклатуры[ИндексМассива][5].Значение;
			Цена          = ТаблицаНоменклатуры[ИндексМассива][4].Значение;
			ПроцентСкидки = ТаблицаНоменклатуры[ИндексМассива][8].Значение;
			Сумма         = ТаблицаНоменклатуры[ИндексМассива][9].Значение;
			НомерСекции   = ТаблицаНоменклатуры[ИндексМассива][3].Значение;
			СтавкаНДС     = ТаблицаНоменклатуры[ИндексМассива][12].Значение;

			Если НЕ ОбщийМодульОборудования.НапечататьФискальнуюСтроку(ОбъектДрайвера, Параметры, ПараметрыПодключения,
											   Наименование, Количество, Цена, ПроцентСкидки, Сумма,
											   НомерСекции, СтавкаНДС, ВыходныеПараметры) Тогда
				ОшибкаПриПечатиСтроки = Истина;   
				Прервать;
			КонецЕсли;
			
		КонецЦикла;

		Если НЕ ОшибкаПриПечатиСтроки Тогда
		  	// Закрываем чек
			Результат = ОбщийМодульОборудования.ЗакрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТаблицаОплат, ВыходныеПараметры);	
		Иначе
			Результат = Ложь;
		КонецЕсли;
		
	КонецЕсли;

	Возврат Результат;
	
КонецФункции
