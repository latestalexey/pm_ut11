////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура ОбработатьНеизвестныеШтрихкоды(СтруктураПараметровДействия, КэшированныеЗначения, ФормаВладелец) Экспорт
	
	Если СтруктураПараметровДействия.НеизвестныеШтрихкоды.Количество() > 0 Тогда
		
		Если Не (КэшированныеЗначения.ПравоРегистрацииШтрихкодовНоменклатурыДоступно
			И СтруктураПараметровДействия.РегистрироватьНовыеШтрихкоды
			И СтруктураПараметровДействия.ИзменятьКоличество)Тогда
			
			Для Каждого ТекНеизвестныйШтрихкод Из СтруктураПараметровДействия.НеизвестныеШтрихкоды Цикл
				
				СтрокаСообщения = НСтр("ru = 'Номенклатура со штрихкодом %1% не найдена'");
				СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1%", СокрЛП(ТекНеизвестныйШтрихкод.Штрихкод));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
				
			КонецЦикла;
		Иначе
			ОткрытьФорму("РегистрСведений.ШтрихкодыНоменклатуры.Форма.РегистрацияШтрихкодовНоменклатуры", СтруктураПараметровДействия, ФормаВладелец, Новый УникальныйИдентификатор);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ВвестиШтрихкод(Штрихкод, Заголовок = "") Экспорт

	Результат = Ложь;

	Штрихкод = "";
	
	Если НЕ ЗначениеЗаполнено(Заголовок) Тогда
		Заголовок = НСтр("ru = 'Введите штрихкод'");
	КонецЕсли;
	
	Если ВвестиЗначение(Штрихкод, Заголовок) Тогда
		Если Не ПустаяСтрока(Штрихкод) Тогда
			Результат = Истина;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ПараметрыОбработкиШтрихкодов() Экспорт
	
	СтруктураОбработкиШтрихкода = Новый Структура;
	СтруктураОбработкиШтрихкода.Вставить("Штрихкоды",                              Неопределено);
	СтруктураОбработкиШтрихкода.Вставить("СтруктураДействийСДобавленнымиСтроками", Неопределено);
	СтруктураОбработкиШтрихкода.Вставить("СтруктураДействийСИзмененнымиСтроками",  Неопределено);
	СтруктураОбработкиШтрихкода.Вставить("ПараметрыУказанияСерий",                 Неопределено);
	СтруктураОбработкиШтрихкода.Вставить("РегистрироватьНовыеШтрихКоды",           Истина);
	СтруктураОбработкиШтрихкода.Вставить("ИмяКолонкиКоличество",                   "КоличествоУпаковок");
	СтруктураОбработкиШтрихкода.Вставить("НеИспользоватьУпаковки",                 Ложь);
	СтруктураОбработкиШтрихкода.Вставить("ИмяТЧ",                                  "Товары");
	СтруктураОбработкиШтрихкода.Вставить("ИзменятьКоличество",                     Истина);
	СтруктураОбработкиШтрихкода.Вставить("БлокироватьДанныеФормы",                 Истина);
	СтруктураОбработкиШтрихкода.Вставить("ТолькоТовары",                           Ложь);
	СтруктураОбработкиШтрихкода.Вставить("НеизвестныеШтрихкоды",                   Новый Массив);
	СтруктураОбработкиШтрихкода.Вставить("ОтложенныеТовары",                       Новый Массив);
	СтруктураОбработкиШтрихкода.Вставить("ПараметрыПроверкиАссортимента",          Неопределено);
	
	Возврат СтруктураОбработкиШтрихкода;
	
КонецФункции

//Возвращает структуру, которая затем обрабатывается процедурами заполнения ТЧ
//Параметры:
//		Штрихкод - штрихкод, который необходимо обработать
//		Количество - количество товаров с указанным штрихкодом
//
Функция ПолучитьСтруктуруДанныхШтрихкода(Штрихкод, Количество) Экспорт

	Возврат Новый Структура("Штрихкод, Количество", Штрихкод, Количество);

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
