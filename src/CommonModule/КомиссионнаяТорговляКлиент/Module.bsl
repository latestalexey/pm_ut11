////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Процедура рассчитывает сумму НДС комиссионного вознаграждения.
//
Процедура РассчитатьКомиссионноеВознаграждениеНДС(Объект, ПроцентНДС) Экспорт
	
	Объект.СуммаНДСВознаграждения = Окр(Объект.СуммаВознаграждения * ПроцентНДС / (1 + ПроцентНДС), 2, РежимОкругления.Окр15как20);
	
КонецПроцедуры

// Функция определяет необходимость расчета комиссионного вознаграждения.
//
Функция НеобходимоРассчитатьВознаграждение(Форма) Экспорт
	
	Если Форма.Объект.СпособРасчетаВознаграждения = ПредопределенноеЗначение("Перечисление.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается") Тогда
		НеобходимоРассчитать = НеобходимоОчиститьКомиссионноеВознаграждение(Форма);
	Иначе
		НеобходимоРассчитать = НеобходимоРассчитатьКомиссионноеВознаграждение(Форма);
	КонецЕсли;
	
	Возврат НеобходимоРассчитать;
	
КонецФункции

// Процедура показывает оповещение пользователю об окончании расчета
// комиссионного вознаграждения.
//
Процедура ОповеститьОбОкончанииРасчетаВознаграждения(СпособРасчетаВознаграждения) Экспорт
	
	Если СпособРасчетаВознаграждения = ПредопределенноеЗначение("Перечисление.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается") Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Вознаграждение очищено'"),
			,
			НСтр("ru='Комиссионное вознаграждение в документе очищено'"),
			БиблиотекаКартинок.Информация32
		);
	Иначе
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Вознаграждение рассчитано'"),
			,
			НСтр("ru='Комиссионное вознаграждение в документе рассчитано'"),
			БиблиотекаКартинок.Информация32
		);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля "СтавкаНДСВознаграждения".
//
Процедура СтавкаНДСВознагражденияПриИзменении(Объект, ПроцентНДС) Экспорт
	
	ПроцентНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(Объект.СтавкаНДСВознаграждения);
	РассчитатьКомиссионноеВознаграждениеНДС(Объект, ПроцентНДС);
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля "СуммаПродажи" таблицы.
//
Процедура СуммаПродажиПриИзменении(СтрокаТаблицы, ЕстьСуммаПродажиНДС) Экспорт
	
	СтрокаТаблицы.ЦенаПродажи = ?(СтрокаТаблицы.КоличествоУпаковок <> 0, Окр(СтрокаТаблицы.СуммаПродажи / СтрокаТаблицы.КоличествоУпаковок, 2, 1), 0);
	Если ЕстьСуммаПродажиНДС Тогда
		СтрокаТаблицы.СуммаПродажиНДС = СуммаПродажиНДС(СтрокаТаблицы.СуммаПродажи, СтрокаТаблицы.СтавкаНДС);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет поле "Дата платежа" в документе.
//
// Параметры:
//	ДатаПлатежа - Дата
//	ПараметрыЗаписи - Структура - Параметры записи документа
//
Процедура ЗаполнитьДатуПлатежа(ДатаПлатежа, ПараметрыЗаписи) Экспорт
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение
	 И Не ЗначениеЗаполнено(ДатаПлатежа) Тогда
		ВвестиДату(ДатаПлатежа, "Введите дату платежа", ЧастиДаты.Дата);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик команды "УстановитьИнтервал".
//
Процедура РедактироватьПериод(Объект) Экспорт
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период.ДатаНачала = Объект.НачалоПериода;
	Диалог.Период.ДатаОкончания = Объект.КонецПериода;
	
	Если Диалог.Редактировать() Тогда
		Объект.НачалоПериода = Диалог.Период.ДатаНачала;
		Объект.КонецПериода = Диалог.Период.ДатаОкончания;
	КонецЕсли;

КонецПроцедуры

// Процедура выводит сообщения пользователю, если заполнение не было выполнено.
//
// Параметры:
//	Объект - ДанныеФорма - Текущий объект
//
Процедура ПроверитьЗаполнениеДокументаПоОстаткам(Объект) Экспорт
	
	Если Объект.Товары.Количество() = 0 Тогда
	   
		Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ОтчетКомитенту") Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Нет данных о продажах товаров комитента ""%1""'"),
				Объект.Партнер
			);
		ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ОтчетКомитентуОСписании") Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Нет данных о списании товаров комитента ""%1""'"),
				Объект.Партнер
			);	
		Иначе
			Текст = "";
		КонецЕсли;
				
		Если Не ПустаяСтрока(Текст) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				Объект.Ссылка,
				, // Поле
				// Отказ
			);
		КонецЕсли;
	   
	КонецЕсли;
	
КонецПроцедуры

// Проверяет заполненность реквизитов, необходимых для пересчета из валюты в валюту
//
// Параметры:
// Документ - ДокументОбъект, для которого выполняются проверки
// СтараяВалюта - Предыдущая валюта документа
//
// Возвращаемое значение:
//	Булево - Ложь, если необходимые данные не заполнены
//
Функция ПроверитьНеобходимостьПересчетаВВалютуОтчетыПоКомиссии(Документ, СтараяВалюта) Экспорт
	
	ПересчитатьВВалюту = Ложь;

	Если ЗначениеЗаполнено(Документ.Валюта)
	 И ЗначениеЗаполнено(СтараяВалюта)
	 И СтараяВалюта <> Документ.Валюта
	 И (Документ.Товары.Итог("Цена") <> 0
	 	ИЛИ Документ.Товары.Итог("СуммаПродажи") <> 0)
	Тогда
	
		ТекстСообщения = НСтр("ru='Пересчитать суммы в документе в валюту ""%Валюта%""?'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Валюта%", Документ.Валюта);
		
		ОтветНаВопрос = Вопрос(ТекстСообщения,РежимДиалогаВопрос.ДаНет);
		ПересчитатьВВалюту = (ОтветНаВопрос = КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
	Возврат ПересчитатьВВалюту;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция НеобходимоРассчитатьКомиссионноеВознаграждение(Форма)
	
	НеобходимоРассчитать = Ложь;
	
	Если Форма.Объект.Товары.Количество() > 0 Тогда
	
		ТекстВопроса = НСтр("ru = 'Рассчитать комиссионное вознаграждение?'");
		
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			Если Форма.Объект.ПроцентВознаграждения = 0 Тогда
				Текст = НСтр("ru = 'Введите процент вознаграждения'");
				ВвестиЧисло(Форма.Объект.ПроцентВознаграждения, Текст, 5, 2); 
			КонецЕсли;
			НеобходимоРассчитать = Истина;
						
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НеобходимоРассчитать;
	
КонецФункции

Функция НеобходимоОчиститьКомиссионноеВознаграждение(Форма)
	
	НеобходимоОчистить = Ложь;
	
	Если Форма.Объект.Товары.Итог("СуммаВознаграждения") <> 0 Тогда
		
		ТекстВопроса = НСтр("ru = 'Очистить комиссионное вознаграждение?'");
	
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			НеобходимоОчистить = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НеобходимоОчистить;
	
КонецФункции

Функция СуммаПродажиНДС(СуммаПродажи, СтавкаНДС)
	
	ТекущийПроцентНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(СтавкаНДС);
	СуммаПродажиНДС = Окр(СуммаПродажи * ТекущийПроцентНДС / (1 + ТекущийПроцентНДС), 2, РежимОкругления.Окр15как20);
	
	Возврат СуммаПродажиНДС;
	
КонецФункции