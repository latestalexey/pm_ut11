
// Функция возвращает курс ставку НДС
//
// Параметры:
//  Валюта - СправочникСсылка.Валюты, валюта, по которой необходимо получить курс
//  ДатаКурса - Дата, календарная дата, на которую необходимо получить курс валюты
//
// Возвращаемое значение:
//	Курс переданной валюты на переданную дату, 1 в случае отсутствия значения.
//
Функция ПолучитьСтавкуНДС(СтавкаНДС) Экспорт

	Если СтавкаНДС = Перечисления.СтавкиНДС.НДС10 ИЛИ СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110 Тогда
		Возврат 10;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС18 ИЛИ СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118 Тогда
		Возврат 18;
	КонецЕсли;

	Возврат 0;

КонецФункции

// Функция определяет наличие счетов-фактур, полученных по документам,
// которые могут являться основаниями для ввода счетов-фактур полученных.
//
// Применяется для контроля за наличием в ИБ информации о наличии счетов-
// фактур.
//
// Параметры:
//  НачПериода       - Дата - Начальная дата выборки (включительно, с 00:00:00)
//  КонПериода       - Дата - Конечная дата выборки (включительно, по 23:59:59)
//  Организация      - Справочник.Ссылка - Организация, по которой
//                     осуществляется отбор. Необязательный параметр. Если не
//                     задан, отбор осуществляется по всем организациям.
//  Фильтр           - Документ.Ссылка, Массив - Документ или список документов,
//                     по которым осуществляется отбор. Необязательный параметр.
//                     Если не задан, отбор осуществляется по всем документам,
//                     которые могут являться основаниями для ввода счетов-фактур
//                     полученных.
//  ВсеКромеФильтра  - Булево - Признак отбора документов, не входящих в Фильтр.
//                     Необязательный параметр. Значение по умолчанию - Ложь.
//                     Если не задан, отбираются документы, заданные в Фильтре.
//  НаличиеСчетаФактуры - Булево - Признак отбора документов:
//                        Истина - Отбирать документы, по которым существуют
//                        счета-фактуры.
//                        Ложь - Отбирать документы, по которым счета-фактуры
//                        отсутствуют.
//                        Необязательный параметр. Если не задан, осуществляется
//                        отбор всех документов.
// СчетФактураПроведен - Булево - Признак отбора счетов-фактур:
//                        Истина - Отбирать проведенные счета-фактуры
//                        Ложь - Отбирать непроведенные счета-фактуры
//                        Необязательный параметр. Если не задан, осуществляется
//                        отбор всех счетов-фактур.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица значений - Состав колонок:
//                    Документ - Документ.Ссылка - Документ, по которому
//                               производился поиск счета-фактуры полученного
//                    СчетФактура - Документ.Ссылка - Ссылка на счет-
//                                  фактуру полученный, либо Неопределено
//
Функция ОпределитьНаличиеСчетовФактурПолученных(НачПериода = Неопределено, КонПериода = Неопределено, Организация, Фильтр = Неопределено, ВсеКромеФильтра = Ложь, НаличиеСчетаФактуры = Неопределено, СчетФактураПроведен = Неопределено, ДатаСФНеБолее = Неопределено) Экспорт

	Запрос = Новый Запрос();

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Основание
	|ПОМЕСТИТЬ ВтОснованияСФПолученные
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Проведен
	|	И ПоступлениеТоваровУслуг.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ПоступлениеТоваровУслуг.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|														ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо),
	|														ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет))
	|	И ПоступлениеТоваровУслуг.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ПоступлениеТоваровУслуг.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ПоступлениеТоваровУслуг.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ПоступлениеТоваровУслуг.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеУслугПрочихАктивов.Ссылка
	|ИЗ
	|	Документ.ПоступлениеУслугПрочихАктивов КАК ПоступлениеУслугПрочихАктивов
	|ГДЕ
	|	ПоступлениеУслугПрочихАктивов.Проведен
	|	И ПоступлениеУслугПрочихАктивов.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ПоступлениеУслугПрочихАктивов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	И ПоступлениеУслугПрочихАктивов.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ПоступлениеУслугПрочихАктивов.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ПоступлениеУслугПрочихАктивов.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ПоступлениеУслугПрочихАктивов.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровОтКлиента.Ссылка
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
	|ГДЕ
	|	ВозвратТоваровОтКлиента.Проведен
	|	И ВозвратТоваровОтКлиента.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ВозвратТоваровОтКлиента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
	|	И ВозвратТоваровОтКлиента.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ВозвратТоваровОтКлиента.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ВозвратТоваровОтКлиента.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ВозвратТоваровОтКлиента.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионера.Ссылка
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ОтчетКомиссионера
	|ГДЕ
	|	ОтчетКомиссионера.Проведен
	|	И ОтчетКомиссионера.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ОтчетКомиссионера.СуммаВознаграждения <> 0
	|	И ОтчетКомиссионера.СтавкаНДСВознаграждения <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ОтчетКомиссионера.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ОтчетКомиссионера.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ОтчетКомиссионера.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПередачаТоваровМеждуОрганизациями.Ссылка
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ПередачаТоваровМеждуОрганизациями
	|ГДЕ
	|	ПередачаТоваровМеждуОрганизациями.Проведен
	|	И ПередачаТоваровМеждуОрганизациями.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ПередачаТоваровМеждуОрганизациями.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|	И ПередачаТоваровМеждуОрганизациями.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И ПередачаТоваровМеждуОрганизациями.РасчетыЧерезОтдельногоКонтрагента
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ПередачаТоваровМеждуОрганизациями.ОрганизацияПолучатель = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ПередачаТоваровМеждуОрганизациями.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ПередачаТоваровМеждуОрганизациями.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровМеждуОрганизациями.Ссылка
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями КАК ВозвратТоваровМеждуОрганизациями
	|ГДЕ
	|	ВозвратТоваровМеждуОрганизациями.Проведен
	|	И ВозвратТоваровМеждуОрганизациями.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ВозвратТоваровМеждуОрганизациями.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями)
	|	И ВозвратТоваровМеждуОрганизациями.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И ВозвратТоваровМеждуОрганизациями.РасчетыЧерезОтдельногоКонтрагента
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ВозвратТоваровМеждуОрганизациями.ОрганизацияПолучатель = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ВозвратТоваровМеждуОрганизациями.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ВозвратТоваровМеждуОрганизациями.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетПоКомиссииМеждуОрганизациями.Ссылка
	|ИЗ
	|	Документ.ОтчетПоКомиссииМеждуОрганизациями КАК ОтчетПоКомиссииМеждуОрганизациями
	|ГДЕ
	|	ОтчетПоКомиссииМеждуОрганизациями.Проведен
	|	И ОтчетПоКомиссииМеждуОрганизациями.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ОтчетПоКомиссииМеждуОрганизациями.СуммаВознаграждения <> 0
	|	И ОтчетПоКомиссииМеждуОрганизациями.СтавкаНДСВознаграждения <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И ОтчетПоКомиссииМеждуОрганизациями.РасчетыЧерезОтдельногоКонтрагента
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ОтчетПоКомиссииМеждуОрганизациями.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ОтчетПоКомиссииМеждуОрганизациями.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ОтчетПоКомиссииМеждуОрганизациями.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыкупВозвратнойТарыУПоставщика.Ссылка
	|ИЗ
	|	Документ.ВыкупВозвратнойТарыУПоставщика КАК ВыкупВозвратнойТарыУПоставщика
	|ГДЕ
	|	ВыкупВозвратнойТарыУПоставщика.Проведен
	|	И ВыкупВозвратнойТарыУПоставщика.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ВыкупВозвратнойТарыУПоставщика.Организация = &Организация
	|	И ВыкупВозвратнойТарыУПоставщика.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ВыкупВозвратнойТарыУПоставщика.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ВыкупВозвратнойТарыУПоставщика.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ВыкупВозвратнойТарыУПоставщика.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаПоступления.Ссылка
	|ИЗ
	|	Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|ГДЕ
	|	КорректировкаПоступления.Проведен
	|	И КорректировкаПоступления.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И КорректировкаПоступления.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА КорректировкаПоступления.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА КорректировкаПоступления.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА КорректировкаПоступления.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПередачаТоваровМеждуОрганизациями.Ссылка КАК Основание
	|ПОМЕСТИТЬ ВтОснованияСФВыданные
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ПередачаТоваровМеждуОрганизациями
	|ГДЕ
	|	ПередачаТоваровМеждуОрганизациями.Проведен
	|	И ПередачаТоваровМеждуОрганизациями.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ПередачаТоваровМеждуОрганизациями.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|	И ПередачаТоваровМеждуОрганизациями.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И НЕ ПередачаТоваровМеждуОрганизациями.РасчетыЧерезОтдельногоКонтрагента
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ПередачаТоваровМеждуОрганизациями.ОрганизацияПолучатель = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ПередачаТоваровМеждуОрганизациями.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ПередачаТоваровМеждуОрганизациями.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровМеждуОрганизациями.Ссылка
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями КАК ВозвратТоваровМеждуОрганизациями
	|ГДЕ
	|	ВозвратТоваровМеждуОрганизациями.Проведен
	|	И ВозвратТоваровМеждуОрганизациями.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ВозвратТоваровМеждуОрганизациями.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями)
	|	И ВозвратТоваровМеждуОрганизациями.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	И НЕ ВозвратТоваровМеждуОрганизациями.РасчетыЧерезОтдельногоКонтрагента
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ВозвратТоваровМеждуОрганизациями.ОрганизацияПолучатель = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ВозвратТоваровМеждуОрганизациями.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ВозвратТоваровМеждуОрганизациями.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетПоКомиссииМеждуОрганизациями.Ссылка
	|ИЗ
	|	Документ.ОтчетПоКомиссииМеждуОрганизациями КАК ОтчетПоКомиссииМеждуОрганизациями
	|ГДЕ
	|	ОтчетПоКомиссииМеждуОрганизациями.Проведен
	|	И ОтчетПоКомиссииМеждуОрганизациями.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И ОтчетПоКомиссииМеждуОрганизациями.СуммаВознаграждения <> 0
	|	И ОтчетПоКомиссииМеждуОрганизациями.СтавкаНДСВознаграждения <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И НЕ ОтчетПоКомиссииМеждуОрганизациями.РасчетыЧерезОтдельногоКонтрагента
	|	И (ВЫБОР
	|			КОГДА &Организация <> Неопределено
	|				ТОГДА ОтчетПоКомиссииМеждуОрганизациями.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &Фильтр И &ВсеКромеФильтра
	|				ТОГДА ОтчетПоКомиссииМеждуОрганизациями.Ссылка НЕ В (&СписокФильтр)
	|			КОГДА &Фильтр И НЕ &ВсеКромеФильтра
	|				ТОГДА ОтчетПоКомиссииМеждуОрганизациями.Ссылка В (&СписокФильтр)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОснованияСФПолученные.Основание КАК ДокументОснование,
	|	ЕСТЬNULL(СчетФактураПолученный.Ссылка,НЕОПРЕДЕЛЕНО) КАК СчетФактура,
	|	ЕСТЬNULL(СчетФактураПолученный.Ссылка.Проведен,ЛОЖЬ) КАК СчетФактураПроведен
	|ИЗ
	|	ВтОснованияСФПолученные КАК ОснованияСФПолученные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|		ПО ОснованияСФПолученные.Основание = СчетФактураПолученный.ДокументОснование
	|			И НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|ГДЕ
	|	(ВЫБОР
	|		КОГДА &НаличиеСчетаФактуры = НЕОПРЕДЕЛЕНО
	|			ТОГДА ИСТИНА
	|		КОГДА &НаличиеСчетаФактуры
	|			ТОГДА НЕ СчетФактураПолученный.Ссылка ЕСТЬ NULL
	|		ИНАЧЕ
	|			СчетФактураПолученный.Ссылка ЕСТЬ NULL
	|	КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &СчетФактураПроведен = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			КОГДА &СчетФактураПроведен
	|				ТОГДА ЕСТЬNULL(СчетФактураПолученный.Ссылка.Проведен,НЕОПРЕДЕЛЕНО) = ИСТИНА
	|			ИНАЧЕ
	|				ЕСТЬNULL(СчетФактураПолученный.Ссылка.Проведен,НЕОПРЕДЕЛЕНО) = ЛОЖЬ
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &ДатаСФНеБолее = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			КОГДА НЕ СчетФактураПолученный.Ссылка ЕСТЬ NULL
	|				ТОГДА СчетФактураПолученный.Ссылка.Дата <= &ДатаСФНеБолее
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОснованияСФВыданные.Основание,
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка.Ссылка,НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка.Проведен,ЛОЖЬ)
	|ИЗ
	|	ВтОснованияСФВыданные КАК ОснованияСФВыданные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ПО ОснованияСФВыданные.Основание = СчетФактураВыданный.ДокументОснование
	|			И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
	|ГДЕ
	|	(ВЫБОР
	|		КОГДА &НаличиеСчетаФактуры = НЕОПРЕДЕЛЕНО
	|			ТОГДА ИСТИНА
	|		КОГДА &НаличиеСчетаФактуры
	|			ТОГДА НЕ СчетФактураВыданный.Ссылка ЕСТЬ NULL
	|		ИНАЧЕ
	|			СчетФактураВыданный.Ссылка ЕСТЬ NULL
	|	КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &СчетФактураПроведен = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			КОГДА &СчетФактураПроведен
	|				ТОГДА ЕСТЬNULL(СчетФактураВыданный.Ссылка.Проведен,НЕОПРЕДЕЛЕНО) = ИСТИНА
	|			ИНАЧЕ
	|				ЕСТЬNULL(СчетФактураВыданный.Ссылка.Проведен,НЕОПРЕДЕЛЕНО) = ЛОЖЬ
	|		КОНЕЦ)
	|	И (ВЫБОР
	|			КОГДА &ДатаСФНеБолее = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			КОГДА НЕ СчетФактураВыданный.Ссылка ЕСТЬ NULL
	|				ТОГДА СчетФактураВыданный.Ссылка.Дата <= &ДатаСФНеБолее
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ)
	|";
	
	Если (ЗначениеЗаполнено(НачПериода)) И (НЕ ЗначениеЗаполнено(КонПериода)) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Дата МЕЖДУ &ПериодНачало И &ПериодОкончание","Дата >= &ПериодНачало");
	ИначеЕсли (НЕ ЗначениеЗаполнено(НачПериода)) И (ЗначениеЗаполнено(КонПериода)) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Дата МЕЖДУ &ПериодНачало И &ПериодОкончание","Дата <= &ПериодОкончание");
	ИначеЕсли (НЕ ЗначениеЗаполнено(НачПериода)) И (НЕ ЗначениеЗаполнено(КонПериода)) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Дата МЕЖДУ &ПериодНачало И &ПериодОкончание","Дата <> ДАТАВРЕМЯ(1,1,1)");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПериодНачало",			НачПериода);
	Запрос.УстановитьПараметр("ПериодОкончание",		?(ЗначениеЗаполнено(КонПериода),КонецДня(КонПериода),КонПериода));
	Запрос.УстановитьПараметр("Организация",			Организация);
	Запрос.УстановитьПараметр("Фильтр",					?(НЕ ЗначениеЗаполнено(Фильтр),Ложь,Истина));
	Запрос.УстановитьПараметр("СписокФильтр",			Фильтр);
	Запрос.УстановитьПараметр("ВсеКромеФильтра",		ВсеКромеФильтра);
	Запрос.УстановитьПараметр("НаличиеСчетаФактуры",	НаличиеСчетаФактуры);
	Запрос.УстановитьПараметр("СчетФактураПроведен",	СчетФактураПроведен);
	Запрос.УстановитьПараметр("ДатаСФНеБолее",			?(НЕ ЗначениеЗаполнено(ДатаСФНеБолее),Неопределено,КонецДня(ДатаСФНеБолее)));
	
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаДокументов;
	
КонецФункции // ОпределитьНаличиеСчетовФактурПолученных()

// Функция возвращает текст запроса, выбирающего корректировочные счета-фактуры. Используется в отчете "Журнал учета счетов-фактур 1137".
//
Функция ТекстЗапросаКорректировочныеСчетаФактуры(Запрос, Период, Отбор) Экспорт

	// В БРУ нет документов счетов-фактур.
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК СчетФактура,
	|	Операция.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры) КАК ЧастьЖурналаУчетаСчетовФактур
	|ИЗ
	|	Документ.СчетФактураВыданный КАК Операция
	|ГДЕ
	|	Операция.Корректировочный
	|	И Операция.Дата >= &Квартал
	|	И Операция.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК СчетФактура,
	|	Операция.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры) КАК ЧастьЖурналаУчетаСчетовФактур
	|ИЗ
	|	Документ.СчетФактураПолученный КАК Операция
	|ГДЕ
	|	Операция.Корректировочный
	|	И Операция.Дата >= &Квартал
	|	И Операция.Организация = &Организация
	|";

	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаКорректировочныеСчетаФактуры()

// Функция возвращает текст запроса со списком документов счетов-фактур полученных,
// в которых сама организация выступает в роли поставщика в книге покупок.
//
Функция ТекстЗапросаСчетаФактурыПолученныеГдеОрганизацияЯвляетсяПоставщиком(Запрос, СписокОрганизаций, Период) Экспорт

	// В БРУ таких документов нет.

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК СчетФактура,
	|	Операция.Организация КАК Организация,
	|	ВЫБОР КОГДА Операция.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.Организация
	|	ИНАЧЕ
	|		Операция.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ЛОЖЬ КАК ЭтоКорректировкаРеализации
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК Операция
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Операция.Организация В (&Организация)
	|	И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|";

	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаСчетаФактурыПолученныеГдеОрганизацияЯвляетсяПоставщиком()

// Функция возвращает текст запроса со списком документов счетов-фактур выданных,
// в которых сама организация выступает в роли покупателя в книге продаж.
//
Функция ТекстЗапросаСчетаФактурыВыданныеГдеОрганизацияЯвляетсяПокупателем(Запрос, СписокОрганизаций, Период) Экспорт

	// В БРУ таких документов нет.

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК СчетФактура,
	|	Операция.ОрганизацияПолучатель КАК Организация,
	|	ВЫБОР КОГДА Операция.ОрганизацияПолучатель.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.ОрганизацияПолучатель
	|	ИНАЧЕ
	|		Операция.ОрганизацияПолучатель.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ЛОЖЬ КАК ЭтоКорректировкаПоступления
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК Операция
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Операция.Организация В (&Организация)
	|	И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|";

	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаСчетаФактурыВыданныеГдеОрганизацияЯвляетсяПокупателем()

// Функция возвращает текст запроса со списком документов счетов-фактур из 
// книги покупок, которые могут являться регистраторами исправительных записей.
//
Функция ТекстЗапросаСчетаФактурыИсправленияКнигиПокупок(Запрос, СписокОрганизаций, Период) Экспорт

	// В БРУ таких документов нет.
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК СчетФактура,
	|	Операция.Организация КАК Организация,
	|	ВЫБОР КОГДА Операция.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.Организация
	|	ИНАЧЕ
	|		Операция.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ЛОЖЬ КАК ЭтоКорректировкаПоступления
	|ИЗ
	|	Документ.СчетФактураПолученный КАК Операция
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Операция.Организация В (&Организация)
	|	И Операция.Исправление
	|";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаСчетаФактурыИсправленияКнигиПокупок()

// Функция возвращает текст запроса со списком документов счетов-фактур из 
// книги продаж, которые могут являться регистраторами исправительных записей.
//
Функция ТекстЗапросаСчетаФактурыИсправленияКнигиПродаж(Запрос, СписокОрганизаций, Период) Экспорт

	// В БРУ таких документов нет.
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК СчетФактура,
	|	Операция.Организация КАК Организация,
	|	ВЫБОР КОГДА Операция.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.Организация
	|	ИНАЧЕ
	|		Операция.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация
	|ИЗ
	|	Документ.СчетФактураВыданный КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации КАК Корректировки
	|	ПО
	|		Операция.ДокументОснование = Корректировки.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Операция.Организация В (&Организация)
	|	И Операция.Исправление
	|";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаСчетаФактурыИсправленияКнигиПродаж()

// Функция возвращает запрос для получения информации о ГТД и стране происхождения
// для счетов фактур.
//
Функция ТекстЗапросаНомераГТДпоСчетамФактурам(Запрос, СписокСчетовФактур) Экспорт

	// В БРУ таких документов нет.

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка) КАК СтранаПроисхождения,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД
	|ГДЕ
	|	ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операция.Ссылка КАК СчетФактура,
	|	Строки.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Строки.НомерГТД КАК НомерГТД
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПоступлениеТоваровУслуг.Товары КАК Строки
	|	ПО
	|		Операция.Ссылка = Строки.Ссылка
	|ГДЕ
	|	Строки.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операция.Ссылка КАК СчетФактура,
	|	Строки.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Строки.НомерГТД КАК НомерГТД
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК Строки
	|	ПО
	|		Операция.Ссылка = Строки.Ссылка
	|ГДЕ
	|	Операция.УказыватьНомераГТД
	|	И Строки.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операция.Ссылка КАК СчетФактура,
	|	Строки.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Строки.НомерГТД КАК НомерГТД
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПередачаТоваровМеждуОрганизациями.ВидыЗапасов КАК Строки
	|	ПО
	|		Операция.Ссылка = Строки.Ссылка
	|ГДЕ
	|	Не Операция.УказыватьНомераГТД
	|	И Строки.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|";

	Возврат ТекстЗапроса;

КонецФункции

// Процедура ПодготовитьВременнуюТаблицуСчетаФактурыПолученные помещает в менеджер временных таблиц Запроса
// временную таблицу "ТаблицаСчетаФактурыДокументы" с реквизитами счетов-фактур полученных для книги покупок.
//
// Параметры:
//	МенеджерВременныхТаблиц - объект МенеджерВременныхТаблиц, с которым выполняется работа. 
//	СписокОрганизаций - массив организаций, по которым выполняется отбор.
//	ИмяТаблицаСчетовФактур - Строка с именем временной таблицы в менеджере временных таблиц, 
//							 по которой отбирать счета-фактуры. В таблице должна присутствовать колонки: 
//		СчетФактура - ДокументСсылка.*
//		Организация - СправочникСсылка.Организации
//		ДокументОплаты - ДокументСсылка.*
//		ДоговорАванса - СправочникСсылка.ДоговорыКонтрагентов
//		НаАванс - Булево
//		СтавкаНДС_Аванс - ПеречислениеСсылка.СтавкиНДС
//
Процедура ПодготовитьВременнуюТаблицуСчетаФактурыПолученные(МенеджерВременныхТаблиц, СписокОрганизаций, ИмяТаблицыСчетовФактур) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", СписокОрганизаций);

	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Строки.ДокументОснование КАК СчетФактура,
	|	Операция.Организация,
	|	ВЫБОР КОГДА Операция.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.Организация
	|	ИНАЧЕ
	|		Операция.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	Операция.Контрагент КАК Контрагент,
	|	Операция.Номер КАК НомерСчетаФактуры,
	|	Операция.Дата КАК ДатаСчетаФактуры,
	|	Операция.Ссылка КАК СчетФактураДокумент,
	|	ЛОЖЬ КАК НаАванс,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДСАванса,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАванса,
	|	1 КАК Приоритет,
	|	NULL КАК Ссылка,
	|	Строки.ДокументОснование.Дата КАК ДатаДокументаОснования
	|
	|ПОМЕСТИТЬ СчетаФактурыДокументы
	|ИЗ
	|	Документ.СчетФактураПолученный КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СчетФактураПолученный.ДокументыОснования КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Организация В (&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.ДокументОснование КАК СчетФактура,
	|	Операция.Организация,
	|	ВЫБОР КОГДА Операция.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.Организация
	|	ИНАЧЕ
	|		Операция.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	Операция.Контрагент КАК Контрагент,
	|	Операция.НомерВходящегоДокумента КАК НомерСчетаФактуры,
	|	Операция.ДатаВходящегоДокумента КАК ДатаСчетаФактуры,
	|	Операция.Ссылка КАК СчетФактураДокумент,
	|	ИСТИНА КАК НаАванс,
	|	Строки.СтавкаНДС КАК СтавкаНДСАванса,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАванса,
	|	1 КАК Приоритет,
	|	NULL КАК Ссылка,
	|	Операция.ДокументОснование.Дата КАК ДатаДокументаОснования
	|ИЗ
	|	Документ.СчетФактураПолученныйАванс КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СчетФактураПолученныйАванс.Авансы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Организация В (&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.ДокументОснование КАК СчетФактура,
	|	Операция.Организация,
	|	ВЫБОР КОГДА Операция.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.Организация
	|	ИНАЧЕ
	|		Операция.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	Операция.Контрагент КАК Контрагент,
	|	Операция.Номер КАК НомерСчетаФактуры,
	|	Операция.Дата КАК ДатаСчетаФактуры,
	|	Операция.Ссылка КАК СчетФактураДокумент,
	|	ИСТИНА КАК НаАванс,
	|	Строки.СтавкаНДС КАК СтавкаНДСАванса,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАванса,
	|	1 КАК Приоритет,
	|	NULL КАК Ссылка,
	|	Операция.ДокументОснование.Дата КАК ДатаДокументаОснования
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СчетФактураВыданныйАванс.Авансы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Организация В (&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.ДокументОснование КАК СчетФактура,
	|	ПередачаТоваров.ОрганизацияПолучатель КАК Организация,
	|	ВЫБОР КОГДА ПередачаТоваров.ОрганизацияПолучатель.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		ПередачаТоваров.ОрганизацияПолучатель
	|	ИНАЧЕ
	|		ПередачаТоваров.ОрганизацияПолучатель.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ПередачаТоваров.Организация КАК Контрагент,
	|	Операция.Номер КАК НомерСчетаФактуры,
	|	Операция.Дата КАК ДатаСчетаФактуры,
	|	Операция.Ссылка КАК СчетФактураДокумент,
	|	Ложь КАК НаАванс,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДСАванса,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАванса,
	|	1 КАК Приоритет,
	|	NULL КАК Ссылка,
	|	ПередачаТоваров.Дата КАК ДатаДокументаОснования
	|ИЗ
	|	Документ.СчетФактураВыданный КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПередачаТоваровМеждуОрганизациями КАК ПередачаТоваров
	|	ПО
	|		ПередачаТоваров.Ссылка = Операция.ДокументОснование
	|ГДЕ
	|	Операция.Проведен
	|	И Не ПередачаТоваров.РасчетыЧерезОтдельногоКонтрагента
	|	И ПередачаТоваров.ОрганизацияПолучатель В (&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.ДокументОснование КАК СчетФактура,
	|	ОтчетПоКомиссии.Организация КАК Организация,
	|	ВЫБОР КОГДА ОтчетПоКомиссии.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		ОтчетПоКомиссии.Организация
	|	ИНАЧЕ
	|		ОтчетПоКомиссии.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ОтчетПоКомиссии.Комиссионер КАК Контрагент,
	|	Операция.Номер КАК НомерСчетаФактуры,
	|	Операция.Дата КАК ДатаСчетаФактуры,
	|	Операция.Ссылка КАК СчетФактураДокумент,
	|	Ложь КАК НаАванс,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДСАванса,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАванса,
	|	1 КАК Приоритет,
	|	NULL КАК Ссылка,
	|	ОтчетПоКомиссии.Дата КАК ДатаДокументаОснования
	|ИЗ
	|	Документ.СчетФактураВыданный КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтчетПоКомиссииМеждуОрганизациями КАК ОтчетПоКомиссии
	|	ПО
	|		ОтчетПоКомиссии.Ссылка = Операция.ДокументОснование
	|ГДЕ
	|	Операция.Проведен
	|	И Не ОтчетПоКомиссии.РасчетыЧерезОтдельногоКонтрагента
	|	И ОтчетПоКомиссии.Организация В (&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ГоловнаяОрганизация
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетаФактурыДокументы.СчетФактура,
	|	СчетаФактурыДокументы.Организация,
	|	СчетаФактурыДокументы.ГоловнаяОрганизация,
	|	СчетаФактурыДокументы.Контрагент,
	|	СчетаФактурыДокументы.ДатаСчетаФактуры,
	|	СчетаФактурыДокументы.НомерСчетаФактуры,
	|	СчетаФактурыДокументы.СчетФактураДокумент,
	|	СчетаФактурыДокументы.ДоговорАванса,
	|	СчетаФактурыДокументы.СтавкаНДСАванса,
	|	СчетаФактурыДокументы.Приоритет,
	|	СчетаФактурыДокументы.СчетФактураДокумент КАК СчетФактураДокументРасшифровка,
	|	СчетаФактурыДокументы.Ссылка КАК Ссылка,
	|	СчетаФактурыДокументы.ДатаДокументаОснования КАК ДатаДокументаОснования
	|ПОМЕСТИТЬ ТаблицаСчетаФактурыДокументыПредварительная
	|ИЗ
	|	ЗаписиКнигиПокупок КАК ЗаписиКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыДокументы КАК СчетаФактурыДокументы
	|		ПО (ЗаписиКнигиПокупок.СчетФактура = СчетаФактурыДокументы.СчетФактура
	|				ИЛИ ЗаписиКнигиПокупок.ДокументОплаты = СчетаФактурыДокументы.СчетФактура)
	|			И ЗаписиКнигиПокупок.ГоловнаяОрганизация = СчетаФактурыДокументы.ГоловнаяОрганизация
	|			И ЗаписиКнигиПокупок.ДоговорАванса = СчетаФактурыДокументы.ДоговорАванса
	|			И ((НЕ ЗаписиКнигиПокупок.НаАванс)
	|				ИЛИ СчетаФактурыДокументы.НаАванс
	|					И ЗаписиКнигиПокупок.СтавкаНДС_Аванс = СчетаФактурыДокументы.СтавкаНДСАванса)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.СчетФактураДокументКонтрагент,
	|	ВложенныйЗапрос.ДоговорАванса,
	|	ВложенныйЗапрос.НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.КолвоСФ = 1
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ВложенныйЗапрос.УчитыватьСФ
	|	КОНЕЦ КАК ОъединятьСФ,
	|	ВложенныйЗапрос.СчетФактураДокумент
	|ПОМЕСТИТЬ ВТ_ОбъединениеСФ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаСчетаФактурыДокументы.Контрагент КАК СчетФактураДокументКонтрагент,
	|		ТаблицаСчетаФактурыДокументы.ДоговорАванса КАК ДоговорАванса,
	|		ТаблицаСчетаФактурыДокументы.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаСчетаФактурыДокументы.СчетФактураДокумент) КАК КолвоСФ,
	|		ИСТИНА КАК УчитыватьСФ,
	|		МАКСИМУМ(ТаблицаСчетаФактурыДокументы.СчетФактураДокумент) КАК СчетФактураДокумент
	|	ИЗ
	|		ТаблицаСчетаФактурыДокументыПредварительная КАК ТаблицаСчетаФактурыДокументы
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаСчетаФактурыДокументы.Контрагент,
	|		ТаблицаСчетаФактурыДокументы.ДоговорАванса,
	|		ТаблицаСчетаФактурыДокументы.НомерСчетаФактуры) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСчетаФактурыДокументыПредварительная.СчетФактура,
	|	ТаблицаСчетаФактурыДокументыПредварительная.Организация,
	|	ТаблицаСчетаФактурыДокументыПредварительная.ГоловнаяОрганизация,
	|	ТаблицаСчетаФактурыДокументыПредварительная.ДатаСчетаФактуры,
	|	ТаблицаСчетаФактурыДокументыПредварительная.НомерСчетаФактуры,
	|	ВТ_ОбъединениеСФ.СчетФактураДокумент,
	|	ТаблицаСчетаФактурыДокументыПредварительная.ДоговорАванса,
	|	ТаблицаСчетаФактурыДокументыПредварительная.СтавкаНДСАванса,
	|	ТаблицаСчетаФактурыДокументыПредварительная.Приоритет,
	|	ВТ_ОбъединениеСФ.ОъединятьСФ КАК ОъединятьСчетФактуры,
	|	ТаблицаСчетаФактурыДокументыПредварительная.СчетФактураДокументРасшифровка,
	|	ТаблицаСчетаФактурыДокументыПредварительная.Ссылка КАК Ссылка,
	|	ТаблицаСчетаФактурыДокументыПредварительная.ДатаДокументаОснования КАК ДатаДокументаОснования
	|ПОМЕСТИТЬ ВТ_ТаблицаСчетаФактурыДокументы
	|ИЗ
	|	ТаблицаСчетаФактурыДокументыПредварительная КАК ТаблицаСчетаФактурыДокументыПредварительная
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбъединениеСФ КАК ВТ_ОбъединениеСФ
	|		ПО ТаблицаСчетаФактурыДокументыПредварительная.Контрагент = ВТ_ОбъединениеСФ.СчетФактураДокументКонтрагент
	|			И ТаблицаСчетаФактурыДокументыПредварительная.ДоговорАванса = ВТ_ОбъединениеСФ.ДоговорАванса
	|			И ТаблицаСчетаФактурыДокументыПредварительная.НомерСчетаФактуры = ВТ_ОбъединениеСФ.НомерСчетаФактуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЖурналУчетаСчетовФактур.Регистратор,
	|	ЖурналУчетаСчетовФактур.Организация,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактур.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЖурналУчетаСчетовФактур.Организация
	|		ИНАЧЕ ЖурналУчетаСчетовФактур.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры,
	|	"""" КАК НомерИсправления,
	|	"""" КАК ДатаИсправления,
	|	"""" КАК НомерИсправленияКорректировки,
	|	"""" КАК ДатаИсправленияКорректировки
	|ПОМЕСТИТЬ ВТ_РегистрацияСчетовФактур
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур КАК ЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЖурналУчетаСчетовФактур.Регистратор В
	|			(ВЫБРАТЬ
	|				ВТ_ТаблицаСчетаФактурыДокументы.Ссылка
	|			ИЗ
	|				ВТ_ТаблицаСчетаФактурыДокументы)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаСчетаФактурыДокументы.СчетФактура КАК СчетФактура,
	|	ВТ_ТаблицаСчетаФактурыДокументы.Организация КАК Организация,
	|	ВТ_ТаблицаСчетаФактурыДокументы.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ЕСТЬNULL(ВТ_РегистрацияСчетовФактур.ДатаСчетаФактуры, ВТ_ТаблицаСчетаФактурыДокументы.ДатаСчетаФактуры) КАК ДатаСчетаФактуры,
	|	ЕСТЬNULL(ВТ_РегистрацияСчетовФактур.НомерСчетаФактуры, ВТ_ТаблицаСчетаФактурыДокументы.НомерСчетаФактуры) КАК НомерСчетаФактуры,
	|	ЕСТЬNULL(ВТ_РегистрацияСчетовФактур.Регистратор, ВТ_ТаблицаСчетаФактурыДокументы.СчетФактураДокумент) КАК СчетФактураДокумент,
	|	ВТ_ТаблицаСчетаФактурыДокументы.ДоговорАванса КАК ДоговорАванса,
	|	ВТ_ТаблицаСчетаФактурыДокументы.СтавкаНДСАванса КАК СтавкаНДСАванса,
	|	ВТ_ТаблицаСчетаФактурыДокументы.Приоритет КАК Приоритет,
	|	ВТ_ТаблицаСчетаФактурыДокументы.ОъединятьСчетФактуры КАК ОъединятьСчетФактуры,
	|	ВТ_ТаблицаСчетаФактурыДокументы.СчетФактураДокументРасшифровка КАК СчетФактураДокументРасшифровка,
	|	ВТ_РегистрацияСчетовФактур.НомерКорректировочногоСчетаФактуры КАК НомерКорректировки,
	|	ВТ_РегистрацияСчетовФактур.ДатаКорректировочногоСчетаФактуры КАК ДатаКорректировки,
	|	ВТ_РегистрацияСчетовФактур.НомерИсправления КАК НомерИсправления,
	|	ВТ_РегистрацияСчетовФактур.ДатаИсправления КАК ДатаИсправления,
	|	ВТ_РегистрацияСчетовФактур.НомерИсправленияКорректировки КАК НомерИсправленияКорректировки,
	|	ВТ_РегистрацияСчетовФактур.ДатаИсправленияКорректировки КАК ДатаИсправленияКорректировки,
	|	Истина КАК ОбрабатыватьНомерДокумента,
	|	ВТ_ТаблицаСчетаФактурыДокументы.ДатаДокументаОснования
	|ПОМЕСТИТЬ ТаблицаСчетаФактурыДокументы
	|ИЗ
	|	ВТ_ТаблицаСчетаФактурыДокументы КАК ВТ_ТаблицаСчетаФактурыДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегистрацияСчетовФактур КАК ВТ_РегистрацияСчетовФактур
	|		ПО ВТ_ТаблицаСчетаФактурыДокументы.Ссылка = ВТ_РегистрацияСчетовФактур.Регистратор
	|			И ВТ_ТаблицаСчетаФактурыДокументы.ГоловнаяОрганизация = ВТ_РегистрацияСчетовФактур.ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаСчетаФактурыДокументыПредварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ОбъединениеСФ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаСчетаФактурыДокументы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_РегистрацияСчетовФактур";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗаписиКнигиПокупок", ИмяТаблицыСчетовФактур);
	
	Запрос.Выполнить();
	
КонецПроцедуры // ПодготовитьВременнуюТаблицуСчетаФактурыПолученные()

// Процедура ПодготовитьВременнуюТаблицуСчетаФактурыВыданные помещает в менеджер временных таблиц Запроса
// временную таблицу "ТаблицаСчетаФактурыДокументы" с реквизитами счетов-фактур для книги продаж.
//
// Параметры:
//	МенеджерВременныхТаблиц - объект МенеджерВременныхТаблиц, с которым выполняется работа. 
//	СписокОрганизаций - массив организаций, по которым выполняется отбор.
//	ИмяТаблицаСчетовФактур - Строка с именем временной таблицы в менеджере временных таблиц, 
//							 по которой отбирать счета-фактуры. В таблице должна присутствовать колонки: 
//		СчетФактура - ДокументСсылка.*
//		ДоговорАванса - СправочникСсылка.ДоговорыКонтрагентов
//		СтавкаНДС_Аванс - ПеречислениеСсылка.СтавкиНДС
//
Процедура ПодготовитьВременнуюТаблицуСчетаФактурыВыданные(МенеджерВременныхТаблиц, СписокОрганизаций, ИмяТаблицыСчетовФактур) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", СписокОрганизаций);

	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Операция.Организация КАК Организация,
	|	ВЫБОР КОГДА Операция.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.Организация
	|	ИНАЧЕ
	|		Операция.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ВЫБОР КОГДА Операция.Исправление ТОГДА
	|		Операция.Ссылка
	|	ИНАЧЕ
	|		Операция.ДокументОснование
	|	КОНЕЦ КАК СчетФактура,
	|	Операция.Номер КАК НомерСчетаФактуры,
	|	Операция.Дата КАК ДатаСчетаФактуры,
	|	Операция.Ссылка КАК СчетФактураДокумент,
	|	ЛОЖЬ КАК НаАванс,
	|	ЛОЖЬ КАК НаСуммовуюРазницу,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДСАванса,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАванса,
	|	4 КАК Приоритет,
	|	NULL КАК Ссылка,
	|	Операция.ДокументОснование.Дата КАК ДатаДокументаОснования,
	|	ЛОЖЬ КАК ЭтоРозничнаяПродажа
	|
	|ПОМЕСТИТЬ СчетаФактурыДокументы
	|ИЗ
	|	Документ.СчетФактураВыданный КАК Операция
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Организация В (&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Организация КАК Организация,
	|	ВЫБОР КОГДА Операция.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.Организация
	|	ИНАЧЕ
	|		Операция.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	Операция.ДокументОснование КАК СчетФактура,
	|	Операция.Номер КАК НомерСчетаФактуры,
	|	Операция.Дата КАК ДатаСчетаФактуры,
	|	Операция.Ссылка КАК СчетФактураДокумент,
	|	ИСТИНА КАК НаАванс,
	|	ЛОЖЬ КАК НаСуммовуюРазницу,
	|	Строки.СтавкаНДС КАК СтавкаНДСАванса,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАванса,
	|	4 КАК Приоритет,
	|	NULL КАК Ссылка,
	|	Операция.ДокументОснование.Дата КАК ДатаДокументаОснования,
	|	ЛОЖЬ КАК ЭтоРозничнаяПродажа
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК Операция
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.СчетФактураВыданныйАванс.Авансы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Организация В (&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Организация КАК Организация,
	|	ВЫБОР КОГДА Операция.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.Организация
	|	ИНАЧЕ
	|		Операция.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	Операция.ДокументОснование КАК СчетФактура,
	|	Операция.НомерВходящегоДокумента КАК НомерСчетаФактуры,
	|	Операция.ДатаВходящегоДокумента КАК ДатаСчетаФактуры,
	|	Операция.Ссылка КАК СчетФактураДокумент,
	|	ИСТИНА КАК НаАванс,
	|	ЛОЖЬ КАК НаСуммовуюРазницу,
	|	Строки.СтавкаНДС КАК СтавкаНДСАванса,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАванса,
	|	4 КАК Приоритет,
	|	NULL КАК Ссылка,
	|	Операция.ДокументОснование.Дата КАК ДатаДокументаОснования,
	|	ЛОЖЬ КАК ЭтоРозничнаяПродажа
	|ИЗ
	|	Документ.СчетФактураПолученныйАванс КАК Операция
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.СчетФактураПолученныйАванс.Авансы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Организация В (&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Организация КАК Организация,
	|	ВЫБОР КОГДА Операция.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|		Операция.Организация
	|	ИНАЧЕ
	|		Операция.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	Операция.Ссылка КАК СчетФактура,
	|	Операция.Номер КАК НомерСчетаФактуры,
	|	Операция.Дата КАК ДатаСчетаФактуры,
	|	Операция.Ссылка КАК СчетФактураДокумент,
	|	ЛОЖЬ КАК НаАванс,
	|	ЛОЖЬ КАК НаСуммовуюРазницу,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДСАванса,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАванса,
	|	5 КАК Приоритет,
	|	NULL КАК Ссылка,
	|	Операция.Дата КАК ДатаДокументаОснования,
	|	ИСТИНА КАК ЭтоРозничнаяПродажа
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК Операция
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|	И Операция.Организация В (&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ГоловнаяОрганизация
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаписиКнигиПродаж.Организация,
	|	ЗаписиКнигиПродаж.ГоловнаяОрганизация,
	|	ЗаписиКнигиПродаж.СчетФактура,
	|	СчетаФактурыДокументы.ДатаСчетаФактуры,
	|	СчетаФактурыДокументы.НомерСчетаФактуры,
	|	СчетаФактурыДокументы.СчетФактураДокумент,
	|	СчетаФактурыДокументы.ДоговорАванса,
	|	СчетаФактурыДокументы.СтавкаНДСАванса,
	|	СчетаФактурыДокументы.Приоритет,
	|	СчетаФактурыДокументы.Ссылка КАК Ссылка,
	|	СчетаФактурыДокументы.ДатаДокументаОснования КАК ДатаДокументаОснования,
	|	СчетаФактурыДокументы.ЭтоРозничнаяПродажа КАК ЭтоРозничнаяПродажа
	|ПОМЕСТИТЬ ВТ_ТаблицаСчетаФактурыДокументы
	|ИЗ
	|	ЗаписиКнигиПродаж КАК ЗаписиКнигиПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыДокументы КАК СчетаФактурыДокументы
	|		ПО ЗаписиКнигиПродаж.СчетФактура = СчетаФактурыДокументы.СчетФактура
	|			И ЗаписиКнигиПродаж.ГоловнаяОрганизация = СчетаФактурыДокументы.ГоловнаяОрганизация
	|			И ЗаписиКнигиПродаж.СтавкаНДС_Аванс = СчетаФактурыДокументы.СтавкаНДСАванса
	|			И ЗаписиКнигиПродаж.ДоговорАванса = СчетаФактурыДокументы.ДоговорАванса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЖурналУчетаСчетовФактур.Регистратор,
	|	ЖурналУчетаСчетовФактур.Организация,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактур.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЖурналУчетаСчетовФактур.Организация
	|		ИНАЧЕ ЖурналУчетаСчетовФактур.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерИсправления КАК НомерИсправления,
	|	ЖурналУчетаСчетовФактур.ДатаИсправления КАК ДатаИсправления,
	|	"""" КАК НомерИсправленияКорректировки,
	|	"""" КАК ДатаИсправленияКорректировки
	|ПОМЕСТИТЬ ВТ_РегистрацияСчетовФактур
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур КАК ЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЖурналУчетаСчетовФактур.Регистратор В (
	|		ВЫБРАТЬ
	|			ВТ_ТаблицаСчетаФактурыДокументы.СчетФактураДокумент
	|		ИЗ
	|			ВТ_ТаблицаСчетаФактурыДокументы
	|		)
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаСчетаФактурыДокументы.Организация КАК Организация,
	|	ВТ_ТаблицаСчетаФактурыДокументы.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВТ_ТаблицаСчетаФактурыДокументы.СчетФактура КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА НЕ (ВТ_РегистрацияСчетовФактур.ДатаСчетаФактуры ЕСТЬ NULL)
	|			ТОГДА ВТ_РегистрацияСчетовФактур.ДатаСчетаФактуры
	|		
	|		КОГДА НЕ (ВТ_ТаблицаСчетаФактурыДокументы.ДатаСчетаФактуры ЕСТЬ NULL)
	|			ТОГДА ВТ_ТаблицаСчетаФактурыДокументы.ДатаСчетаФактуры
	|
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДатаСчетаФактуры,
	|	ВЫБОР
	|		КОГДА НЕ (ВТ_РегистрацияСчетовФактур.НомерСчетаФактуры ЕСТЬ NULL)
	|			ТОГДА ВТ_РегистрацияСчетовФактур.НомерСчетаФактуры
	|
	|		КОГДА НЕ (ВТ_ТаблицаСчетаФактурыДокументы.НомерСчетаФактуры ЕСТЬ NULL)
	|			ТОГДА ВТ_ТаблицаСчетаФактурыДокументы.НомерСчетаФактуры
	|
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НомерСчетаФактуры,
	|	ЕСТЬNULL(ВТ_РегистрацияСчетовФактур.Регистратор, ВТ_ТаблицаСчетаФактурыДокументы.СчетФактураДокумент) КАК СчетФактураДокумент,
	|	ВТ_ТаблицаСчетаФактурыДокументы.ДоговорАванса КАК ДоговорАванса,
	|	ВТ_ТаблицаСчетаФактурыДокументы.СтавкаНДСАванса КАК СтавкаНДСАванса,
	|	ВТ_ТаблицаСчетаФактурыДокументы.Приоритет КАК Приоритет,
	|	ВТ_РегистрацияСчетовФактур.НомерКорректировочногоСчетаФактуры КАК НомерКорректировки,
	|	ВТ_РегистрацияСчетовФактур.ДатаКорректировочногоСчетаФактуры КАК ДатаКорректировки,
	|	ВТ_РегистрацияСчетовФактур.НомерИсправления КАК НомерИсправления,
	|	ВТ_РегистрацияСчетовФактур.ДатаИсправления КАК ДатаИсправления,
	|	ВТ_РегистрацияСчетовФактур.НомерИсправленияКорректировки КАК НомерИсправленияКорректировки,
	|	ВТ_РегистрацияСчетовФактур.ДатаИсправленияКорректировки КАК ДатаИсправленияКорректировки,
	|	Истина КАК ОбрабатыватьНомерДокумента,
	|	ВТ_ТаблицаСчетаФактурыДокументы.ДатаДокументаОснования,
	|	ВТ_ТаблицаСчетаФактурыДокументы.ЭтоРозничнаяПродажа
	|ПОМЕСТИТЬ ТаблицаСчетаФактурыДокументы
	|ИЗ
	|	ВТ_ТаблицаСчетаФактурыДокументы КАК ВТ_ТаблицаСчетаФактурыДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегистрацияСчетовФактур КАК ВТ_РегистрацияСчетовФактур
	|		ПО ВТ_ТаблицаСчетаФактурыДокументы.СчетФактураДокумент = ВТ_РегистрацияСчетовФактур.Регистратор
	|			И ВТ_ТаблицаСчетаФактурыДокументы.ГоловнаяОрганизация = ВТ_РегистрацияСчетовФактур.ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаСчетаФактурыДокументы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_РегистрацияСчетовФактур
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗаписиКнигиПродаж", ИмяТаблицыСчетовФактур);
	
	Запрос.Выполнить();

КонецПроцедуры // ПодготовитьВременнуюТаблицуСчетаФактурыВыданные()

// Функция возвращает таблицу значений со ссылками на документы счета-фактуры, 
// которые записываются в субконто субсчетов 19 и в регистры НДС по таблице первичных 
// документов поступлений. Используется в отчете "ОтчетПоНаличиюСчетовФактур".
// 
// Параметры:
//	ТаблицаДокументов - таблица значений с колонками:
//		Документ - ДокументСсылка.*, ссылка на первичный документ поступления.
//
// Возвращаемое значение:
//	Таблица значений с колонкой "СчетФактура"
//
Функция ПреобразоватьДокументыВСчетаФактуры(ТаблицаДокументов) Экспорт

	// В БРУ нет специальных документов счетов-фактур, поэтому возвращаем исходную таблицу.
	ТаблицаРезультат = ТаблицаДокументов.Скопировать(, "Документ");
	ТаблицаРезультат.Колонки.Документ.Имя = "СчетФактура";
	
	Возврат ТаблицаРезультат;

КонецФункции // ПреобразоватьДокументыВСчетаФактуры()

// Функция возвращает массив пустых ссылок на документы, являющиеся типами для ИсправленногоСчетаФактуры
//
Функция ПолучитьМассивПустыхИсправленныхСчетовФактур() Экспорт

	МассивДокументовИсправлений = Новый Массив;
	
	МассивТипов = Метаданные.РегистрыНакопления.НДСЗаписиКнигиПокупок.Измерения.ИсправленныйСчетФактура.Тип.Типы();
	Для Каждого ТипДокумента Из МассивТипов Цикл
		МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипДокумента);
		Если МетаданныеДокумента <> Неопределено Тогда
			ИмяДокумента = МетаданныеДокумента.Имя;
			МассивДокументовИсправлений.Добавить(Документы[ИмяДокумента].ПустаяСсылка());
		КонецЕсли;
	КонецЦикла;
	
	МассивДокументовИсправлений.Добавить(Неопределено);

	Возврат МассивДокументовИсправлений;

КонецФункции // ПолучитьМассивПустыхИсправленныхСчетовФактур()

// Функция возвращает строку с номеров и датой счета-фактуры для вывода на печать в книге покупок.
//
// Параметры:
//	ДокументОснование - ДокументСсылка.*, ссылка на документ, указываемый в качестве измерения
//						в регистрах НДС.
//	НомерСчетаФактуры - Строка, номер счета-фактуры полученный предварительно.
//	ДатаСчетаФактуры  - Дата, дата счета-фактуры, полученная предварительно.
//	ОбрабатыватьНомерДокумента - Булево, признак того, что необходимо исключить префиксы из номера
//									документа перед печатью.
//	СчетФактураДокумент - ДокументСсылка.СчетФактураПолученный/СчетФактураВыданный - ссылка на 
//							подчиненный документ "счет-фактура" (необязательный)
//
Функция ОпределитьДатуИНомерСчетаФактурыДляПечати(ДокументОснование, НомерСчетаФактуры, ДатаСчетаФактуры, ОбрабатыватьНомерДокумента = Ложь, СчетФактураДокумент = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат "";
	КонецЕсли;

	ДатаНомер = "";
	
	Если ЗначениеЗаполнено(ДатаСчетаФактуры) И ЗначениеЗаполнено(НомерСчетаФактуры) Тогда
		Если ОбрабатыватьНомерДокумента Тогда			
			ДатаНомер = "" + Формат(ДатаСчетаФактуры, "ДФ=dd.MM.yyyy") 
				+ ";" + ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(НомерСчетаФактуры, Истина, Истина);
		Иначе
			ДатаНомер = "" + Формат(ДатаСчетаФактуры, "ДФ=dd.MM.yyyy") 
				+ ";" + СокрЛП(НомерСчетаФактуры);
		КонецЕсли;
	КонецЕсли;
		
	Возврат ДатаНомер;

КонецФункции // ОпределитьДатуИНомерСчетаФактурыДляПечати()

