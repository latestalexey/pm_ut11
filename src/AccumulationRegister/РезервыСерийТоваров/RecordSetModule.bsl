#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ 

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу "ДвиженияРезервыСерийТоваровПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Номенклатура    КАК Номенклатура,
	|	Таблица.Характеристика  КАК Характеристика,
	|	Таблица.Серия           КАК Серия,
	|	Таблица.ДокументРезерва КАК ДокументРезерва,
	|	Таблица.Склад           КАК Склад,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Количество
	|		ИНАЧЕ
	|			-Таблица.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью
	|
	|ПОМЕСТИТЬ ДвиженияРезервыСерийТоваровПередЗаписью
	|
	|ИЗ
	|	РегистрНакопления.РезервыСерийТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И (НЕ &ЭтоНовый)
	|";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;

	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Номенклатура               КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика             КАК Характеристика,
	|	ТаблицаИзменений.Серия                      КАК Серия,
	|	ТаблицаИзменений.ДокументРезерва            КАК ДокументРезерва,
	|	ТаблицаИзменений.Склад                      КАК Склад,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение
	|
	|ПОМЕСТИТЬ ДвиженияРезервыСерийТоваровИзменение
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Номенклатура           КАК Номенклатура,
	|		Таблица.Характеристика         КАК Характеристика,
	|		Таблица.Серия                  КАК Серия,
	|		Таблица.ДокументРезерва        КАК ДокументРезерва,
	|		Таблица.Склад                  КАК Склад,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияРезервыСерийТоваровПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Номенклатура,
	|		Таблица.Характеристика,
	|		Таблица.Серия,
	|		Таблица.ДокументРезерва,
	|		Таблица.Склад,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Количество
	|			ИНАЧЕ
	|				Таблица.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РезервыСерийТоваров КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.ДокументРезерва,
	|	ТаблицаИзменений.Склад,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Номенклатура             КАК Номенклатура,
	|	Т.Характеристика           КАК Характеристика,
	|	Т.Серия                    КАК Серия,
	|	Т.Склад                    КАК Склад,
	|	СУММА(Т.УвеличениеПрихода) КАК УвеличениеПрихода
	|
	|ПОМЕСТИТЬ ДвиженияРезервыСерийТоваровИзменениеСводно
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Номенклатура            КАК Номенклатура,
	|		Таблица.Характеристика          КАК Характеристика,
	|		Таблица.Серия                   КАК Серия,
	|		Таблица.Склад                   КАК Склад,
	|		-Таблица.КоличествоПередЗаписью КАК УвеличениеПрихода
	|	ИЗ
	|		ДвиженияРезервыСерийТоваровПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Номенклатура,
	|		Таблица.Характеристика,
	|		Таблица.Серия,
	|		Таблица.Склад,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				Таблица.Количество
	|			ИНАЧЕ
	|				-Таблица.Количество
	|		КОНЕЦ
	|
	|	ИЗ
	|		РегистрНакопления.РезервыСерийТоваров КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	|	) КАК Т
	|
	|ГДЕ
	|	Т.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Номенклатура,
	|	Т.Склад,
	|	Т.Характеристика,
	|	Т.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(Т.УвеличениеПрихода) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияРезервыСерийТоваровПередЗаписью
	|";
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	Выборка.Следующий();
	
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияРезервыСерийТоваровИзменение", Выборка.Количество > 0);

	Выборка = Результат[1].Выбрать();
	Выборка.Следующий();

	СтруктураВременныеТаблицы.Вставить("ДвиженияРезервыСерийТоваровИзменениеСводно", Выборка.Количество > 0);

КонецПроцедуры

#КонецЕсли