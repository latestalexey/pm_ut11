
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура АктуализироватьПартии(Команда)
	
	АктуализироватьПартииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГраницуПоследовательности(Команда)
	
	СтрокаСписка = Элементы.Список.ТекущиеДанные;
	Если СтрокаСписка <> Неопределено Тогда
		УстановитьГраницуПоследовательностиНаДокумент(СтрокаСписка.Регистратор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьВсеПоследовательности(Команда)
	
	СброситьВсеПоследовательностиСервер();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Прочее 

&НаСервере
Процедура АктуализироватьПартииСервер()
	
	ПартионныйУчетСервер.ВыполнитьРегламентноеФормированиеДвиженийПоПартиямТоваров();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьГраницуПоследовательностиНаДокумент(Документ)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеРегистра.Номенклатура КАК Номенклатура,
	|	ДанныеРегистра.Характеристика КАК Характеристика,
	|	ДанныеРегистра.МоментВремени КАК МоментВремени
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.Регистратор = &Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка", Документ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураОтбор = Новый Структура("Номенклатура, Характеристика", Выборка.Номенклатура, Выборка.Характеристика);
		Если Последовательности.ПартииТоваровОрганизаций.Проверить(Выборка.МоментВремени, СтруктураОтбор) Тогда
			Последовательности.ПартииТоваровОрганизаций.УстановитьГраницу(Выборка.МоментВремени, СтруктураОтбор);
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура СброситьВсеПоследовательностиСервер()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Границы.Номенклатура,
	|	Границы.Характеристика
	|ИЗ
	|	Последовательность.ПартииТоваровОрганизаций.Границы КАК Границы
	|");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураОтбор = Новый Структура("Номенклатура, Характеристика", Выборка.Номенклатура, Выборка.Характеристика);
		Последовательности.ПартииТоваровОрганизаций.УстановитьГраницу(Дата(1980,1,1), СтруктураОтбор);
	КонецЦикла;
	
	
	Элементы.Список.Обновить();
	
КонецПроцедуры


