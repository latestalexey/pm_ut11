
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Договор") И ЗначениеЗаполнено(Параметры.Договор) Тогда
		
		СоставПараметров = "
		|ОграничиватьСуммуЗадолженности,
		|ДопустимаяСуммаЗадолженности,
		|ЗапрещаетсяПросроченнаяЗадолженность";
		
		ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Договор, СоставПараметров);
		
		ПараметрДанных = Отчет.КомпоновщикНастроек.ФиксированныеНастройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДопустимаяСуммаЗадолженности"));
		ПараметрДанных.Использование = Истина;
		ПараметрДанных.Значение = ДанныеДоговора.ДопустимаяСуммаЗадолженности;
		
		Если ДанныеДоговора.ОграничиватьСуммуЗадолженности И ДанныеДоговора.ЗапрещаетсяПросроченнаяЗадолженность Тогда
			КлючТекущегоВарианта = "ЗапретОтгрузкиПоКредитуИПросрочке";
		ИначеЕсли ДанныеДоговора.ОграничиватьСуммуЗадолженности Тогда
			КлючТекущегоВарианта = "ЗапретОтгрузкиПоКредиту";
		ИначеЕсли ДанныеДоговора.ЗапрещаетсяПросроченнаяЗадолженность Тогда
			КлючТекущегоВарианта = "ЗапретОтгрузкиПоПросрочке";
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрДанных = Отчет.КомпоновщикНастроек.ФиксированныеНастройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("СуммаПродажи"));
	ПараметрДанных.Использование = Истина;
	Если Параметры.Свойство("ДанныеПоСуммеПродажи") И Параметры.ДанныеПоСуммеПродажи.Свойство("СуммаВзаиморасчетов") Тогда
		ПараметрДанных.Значение = Параметры.ДанныеПоСуммеПродажи.СуммаВзаиморасчетов;
	ИначеЕсли Параметры.Свойство("ДанныеПоСуммеПродажи") Тогда
		ПараметрДанных.Значение = РассчитатьСуммуВзаиморасчетов(Параметры.ДанныеПоСуммеПродажи);
	КонецЕсли;
	
	ПараметрДанных = Отчет.КомпоновщикНастроек.ФиксированныеНастройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаОтчета"));
	ПараметрДанных.Использование = Истина;
	Если Параметры.Свойство("ДанныеПоСуммеПродажи") И Параметры.ДанныеПоСуммеПродажи.Свойство("Дата") Тогда
		ПараметрДанных.Значение = ?(Параметры.ДанныеПоСуммеПродажи.Дата > ТекущаяДата(), КонецДня(Параметры.ДанныеПоСуммеПродажи.Дата), КонецДня(ТекущаяДата()));
	Иначе
		ПараметрДанных.Значение = КонецДня(ТекущаяДата());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	
	ПартнерыИКонтрагенты.УдалитьЭлементИзНастроекОтборовОтчета(Отчет.КомпоновщикНастроек, "Контрагент");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
//Прочее

&НаСервере
Функция РассчитатьСуммуВзаиморасчетов(СтруктураПараметров)
	
	СтруктураПараметров.Вставить("СуммаВзаиморасчетов");
	ВзаиморасчетыСервер.ЗаполнитьСуммуВзаиморасчетов(СтруктураПараметров);
	Возврат СтруктураПараметров.СуммаВзаиморасчетов;
	
КонецФункции
