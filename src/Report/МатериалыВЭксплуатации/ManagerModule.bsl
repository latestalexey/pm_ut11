///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// СтандартныеПодсистемы.ВариантыОтчетов

// Методы расширения общей формы отчетов
//

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ФормаОтчетаПереопределяемый.ПриСозданииНаСервере
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	Перем Ссылка;
	
	КомпоновщикНастроек = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	// Передача параметра "ПоДаннымДокумента"
	ПараметрДанныхПоДаннымДокумента = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПоДаннымДокумента"));
	ПараметрДанныхПользовательскихНастроекПоДаннымДокумента = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанныхПоДаннымДокумента.ИдентификаторПользовательскойНастройки);
	
	Если Параметры.Свойство("Отбор")
	 И Параметры.Отбор.Свойство("ДокументСсылка", Ссылка) Тогда
		
		ПараметрДанныхПользовательскихНастроекПоДаннымДокумента.Использование = Истина;
		ПараметрДанныхПользовательскихНастроекПоДаннымДокумента.Значение      = Ссылка;
		
		// Настройка периода отчета
		ПараметрДанныхПериод = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Период");
		ПользовательскаяНастройкаПериода = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанныхПериод.ИдентификаторПользовательскойНастройки);
		ПользовательскаяНастройкаПериода.Значение = Новый СтандартныйПериод();
		
		// Отключение параметра "Только находящиеся в эксплуатации"
		ПараметрДанныхНаходитсяВЭксплуатации = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("НаходитсяВЭксплуатации"));
		ПараметрДанныхПользовательскихНастроекНаходитсяВЭксплуатации = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанныхНаходитсяВЭксплуатации.ИдентификаторПользовательскойНастройки);
		ПараметрДанныхПользовательскихНастроекНаходитсяВЭксплуатации.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
	Иначе
		
		ПараметрДанныхПользовательскихНастроекПоДаннымДокумента.Использование = Ложь;
		
	КонецЕсли;
	
	Параметры.ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Конец СтандартныеПодсистемы.ВариантыОтчетов
 