
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем Ссылка;
	
	// Передача параметра "ПоДаннымДокумента"
	ПараметрДанныхПоДаннымДокумента = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПоДаннымДокумента"));
	ПараметрДанныхПользовательскихНастроекПоДаннымДокумента = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанныхПоДаннымДокумента.ИдентификаторПользовательскойНастройки);
	
	Если Параметры.Свойство("Отбор")
	 И Параметры.Отбор.Свойство("ДокументСсылка", Ссылка) Тогда
		
		ПараметрДанныхПользовательскихНастроекПоДаннымДокумента.Использование = Истина;
		ПараметрДанныхПользовательскихНастроекПоДаннымДокумента.Значение      = Ссылка;
		
		// Настройка периода отчета
		ПараметрДанныхПериод = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Период");
		ПользовательскаяНастройкаПериода = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанныхПериод.ИдентификаторПользовательскойНастройки);
		ПользовательскаяНастройкаПериода.Значение = Новый СтандартныйПериод();
		
		// Отключение параметра "Только находящиеся в эксплуатации"
		ПараметрДанныхНаходитсяВЭксплуатации = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("НаходитсяВЭксплуатации"));
		ПараметрДанныхПользовательскихНастроекНаходитсяВЭксплуатации = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанныхНаходитсяВЭксплуатации.ИдентификаторПользовательскойНастройки);
		ПараметрДанныхПользовательскихНастроекНаходитсяВЭксплуатации.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
	Иначе
		
		ПараметрДанныхПользовательскихНастроекПоДаннымДокумента.Использование = Ложь;
		
	КонецЕсли;
	
	Параметры.ПользовательскиеНастройки = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	
КонецПроцедуры
