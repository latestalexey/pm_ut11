
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ГруппаНастройки.Видимость = НЕ Параметры.ЭтоРасшифровка;
	Если Параметры.ЭтоРасшифровка Тогда
		Отчет.АнализируемыйПериод = Параметры.АнализируемыйПериод;
		Отчет.ПериодДляСравнения = Параметры.ПериодДляСравнения;
		Отчет.НомерКолонкиРасшифровки = Параметры.НомерКолонкиРасшифровки;
	Иначе
		Если НЕ ЗначениеЗаполнено(Отчет.АнализируемыйПериод) Тогда
			Отчет.АнализируемыйПериод.Вариант = ВариантСтандартногоПериода.ЭтотМесяц;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Отчет.ПериодДляСравнения) Тогда
			Отчет.ПериодДляСравнения.Вариант = ВариантСтандартногоПериода.ПрошлыйМесяц;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	Отчет.ВыбранныйВариант = КлючТекущегоВарианта;
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	ТипРасшифровки = ТипЗнч(Расшифровка);
	СтандартнаяОбработка = ВозможнаРасшифровка(ТипРасшифровки);
	Если НЕ СтандартнаяОбработка Тогда
		Если ТипРасшифровки = Тип("Структура") Тогда
			Если Расшифровка.ИмяГруппировки = "ВидНоменклатуры"
				ИЛИ Расшифровка.ИмяГруппировки = "ТоварнаяКатегория" Тогда
				
				НовыеНастройки = Неопределено;
				НомерКолонкиРасшифровки = Элемент.ТекущаяОбласть.Лево;
				КопироватьИДополнитьНастройки(НовыеНастройки, Расшифровка);
				
				ПараметрыОткрытия = Новый Структура;
				ПараметрыОткрытия.Вставить("ФиксированныеНастройки", НовыеНастройки);
				ПараметрыОткрытия.Вставить("АнализируемыйПериод", Отчет.АнализируемыйПериод);
				ПараметрыОткрытия.Вставить("ПериодДляСравнения", Отчет.ПериодДляСравнения);
				ПараметрыОткрытия.Вставить("ЭтоРасшифровка", Истина);
				ПараметрыОткрытия.Вставить("НомерКолонкиРасшифровки", НомерКолонкиРасшифровки);
				ПараметрыОткрытия.Вставить("КлючВарианта", "ПоНоменклатуреКонтекст");
				ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина);
				ОткрытьФорму(
					"Отчет.СравнительныйАнализДинамикиАссортиментаИПродаж.Форма",
					ПараметрыОткрытия,
					ЭтаФорма,
					Истина,
					);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ВозможнаРасшифровка(ТипРасшифровки)
	
	Возврат Справочники.ТипВсеСсылки().СодержитТип(ТипРасшифровки);
	
КонецФункции

&НаСервере
Процедура КопироватьИДополнитьНастройки(НовыеНастройки, Расшифровка)
	
	НовыеНастройки = Отчет.КомпоновщикНастроек.ПолучитьНастройки();
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		НовыеНастройки.Отбор,
		"Магазин",
		Расшифровка.Магазин,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина);
	
	Если Расшифровка.ИмяГруппировки = "ВидНоменклатуры" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			НовыеНастройки.Отбор,
			"ВидНоменклатуры",
			Расшифровка.ВидНоменклатуры,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			НовыеНастройки.Отбор,
			"ТоварнаяКатегория",
			Расшифровка.ТоварнаяКатегория,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина);
	КонецЕсли;
	
КонецПроцедуры

