#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Параметр = НастройкаПараметра("ПоказыватьПродажи");
	Параметр.Использование = Истина;
	
	Параметр = НастройкаПараметра("ПланируемыеПродажи");
	Параметр.Использование = Истина;
	ПараметрВключаяЗапланированныеПродажи = НастройкаПараметра("ВключаяЗапланированныеПродажи");
	Если ПараметрВключаяЗапланированныеПродажи.Использование
	 И ПараметрВключаяЗапланированныеПродажи.Значение Тогда
		Параметр.Значение = Истина;
	Иначе
		Параметр.Значение = Ложь;
	КонецЕсли;
	
	Параметр = НастройкаПараметра("ПланируемыеРасходы");
	Параметр.Использование = Истина;
	ПараметрВключаяЗапланированныеРасходы = НастройкаПараметра("ВключаяЗапланированныеРасходы");
	Если ПараметрВключаяЗапланированныеРасходы.Использование
	 И ПараметрВключаяЗапланированныеРасходы.Значение Тогда
		Параметр.Значение = Истина;
	Иначе
		Параметр.Значение = Ложь;
	КонецЕсли;
	
	Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВалютаУправленческогоУчета"));
	Если Параметр <> Неопределено Тогда
		Параметр.Значение = Константы.ВалютаУправленческогоУчета.Получить();
		Параметр.Использование = Истина;
	КонецЕсли;
	
	ДатаНачала = Дата(1, 1, 1);
	ДатаОкончания = Дата(2399, 1, 1);
	
	ПараметрПериод = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ПараметрПериод <> Неопределено
	   И ПараметрПериод.Использование Тогда
		ДатаНачала = НачалоМесяца(ПараметрПериод.Значение.ДатаНачала);
		ДатаОкончания = КонецМесяца(ПараметрПериод.Значение.ДатаОкончания);
	КонецЕсли;
	
	ПараметрДатаНачала = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаНачала"));
	Если ПараметрДатаНачала <> Неопределено Тогда
		ПараметрДатаНачала.Значение = ДатаНачала;
		ПараметрДатаНачала.Использование = Истина;
	КонецЕсли;
	
	ПараметрДатаОкончания = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаОкончания"));
	Если ПараметрДатаОкончания <> Неопределено Тогда
		ПараметрДатаОкончания.Значение = ДатаОкончания;
		ПараметрДатаОкончания.Использование = Истина;
	КонецЕсли;
	
	СегментыСервер.ВключитьОтборПоСегментуПартнеровВСКД(КомпоновщикНастроек);							
	СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(КомпоновщикНастроек);								
		
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Прочее

Функция НастройкаПараметра(ИмяПараметра)

	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки;
		Иначе
			Возврат ПараметрДанных;
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;

КонецФункции

#КонецЕсли