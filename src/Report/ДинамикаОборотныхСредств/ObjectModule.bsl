#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	
	Параметр = НастройкаПараметра("ВалютаУправленческогоУчета");
	Если Параметр <> Неопределено Тогда
		Параметр.Значение = ВалютаУправленческогоУчета;
		Параметр.Использование = Истина;
	КонецЕсли;
	
	Параметр = НастройкаПараметра("ВалютаОтчета");
	Если Параметр <> Неопределено Тогда
		Параметр.Использование = Истина;
		Если Не ЗначениеЗаполнено(Параметр.Значение) Тогда
			Параметр.Значение = ВалютаУправленческогоУчета;
		КонецЕсли;
	КонецЕсли;
	
	СхемаКомпоновкиДанных.НаборыДанных.ОборотныеСредства.Запрос = ТекстЗапросаДинамикаОборотныхСредств();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Прочее

Функция НастройкаПараметра(ИмяПараметра)

	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки;
		Иначе
			Возврат ПараметрДанных;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

Функция ТекстЗапросаСебестоимостьТоваров()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	""Активы"" КАК ТипПоказателя,
	|	ВЫБОР КОГДА УчетСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) ТОГДА
	|		""Товары на складе""
	|	ИНАЧЕ
	|		""Товары, переданные на комиссию""
	|	КОНЕЦ КАК Показатель,
	|	
	|	УчетСебестоимости.Организация КАК Организация,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	NULL КАК Партнер,
	|	
	|	УчетСебестоимости.ПериодСекунда КАК ПериодСекунда,
	|	УчетСебестоимости.ПериодДень КАК ПериодДень,
	|	УчетСебестоимости.ПериодНеделя КАК ПериодНеделя,
	|	УчетСебестоимости.ПериодДекада КАК ПериодДекада,
	|	УчетСебестоимости.ПериодМесяц КАК ПериодМесяц,
	|	УчетСебестоимости.ПериодКвартал КАК ПериодКвартал,
	|	УчетСебестоимости.ПериодПолугодие КАК ПериодПолугодие,
	|	УчетСебестоимости.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА УчетСебестоимости.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		УчетСебестоимости.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	УчетСебестоимости.СтоимостьНачальныйОстаток
	|	+ УчетСебестоимости.СуммаДопРасходовНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	
	|	УчетСебестоимости.СтоимостьКонечныйОстаток
	|	+ УчетСебестоимости.СуммаДопРасходовКонечныйОстаток КАК СуммаКонечныйОстаток
	|	
	|ПОМЕСТИТЬ ОборотныеСредства	
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.ОстаткиИОбороты(, , Авто, , ) КАК УчетСебестоимости
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДенежныеСредства()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	""Активы"" КАК ТипПоказателя,
	|	""Денежные средства (наличные)"" КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.Касса.ВалютаДенежныхСредств КАК Валюта,
	|	NULL КАК Партнер,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ДенежныеСредства.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ДенежныеСредства.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Активы"" КАК ТипПоказателя,
	|	""Денежные средства (безналичные)"" КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.БанковскийСчет.ВалютаДенежныхСредств КАК Валюта,
	|	NULL КАК Партнер,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ДенежныеСредства.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ДенежныеСредства.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваБезналичные.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДенежныеСредстваУПодотчетныхЛиц()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ДенежныеСредства.СуммаНачальныйОстаток > 0 ТОГДА
	|		""Активы""
	|	ИНАЧЕ
	|		""Обязательства""
	|	КОНЕЦ КАК ТипПоказателя,
	|	""Денежные средства (у подотчетных лиц)"" КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.Валюта КАК Валюта,
	|	NULL КАК Партнер,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ДенежныеСредства.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	0 КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства	
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ДенежныеСредства.СуммаКонечныйОстаток > 0 ТОГДА
	|		""Активы""
	|	ИНАЧЕ
	|		""Обязательства""
	|	КОНЕЦ КАК ТипПоказателя,
	|	""Денежные средства (у подотчетных лиц)"" КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.Валюта КАК Валюта,
	|	NULL КАК Партнер,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	0 КАК СуммаНачальныйОстаток,
	|	ДенежныеСредства.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасходыБудущихПериодов()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	""Активы"" КАК ТипПоказателя,
	|	""Расходы будущих периодов"" КАК Показатель,
	|	
	|	ПрочиеРасходы.Организация КАК Организация,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	NULL КАК Партнер,
	|	
	|	ПрочиеРасходы.ПериодСекунда КАК ПериодСекунда,
	|	ПрочиеРасходы.ПериодДень КАК ПериодДень,
	|	ПрочиеРасходы.ПериодНеделя КАК ПериодНеделя,
	|	ПрочиеРасходы.ПериодДекада КАК ПериодДекада,
	|	ПрочиеРасходы.ПериодМесяц КАК ПериодМесяц,
	|	ПрочиеРасходы.ПериодКвартал КАК ПериодКвартал,
	|	ПрочиеРасходы.ПериодПолугодие КАК ПериодПолугодие,
	|	ПрочиеРасходы.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ПрочиеРасходы.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ПрочиеРасходы.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ПрочиеРасходы.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ПрочиеРасходы.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|	
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.ОстаткиИОбороты({&НачалоПериода},{&ПустаяДата}, Авто, Движения,
	|		СтатьяРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов)
	|	) КАК ПрочиеРасходы
	|	
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыМеждуОрганизациямиКлиенты()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА РасчетыСКлиентами.СуммаНачальныйОстаток > 0 ТОГДА
	|		""Активы""
	|	ИНАЧЕ
	|		""Обязательства""
	|	КОНЕЦ КАК ТипПоказателя,
	|	""Расчеты между организациями"" КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентами.Валюта КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Контрагент КАК Партнер,
	|	
	|	РасчетыСКлиентами.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСКлиентами.ПериодДень КАК ПериодДень,
	|	РасчетыСКлиентами.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСКлиентами.ПериодДекада КАК ПериодДекада,
	|	РасчетыСКлиентами.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСКлиентами.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСКлиентами.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСКлиентами.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСКлиентами.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	РасчетыСКлиентами.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	0 КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(,, Авто) КАК РасчетыСКлиентами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА РасчетыСКлиентами.СуммаКонечныйОстаток > 0 ТОГДА
	|		""Активы""
	|	ИНАЧЕ
	|		""Обязательства""
	|	КОНЕЦ КАК ТипПоказателя,
	|	""Расчеты между организациями"" КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентами.Валюта КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Контрагент КАК Партнер,
	|	
	|	РасчетыСКлиентами.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСКлиентами.ПериодДень КАК ПериодДень,
	|	РасчетыСКлиентами.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСКлиентами.ПериодДекада КАК ПериодДекада,
	|	РасчетыСКлиентами.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСКлиентами.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСКлиентами.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСКлиентами.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСКлиентами.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	0 КАК СуммаНачальныйОстаток,
	|	РасчетыСКлиентами.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(,, Авто) КАК РасчетыСКлиентами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыМеждуОрганизациямиПоставщики()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА РасчетыСПоставщиками.СуммаНачальныйОстаток > 0 ТОГДА
	|		""Активы""
	|	ИНАЧЕ
	|		""Обязательства""
	|	КОНЕЦ КАК ТипПоказателя,
	|	""Расчеты между организациями"" КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСПоставщиками.Валюта КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Контрагент КАК Партнер,
	|	
	|	РасчетыСПоставщиками.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСПоставщиками.ПериодДень КАК ПериодДень,
	|	РасчетыСПоставщиками.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСПоставщиками.ПериодДекада КАК ПериодДекада,
	|	РасчетыСПоставщиками.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСПоставщиками.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСПоставщиками.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСПоставщиками.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСПоставщиками.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	РасчетыСПоставщиками.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	0 КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.ОстаткиИОбороты(,, Авто) КАК РасчетыСПоставщиками
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
   	|ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА РасчетыСПоставщиками.СуммаКонечныйОстаток > 0 ТОГДА
	|		""Активы""
	|	ИНАЧЕ
	|		""Обязательства""
	|	КОНЕЦ КАК ТипПоказателя,
	|	""Расчеты между организациями"" КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСПоставщиками.Валюта КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Контрагент КАК Партнер,
	|	
	|	РасчетыСПоставщиками.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСПоставщиками.ПериодДень КАК ПериодДень,
	|	РасчетыСПоставщиками.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСПоставщиками.ПериодДекада КАК ПериодДекада,
	|	РасчетыСПоставщиками.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСПоставщиками.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСПоставщиками.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСПоставщиками.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСПоставщиками.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	0 КАК СуммаНачальныйОстаток,
	|	РасчетыСПоставщиками.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.ОстаткиИОбороты(,, Авто) КАК РасчетыСПоставщиками
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
   	|ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСКлиентами()
	
	ТекстЗапроса = "	
	|ВЫБРАТЬ
	|	""Активы"" КАК ТипПоказателя,
	|	""Дебиторская задолженность"" КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентами.Валюта КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	
	|	РасчетыСКлиентами.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСКлиентами.ПериодДень КАК ПериодДень,
	|	РасчетыСКлиентами.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСКлиентами.ПериодДекада КАК ПериодДекада,
	|	РасчетыСКлиентами.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСКлиентами.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСКлиентами.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСКлиентами.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСКлиентами.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.СуммаНачальныйОстаток > 0
	|		И (РасчетыСКлиентами.СуммаНачальныйОстаток - РасчетыСКлиентами.КОплатеНачальныйОстаток) > 0
	|	ТОГДА
	|		РасчетыСКлиентами.СуммаНачальныйОстаток - РасчетыСКлиентами.КОплатеНачальныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	
	|	ВЫБОР КОГДА РасчетыСКлиентами.СуммаКонечныйОстаток > 0
	|		И (РасчетыСКлиентами.СуммаКонечныйОстаток - РасчетыСКлиентами.КОплатеКонечныйОстаток) > 0
	|	ТОГДА
	|		РасчетыСКлиентами.СуммаКонечныйОстаток - РасчетыСКлиентами.КОплатеКонечныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(,, Авто) КАК РасчетыСКлиентами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Активы"" КАК ТипПоказателя,
	|	""Дебиторская задолженность (просроченная)"" КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентами.Валюта КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	
	|	РасчетыСКлиентами.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСКлиентами.ПериодДень КАК ПериодДень,
	|	РасчетыСКлиентами.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСКлиентами.ПериодДекада КАК ПериодДекада,
	|	РасчетыСКлиентами.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСКлиентами.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСКлиентами.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСКлиентами.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСКлиентами.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.КОплатеНачальныйОстаток > 0 ТОГДА
	|		ВЫБОР КОГДА РасчетыСКлиентами.СуммаНачальныйОстаток > РасчетыСКлиентами.КОплатеНачальныйОстаток ТОГДА
	|			РасчетыСКлиентами.КОплатеНачальныйОстаток
	|		ИНАЧЕ
	|			РасчетыСКлиентами.СуммаНачальныйОстаток
	|		КОНЕЦ
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА РасчетыСКлиентами.КОплатеКонечныйОстаток > 0 ТОГДА
	|		ВЫБОР КОГДА РасчетыСКлиентами.СуммаКонечныйОстаток > РасчетыСКлиентами.КОплатеКонечныйОстаток ТОГДА
	|			РасчетыСКлиентами.КОплатеКонечныйОстаток
	|		ИНАЧЕ
	|			РасчетыСКлиентами.СуммаКонечныйОстаток
	|		КОНЕЦ
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(,, Авто) КАК РасчетыСКлиентами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Обязательства"" КАК ТипПоказателя,
	|	""Полученные авансы"" КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентами.Валюта КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	
	|	РасчетыСКлиентами.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСКлиентами.ПериодДень КАК ПериодДень,
	|	РасчетыСКлиентами.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСКлиентами.ПериодДекада КАК ПериодДекада,
	|	РасчетыСКлиентами.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСКлиентами.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСКлиентами.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСКлиентами.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСКлиентами.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.СуммаНачальныйОстаток < 0 ТОГДА
	|		РасчетыСКлиентами.СуммаНачальныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.СуммаКонечныйОстаток < 0 ТОГДА
	|		РасчетыСКлиентами.СуммаКонечныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(,, Авто) КАК РасчетыСКлиентами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСПоставщиками()
	
	ТекстЗапроса = "	
	|ВЫБРАТЬ
	|	""Обязательства"" КАК ТипПоказателя,
	|	""Кредиторская задолженность"" КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСПоставщиками.Валюта КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	
	|	РасчетыСПоставщиками.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСПоставщиками.ПериодДень КАК ПериодДень,
	|	РасчетыСПоставщиками.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСПоставщиками.ПериодДекада КАК ПериодДекада,
	|	РасчетыСПоставщиками.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСПоставщиками.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСПоставщиками.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСПоставщиками.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСПоставщиками.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА РасчетыСПоставщиками.СуммаНачальныйОстаток < 0
	|		И (РасчетыСПоставщиками.СуммаНачальныйОстаток - РасчетыСПоставщиками.КОплатеНачальныйОстаток) < 0
	|	ТОГДА
	|		РасчетыСПоставщиками.СуммаНачальныйОстаток - РасчетыСПоставщиками.КОплатеНачальныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА РасчетыСПоставщиками.СуммаКонечныйОстаток < 0
	|		И (РасчетыСПоставщиками.СуммаКонечныйОстаток - РасчетыСПоставщиками.КОплатеКонечныйОстаток) < 0
	|	ТОГДА
	|		РасчетыСПоставщиками.СуммаКонечныйОстаток - РасчетыСПоставщиками.КОплатеКонечныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.ОстаткиИОбороты(,, Авто) КАК РасчетыСПоставщиками
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
    |ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Обязательства"" КАК ТипПоказателя,
	|	""Кредиторская задолженность (просроченная)"" КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСПоставщиками.Валюта КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	
	|	РасчетыСПоставщиками.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСПоставщиками.ПериодДень КАК ПериодДень,
	|	РасчетыСПоставщиками.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСПоставщиками.ПериодДекада КАК ПериодДекада,
	|	РасчетыСПоставщиками.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСПоставщиками.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСПоставщиками.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСПоставщиками.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСПоставщиками.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА РасчетыСПоставщиками.КОплатеНачальныйОстаток < 0 ТОГДА
	|		ВЫБОР КОГДА (-РасчетыСПоставщиками.СуммаНачальныйОстаток) > (-РасчетыСПоставщиками.КОплатеНачальныйОстаток) ТОГДА
	|			РасчетыСПоставщиками.КОплатеНачальныйОстаток
	|		ИНАЧЕ
	|			РасчетыСПоставщиками.СуммаНачальныйОстаток
	|		КОНЕЦ
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА РасчетыСПоставщиками.КОплатеКонечныйОстаток < 0 ТОГДА
	|		ВЫБОР КОГДА (-РасчетыСПоставщиками.СуммаКонечныйОстаток) > (-РасчетыСПоставщиками.КОплатеКонечныйОстаток) ТОГДА
	|			РасчетыСПоставщиками.КОплатеКонечныйОстаток
	|		ИНАЧЕ
	|			РасчетыСПоставщиками.СуммаКонечныйОстаток
	|		КОНЕЦ
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.ОстаткиИОбороты(,, Авто) КАК РасчетыСПоставщиками
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
    |ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Активы"" КАК ТипПоказателя,
	|	""Выданные авансы"" КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСПоставщиками.Валюта КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	
	|	РасчетыСПоставщиками.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСПоставщиками.ПериодДень КАК ПериодДень,
	|	РасчетыСПоставщиками.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСПоставщиками.ПериодДекада КАК ПериодДекада,
	|	РасчетыСПоставщиками.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСПоставщиками.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСПоставщиками.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСПоставщиками.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСПоставщиками.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА РасчетыСПоставщиками.КОплатеНачальныйОстаток > 0 ТОГДА
	|		РасчетыСПоставщиками.СуммаНачальныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА РасчетыСПоставщиками.КОплатеКонечныйОстаток > 0 ТОГДА
	|		РасчетыСПоставщиками.СуммаКонечныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.ОстаткиИОбороты(,, Авто) КАК РасчетыСПоставщиками
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
    |ГДЕ
	|	РегистрАналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДинамикаОборотныхСредств()
	
	ТекстЗапроса = ТекстЗапросаСебестоимостьТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаДенежныеСредства()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаДенежныеСредстваУПодотчетныхЛиц()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаРасходыБудущихПериодов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаРасчетыМеждуОрганизациямиКлиенты()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаРасчетыМеждуОрганизациямиПоставщики()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаРасчетыСКлиентами()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаРасчетыСПоставщиками()
	;
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КурсВалюты.Валюта КАК Валюта,
	|
	|	(КурсВалюты.Курс * КурсВалютыОтчета.Кратность)
	|   / (КурсВалюты.Кратность * КурсВалютыОтчета.Курс) КАК КоэффициентПересчета
	|
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних({&КонецПериода}, ) КАК КурсВалюты
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.КурсыВалют.СрезПоследних({&КонецПериода}, 
	|			Валюта = &ВалютаОтчета
	|		) КАК КурсВалютыОтчета
	|	ПО
	|		Истина
	|
	|ГДЕ
	|	КурсВалюты.Кратность <> 0
	|	И КурсВалютыОтчета.Курс <> 0
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Активы минус обязательства"" КАК АктивыМинусОбязательства,
	|	ОборотныеСредства.ТипПоказателя КАК ТипПоказателя,
	|	ОборотныеСредства.Показатель КАК Показатель,
	|	ОборотныеСредства.Организация,
	|	ОборотныеСредства.Валюта КАК Валюта,
	|	ОборотныеСредства.Партнер КАК Партнер,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
	|		ОборотныеСредства.Валюта
	|	ИНАЧЕ
	|		&ВалютаОтчета
	|	КОНЕЦ КАК ВалютаОтчета,
	|	
	|	ОборотныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ОборотныеСредства.ПериодДень КАК ПериодДень,
	|	ОборотныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ОборотныеСредства.ПериодДекада КАК ПериодДекада,
	|	ОборотныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ОборотныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ОборотныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ОборотныеСредства.ПериодГод КАК ПериодГод,
	|	ОборотныеСредства.Регистратор КАК Регистратор,
	|	
	|	СУММА(ОборотныеСредства.СуммаНачальныйОстаток) КАК СуммаНачальныйОстаток,
	|	СУММА(ОборотныеСредства.СуммаКонечныйОстаток) КАК СуммаКонечныйОстаток,
	|	СУММА(ОборотныеСредства.СуммаКонечныйОстаток - ОборотныеСредства.СуммаНачальныйОстаток) КАК Изменение,
	|	
	|	СУММА(ОборотныеСредства.СуммаНачальныйОстаток
	|		* ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 1)
	|	) КАК СуммаНачальныйОстатокВВалюте,
	|	СУММА(ОборотныеСредства.СуммаКонечныйОстаток
	|		* ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 1)
	|	) КАК СуммаКонечныйОстатокВВалюте,
	|	СУММА((ОборотныеСредства.СуммаКонечныйОстаток - ОборотныеСредства.СуммаНачальныйОстаток)
	|		* ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 1)
	|	) КАК ИзменениеВВалюте
	|ИЗ
	|	ОборотныеСредства КАК ОборотныеСредства
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсыВалют
	|	ПО
	|		ОборотныеСредства.Валюта = КурсыВалют.Валюта
	|ГДЕ
	|	Не ОборотныеСредства.Валюта ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотныеСредства.ТипПоказателя,
	|	ОборотныеСредства.Показатель,
	|	ОборотныеСредства.Организация,
	|	ОборотныеСредства.Валюта,
	|	ОборотныеСредства.Партнер,
	|	ВЫБОР КОГДА &ВалютаОтчета = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
	|		ОборотныеСредства.Валюта
	|	ИНАЧЕ
	|		&ВалютаОтчета
	|	КОНЕЦ,
	|	
	|	ОборотныеСредства.ПериодСекунда,
	|	ОборотныеСредства.ПериодДень,
	|	ОборотныеСредства.ПериодНеделя,
	|	ОборотныеСредства.ПериодДекада,
	|	ОборотныеСредства.ПериодМесяц,
	|	ОборотныеСредства.ПериодКвартал,
	|	ОборотныеСредства.ПериодПолугодие,
	|	ОборотныеСредства.ПериодГод,
	|	ОборотныеСредства.Регистратор
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ОборотныеСредства.СуммаНачальныйОстаток) <> 0
	|	ИЛИ СУММА(ОборотныеСредства.СуммаКонечныйОстаток) <> 0
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецЕсли