#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	// Если отчет открывается не из документа - нужно показывать только типовые соглашения
	
	ПараметрЗаполнен = Ложь;
	
	ПараметрСоглашение = ОбщегоНазначенияУТКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек.ФиксированныеНастройки, "Документ");
	ПараметрЗаполнен   = ПараметрЗаполнен(ПараметрСоглашение);
	
	ПараметрСоглашение = ОбщегоНазначенияУТКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек.Настройки, "Документ");
	ПараметрЗаполнен   = ПараметрЗаполнен(ПараметрСоглашение) Или ПараметрЗаполнен;
	
	ПараметрПартнер  = ОбщегоНазначенияУТКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек.ФиксированныеНастройки, "Партнер");
	ПараметрЗаполнен = ПараметрЗаполнен(ПараметрПартнер) Или ПараметрЗаполнен;
	
	ПараметрПартнер  = ОбщегоНазначенияУТКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек.Настройки, "Партнер");
	ПараметрЗаполнен = ПараметрЗаполнен(ПараметрПартнер) Или ПараметрЗаполнен;
	
	Если Не ПараметрЗаполнен Тогда
		ПараметрТолькоТиповые = ОбщегоНазначенияУТКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек.Настройки, "ТолькоТиповые");
		Если ПараметрТолькоТиповые <> Неопределено Тогда
			ПараметрТолькоТиповые.Значение      = Истина;
			ПараметрТолькоТиповые.Использование = Истина;
		КонецЕсли;
	КонецЕсли;
	
	СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(КомпоновщикНастроек);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Осуществляет проверку заполненности параметра компоновки
//
// Параметры:
//	Параметр - ЗначениеПараметраНастроекКомпоновкиДанных - параметр, заполненность которого необходимо проверить
//
// Возвращаемое значение:
//	Булево - Истина, если параметр заполнен, Ложь, в противном случае
//
Функция ПараметрЗаполнен(Параметр)
	
	Если Параметр <> Неопределено Тогда
		
		ЗначениеПараметра = Параметр.Значение;
		ТипПараметра      = ТипЗнч(ЗначениеПараметра);
		ПараметрЗаполнен  = ЗначениеЗаполнено(ЗначениеПараметра);
		
		Если ТипПараметра = Тип("СписокЗначений") Тогда
			Если ЗначениеПараметра.Количество() = 0 Тогда
				ПараметрЗаполнен = Ложь;
			Иначе
				ПараметрЗаполнен = Истина;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		ПараметрЗаполнен = Ложь;
		
	КонецЕсли;
	
	Возврат ПараметрЗаполнен;

КонецФункции

#КонецЕсли