////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеНастроекМонитораПоказателей" Тогда
		
		ОбновлениеСтруктурыНастроекСервер();
		
		ИнициализироватьОбработчикАвтообновления(СтруктураНастроек.ПериодАвтообновления * 60);
		
		ОбновитьСоставМонитораКлиент();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске().НеобходимоОбновлениеИнформационнойБазы Тогда
		ИнициализироватьОбработчикАвтообновления();
		
		Состояние(НСтр("ru='Пожалуйста подождите, идет пересчет показателей...'"));
		ИнициализироватьОбработчикПервогоЗапуска();
		Состояние();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураНастроек = МониторингЦелевыхПоказателей.ПолучитьНастройкиМонитораЦелевыхПоказателей();
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ЗаполнитьСписокВыбораОтбораПоСтатусу();
	
	Если НЕ ПустаяСтрока(Параметры.ИдентификаторВариантаАнализа) Тогда
		РежимАнализаВзаимосвязанных = Истина;
		Элементы.ФормаНастроить.Видимость = НЕ РежимАнализаВзаимосвязанных;
		
		Заголовок = "Анализ взаимосвязанных целевых показателей";
		АвтоЗаголовок = НЕ РежимАнализаВзаимосвязанных;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОтборСтатусПриИзменении(Элемент)
	
	ОбновитьСоставМонитораКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьАнализПоказателяНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИдентификаторВариантаАнализа = ПолучитьИдентификаторЭлементаПоИмениБезПозиции(Элемент.Имя);
	
	ВариантАнализа = ПолучитьСсылкуСправочникаПоИдентификатору("ВариантыАнализаЦелевыхПоказателей", ИдентификаторВариантаАнализа);
	
	Отбор = Новый Структура("ВариантАнализа", ВариантАнализа);
	НайденныеСтроки = СоставМонитора.НайтиСтроки(Отбор);
	
	ВариантыАнализаДляПечати.Очистить();
	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		НовыйВариант = ВариантыАнализаДляПечати.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйВариант, НайденнаяСтрока);
		
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("ВариантыАнализа", ВариантыАнализаДляПечати);
	
	ОткрытьФорму("Отчет.МониторЦелевыхПоказателей.Форма.ПечатнаяФормаВариантаАнализа", 
	ПараметрыФормы, 
	ЭтаФорма, 
	Новый УникальныйИдентификатор());
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Настроить(Команда)
	
	ПараметрыФормы = Новый Структура("ТекущийПользователь", ТекущийПользователь);
	ОткрытьФорму("Отчет.МониторЦелевыхПоказателей.Форма.ПерсональныеНастройки", 
	ПараметрыФормы, ЭтаФорма, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ИнициализироватьОбработчикАвтообновления();
	
	ОбновитьСоставМонитораКлиент(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АнализВзаимосвязанныхВариантовАнализа(Команда)
	
	ИмяКоманды = Команда.Имя;
	
	ИдентификаторТекущегоЭлемента = ПолучитьИдентификаторЭлементаПоИмениСПозицией(ИмяКоманды);
	
	ИдентификаторВариантаАнализа = ПолучитьИдентификаторЭлементаПоИмениБезПозиции(ИмяКоманды);
	ЧистоеИмяКоманды = ПолучитьЧистоеИмяКоманды(ИмяКоманды);
	
	ПараметрыФормы = Новый Структура("ИдентификаторВариантаАнализа, 
									|ВидПоиска, 
									|ОбновлятьДанныеПринудительно,
									|СоставМонитора", 
									ИдентификаторВариантаАнализа,
									ЧистоеИмяКоманды,
									Истина,
									СоставМонитора);
	ОткрытьФорму("Отчет.МониторЦелевыхПоказателей.Форма.ФормаМониторЦелевыхПоказателей", ПараметрыФормы, ЭтаФорма, ИдентификаторВариантаАнализа);
	
КонецПроцедуры

&НаКлиенте 
Процедура Подключаемый_ИзменитьОбъектАнализаДиаграммы(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ Элементы[Команда.Имя].Пометка Тогда
		ИдентификаторПоказателя = ПолучитьИдентификаторЭлементаПоИмениБезПозиции(Команда.Имя);
		
		ВариантАнализа = ПолучитьСсылкуСправочникаПоИдентификатору("ВариантыАнализаЦелевыхПоказателей", ИдентификаторПоказателя);
		
		ИдентификаторОбъектаАнализа = ПолучитьИдентификаторДинамическогоПараметра(Команда.Имя);
		
		ПараметрыИсточникаДанных = Новый Структура("ОбъектАнализа", ИдентификаторОбъектаАнализа);
		
		// Поместим динамические параметры во временное хранилище
		ОтборПоВариантуАнализа = Новый Структура("ВариантАнализа", ВариантАнализа);
		НайденныеСоставМонитора = СоставМонитора.НайтиСтроки(ОтборПоВариантуАнализа);
		Если НайденныеСоставМонитора.Количество() > 0 Тогда
			НайденныеСоставМонитора[0].АдресДинамическихПараметров = ПоместитьВоВременноеХранилище(ПараметрыИсточникаДанных, Новый УникальныйИдентификатор(СтрЗаменить(ИдентификаторПоказателя,"_","-"))); 
		Иначе
			НовыйАдрес = СоставМонитора.Добавить();
			НовыйАдрес.ВариантАнализа = ВариантАнализа;
			НовыйАдрес.АдресДинамическихПараметров = ПоместитьВоВременноеХранилище(ПараметрыИсточникаДанных, Новый УникальныйИдентификатор(СтрЗаменить(ИдентификаторПоказателя,"_","-"))); 
		КонецЕсли;
		
		ИзменитьДинамическийПараметрДляДиаграммы(ВариантАнализа, ПараметрыИсточникаДанных, ИдентификаторПоказателя);
		
		// Изменим пометки кнопок
		Элементы[Команда.Имя].Пометка = Истина;
		КнопкиОбъектовАнализа = Элементы["ОбъектыАнализа__" + ИдентификаторПоказателя].ПодчиненныеЭлементы;
		
		Для Каждого КнопкаОбъектаАнализа Из КнопкиОбъектовАнализа Цикл 
			Если НЕ КнопкаОбъектаАнализа = Элементы[Команда.Имя] Тогда
				КнопкаОбъектаАнализа.Пометка = Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Процедура Подключаемый_ИзменитьОбъектАнализаТаблицы(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ Элементы[Команда.Имя].Пометка Тогда
		ИдентификаторПоказателя = ПолучитьИдентификаторЭлементаПоИмениБезПозиции(Команда.Имя);
		
		ВариантАнализа = ПолучитьСсылкуСправочникаПоИдентификатору("ВариантыАнализаЦелевыхПоказателей", ИдентификаторПоказателя);
		
		ИдентификаторОбъектаАнализа = ПолучитьИдентификаторДинамическогоПараметра(Команда.Имя);
		
		ПараметрыИсточникаДанных = Новый Структура("ОбъектАнализа", ИдентификаторОбъектаАнализа);
		
		// Поместим динамические параметры во временное хранилище
		ОтборПоВариантуАнализа = Новый Структура("ВариантАнализа", ВариантАнализа);
		НайденныеСоставМонитора = СоставМонитора.НайтиСтроки(ОтборПоВариантуАнализа);
		Если НайденныеСоставМонитора.Количество() > 0 Тогда
			НайденныеСоставМонитора[0].АдресДинамическихПараметров = ПоместитьВоВременноеХранилище(ПараметрыИсточникаДанных, Новый УникальныйИдентификатор(СтрЗаменить(ИдентификаторПоказателя,"_","-"))); 
		Иначе
			НовыйАдрес = СоставМонитора.Добавить();
			НовыйАдрес.ВариантАнализа = ВариантАнализа;
			НовыйАдрес.АдресДинамическихПараметров = ПоместитьВоВременноеХранилище(ПараметрыИсточникаДанных, Новый УникальныйИдентификатор(СтрЗаменить(ИдентификаторПоказателя,"_","-"))); 
		КонецЕсли;
		
		ИзменитьДинамическийПараметрДляТаблицы(ВариантАнализа, ПараметрыИсточникаДанных, ИдентификаторПоказателя);
		
		// Изменим пометки кнопок
		Элементы[Команда.Имя].Пометка = Истина;
		КнопкиОбъектовАнализа = Элементы["ОбъектыАнализа__" + ИдентификаторПоказателя].ПодчиненныеЭлементы;
		
		Для Каждого КнопкаОбъектаАнализа Из КнопкиОбъектовАнализа Цикл 
			Если НЕ КнопкаОбъектаАнализа = Элементы[Команда.Имя] Тогда
				КнопкаОбъектаАнализа.Пометка = Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Процедура Подключаемый_ИзменитьПериодичность(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ Элементы[Команда.Имя].Пометка Тогда
		ИдентификаторПоказателя = ПолучитьИдентификаторЭлементаПоИмениБезПозиции(Команда.Имя);
		
		ВариантАнализа = ПолучитьСсылкуСправочникаПоИдентификатору("ВариантыАнализаЦелевыхПоказателей", ИдентификаторПоказателя);
		
		ИдентификаторПериодичности = ПолучитьИдентификаторДинамическогоПараметра(Команда.Имя);
		
		ПараметрыИсточникаДанных = Новый Структура("ПериодичностьКонтроля", ПолучитьСсылкуПеречисленияПоИдентификатору("Периодичность", ИдентификаторПериодичности));
		
		// Поместим динамические параметры во временное хранилище
		ОтборПоВариантуАнализа = Новый Структура("ВариантАнализа", ВариантАнализа);
		НайденныеСоставМонитора = СоставМонитора.НайтиСтроки(ОтборПоВариантуАнализа);
		Если НайденныеСоставМонитора.Количество() > 0 Тогда
			НайденныеСоставМонитора[0].АдресДинамическихПараметров = ПоместитьВоВременноеХранилище(ПараметрыИсточникаДанных, Новый УникальныйИдентификатор(СтрЗаменить(ИдентификаторПоказателя,"_","-"))); 
		Иначе
			НовыйАдрес = СоставМонитора.Добавить();
			НовыйАдрес.ВариантАнализа = ВариантАнализа;
			НовыйАдрес.АдресДинамическихПараметров = ПоместитьВоВременноеХранилище(ПараметрыИсточникаДанных, Новый УникальныйИдентификатор(СтрЗаменить(ИдентификаторПоказателя,"_","-"))); 
		КонецЕсли;
		
		ИзменитьДинамическийПараметрДляДиаграммы(ВариантАнализа, ПараметрыИсточникаДанных, ИдентификаторПоказателя);
		
		// Изменим пометки кнопок
		Элементы[Команда.Имя].Пометка = Истина;
		КнопкиОбъектовАнализа = Элементы["Периодичность__" + ИдентификаторПоказателя].ПодчиненныеЭлементы;
		
		Для Каждого КнопкаОбъектаАнализа Из КнопкиОбъектовАнализа Цикл 
			Если НЕ КнопкаОбъектаАнализа = Элементы[Команда.Имя] Тогда
				КнопкаОбъектаАнализа.Пометка = Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьАнализПоказателя(Команда)
	
	ИдентификаторПоказателя = ПолучитьИдентификаторЭлементаПоИмениБезПозиции(Команда.Имя);
	
	ВариантАнализа = ПолучитьСсылкуСправочникаПоИдентификатору("ВариантыАнализаЦелевыхПоказателей", ИдентификаторПоказателя);
	
	Отбор = Новый Структура("ВариантАнализа", ВариантАнализа);
	НайденныеСтроки = СоставМонитора.НайтиСтроки(Отбор);
	
	ВариантыАнализаДляПечати.Очистить();
	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		НовыйВариант = ВариантыАнализаДляПечати.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйВариант, НайденнаяСтрока);
		
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("ВариантыАнализа", ВариантыАнализаДляПечати);
	
	ОткрытьФорму("Отчет.МониторЦелевыхПоказателей.Форма.ПечатнаяФормаВариантаАнализа", 
	ПараметрыФормы, 
	ЭтаФорма, 
	Новый УникальныйИдентификатор());
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьОтчет(Команда)
	
	ИдентификаторВариантаАнализа = ПолучитьИдентификаторЭлементаПоИмениБезПозиции(Команда.Имя);
	СвойстваВариантаАнализа = ПолучитьСвойстваВариантаАнализаПоИдентификатору(ИдентификаторВариантаАнализа);
	
	ИдентификаторВариантаОтчета = ПолучитьИдентификаторВариантаОтчета(Команда.Имя);
	ВариантОтчета = ПолучитьВариантОтчетаПоИдентификатору(ИдентификаторВариантаОтчета);
	СвойстваВариантаОтчета = ПолучитьСвойстваВариантаОтчета(ВариантОтчета);
	
	НастройкиВариантаОтчета = МониторингЦелевыхПоказателейВызовСервера.СформироватьНастройкиВариантаОтчета(СвойстваВариантаАнализа, СвойстваВариантаОтчета);
	
	Если НастройкиВариантаОтчета = Неопределено Тогда
		Предупреждение(НСтр("ru= 'Выбранный отчет не найден в системе'")); 
	Иначе
		ПараметрыОтчета = Новый Структура("КлючПользовательскихНастроек,
										   |ПользовательскиеНастройки, 
										   |ФиксированныеНастройки, 
										   |КлючВарианта, 
										   |КлючНазначенияИспользования, 
										   |СформироватьПриОткрытии",
										   Команда.Имя,
										   НастройкиВариантаОтчета.ПользовательскиеНастройки,
										   НастройкиВариантаОтчета.ФиксированныеНастройки,
										   СвойстваВариантаОтчета.КлючВарианта,
										   Команда.Имя,
										   Истина);
		
		ОткрытьФорму(СвойстваВариантаОтчета.КлючОбъекта + ".Форма", ПараметрыОтчета, СвойстваВариантаАнализа.Ссылка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СводныйОтчет(Команда)
	
	Если СоставМонитораНеопределен Тогда
		Предупреждение(НСтр("ru='Сформировать сводный отчет невозможно, т.к. монитор показателей не настроен.'"),, НСтр("ru='Ошибка формирования сводного отчета'"));
		
	ИначеЕсли СоставМонитораПустойПоОтбору Тогда
		Предупреждение(НСтр("ru='Сформировать сводный отчет невозможно, т.к. в текущий отбор не входит ни один показатель.'"),, НСтр("ru='Ошибка формирования сводного отчета'"));
		
	Иначе 
		
		ВариантыАнализаДляПечати.Очистить();
		
		Для Каждого ЭлементМонитора Из СоставМонитора Цикл 
			НовыйВариант = ВариантыАнализаДляПечати.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйВариант, ЭлементМонитора);
			
		КонецЦикла;
		
		ПараметрыФормы = Новый Структура("ВариантыАнализа", ВариантыАнализаДляПечати);
		
		ОткрытьФорму("Отчет.МониторЦелевыхПоказателей.Форма.ПечатнаяФормаВариантаАнализа", 
		ПараметрыФормы, 
		ЭтаФорма, 
		Новый УникальныйИдентификатор());
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Создание элементов формы

&НаСервере
Функция ВертикальнаяГруппа(Имя, Заголовок, РодительскаяГруппа = Неопределено)
	
	ГруппаСтрокаЭлементов = Элементы.Добавить(Имя, Тип("ГруппаФормы"), РодительскаяГруппа);
	ГруппаСтрокаЭлементов.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСтрокаЭлементов.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаСтрокаЭлементов.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСтрокаЭлементов.Заголовок = Заголовок;
	ГруппаСтрокаЭлементов.РазрешитьИзменениеСостава = Ложь;
	ГруппаСтрокаЭлементов.ОтображатьЗаголовок = Ложь;
	
	Возврат ГруппаСтрокаЭлементов;
	
КонецФункции

&НаСервере
Функция ГоризонтальнаяГруппа(Имя, Заголовок, РодительскаяГруппа = Неопределено)
	
	ГруппаСтрокаЭлементов = Элементы.Добавить(Имя, Тип("ГруппаФормы"), РодительскаяГруппа);
	ГруппаСтрокаЭлементов.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСтрокаЭлементов.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСтрокаЭлементов.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаСтрокаЭлементов.Заголовок = Заголовок;
	ГруппаСтрокаЭлементов.ОтображатьЗаголовок = Ложь;
	
	Возврат ГруппаСтрокаЭлементов;
	
КонецФункции

// Добавляет вертикальные группы элементов формы, в которых 
// затем будут будут размещены элементы формы показателей
// 
// Параметры:
//	РодительскаяГруппа - ГруппаФормы
//	ВариантОтображенияПоказателейСтроки - ПеречислениеСсылка.ВариантыОтображенияВариантовАнализа, Неопределено - указывает на краткое отображение или в виде диаграммы/таблицы
//	КлючУникальностиГруппы - Строка - уникальный идентификатор группы, основанный на имени зоны внимания
//
&НаСервере 
Процедура ДобавитьГруппыКолонкиЭлементов(РодительскаяГруппа, ВариантОтображенияПоказателейСтроки = Неопределено, КлючУникальностиГруппы)
	
	ВариантыОтображенияВариантовАнализа = Перечисления.ВариантыОтображенияВариантовАнализа;
	
	Если ВариантОтображенияПоказателейСтроки = ВариантыОтображенияВариантовАнализа.Кратко Тогда
		Для Сч = 1 По СтруктураНастроек.КоличествоКолонокМонитораПоказателей Цикл 
			ГруппаКолонкаЭлементов = ВертикальнаяГруппа("ГруппаКолонкаЭлементовКратко" + КлючУникальностиГруппы + "__" + Сч, НСтр("ru='Колонка №'") + Сч, РодительскаяГруппа);//ГруппаВариантаОтображения); 
			
		КонецЦикла;
		
	Иначе
		Если Элементы.Найти("ГруппаЭлементовПодробно" + КлючУникальностиГруппы) = Неопределено Тогда
			Для Сч = 1 По СтруктураНастроек.КоличествоКолонокМонитораПоказателей Цикл 
				
				ГруппаКолонкаЭлементов = ВертикальнаяГруппа("ГруппаКолонкаПодробныхЭлементов" + КлючУникальностиГруппы + "__" + Сч, НСтр("ru='Колонка №'") + Сч, РодительскаяГруппа);//ГруппаВариантаОтображения); 
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет команды, доступные для указанного варианта анализа.
// Возможные команды: "Влияющие показатели", "Зависимые показатели", "Периодичность", "Объект анализа", "Отчеты", "Анализ показателя"
//
// Параметры:
//	ВариантАнализа - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей - вариант, для которого добавляются команды
//	ГруппаЭлементов - ГруппаФормы - командная панель, в которую нужно добавить кнопки
//	ИдентификаторЭлементаФормы - Строка, содержащая уникальный идентификатор элемента формы
//	ВариантОтображения - ПеречислениеСсылка.ВариантыОтображенияВариантовАнализа - указывает на краткое отображение или в виде диаграммы/таблицы
//
&НаСервере
Процедура ДобавитьКомандыВариантовАнализа(ВариантАнализа, ГруппаЭлементов, ИдентификаторЭлементаФормы, ВариантОтображения)
	
	ВариантыОтображения = Перечисления.ВариантыОтображенияВариантовАнализа;
	
	ПараметрыДоступностиАнализаВзаимосвязанных = МониторингЦелевыхПоказателей.ПолучитьПараметрыДоступностиАнализаВзаимосвязанных(ВариантАнализа);
	
	ДобавлятьПодменюОбъектовАнализа = (ВариантАнализа.ТипАнализа = Перечисления.ТипыАнализаПоказателей.ПокомпонентноеСравнение);
	ДобавлятьПодменюПериодичности = (ВариантАнализа.ТипАнализа = Перечисления.ТипыАнализаПоказателей.ДинамикаИзменения);
	
	Если ВариантОтображения = ВариантыОтображения.Диаграмма 
		ИЛИ ВариантОтображения = ВариантыОтображения.Таблица Тогда
		
		КоманднаяПанельКомандПоказателя = Элементы.Добавить("КоманднаяПанельКомандПоказателя__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), Элементы["Статусы__" + ИдентификаторЭлементаФормы]);
		КоманднаяПанельКомандПоказателя.Вид = ВидГруппыФормы.КоманднаяПанель;
		КоманднаяПанельКомандПоказателя.Заголовок = НСтр("ru='Команды показателя'");
		КоманднаяПанельКомандПоказателя.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
		
		// Добавим подменю "Все действия"
		ПодменюВсеДействия = Элементы.Добавить("ВсеДействия__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), КоманднаяПанельКомандПоказателя);
		ПодменюВсеДействия.Вид = ВидГруппыФормы.Подменю;
		ПодменюВсеДействия.Картинка = БиблиотекаКартинок.ПиктограммаДеталиВариантаАнализа;
		ПодменюВсеДействия.Отображение = ОтображениеКнопки.Картинка;
		ПодменюВсеДействия.Заголовок = НСтр("ru='Все действия'");
		
		// Добавим реквизиты формы
		
		Если НЕ (ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеЗависимые ИЛИ ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеВлияющие) Тогда	
			ПустаяКоманда = Команды.Добавить("ПустаяКоманда__" + ИдентификаторЭлементаФормы);
			ПустаяКоманда.Заголовок = НСтр("ru='Взаимосвязей нет'");
			ПустаяКоманда.Картинка = БиблиотекаКартинок.ПиктограммаВсеПоказатели;
			
		КонецЕсли;
		
		Если ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеЗависимые Тогда	
			КомандаПоискаСвязи = Команды.Добавить("ЗависимыеПоказатели__" + ИдентификаторЭлементаФормы);
			КомандаПоискаСвязи.Заголовок = НСтр("ru='Зависимые показатели'");
			КомандаПоискаСвязи.Действие = "Подключаемый_АнализВзаимосвязанныхВариантовАнализа";
			КомандаПоискаСвязи.Картинка = БиблиотекаКартинок.ПиктограммаЗависимыеПоказатели;
			
		КонецЕсли;	
		
		Если ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеВлияющие Тогда	
			КомандаПоискаСвязи = Команды.Добавить("ВлияющиеПоказатели__" + ИдентификаторЭлементаФормы);
			КомандаПоискаСвязи.Заголовок = НСтр("ru='Влияющие показатели'");
			КомандаПоискаСвязи.Действие = "Подключаемый_АнализВзаимосвязанныхВариантовАнализа";
			КомандаПоискаСвязи.Картинка = БиблиотекаКартинок.ПиктограммаВлияющиеПоказатели;
			
		КонецЕсли;
		
		КомандаАнализПоказателя = Команды.Добавить("ОткрытьАнализПоказателя__" + ИдентификаторЭлементаФормы);
		КомандаАнализПоказателя.Заголовок = НСтр("ru='Анализ показателя'");
		КомандаАнализПоказателя.Действие = "Подключаемый_ОткрытьАнализПоказателя";
		КомандаАнализПоказателя.Картинка = БиблиотекаКартинок.ПиктограммаАналитическойФормыОтчета;
		
		// Добавим меню по объектам анализа
		ДоступныеОбъектыАнализа = Справочники.СтруктураЦелей.ДоступныеОбъектыАнализа(ВариантАнализа.Владелец);
		ОбъектАнализаПоУмолчанию = ВариантАнализа.ОбъектАнализа.Получить();
		
		Если ДобавлятьПодменюОбъектовАнализа И ДоступныеОбъектыАнализа.Количество() > 1 Тогда
			Для Каждого ОбъектАнализа Из ДоступныеОбъектыАнализа Цикл 
				КомандаИзмененияОбъектаАнализа = Команды.Добавить("ИзменитьОбъектАнализа__" + ИдентификаторЭлементаФормы + "__" + Строка(ОбъектАнализа.Значение));
				КомандаИзмененияОбъектаАнализа.Заголовок = ОбъектАнализа.Представление;
				Если ВариантОтображения = ВариантыОтображения.Диаграмма Тогда
					КомандаИзмененияОбъектаАнализа.Действие = "Подключаемый_ИзменитьОбъектАнализаДиаграммы";
				Иначе
					КомандаИзмененияОбъектаАнализа.Действие = "Подключаемый_ИзменитьОбъектАнализаТаблицы";
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		// Добавим меню периодичности
		// Получим адрес динамических параметров
		ОтборПоВариантуАнализа = Новый Структура("ВариантАнализа", ВариантАнализа);
		НайденныеСоставМонитора = СоставМонитора.НайтиСтроки(ОтборПоВариантуАнализа);
		Если НайденныеСоставМонитора.Количество() > 0 Тогда
			АдресДинамическихПараметров = НайденныеСоставМонитора[0].АдресДинамическихПараметров;
			
			Если НЕ ПустаяСтрока(АдресДинамическихПараметров) Тогда
				ДинамическиеПараметры = ПолучитьИзВременногоХранилища(АдресДинамическихПараметров);
				
				Если ДинамическиеПараметры.Свойство("ПериодичностьКонтроля") Тогда
					ПериодичностьПоУмолчанию = ДинамическиеПараметры.ПериодичностьКонтроля;
					
				Иначе
					ПериодичностьПоУмолчанию = ВариантАнализа.ПериодичностьКонтроля;
					
				КонецЕсли;
				
			Иначе
				ПериодичностьПоУмолчанию = ВариантАнализа.ПериодичностьКонтроля;
				
			КонецЕсли;
			
		КонецЕсли;
	
		Если ДобавлятьПодменюПериодичности Тогда
			КомандаИзмененияПериодичности = Команды.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='День'"));
			КомандаИзмененияПериодичности.Заголовок = НСтр("ru='День'");
			КомандаИзмененияПериодичности.Действие = "Подключаемый_ИзменитьПериодичность";
			
			КомандаИзмененияПериодичности = Команды.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Неделя'"));
			КомандаИзмененияПериодичности.Заголовок = НСтр("ru='Неделя'");
			КомандаИзмененияПериодичности.Действие = "Подключаемый_ИзменитьПериодичность";
			
			КомандаИзмененияПериодичности = Команды.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Декада'"));
			КомандаИзмененияПериодичности.Заголовок = НСтр("ru='Декада'");
			КомандаИзмененияПериодичности.Действие = "Подключаемый_ИзменитьПериодичность";
			
			КомандаИзмененияПериодичности = Команды.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Месяц'"));
			КомандаИзмененияПериодичности.Заголовок = НСтр("ru='Месяц'");
			КомандаИзмененияПериодичности.Действие = "Подключаемый_ИзменитьПериодичность";
			
			КомандаИзмененияПериодичности = Команды.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Квартал'"));
			КомандаИзмененияПериодичности.Заголовок = НСтр("ru='Квартал'");
			КомандаИзмененияПериодичности.Действие = "Подключаемый_ИзменитьПериодичность";
			
			КомандаИзмененияПериодичности = Команды.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Полугодие'"));
			КомандаИзмененияПериодичности.Заголовок = НСтр("ru='Полугодие'");
			КомандаИзмененияПериодичности.Действие = "Подключаемый_ИзменитьПериодичность";
			
			КомандаИзмененияПериодичности = Команды.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Год'"));
			КомандаИзмененияПериодичности.Заголовок = НСтр("ru='Год'");
			КомандаИзмененияПериодичности.Действие = "Подключаемый_ИзменитьПериодичность";
			
		КонецЕсли;
		
		// Получим количество варианты отчетов для показателя
		ВариантыОтчетовПоказателя = ПолучитьВариантыОтчетовПоказателя(ВариантАнализа);
		КоличествоВариантовОтчетов = ВариантыОтчетовПоказателя.Количество();
		
		Если КоличествоВариантовОтчетов Тогда
			Для Каждого ВариантОтчетаПоказателя Из ВариантыОтчетовПоказателя Цикл 
				ИдентификаторВарианта = ВариантОтчетаПоказателя.Значение;
				НаименованиеВарианта = ВариантОтчетаПоказателя.Представление;
				
				КомандаОткрытьОтчет = Команды.Добавить("ОткрытьОтчет__" + ИдентификаторЭлементаФормы + "__" + ИдентификаторВарианта);
				КомандаОткрытьОтчет.Заголовок = НаименованиеВарианта;
				КомандаОткрытьОтчет.Действие = "Подключаемый_ОткрытьОтчет";
				КомандаОткрытьОтчет.Картинка = БиблиотекаКартинок.Отчет;
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Добавим элементы формы
		// Добавим подменю связей
		Если ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеЗависимые ИЛИ ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеВлияющие Тогда	
			ПодменюЗависимостей = Элементы.Добавить("ЗависимостиПоказателей__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), ПодменюВсеДействия);
			ПодменюЗависимостей.Вид = ВидГруппыФормы.Подменю;
			ПодменюЗависимостей.Заголовок = НСтр("ru='Взаимосвязи'");
			ПодменюЗависимостей.Картинка = БиблиотекаКартинок.ПиктограммаВсеПоказатели;
			
		Иначе
			КнопкаПоискаСвязи = Элементы.Добавить("ЗависимостиПоказателей__" + ИдентификаторЭлементаФормы, Тип("КнопкаФормы"), ПодменюВсеДействия);
			КнопкаПоискаСвязи.ИмяКоманды = "ПустаяКоманда__" + ИдентификаторЭлементаФормы;
			КнопкаПоискаСвязи.Доступность = Ложь;
			
		КонецЕсли;	
		
		Если ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеЗависимые Тогда	
			КнопкаПоискаСвязи =Элементы.Добавить("ФормаЗависимыеПоказатели__" + ИдентификаторЭлементаФормы, Тип("КнопкаФормы"), ПодменюЗависимостей);
			КнопкаПоискаСвязи.ИмяКоманды = "ЗависимыеПоказатели__" + ИдентификаторЭлементаФормы;
			
		КонецЕсли;
		
		Если ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеВлияющие Тогда	
			КнопкаПоискаСвязи =Элементы.Добавить("ФормаВлияющиеПоказатели__" + ИдентификаторЭлементаФормы, Тип("КнопкаФормы"), ПодменюЗависимостей);
			КнопкаПоискаСвязи.ИмяКоманды = "ВлияющиеПоказатели__" + ИдентификаторЭлементаФормы;
			
		КонецЕсли;
		
		Если ДобавлятьПодменюОбъектовАнализа И ДоступныеОбъектыАнализа.Количество() > 1 Тогда
			ПодменюОбъектыАнализа = Элементы.Добавить("ОбъектыАнализа__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), ПодменюВсеДействия);
			ПодменюОбъектыАнализа.Вид = ВидГруппыФормы.Подменю;
			ПодменюОбъектыАнализа.Заголовок = НСтр("ru='Объекты анализа'");
			
			Для Каждого ОбъектАнализа Из ДоступныеОбъектыАнализа Цикл 
				КнопкаОбъектаАнализа = Элементы.Добавить("ИзменитьОбъектАнализа__" + ИдентификаторЭлементаФормы + "__" + Строка(ОбъектАнализа.Значение), Тип("КнопкаФормы"), ПодменюОбъектыАнализа);
				КнопкаОбъектаАнализа.ИмяКоманды = "ИзменитьОбъектАнализа__" + ИдентификаторЭлементаФормы + "__" + Строка(ОбъектАнализа.Значение);
				Если ОбъектАнализа.Значение = ОбъектАнализаПоУмолчанию Тогда
					КнопкаОбъектаАнализа.Пометка = Истина;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		Если ДобавлятьПодменюПериодичности Тогда
			ПодменюПериодичности = Элементы.Добавить("Периодичность__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), ПодменюВсеДействия);
			ПодменюПериодичности.Вид = ВидГруппыФормы.Подменю;
			ПодменюПериодичности.Заголовок = НСтр("ru='Периодичность'");
			
			КнопкаПериодичности = Элементы.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='День'"), Тип("КнопкаФормы"), ПодменюПериодичности);
			КнопкаПериодичности.ИмяКоманды = "ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='День'");
			Если НСтр("ru='День'") = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ПериодичностьПоУмолчанию) Тогда
				КнопкаПериодичности.Пометка = Истина;
			КонецЕсли;
				
			КнопкаПериодичности = Элементы.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Неделя'"), Тип("КнопкаФормы"), ПодменюПериодичности);
			КнопкаПериодичности.ИмяКоманды = "ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Неделя'");
			Если НСтр("ru='Неделя'") = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ПериодичностьПоУмолчанию) Тогда
				КнопкаПериодичности.Пометка = Истина;
			КонецЕсли;
				
			КнопкаПериодичности = Элементы.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Декада'"), Тип("КнопкаФормы"), ПодменюПериодичности);
			КнопкаПериодичности.ИмяКоманды = "ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Декада'");
			Если НСтр("ru='Декада'") = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ПериодичностьПоУмолчанию) Тогда
				КнопкаПериодичности.Пометка = Истина;
			КонецЕсли;
				
			КнопкаПериодичности = Элементы.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Месяц'"), Тип("КнопкаФормы"), ПодменюПериодичности);
			КнопкаПериодичности.ИмяКоманды = "ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Месяц'");
			Если НСтр("ru='Месяц'") = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ПериодичностьПоУмолчанию) Тогда
				КнопкаПериодичности.Пометка = Истина;
			КонецЕсли;
				
			КнопкаПериодичности = Элементы.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Квартал'"), Тип("КнопкаФормы"), ПодменюПериодичности);
			КнопкаПериодичности.ИмяКоманды = "ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Квартал'");
			Если НСтр("ru='Квартал'") = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ПериодичностьПоУмолчанию) Тогда
				КнопкаПериодичности.Пометка = Истина;
			КонецЕсли;
				
			КнопкаПериодичности = Элементы.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Полугодие'"), Тип("КнопкаФормы"), ПодменюПериодичности);
			КнопкаПериодичности.ИмяКоманды = "ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Полугодие'");
			Если НСтр("ru='Полугодие'") = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ПериодичностьПоУмолчанию) Тогда
				КнопкаПериодичности.Пометка = Истина;
			КонецЕсли;
				
			КнопкаПериодичности = Элементы.Добавить("ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Год'"), Тип("КнопкаФормы"), ПодменюПериодичности);
			КнопкаПериодичности.ИмяКоманды = "ИзменитьПериодичность__" + ИдентификаторЭлементаФормы + "__" + НСтр("ru='Год'");
			Если НСтр("ru='Год'") = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ПериодичностьПоУмолчанию) Тогда
				КнопкаПериодичности.Пометка = Истина;
			КонецЕсли;
				
		КонецЕсли;
		
		Если КоличествоВариантовОтчетов Тогда
			ПодменюОтчеты = Элементы.Добавить("Отчеты__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), ПодменюВсеДействия);
			ПодменюОтчеты.Вид = ВидГруппыФормы.Подменю;
			ПодменюОтчеты.Заголовок = НСтр("ru='Отчеты'");
			ПодменюОтчеты.Картинка = БиблиотекаКартинок.Отчет;
			
			Для Каждого ВариантОтчетаПоказателя Из ВариантыОтчетовПоказателя Цикл 
				ИдентификаторВарианта = ВариантОтчетаПоказателя.Значение;
				
				КнопкаОтчета = Элементы.Добавить("ФормаОткрытьОтчет__" + ИдентификаторЭлементаФормы + "__" + ИдентификаторВарианта, Тип("КнопкаФормы"), ПодменюОтчеты);
				КнопкаОтчета.ИмяКоманды = "ОткрытьОтчет__" + ИдентификаторЭлементаФормы + "__" + ИдентификаторВарианта;
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Добавим команды анализа, расшифровки и настройки
		КнопкаАнализПоказателя = Элементы.Добавить("ФормаОткрытьАнализПоказателя__" + ИдентификаторЭлементаФормы, Тип("КнопкаФормы"), ПодменюВсеДействия);
		КнопкаАнализПоказателя.ИмяКоманды = "ОткрытьАнализПоказателя__" + ИдентификаторЭлементаФормы;
		
	ИначеЕсли ВариантОтображения = ВариантыОтображения.Кратко Тогда
		КонтекстноеМенюТекстовогоПоказателя = Элементы["Значение__"+ ИдентификаторЭлементаФормы + "КонтекстноеМеню"];
		
		// Добавим реквизиты формы
		Если НЕ (ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеЗависимые ИЛИ ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеВлияющие) Тогда	
			ПустаяКоманда = Команды.Добавить("ПустаяКоманда__" + ИдентификаторЭлементаФормы);
			ПустаяКоманда.Заголовок = НСтр("ru='Взаимосвязей нет'");
			ПустаяКоманда.Картинка = БиблиотекаКартинок.ПиктограммаВсеПоказатели;
			
		КонецЕсли;
		
		Если ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеЗависимые Тогда	
			КомандаПоискаСвязи = Команды.Добавить("ЗависимыеПоказатели__" + ИдентификаторЭлементаФормы);
			КомандаПоискаСвязи.Заголовок = НСтр("ru='Зависимые показатели'");
			КомандаПоискаСвязи.Действие = "Подключаемый_АнализВзаимосвязанныхВариантовАнализа";
			КомандаПоискаСвязи.Картинка = БиблиотекаКартинок.ПиктограммаЗависимыеПоказатели;
			
		КонецЕсли;	
		
		Если ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеВлияющие Тогда	
			КомандаПоискаСвязи = Команды.Добавить("ВлияющиеПоказатели__" + ИдентификаторЭлементаФормы);
			КомандаПоискаСвязи.Заголовок = НСтр("ru='Влияющие показатели'");
			КомандаПоискаСвязи.Действие = "Подключаемый_АнализВзаимосвязанныхВариантовАнализа";
			КомандаПоискаСвязи.Картинка = БиблиотекаКартинок.ПиктограммаВлияющиеПоказатели;
			
		КонецЕсли;
		
		КомандаАнализПоказателя = Команды.Добавить("ОткрытьАнализПоказателя__" + ИдентификаторЭлементаФормы);
		КомандаАнализПоказателя.Заголовок = НСтр("ru='Анализ показателя'");
		КомандаАнализПоказателя.Действие = "Подключаемый_ОткрытьАнализПоказателя";
		КомандаАнализПоказателя.Картинка = БиблиотекаКартинок.ПиктограммаАналитическойФормыОтчета;
		
		// Получим количество варианты отчетов для показателя
		ВариантыОтчетовПоказателя = ПолучитьВариантыОтчетовПоказателя(ВариантАнализа);
		КоличествоВариантовОтчетов = ВариантыОтчетовПоказателя.Количество();
		
		Если КоличествоВариантовОтчетов Тогда
			Для Каждого ВариантОтчетаПоказателя Из ВариантыОтчетовПоказателя Цикл 
				ИдентификаторВарианта = ВариантОтчетаПоказателя.Значение;
				НаименованиеВарианта = ВариантОтчетаПоказателя.Представление;
				
				КомандаОткрытьОтчет = Команды.Добавить("ОткрытьОтчет__" + ИдентификаторЭлементаФормы + "__" + ИдентификаторВарианта);
				КомандаОткрытьОтчет.Заголовок = НаименованиеВарианта;
				КомандаОткрытьОтчет.Действие = "Подключаемый_ОткрытьОтчет";
				КомандаОткрытьОтчет.Картинка = БиблиотекаКартинок.Отчет;
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Добавим элементы формы
		Если ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеЗависимые ИЛИ ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеВлияющие Тогда	
			// Добавим подменю связей
			ПодменюЗависимостей = Элементы.Добавить("ЗависимостиПоказателей__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), КонтекстноеМенюТекстовогоПоказателя);
			ПодменюЗависимостей.Вид = ВидГруппыФормы.Подменю;
			ПодменюЗависимостей.Заголовок = НСтр("ru='Взаимосвязи'");
			ПодменюЗависимостей.Картинка = БиблиотекаКартинок.ПиктограммаВсеПоказатели;
			
		Иначе
			КнопкаПоискаСвязи = Элементы.Добавить("ЗависимостиПоказателей__" + ИдентификаторЭлементаФормы, Тип("КнопкаФормы"), КонтекстноеМенюТекстовогоПоказателя);
			КнопкаПоискаСвязи.ИмяКоманды = "ПустаяКоманда__" + ИдентификаторЭлементаФормы;
			КнопкаПоискаСвязи.Доступность = Ложь;
			
		КонецЕсли;	
		
		Если ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеЗависимые Тогда	
			КнопкаПоискаСвязи =Элементы.Добавить("ФормаЗависимыеПоказатели__" + ИдентификаторЭлементаФормы, Тип("КнопкаФормы"), ПодменюЗависимостей);
			КнопкаПоискаСвязи.ИмяКоманды = "ЗависимыеПоказатели__" + ИдентификаторЭлементаФормы;
			
		КонецЕсли;
		
		Если ПараметрыДоступностиАнализаВзаимосвязанных.ЕстьДоступныеВлияющие Тогда	
			КнопкаПоискаСвязи =Элементы.Добавить("ФормаВлияющиеПоказатели__" + ИдентификаторЭлементаФормы, Тип("КнопкаФормы"), ПодменюЗависимостей);
			КнопкаПоискаСвязи.ИмяКоманды = "ВлияющиеПоказатели__" + ИдентификаторЭлементаФормы;
			
		КонецЕсли;
		
		Если КоличествоВариантовОтчетов Тогда
			ПодменюОтчеты = Элементы.Добавить("Отчеты__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), КонтекстноеМенюТекстовогоПоказателя);
			ПодменюОтчеты.Вид = ВидГруппыФормы.Подменю;
			ПодменюОтчеты.Заголовок = НСтр("ru='Отчеты'");
			ПодменюОтчеты.Картинка = БиблиотекаКартинок.Отчет;
			
			Для Каждого ВариантОтчетаПоказателя Из ВариантыОтчетовПоказателя Цикл 
				ИдентификаторВарианта = ВариантОтчетаПоказателя.Значение;
				
				КнопкаОтчета =Элементы.Добавить("ФормаОткрытьОтчет__" + ИдентификаторЭлементаФормы + "__" + ИдентификаторВарианта, Тип("КнопкаФормы"), ПодменюОтчеты);
				КнопкаОтчета.ИмяКоманды = "ОткрытьОтчет__" + ИдентификаторЭлементаФормы + "__" + ИдентификаторВарианта;
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Добавим команды анализа, расшифровки и настройки
		КнопкаАнализПоказателя =Элементы.Добавить("ФормаОткрытьАнализПоказателя__" + ИдентификаторЭлементаФормы, Тип("КнопкаФормы"), КонтекстноеМенюТекстовогоПоказателя);
		КнопкаАнализПоказателя.ИмяКоманды = "ОткрытьАнализПоказателя__" + ИдентификаторЭлементаФормы;
		
	КонецЕсли;
	
КонецПроцедуры

// Кэширует свойства анализируемого состава монитора
// Анализируется количество источников данных: 
//	- по которым требуется вывод подробной информации
//	- по которым требуется вывод итога таблицы
//	- по которым требуется анализ состояния по нарастающему итогу
// 
// Параметры:
//	АнализируемыйСоставМонитора - Структура - содержит набор источников данных и обобщенную информацию об источниках
//	АдресаДинамическихПараметров - ТаблицаЗначений, Неопределено - содержит вариант анализа и адрес дин. настроек во временном хранилище
//
&НаСервере 
Процедура ЗаполнитьСводнуюИнформациюПоИсточникамДанных(АнализируемыйСоставМонитора, АдресаДинамическихПараметров = Неопределено)
	
	НаборИсточниковДанных = АнализируемыйСоставМонитора.НаборИсточниковДанных;
	
	Если НЕ АнализируемыйСоставМонитора.Пустой Тогда 
		ВариантыАнализаСПодробностями.Очистить();
		ОтборПоПодробным = Новый Структура("ВыводитьПодробности", Истина);
		НайденныеВариантыАнализа = НаборИсточниковДанных.НайтиСтроки(ОтборПоПодробным);
		Для Каждого НайденныйВариантАнализа Из НайденныеВариантыАнализа Цикл 
			ВариантыАнализаСПодробностями.Добавить(НайденныйВариантАнализа.ВариантАнализа);
			
		КонецЦикла;
		
		ТабличныеВариантыАнализаСИтогами.Очистить();
		ОтборПоТабличнымСИтогами = Новый Структура("ВыводитьИтогТаблицы", Истина);
		НайденныеВариантыАнализа = НаборИсточниковДанных.НайтиСтроки(ОтборПоТабличнымСИтогами);
		Для Каждого НайденныйВариантАнализа Из НайденныеВариантыАнализа Цикл 
			ТабличныеВариантыАнализаСИтогами.Добавить(НайденныйВариантАнализа.ВариантАнализа);
			
		КонецЦикла;
		
		ВариантыАнализаСНарастающимИтогом.Очистить();
		ОтборПоНарастающемуИтогу = Новый Структура("СостояниеПоНарастающемуИтогу", Истина);
		НайденныеВариантыАнализа = НаборИсточниковДанных.НайтиСтроки(ОтборПоНарастающемуИтогу);
		Для Каждого НайденныйВариантАнализа Из НайденныеВариантыАнализа Цикл 
			ВариантыАнализаСНарастающимИтогом.Добавить(НайденныйВариантАнализа.ВариантАнализа);
			
		КонецЦикла;
		
		СоставМонитораОбъект = НаборИсточниковДанных.Скопировать(,"ВариантАнализа, 
																  |ОшибкаРасчета,
																  |РасчетыНеактуальны,
																  |Пустой, 
																  |СостояниеПоНарастающемуИтогу, 
																  |ВыводитьИтогТаблицы, 
																  |ВыводитьПодробности, 
																  |Группа, 
																  |ЗонаВнимания, 
																  |ПорядокЗоныВнимания");
		СоставМонитораОбъект.Колонки.Добавить("АдресДинамическихПараметров");
		
		// Заполним переданные адреса динамических параметров
		Если НЕ АдресаДинамическихПараметров = Неопределено Тогда
			Для Каждого ЭлементСоставаМонитора Из СоставМонитораОбъект Цикл 
				ОтборПоВариантуАнализа = Новый Структура("ВариантАнализа", ЭлементСоставаМонитора.ВариантАнализа);
				НайденныеСоставМонитора = АдресаДинамическихПараметров.НайтиСтроки(ОтборПоВариантуАнализа);
				Если НайденныеСоставМонитора.Количество() > 0 Тогда
					ЭлементСоставаМонитора.АдресДинамическихПараметров = НайденныеСоставМонитора[0].АдресДинамическихПараметров;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ЗначениеВДанныеФормы(СоставМонитораОбъект, СоставМонитора);
	Иначе
		ВариантыАнализаСПодробностями.Очистить();
		ТабличныеВариантыАнализаСИтогами.Очистить();
		ВариантыАнализаСНарастающимИтогом.Очистить();
		СоставМонитора.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает источник данных для переданного варианта анализа при изменении объекта анализа или периодичности.
// Обновляет диаграмму варианта анализа
// 
// Параметры:
//	ВариантАнализа - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей - вариант, для которого изменили дин. параметр
//	ПараметрыИсточникаДанных - Структура, содержащая состояние динамических параметров варианта анализа
//	ИдентификаторЭлементаФормы - Строка - уникальный идентификатор(имя) обновляемой диаграммы
//
&НаСервере 
Процедура ИзменитьДинамическийПараметрДляДиаграммы(ВариантАнализа, ПараметрыИсточникаДанных, ИдентификаторЭлементаФормы)
	
	Если ПараметрыИсточникаДанных.Свойство("ОбъектАнализа") Тогда
		Если Строка(ВариантАнализа.ОбъектАнализа.Получить()) = ПараметрыИсточникаДанных.ОбъектАнализа Тогда
			ПринудительноеОбновление = Ложь;
		Иначе
			ПринудительноеОбновление = Истина;
		КонецЕсли;
		
	ИначеЕсли ПараметрыИсточникаДанных.Свойство("ПериодичностьКонтроля") Тогда	
		Если ВариантАнализа.ПериодичностьКонтроля = ПараметрыИсточникаДанных.ПериодичностьКонтроля Тогда
			ПринудительноеОбновление = Ложь;
		Иначе
			ПринудительноеОбновление = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ИсточникДанныхВариантаАнализа = МониторингЦелевыхПоказателей.ПолучитьИсточникДанныхВариантаАнализа(ВариантАнализа, Перечисления.ВариантыОтображенияВариантовАнализа.Диаграмма, ПринудительноеОбновление,ПараметрыИсточникаДанных);
	
	Если НЕ ИсточникДанныхВариантаАнализа.Пустой Тогда
	
		ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы].ИсточникДанных = Новый ТаблицаЗначений;
		ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы].ИсточникДанных = Неопределено;
		ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы] = МониторингЦелевыхПоказателей.ЗаполнитьДиаграммуДляМонитора(ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы], ВариантАнализа, ИсточникДанныхВариантаАнализа);
		
	Иначе
		Если НЕ ИсточникДанныхВариантаАнализа.РасчетыАктуальны Тогда
			ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы] = ПолучитьДиаграммуПриОшибкахРасчета(ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы], "Данные неактуальны.", ЦветаСтиля.ПоясняющийОшибкуТекст);
			
		ИначеЕсли ИсточникДанныхВариантаАнализа.ОшибкаРасчета Тогда
			ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы] = ПолучитьДиаграммуПриОшибкахРасчета(ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы], "Ошибка при расчете значений показателя.", ЦветаСтиля.ПоясняющийОшибкуТекст);
			
		Иначе
			ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы] = ПолучитьДиаграммуПриОшибкахРасчета(ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы], "Недостаточно данных для расчета показателя.", ЦветаСтиля.ПоясняющийТекст);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыИсточникаДанных.Свойство("ОбъектАнализа") Тогда
		// Установим новое наименование диаграммы
		ДинамическиеСвойстваВариантаАнализа = ИсточникДанныхВариантаАнализа.ДинамическиеСвойстваВариантаАнализа;
		НачалоПериода = Формат(ИсточникДанныхВариантаАнализа.СвойстваДанныхПоПериодам.ДатаПервогоЗначения, "ДФ=dd.MM.yy");
		КонецПериода= Формат(ИсточникДанныхВариантаАнализа.СвойстваДанныхПоПериодам.ДатаПоследнегоЗначения, "ДФ=dd.MM.yy");
		ТекстовоеПредставлениеПериода = НачалоПериода + " - " + КонецПериода;
		Если ВариантАнализа.КратностьЗначений = Перечисления.ВидыПредставленияЧисел.БезИзменений Тогда
			ЭтаФорма["НаименованиеПоказателя__" + ИдентификаторЭлементаФормы] = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ": " + ВариантАнализа.Наименование + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность)," (" + ДинамическиеСвойстваВариантаАнализа.Размерность + ")", "") + " " + ТекстовоеПредставлениеПериода;
		Иначе
			ЭтаФорма["НаименованиеПоказателя__" + ИдентификаторЭлементаФормы] = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ": " + ВариантАнализа.Наименование + " (" + МониторингЦелевыхПоказателей.ПолучитьСтрокуПредставленияЧисел(ВариантАнализа) + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность), " " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ")" + " " + ТекстовоеПредставлениеПериода;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает источник данных для переданного варианта анализа при изменении объекта анализа или периодичности.
// Обновляет таблицу варианта анализа
// 
// Параметры:
//	ВариантАнализа - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей - вариант, для которого изменили дин. параметр
//	ПараметрыИсточникаДанных - Структура, содержащая состояние динамических параметров варианта анализа
//	ИдентификаторЭлементаФормы - Строка - уникальный идентификатор(имя) обновляемой таблицы
//
&НаСервере 
Процедура ИзменитьДинамическийПараметрДляТаблицы(ВариантАнализа, ПараметрыИсточникаДанных, ИдентификаторЭлементаФормы)
	
	Если ПараметрыИсточникаДанных.Свойство("ОбъектАнализа") Тогда
		Если Строка(ВариантАнализа.ОбъектАнализа.Получить()) = ПараметрыИсточникаДанных.ОбъектАнализа Тогда
			ПринудительноеОбновление = Ложь;
		Иначе
			ПринудительноеОбновление = Истина;
		КонецЕсли;
	ИначеЕсли ПараметрыИсточникаДанных.Свойство("ПериодичностьКонтроля") Тогда	
		Если ВариантАнализа.ПериодичностьКонтроля = ПараметрыИсточникаДанных.ПериодичностьКонтроля Тогда
			ПринудительноеОбновление = Ложь;
		Иначе
			ПринудительноеОбновление = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ИсточникДанныхВариантаАнализа = МониторингЦелевыхПоказателей.ПолучитьИсточникДанныхВариантаАнализа(ВариантАнализа, Перечисления.ВариантыОтображенияВариантовАнализа.Диаграмма, ПринудительноеОбновление,ПараметрыИсточникаДанных);
	
	Если НЕ ИсточникДанныхВариантаАнализа.Пустой Тогда
		ДинамическиеСвойстваВариантаАнализа = ИсточникДанныхВариантаАнализа.ДинамическиеСвойстваВариантаАнализа;
		ВыводитьИтогВТаблице = (НЕ ТабличныеВариантыАнализаСИтогами.НайтиПоЗначению(ВариантАнализа) = Неопределено);
		
		Если ВыводитьИтогВТаблице Тогда 
			КратностьЗначений			= ВариантАнализа.КратностьЗначений;
			ТочностьРасчетаДробнойЧасти = ВариантАнализа.ТочностьРасчетаДробнойЧасти;
			Размерность					= СокрЛП(Строка(ДинамическиеСвойстваВариантаАнализа.Размерность));
			
			ЭтаФорма["ИтогТаблицаЗначений__" + ИдентификаторЭлементаФормы] = МониторингЦелевыхПоказателей.ПолучитьСокращенноеПредставлениеЧисла(ИсточникДанныхВариантаАнализа.Данные.Итог(ИсточникДанныхВариантаАнализа.ДинамическиеСвойстваВариантаАнализа.ЗначениеАнализа), ТочностьРасчетаДробнойЧасти, КратностьЗначений) + " " + Размерность;
			
		КонецЕсли;
		
		МониторингЦелевыхПоказателей.ЗаполнитьТаблицуДляМонитора(ЭтаФорма["ТаблицаЗначений__" + ИдентификаторЭлементаФормы], ИдентификаторЭлементаФормы, ВариантАнализа, ИсточникДанныхВариантаАнализа);
	Иначе
		Если НЕ ИсточникДанныхВариантаАнализа.РасчетыАктуальны Тогда
			ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы] = ПолучитьДиаграммуПриОшибкахРасчета(ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы], "Данные неактуальны.", ЦветаСтиля.ПоясняющийОшибкуТекст);
			
		ИначеЕсли ИсточникДанныхВариантаАнализа.ОшибкаРасчета Тогда
			ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы] = ПолучитьДиаграммуПриОшибкахРасчета(ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы], "Ошибка при расчете значений показателя.", ЦветаСтиля.ПоясняющийОшибкуТекст);
			
		Иначе
			ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы] = ПолучитьДиаграммуПриОшибкахРасчета(ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы], "Недостаточно данных для расчета показателя.", ЦветаСтиля.ПоясняющийТекст);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет элементы формы для вида отображения "Диаграмма"
//
// Параметры:
//	ВариантАнализа - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей
//	ИсточникДанных - Структура, содержащая результаты расчета для варианта анализа
//	ГруппаЭлементов - ГруппаФормы, в которую нужно добавить элементы формы
//	ИдентификаторЭлементаФормы - Строка, содержащая уникальный идентификатор элемента формы
//
&НаСервере
Процедура НарисоватьДиаграмму(ВариантАнализа, ИсточникДанных, ГруппаЭлементов, ИдентификаторЭлементаФормы)
	
	СвойстваЗоныАнализаИТренда = ИсточникДанных.СвойстваЗоныАнализаИТренда;
	СвойстваДанныхПоПериодамСравнения = ИсточникДанных.СвойстваДанныхПоПериодамСравнения;
	
	ДинамическиеСвойстваВариантаАнализа = ИсточникДанных.ДинамическиеСвойстваВариантаАнализа;
	
	ПустойИсточникДанных = ИсточникДанных.Пустой;
	ОшибкаРасчета = ИсточникДанных.ОшибкаРасчета;
	РасчетыНеактуальны = НЕ ИсточникДанных.РасчетыАктуальны;
	
	ВидыПредставленияЧисел = Перечисления.ВидыПредставленияЧисел;
	ПустаяКартинка = Новый Картинка;
	МассивРеквизитов = Новый Массив;
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Диаграмма"));
	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	
	МассивРеквизитов.Добавить(Новый РеквизитФормы("Диаграмма__" + ИдентификаторЭлементаФормы, ОписаниеТипов, "", "Диаграмма для '" + ВариантАнализа + "'"));
	
	// Добавим картинки
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Картинка"));
	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	
	МассивРеквизитов.Добавить(Новый РеквизитФормы("КартинкаСостояния__" + ИдентификаторЭлементаФормы, ОписаниеТипов, "", НСтр("ru='Картинка состояния'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("КартинкаТренда__" + ИдентификаторЭлементаФормы, ОписаниеТипов, "", НСтр("ru='Картинка тренда'")));
	
	// Добавим строковый реквизит
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Строка"));
	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	
	МассивРеквизитов.Добавить(Новый РеквизитФормы("НаименованиеПоказателя__" + ИдентификаторЭлементаФормы, ОписаниеТипов, "", "Наименование показателя"));
	
	ИзменитьРеквизиты(МассивРеквизитов);
	
	// Добавим поля формы
	ГруппаСтатусов = ГоризонтальнаяГруппа("Статусы__" + ИдентификаторЭлементаФормы, "Статусы", ГруппаЭлементов);
	
	ПолеЗначенияИндикатора = Элементы.Вставить("КартинкаСостояния__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаСтатусов);
	ПолеЗначенияИндикатора.Вид = ВидПоляФормы.ПолеКартинки;
	ПолеЗначенияИндикатора.ПутьКДанным = "КартинкаСостояния__" + ИдентификаторЭлементаФормы;
	ПолеЗначенияИндикатора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначенияИндикатора.Ширина = 2;
	ПолеЗначенияИндикатора.Высота = 1;
	ПолеЗначенияИндикатора.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	ПолеЗначенияИндикатора.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначенияИндикатора.РастягиватьПоВертикали = Ложь;
	ПолеЗначенияИндикатора.Подсказка = ИсточникДанных.СвойстваЗоныАнализаИТренда.ПояснениеКСтатусу;
	
	ПолеЗначенияИндикатора = Элементы.Вставить("КартинкаТренда__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаСтатусов);
	ПолеЗначенияИндикатора.Вид = ВидПоляФормы.ПолеКартинки;
	ПолеЗначенияИндикатора.ПутьКДанным = "КартинкаТренда__" + ИдентификаторЭлементаФормы;
	ПолеЗначенияИндикатора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначенияИндикатора.Ширина = 2;
	ПолеЗначенияИндикатора.Высота = 1;
	ПолеЗначенияИндикатора.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	ПолеЗначенияИндикатора.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначенияИндикатора.РастягиватьПоВертикали = Ложь;
	ПолеЗначенияИндикатора.Подсказка = ИсточникДанных.СвойстваЗоныАнализаИТренда.ПояснениеКТренду;
	
	ПолеНаименования = Элементы.Вставить("НаименованиеПоказателя__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаСтатусов);
	ПолеНаименования.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеНаименования.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеНаименования.ПутьКДанным = "НаименованиеПоказателя__" + ИдентификаторЭлементаФормы;
	ПолеНаименования.Ширина = 36;
	
	ПолеДиаграммы = Элементы.Вставить("Диаграмма__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаЭлементов);
	ПолеДиаграммы.Вид = ВидПоляФормы.ПолеДиаграммы;
	ПолеДиаграммы.ПутьКДанным = "Диаграмма__" + ИдентификаторЭлементаФормы;
	ПолеДиаграммы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеДиаграммы.Ширина = 56;
	ПолеДиаграммы.Высота = 8 * СтруктураНастроек.РазмерДиаграмм;
	
	ЭтаФорма["КартинкаСостояния__" + ИдентификаторЭлементаФормы] = МониторингЦелевыхПоказателей.ПолучитьКартинкуСостоянияДляПоказателя(ВариантАнализа, ИсточникДанных);
	ЭтаФорма["КартинкаТренда__" + ИдентификаторЭлементаФормы] = МониторингЦелевыхПоказателей.ПолучитьКартинкуТрендаДляПоказателя(ВариантАнализа, ИсточникДанных);
	
	НачалоПериода = Формат(ИсточникДанных.СвойстваДанныхПоПериодам.ДатаПервогоЗначения, "ДФ=dd.MM.yy");
	КонецПериода= Формат(ИсточникДанных.СвойстваДанныхПоПериодам.ДатаПоследнегоЗначения, "ДФ=dd.MM.yy");
	ТекстовоеПредставлениеПериода = НачалоПериода + " - " + КонецПериода;
	Если ВариантАнализа.КратностьЗначений = ВидыПредставленияЧисел.БезИзменений Тогда
		ЭтаФорма["НаименованиеПоказателя__" + ИдентификаторЭлементаФормы] = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ": " + ВариантАнализа.Наименование + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность),", " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ", " + ТекстовоеПредставлениеПериода;
	Иначе
		ЭтаФорма["НаименованиеПоказателя__" + ИдентификаторЭлементаФормы] = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ": " + ВариантАнализа.Наименование + ", " + МониторингЦелевыхПоказателей.ПолучитьСтрокуПредставленияЧисел(ВариантАнализа) + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность), " " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ", " + ТекстовоеПредставлениеПериода;
	КонецЕсли;
	
	Если НЕ ПустойИсточникДанных И НЕ РасчетыНеактуальны И НЕ ОшибкаРасчета Тогда
		Диаграмма = ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы];
		Диаграмма = МониторингЦелевыхПоказателей.ЗаполнитьДиаграммуДляМонитора(Диаграмма, ВариантАнализа, ИсточникДанных);
		
	Иначе
		Диаграмма = ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы];
		
		Если ИсточникДанных.Свойство("ОписаниеОшибки") Тогда
			ОписаниеОшибки = ИсточникДанных.ОписаниеОшибки;
			
		Иначе
			ОписаниеОшибки = НСтр("ru='Ошибка расчета показателя'");
			
		КонецЕсли;

		Диаграмма = ПолучитьДиаграммуПриОшибкахРасчета(Диаграмма, ОписаниеОшибки, ЦветаСтиля.ПоясняющийОшибкуТекст);
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет элементы формы для вида отображения "Кратко"
//
// Параметры:
//	ВариантАнализа - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей
//	ИсточникДанных - Структура, содержащая результаты расчета для варианта анализа
//	ГруппаЭлементов - ГруппаФормы, в которую нужно добавить элементы формы
//	ИдентификаторЭлементаФормы - Строка, содержащая уникальный идентификатор элемента формы
//
&НаСервере
Процедура НарисоватьКратко(ВариантАнализа, ИсточникДанных, ГруппаЭлементов, ИдентификаторЭлементаФормы)
	
	МассивРеквизитов = Новый Массив;
	
	ПоказыватьТекущееЗначениеИДеталиПоказателя = (НЕ ВариантыАнализаСПодробностями.НайтиПоЗначению(ВариантАнализа) = Неопределено);
	ИспользоватьНарастающийИтог = (НЕ ВариантыАнализаСНарастающимИтогом.НайтиПоЗначению(ВариантАнализа) = Неопределено);
	
	СвойстваЗоныАнализаИТренда = ИсточникДанных.СвойстваЗоныАнализаИТренда;
	СвойстваДанныхПоПериодамСравнения = ИсточникДанных.СвойстваДанныхПоПериодамСравнения;
	ДинамическиеСвойстваВариантаАнализа = ИсточникДанных.ДинамическиеСвойстваВариантаАнализа;
	
	ВидыПредставленияЧисел = Перечисления.ВидыПредставленияЧисел;
	
	ПустойИсточникДанных = ИсточникДанных.Пустой;
	РасчетыНеактуальны = НЕ ИсточникДанных.РасчетыАктуальны;
	ОшибкаРасчета = ИсточникДанных.ОшибкаРасчета;
	
	// Добавим строковые реквизиты формы
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Строка"));
	ОписаниеТиповСтрок = Новый ОписаниеТипов(МассивТипов);
	
	МассивРеквизитов.Добавить(Новый РеквизитФормы("КраткоеНаименование__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Краткое наименование'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("Значение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Значение'")));
	
	Если ПоказыватьТекущееЗначениеИДеталиПоказателя И СтруктураНастроек.ВариантОтображенияДеталей = "ПоказыватьТекущееЗначениеИДетали" Тогда
		МассивРеквизитов.Добавить(Новый РеквизитФормы("ОтнИзменение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Отн. изменение'")));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("АбсИзменение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Абс. изменение'")));
	КонецЕсли;
	
	// Добавим картинки-реквизиты
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Картинка"));
	ОписаниеТиповКартинок = Новый ОписаниеТипов(МассивТипов);
	
	МассивРеквизитов.Добавить(Новый РеквизитФормы("КартинкаСостояния__" + ИдентификаторЭлементаФормы, ОписаниеТиповКартинок, "", НСтр("ru='Картинка состояния'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("КартинкаТренда__" + ИдентификаторЭлементаФормы, ОписаниеТиповКартинок, "", НСтр("ru='Картинка тренда'")));
	
	ИзменитьРеквизиты(МассивРеквизитов);
	
	// Добавим элементы формы
	ГруппаСтатусов = ГоризонтальнаяГруппа("Статусы__" + ИдентификаторЭлементаФормы, НСтр("ru='Статусы'"), ГруппаЭлементов);
	
	ПолеЗначенияИндикатора = Элементы.Вставить("КартинкаСостояния__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаСтатусов);
	ПолеЗначенияИндикатора.Вид = ВидПоляФормы.ПолеКартинки;
	ПолеЗначенияИндикатора.ПутьКДанным = "КартинкаСостояния__" + ИдентификаторЭлементаФормы;
	ПолеЗначенияИндикатора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначенияИндикатора.Ширина = 2;
	ПолеЗначенияИндикатора.Высота = 1;
	ПолеЗначенияИндикатора.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	ПолеЗначенияИндикатора.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначенияИндикатора.РастягиватьПоВертикали = Ложь;
	ПолеЗначенияИндикатора.Подсказка = ИсточникДанных.СвойстваЗоныАнализаИТренда.ПояснениеКСтатусу;
	
	ПолеИндикатора = Элементы.Вставить("КартинкаТренда__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаЭлементов);//ГруппаСтатусов);
	ПолеИндикатора.Вид = ВидПоляФормы.ПолеКартинки;
	ПолеИндикатора.ПутьКДанным = "КартинкаТренда__" + ИдентификаторЭлементаФормы;
	ПолеИндикатора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеИндикатора.Ширина = 2;
	ПолеИндикатора.Высота = 1;
	ПолеИндикатора.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	ПолеИндикатора.РастягиватьПоГоризонтали = Ложь;
	ПолеИндикатора.РастягиватьПоВертикали = Ложь;
	ПолеИндикатора.Подсказка = ИсточникДанных.СвойстваЗоныАнализаИТренда.ПояснениеКСтатусу + Символы.ПС + Символы.ПС + ИсточникДанных.СвойстваЗоныАнализаИТренда.ПояснениеКТренду;
	
	ПолеКрНаименования = Элементы.Вставить("КраткоеНаименование__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаЭлементов);
	ПолеКрНаименования.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеКрНаименования.ПутьКДанным = "КраткоеНаименование__" + ИдентификаторЭлементаФормы;
	ПолеКрНаименования.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеКрНаименования.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	ПолеКрНаименования.Ширина = 24;
	ПолеКрНаименования.Высота = 1;
	ПолеКрНаименования.РастягиватьПоВертикали = Ложь;
	
	ЦветСостоянияПоказателя = МониторингЦелевыхПоказателей.ПолучитьЦветСостоянияДляПоказателя(ВариантАнализа, ИсточникДанных);
	
	Если ПоказыватьТекущееЗначениеИДеталиПоказателя И СтруктураНастроек.ВариантОтображенияДеталей = "ПоказыватьТекущееЗначениеИДетали" Тогда
		ПолеИндикатора = Элементы.Вставить("АбсИзменение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаЭлементов);
		ПолеИндикатора.Вид = ВидПоляФормы.ПолеНадписи;
		ПолеИндикатора.ПутьКДанным = "АбсИзменение__" + ИдентификаторЭлементаФормы;
		ПолеИндикатора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ПолеИндикатора.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
		ПолеИндикатора.Ширина = 12;
		ПолеИндикатора.Высота = 1;
		ПолеИндикатора.РастягиватьПоВертикали = Ложь;
		ПолеИндикатора.ЦветТекста = ЦветСостоянияПоказателя;
		
		ПолеИндикатора = Элементы.Вставить("ОтнИзменение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаЭлементов);
		ПолеИндикатора.Вид = ВидПоляФормы.ПолеНадписи;
		ПолеИндикатора.ПутьКДанным = "ОтнИзменение__" + ИдентификаторЭлементаФормы;
		ПолеИндикатора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ПолеИндикатора.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
		ПолеИндикатора.Ширина = 7;
		ПолеИндикатора.Высота = 1;
		ПолеИндикатора.РастягиватьПоВертикали = Ложь;
		ПолеИндикатора.ЦветТекста = ЦветСостоянияПоказателя;
		
	КонецЕсли;
	
	ПолеЗначения = Элементы.Вставить("Значение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.Гиперссылка = Истина;
	ПолеЗначения.ПутьКДанным = "Значение__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	ПолеЗначения.Ширина = 12;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	ПолеЗначения.ЦветТекста = ЦветСостоянияПоказателя;
	
	ПолеЗначения.УстановитьДействие("Нажатие", "Подключаемый_ОткрытьАнализПоказателяНажатие");
	
	КратностьЗначений = ВариантАнализа.КратностьЗначений;
	ТочностьРасчетаДробнойЧасти = ВариантАнализа.ТочностьРасчетаДробнойЧасти;
	
	// Заполним значения созданных полей
	ЭтаФорма["КартинкаСостояния__" + ИдентификаторЭлементаФормы] = МониторингЦелевыхПоказателей.ПолучитьКартинкуСостоянияДляПоказателя(ВариантАнализа, ИсточникДанных);
	ЭтаФорма["КартинкаТренда__" + ИдентификаторЭлементаФормы] = МониторингЦелевыхПоказателей.ПолучитьКартинкуТрендаДляПоказателя(ВариантАнализа, ИсточникДанных);
	
	НачалоПериода = Формат(ИсточникДанных.СвойстваДанныхПоПериодам.ДатаПервогоЗначения, "ДФ=dd.MM.yy");
	КонецПериода= Формат(ИсточникДанных.СвойстваДанныхПоПериодам.ДатаПоследнегоЗначения, "ДФ=dd.MM.yy");
	ТекстовоеПредставлениеПериода = НачалоПериода + " - " + КонецПериода;
	Если ВариантАнализа.КратностьЗначений = ВидыПредставленияЧисел.БезИзменений Тогда
		ЭтаФорма["КраткоеНаименование__" + ИдентификаторЭлементаФормы] = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ": " + ВариантАнализа.Наименование + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность),", " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ", " + ТекстовоеПредставлениеПериода;
	Иначе
		ЭтаФорма["КраткоеНаименование__" + ИдентификаторЭлементаФормы] = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ": " + ВариантАнализа.Наименование + ", " + МониторингЦелевыхПоказателей.ПолучитьСтрокуПредставленияЧисел(ВариантАнализа) + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность), " " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ", " + ТекстовоеПредставлениеПериода;
	КонецЕсли;
	
	Если НЕ ПустойИсточникДанных И НЕ РасчетыНеактуальны И НЕ ОшибкаРасчета Тогда
		ПоследнееЗначение = Окр(ИсточникДанных.СвойстваДанныхПоПериодам.ПоследнееЗначение, ТочностьРасчетаДробнойЧасти);
		ОтображаемоеЗначение = ПоследнееЗначение;
		
		РазмерностьПоказателя = СокрЛП(Строка(ДинамическиеСвойстваВариантаАнализа.Размерность));
		
		Если ЗначениеЗаполнено(РазмерностьПоказателя) Тогда
			СтрокаРазмерности = " " + РазмерностьПоказателя;
		Иначе
			СтрокаРазмерности = "";
		КонецЕсли;
		
		ЭтаФорма["Значение__" + ИдентификаторЭлементаФормы] = СокрЛП(МониторингЦелевыхПоказателей.ПолучитьСокращенноеПредставлениеЧисла(ОтображаемоеЗначение, ТочностьРасчетаДробнойЧасти, КратностьЗначений) + СтрокаРазмерности);
		
		Если ПоказыватьТекущееЗначениеИДеталиПоказателя И СтруктураНастроек.ВариантОтображенияДеталей = "ПоказыватьТекущееЗначениеИДетали" Тогда
			Если НЕ ПустойИсточникДанных И НЕ РасчетыНеактуальны Тогда
				ПоследнееЦелевоеЗначение = Окр(ИсточникДанных.СвойстваДанныхПоПериодам.ЦелевоеЗначение, ТочностьРасчетаДробнойЧасти);
				ПредпоследнееЗначение = Окр(ИсточникДанных.СвойстваДанныхПоПериодам.ПредпоследнееЗначение, ТочностьРасчетаДробнойЧасти);
				
			Иначе
				ПоследнееЦелевоеЗначение = 0;
				ПредпоследнееЗначение = 0;
				
			КонецЕсли;
			
			Если НЕ ПредпоследнееЗначение = 0 Тогда
				
				ОтносительноеИзменение = Окр(100 * (ПоследнееЗначение - ПредпоследнееЗначение)/ПредпоследнееЗначение, ТочностьРасчетаДробнойЧасти);
				
				Если ОтносительноеИзменение > 0 Тогда
					ЗнакИзменения = "+";
				ИначеЕсли ОтносительноеИзменение = 0 Тогда
					ЗнакИзменения = "";
				ИначеЕсли ОтносительноеИзменение < 0 Тогда
					ЗнакИзменения = "-";
				КонецЕсли;
				
				ЭтаФорма["ОтнИзменение__" + ИдентификаторЭлементаФормы] = ЗнакИзменения + ОтносительноеИзменение + " %";
			Иначе
				ЭтаФорма["ОтнИзменение__" + ИдентификаторЭлементаФормы] = "---";
			КонецЕсли; 
			
			АбсолютноеИзменение = ПоследнееЗначение - ПредпоследнееЗначение;
			
			Если АбсолютноеИзменение > 0 Тогда
				ЗнакИзменения = "+";
			ИначеЕсли АбсолютноеИзменение = 0 Тогда
				ЗнакИзменения = "";
			ИначеЕсли АбсолютноеИзменение < 0 Тогда
				ЗнакИзменения = "-";
			КонецЕсли;
			
			ЭтаФорма["АбсИзменение__" + ИдентификаторЭлементаФормы] = ЗнакИзменения + МониторингЦелевыхПоказателей.ПолучитьСокращенноеПредставлениеЧисла(АбсолютноеИзменение, ТочностьРасчетаДробнойЧасти, КратностьЗначений) + СтрокаРазмерности;
			
		КонецЕсли;
		
	Иначе 
		Если ИсточникДанных.Свойство("ОписаниеОшибки") Тогда
			ЭтаФорма["Значение__" + ИдентификаторЭлементаФормы] = ИсточникДанных.ОписаниеОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет элементы формы справочной информации о показателе для анализа взаимосвязанных
//
// Параметры:
//	ВариантАнализа - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей
//	ИсточникДанных - Структура, содержащая результаты расчета для варианта анализа
//	ГруппаЭлементов - ГруппаФормы, в которую нужно добавить элементы формы
//	ИдентификаторЭлементаФормы - Строка, содержащая уникальный идентификатор элемента формы
//
&НаСервере
Процедура НарисоватьПоляСправкиОПоказателе(ВариантАнализа, ИсточникДанных, ГруппаЭлементов, ИдентификаторЭлементаФормы)
	
	МассивРеквизитов = Новый Массив;
	
	СвойстваЗоныАнализаИТренда = ИсточникДанных.СвойстваЗоныАнализаИТренда;
	СвойстваДанныхПоПериодамСравнения = ИсточникДанных.СвойстваДанныхПоПериодамСравнения;
	ДинамическиеСвойстваВариантаАнализа = ИсточникДанных.ДинамическиеСвойстваВариантаАнализа;
	
	ПустойИсточникДанных = ИсточникДанных.Пустой;
	РасчетыНеактуальны = НЕ ИсточникДанных.РасчетыАктуальны;
	ОшибкаРасчета = ИсточникДанных.ОшибкаРасчета;
	
	ЦветСостоянияПоказателя = МониторингЦелевыхПоказателей.ПолучитьЦветСостоянияДляПоказателя(ВариантАнализа, ИсточникДанных);
	
	// Добавим строковые реквизиты формы
	ОписаниеТиповСтрок = МониторингЦелевыхПоказателей.ПолучитьОписаниеТиповСтроки(0);
	
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ЦельЗаголовок__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Цель'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ЦелевойТрендЗаголовок__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Целевой тренд'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ЦелевыеЗначенияЗаголовок__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Целевые значения'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ФактическоеСостояниеЗаголовок__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Состояние'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ФактическийТрендЗаголовок__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Тренд'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ФактическоеЗначениеЗаголовок__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Заголовок значения'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ОтветственныйЗаголовок__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Ответственный'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ФормулаЗаголовок__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Формула'")));
	
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ЦельЗначение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Значение цели'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ЦелевойТрендЗначение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Значение целевого тренда'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ЦелевыеЗначенияЗначение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Значения целевых значений'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ФактическоеСостояниеЗначение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Значение состояния'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ФактическийТрендЗначение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Значение тренда'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ФактическоеЗначениеЗначение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Значение'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ОтветственныйЗначение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Значение ответственного'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ФормулаЗначение__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Значение формулы'")));
	
	ИзменитьРеквизиты(МассивРеквизитов);
	
	ГруппаИнформационныхЭлементов = ГоризонтальнаяГруппа("ЭлементЦель__" + ИдентификаторЭлементаФормы, "", ГруппаЭлементов);
	
	// Добавим элементы формы
	ПолеЗначения = Элементы.Вставить("ЦельЗаголовок__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ЦельЗаголовок__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Ширина = 16;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	
	ПолеЗначения = Элементы.Вставить("ЦельЗначение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ЦельЗначение__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	ПолеЗначения.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	
	ГруппаИнформационныхЭлементов = ГоризонтальнаяГруппа("ЭлементЦелевойТренд__" + ИдентификаторЭлементаФормы, "", ГруппаЭлементов);
	
	ПолеЗначения = Элементы.Вставить("ЦелевойТрендЗаголовок__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ЦелевойТрендЗаголовок__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Ширина = 16;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	
	ПолеЗначения = Элементы.Вставить("ЦелевойТрендЗначение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ЦелевойТрендЗначение__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	ПолеЗначения.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	
	ГруппаИнформационныхЭлементов = ГоризонтальнаяГруппа("ЭлементЦелевыеЗначения__" + ИдентификаторЭлементаФормы, "", ГруппаЭлементов);
	
	ПолеЗначения = Элементы.Вставить("ЦелевыеЗначенияЗаголовок__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ЦелевыеЗначенияЗаголовок__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Ширина = 16;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	
	ПолеЗначения = Элементы.Вставить("ЦелевыеЗначенияЗначение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ЦелевыеЗначенияЗначение__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	ПолеЗначения.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	
	ГруппаИнформационныхЭлементов = ГоризонтальнаяГруппа("ЭлементСостояние__" + ИдентификаторЭлементаФормы, "", ГруппаЭлементов);
	
	// Добавим элементы формы
	ПолеЗначения = Элементы.Вставить("ФактическоеСостояниеЗаголовок__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ФактическоеСостояниеЗаголовок__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Ширина = 16;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	
	ПолеЗначения = Элементы.Вставить("ФактическоеСостояниеЗначение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ФактическоеСостояниеЗначение__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	ПолеЗначения.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	
	ГруппаИнформационныхЭлементов = ГоризонтальнаяГруппа("ЭлементФактическийТренд__" + ИдентификаторЭлементаФормы, "", ГруппаЭлементов);
	
	ПолеЗначения = Элементы.Вставить("ФактическийТрендЗаголовок__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ФактическийТрендЗаголовок__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Ширина = 16;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	
	ПолеЗначения = Элементы.Вставить("ФактическийТрендЗначение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ФактическийТрендЗначение__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	ПолеЗначения.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	
	ГруппаИнформационныхЭлементов = ГоризонтальнаяГруппа("ЭлементФактическоеЗначение__" + ИдентификаторЭлементаФормы, "", ГруппаЭлементов);
	
	ПолеЗначения = Элементы.Вставить("ФактическоеЗначениеЗаголовок__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ФактическоеЗначениеЗаголовок__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Ширина = 16;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	
	ПолеЗначения = Элементы.Вставить("ФактическоеЗначениеЗначение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ФактическоеЗначениеЗначение__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	Если НЕ ПустойИсточникДанных И НЕ РасчетыНеактуальны И НЕ ОшибкаРасчета Тогда
		ПолеЗначения.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	Иначе
		ПолеЗначения.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
	КонецЕсли;
	
	ГруппаИнформационныхЭлементов = ГоризонтальнаяГруппа("ЭлементОтвественный__" + ИдентификаторЭлементаФормы, "", ГруппаЭлементов);
	
	ПолеЗначения = Элементы.Вставить("ОтветственныйЗаголовок__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ОтветственныйЗаголовок__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Ширина = 16;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	
	ПолеЗначения = Элементы.Вставить("ОтветственныйЗначение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ОтветственныйЗначение__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоВертикали = Ложь;
	ПолеЗначения.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	
	ГруппаИнформационныхЭлементов = ГоризонтальнаяГруппа("ЭлементФормула__" + ИдентификаторЭлементаФормы, "", ГруппаЭлементов);
	
	ПолеЗначения = Элементы.Вставить("ФормулаЗаголовок__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ФормулаЗаголовок__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.Ширина = 16;
	ПолеЗначения.Высота = 1;
	ПолеЗначения.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначения.РастягиватьПоВертикали = Истина;
	ПолеЗначения.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Верх;
	
	ПолеЗначения = Элементы.Вставить("ФормулаЗначение__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаИнформационныхЭлементов);
	ПолеЗначения.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеЗначения.ПутьКДанным = "ФормулаЗначение__" + ИдентификаторЭлементаФормы;
	ПолеЗначения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначения.РастягиватьПоВертикали = Истина;
	ПолеЗначения.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Верх;
	ПолеЗначения.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	
	// Заполним значения созданных полей
	ЭтаФорма["ЦельЗаголовок__" + ИдентификаторЭлементаФормы]	= НСтр("ru='Цель:'");
	ЭтаФорма["ЦелевойТрендЗаголовок__" + ИдентификаторЭлементаФормы]	= НСтр("ru='Целевой тренд:'");
	ЭтаФорма["ЦелевыеЗначенияЗаголовок__" + ИдентификаторЭлементаФормы]	= НСтр("ru='Целевые значения:'");
	
	ЭтаФорма["ЦельЗначение__" + ИдентификаторЭлементаФормы]	= ВариантАнализа.Владелец;
	ЭтаФорма["ЦелевойТрендЗначение__" + ИдентификаторЭлементаФормы]	= ВариантАнализа.Владелец.ЦелевойТренд;
	ЭтаФорма["ЦелевыеЗначенияЗначение__" + ИдентификаторЭлементаФормы]	= МониторингЦелевыхПоказателей.ПолучитьСвойстваВариантаАнализа(ВариантАнализа, ИсточникДанных).ЦелевыеЗначения;
	
	КратностьЗначений = ВариантАнализа.КратностьЗначений;
	ТочностьРасчетаДробнойЧасти = ВариантАнализа.ТочностьРасчетаДробнойЧасти;
	
	Если НЕ ПустойИсточникДанных И НЕ РасчетыНеактуальны И НЕ ОшибкаРасчета Тогда
		ПоследнееЗначение = Окр(ИсточникДанных.СвойстваДанныхПоПериодам.ПоследнееЗначение, ТочностьРасчетаДробнойЧасти);
		ОтображаемоеЗначение = ПоследнееЗначение;
		
	Иначе 
		ПоследнееЗначение = 0;
		ОтображаемоеЗначение = 0;
		
	КонецЕсли;
	
	РазмерностьПоказателя = СокрЛП(Строка(ДинамическиеСвойстваВариантаАнализа.Размерность));
	
	Если ЗначениеЗаполнено(РазмерностьПоказателя) Тогда
		СтрокаРазмерности = " " + РазмерностьПоказателя;
	Иначе
		СтрокаРазмерности = "";
	КонецЕсли;
	
	ЭтаФорма["ФактическоеСостояниеЗаголовок__" + ИдентификаторЭлементаФормы]	= НСтр("ru='Состояние:'");
	ЭтаФорма["ФактическийТрендЗаголовок__" + ИдентификаторЭлементаФормы]	= НСтр("ru='Тренд:'");
	ЭтаФорма["ФактическоеЗначениеЗаголовок__" + ИдентификаторЭлементаФормы]	= НСтр("ru='Значение:'");
	ЭтаФорма["ФормулаЗаголовок__" + ИдентификаторЭлементаФормы]	= НСтр("ru='Формула:'");
	ЭтаФорма["ОтветственныйЗаголовок__" + ИдентификаторЭлементаФормы]	= НСтр("ru='Ответственный:'");
	
	ЭтаФорма["ФактическоеСостояниеЗначение__" + ИдентификаторЭлементаФормы]	= ИсточникДанных.СвойстваЗоныАнализаИТренда.Статус;
	ЭтаФорма["ФактическийТрендЗначение__" + ИдентификаторЭлементаФормы]	= ИсточникДанных.СвойстваЗоныАнализаИТренда.ПояснениеКТренду;
	
	Если НЕ ПустойИсточникДанных И НЕ РасчетыНеактуальны И НЕ ОшибкаРасчета Тогда
		ЭтаФорма["ФактическоеЗначениеЗначение__" + ИдентификаторЭлементаФормы]	= СокрЛП(МониторингЦелевыхПоказателей.ПолучитьСокращенноеПредставлениеЧисла(ОтображаемоеЗначение, ТочностьРасчетаДробнойЧасти, КратностьЗначений) + СтрокаРазмерности);
	Иначе
		ЭтаФорма["ФактическоеЗначениеЗначение__" + ИдентификаторЭлементаФормы]	= ИсточникДанных.ОписаниеОшибки;
	КонецЕсли;
	
	ЭтаФорма["ФормулаЗначение__" + ИдентификаторЭлементаФормы]	= ВариантАнализа.Владелец.ФормулаРасчета;
	ЭтаФорма["ОтветственныйЗначение__" + ИдентификаторЭлементаФормы]	= ВариантАнализа.Ответственный;
	
КонецПроцедуры

// Добавляет группу элементов формы заданной ширины и высоты, содержащую пустую картинку.
// Используется для выравнивания элементов монитора.
// 
// Параметры:
//	ГруппаЭлементов - ГруппаФормы - группа, в кот. добавляется элемент
//	ИдентификаторЭлементаФормы - Строка - уникальный идентиф. добавляемого элмента
//	Высота - Число
//	Ширина - Число
//	РастягиватьПоГоризонтали - Булево - признак растягивания элемента по горизонтали
//
&НаСервере 
Процедура НарисоватьПустойЭлемент(ГруппаЭлементов, ИдентификаторЭлементаФормы, Высота, Ширина, РастягиватьПоГоризонтали = Ложь)
	// Добавим декорацию формы
	ПолеДекорации = Элементы.Вставить("ВыравнивающийЭлемент__" + ИдентификаторЭлементаФормы, Тип("ДекорацияФормы"), ГруппаЭлементов);
	ПолеДекорации.Вид = ВидДекорацииФормы.Картинка;
	ПолеДекорации.Картинка = Новый Картинка;
	ПолеДекорации.Ширина = Ширина;
	ПолеДекорации.Высота = Высота;
	ПолеДекорации.Заголовок = "Выравнивающий элемент";
	ПолеДекорации.РастягиватьПоГоризонтали = РастягиватьПоГоризонтали;
	
КонецПроцедуры

// Добавляет элементы формы для вида отображения "Таблица"
//
// Параметры:
//	ВариантАнализа - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей
//	ИсточникДанных - Структура, содержащая результаты расчета для варианта анализа
//	ГруппаЭлементов - ГруппаФормы, в которую нужно добавить элементы формы
//	ИдентификаторЭлементаФормы - Строка, содержащая уникальный идентификатор элемента формы
//
&НаСервере
Процедура НарисоватьТаблицу(ВариантАнализа, ИсточникДанных, ГруппаЭлементов, ИдентификаторЭлементаФормы)
	
	ВыводитьИтогВТаблице = (НЕ ТабличныеВариантыАнализаСИтогами.НайтиПоЗначению(ВариантАнализа) = Неопределено);
	
	СвойстваЗоныАнализаИТренда = ИсточникДанных.СвойстваЗоныАнализаИТренда;
	СвойстваДанныхПоПериодамСравнения = ИсточникДанных.СвойстваДанныхПоПериодамСравнения;
	ДинамическиеСвойстваВариантаАнализа = ИсточникДанных.ДинамическиеСвойстваВариантаАнализа;
	
	ПустойИсточникДанных = ИсточникДанных.Пустой;
	ОшибкаРасчета = ИсточникДанных.ОшибкаРасчета;
	РасчетыНеактуальны = НЕ ИсточникДанных.РасчетыАктуальны;
	
	ВидыПредставленияЧисел = Перечисления.ВидыПредставленияЧисел;
	ПустаяКартинка = Новый Картинка;
	МассивРеквизитов = Новый Массив;
	
	Если НЕ ПустойИсточникДанных И НЕ РасчетыНеактуальны Тогда
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("ТаблицаЗначений"));
		ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
		
		МассивРеквизитов.Добавить(Новый РеквизитФормы("ТаблицаЗначений__" + ИдентификаторЭлементаФормы, ОписаниеТипов, "", НСтр("ru='Таблица для '" + ВариантАнализа + "'")));
		
	Иначе
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("Диаграмма"));
		ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
		
		МассивРеквизитов.Добавить(Новый РеквизитФормы("Диаграмма__" + ИдентификаторЭлементаФормы, ОписаниеТипов, "", "Диаграмма для '" + ВариантАнализа + "'"));
		
	КонецЕсли;
	
	// Добавим картинки
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Картинка"));
	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	
	МассивРеквизитов.Добавить(Новый РеквизитФормы("КартинкаСостояния__" + ИдентификаторЭлементаФормы, ОписаниеТипов, "", НСтр("ru='Картинка состояния'")));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("КартинкаТренда__" + ИдентификаторЭлементаФормы, ОписаниеТипов, "", НСтр("ru='Картинка тренда'")));
	
	// Добавим строковый реквизит
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Строка"));
	ОписаниеТиповСтрок = Новый ОписаниеТипов(МассивТипов);
	
	МассивРеквизитов.Добавить(Новый РеквизитФормы("НаименованиеПоказателя__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "", НСтр("ru='Наименование показателя'")));
	
	Если НЕ ПустойИсточникДанных И НЕ РасчетыНеактуальны Тогда
		МассивРеквизитов.Добавить(Новый РеквизитФормы("КолонкаОбъектАнализа" + "__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "ТаблицаЗначений__" + ИдентификаторЭлементаФормы, ИсточникДанных.ДинамическиеСвойстваВариантаАнализа.ОбъектАнализаПредставление));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("КолонкаЗначениеАнализа" + "__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок, "ТаблицаЗначений__" + ИдентификаторЭлементаФормы, ИсточникДанных.ДинамическиеСвойстваВариантаАнализа.ЗначениеАнализаПредставление));
		
		Если ВыводитьИтогВТаблице Тогда
			МассивРеквизитов.Добавить(Новый РеквизитФормы("ИтогТаблицаЗначений__" + ИдентификаторЭлементаФормы, ОписаниеТиповСтрок));
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИзменитьРеквизиты(МассивРеквизитов);
	
	// Заполним данные реквизитов формы
	ЭтаФорма["КартинкаСостояния__" + ИдентификаторЭлементаФормы] = МониторингЦелевыхПоказателей.ПолучитьКартинкуСостоянияДляПоказателя(ВариантАнализа, ИсточникДанных);
	ЭтаФорма["КартинкаТренда__" + ИдентификаторЭлементаФормы] = МониторингЦелевыхПоказателей.ПолучитьКартинкуТрендаДляПоказателя(ВариантАнализа, ИсточникДанных);
	
	НачалоПериода = Формат(ИсточникДанных.СвойстваДанныхПоПериодам.ДатаПервогоЗначения, "ДФ=dd.MM.yy");
	КонецПериода= Формат(ИсточникДанных.СвойстваДанныхПоПериодам.ДатаПоследнегоЗначения, "ДФ=dd.MM.yy");
	ТекстовоеПредставлениеПериода = НачалоПериода + " - " + КонецПериода;
	Если ВариантАнализа.КратностьЗначений = ВидыПредставленияЧисел.БезИзменений Тогда
		ЭтаФорма["НаименованиеПоказателя__" + ИдентификаторЭлементаФормы] = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ": " + ВариантАнализа.Наименование + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность),", " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ", " + ТекстовоеПредставлениеПериода;
	Иначе
		ЭтаФорма["НаименованиеПоказателя__" + ИдентификаторЭлементаФормы] = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ": " + ВариантАнализа.Наименование + ", " + МониторингЦелевыхПоказателей.ПолучитьСтрокуПредставленияЧисел(ВариантАнализа) + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность), " " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ", " + ТекстовоеПредставлениеПериода;
	КонецЕсли;
	
	// Добавим поля формы
	ГруппаСтатусов = ГоризонтальнаяГруппа("Статусы__" + ИдентификаторЭлементаФормы, "Статусы", ГруппаЭлементов);
	
	ПолеЗначенияИндикатора = Элементы.Вставить("КартинкаСостояния__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаСтатусов);
	ПолеЗначенияИндикатора.Вид = ВидПоляФормы.ПолеКартинки;
	ПолеЗначенияИндикатора.ПутьКДанным = "КартинкаСостояния__" + ИдентификаторЭлементаФормы;
	ПолеЗначенияИндикатора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначенияИндикатора.Ширина = 2;
	ПолеЗначенияИндикатора.Высота = 1;
	ПолеЗначенияИндикатора.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	ПолеЗначенияИндикатора.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначенияИндикатора.РастягиватьПоВертикали = Ложь;
	ПолеЗначенияИндикатора.Подсказка = ИсточникДанных.СвойстваЗоныАнализаИТренда.ПояснениеКСтатусу;
	
	ПолеЗначенияИндикатора = Элементы.Вставить("КартинкаТренда__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаСтатусов);
	ПолеЗначенияИндикатора.Вид = ВидПоляФормы.ПолеКартинки;
	ПолеЗначенияИндикатора.ПутьКДанным = "КартинкаТренда__" + ИдентификаторЭлементаФормы;
	ПолеЗначенияИндикатора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеЗначенияИндикатора.Ширина = 2;
	ПолеЗначенияИндикатора.Высота = 1;
	ПолеЗначенияИндикатора.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	ПолеЗначенияИндикатора.РастягиватьПоГоризонтали = Ложь;
	ПолеЗначенияИндикатора.РастягиватьПоВертикали = Ложь;
	ПолеЗначенияИндикатора.Подсказка = ИсточникДанных.СвойстваЗоныАнализаИТренда.ПояснениеКТренду;
	
	ПолеНаименования = Элементы.Вставить("НаименованиеПоказателя__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаСтатусов);
	ПолеНаименования.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеНаименования.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеНаименования.ПутьКДанным = "НаименованиеПоказателя__" + ИдентификаторЭлементаФормы;
	ПолеНаименования.Ширина = 36;
	
	Если НЕ ПустойИсточникДанных И НЕ РасчетыНеактуальны И НЕ ОшибкаРасчета Тогда
		ПолеТаблицы = Элементы.Вставить("ТаблицаЗначений__" + ИдентификаторЭлементаФормы, Тип("ТаблицаФормы"), ГруппаЭлементов);
		ПолеТаблицы.ПутьКДанным = "ТаблицаЗначений__" + ИдентификаторЭлементаФормы;
		ПолеТаблицы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ПолеТаблицы.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
		ПолеТаблицы.ТолькоПросмотр = Истина;
		ПолеТаблицы.Ширина = 56;
		
		Если СтруктураНастроек.РазмерДиаграмм = 1 Тогда
			ПолеТаблицы.Высота = 8;
			
		Иначе
			ПолеТаблицы.Высота = 19;
			
		КонецЕсли;
		
		ПолеТаблицы.Шапка = Ложь;
		ПолеТаблицы.Подвал = ВыводитьИтогВТаблице;
		ПолеТаблицы.ГоризонтальныеЛинии = Ложь;
		ПолеТаблицы.ВертикальныеЛинии = Ложь;
		ПолеТаблицы.МножественныйВыбор = Ложь;
		ПолеТаблицы.КонтекстноеМеню.Доступность = Ложь;
		ПолеТаблицы.ЧередованиеЦветовСтрок = Истина;
		
		ПолеПоляТаблицы = Элементы.Вставить("КолонкаОбъектАнализа" + "__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ПолеТаблицы);
		ПолеПоляТаблицы.Вид = ВидПоляФормы.ПолеВвода;
		ПолеПоляТаблицы.АвтоВысотаЯчейки = Истина;
		ПолеПоляТаблицы.ПутьКДанным = "ТаблицаЗначений__" + ИдентификаторЭлементаФормы + "." + "КолонкаОбъектАнализа" + "__" + ИдентификаторЭлементаФормы;
		ПолеПоляТаблицы.ОтображатьВПодвале = Ложь;
		
		ПолеПоляТаблицы = Элементы.Вставить("КолонкаЗначениеАнализа" + "__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ПолеТаблицы);
		ПолеПоляТаблицы.Вид = ВидПоляФормы.ПолеВвода;
		ПолеПоляТаблицы.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
		ПолеПоляТаблицы.ПутьКДанным = "ТаблицаЗначений__" + ИдентификаторЭлементаФормы + "." + "КолонкаЗначениеАнализа" + "__" + ИдентификаторЭлементаФормы;
		
		Если ВыводитьИтогВТаблице Тогда 
			ПолеПоляТаблицы.ПутьКДаннымПодвала = "ИтогТаблицаЗначений__" + ИдентификаторЭлементаФормы;
			ПолеПоляТаблицы.ГоризонтальноеПоложениеВПодвале = ГоризонтальноеПоложениеЭлемента.Право;
			ПолеПоляТаблицы.ШрифтПодвала = Новый Шрифт(ПолеПоляТаблицы.ШрифтПодвала,,, Истина);
			
			КратностьЗначений			= ВариантАнализа.КратностьЗначений;
			ТочностьРасчетаДробнойЧасти = ВариантАнализа.ТочностьРасчетаДробнойЧасти;
			Размерность					= СокрЛП(Строка(ДинамическиеСвойстваВариантаАнализа.Размерность));
			
			ЭтаФорма["ИтогТаблицаЗначений__" + ИдентификаторЭлементаФормы] = МониторингЦелевыхПоказателей.ПолучитьСокращенноеПредставлениеЧисла(ИсточникДанных.Данные.Итог(ИсточникДанных.ДинамическиеСвойстваВариантаАнализа.ЗначениеАнализа), ТочностьРасчетаДробнойЧасти, КратностьЗначений) + " " + Размерность;
			
		КонецЕсли;
		
		МониторингЦелевыхПоказателей.ЗаполнитьТаблицуДляМонитора(ЭтаФорма["ТаблицаЗначений__" + ИдентификаторЭлементаФормы], ИдентификаторЭлементаФормы, ВариантАнализа, ИсточникДанных);
		
	Иначе
		ПолеДиаграммы = Элементы.Вставить("Диаграмма__" + ИдентификаторЭлементаФормы, Тип("ПолеФормы"), ГруппаЭлементов);
		ПолеДиаграммы.Вид = ВидПоляФормы.ПолеДиаграммы;
		ПолеДиаграммы.ПутьКДанным = "Диаграмма__" + ИдентификаторЭлементаФормы;
		ПолеДиаграммы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ПолеДиаграммы.Ширина = 56;
		ПолеДиаграммы.Высота = 8 * СтруктураНастроек.РазмерДиаграмм;
		
		Диаграмма = ЭтаФорма["Диаграмма__" + ИдентификаторЭлементаФормы];
		
		Если ИсточникДанных.Свойство("ОписаниеОшибки") Тогда
			ОписаниеОшибки = ИсточникДанных.ОписаниеОшибки;
			
		Иначе
			ОписаниеОшибки = НСтр("ru='Ошибка расчета показателя'");
			
		КонецЕсли;
		
		Диаграмма = ПолучитьДиаграммуПриОшибкахРасчета(Диаграмма, ОписаниеОшибки, ЦветаСтиля.ПоясняющийОшибкуТекст);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывает процедуры добавления элементов формы монитора - краткое представление, диаграммы и таблицы
// 
// Параметры:
//	СоставМонитора - Структура - содержит набор источников данных и обобщенную информацию об источниках
//
&НаСервере
Процедура НарисоватьЭлементыМонитора(СоставМонитора)
	
	СоздатьБазовуюСтруктуруЭлементовФормы(СоставМонитора);
	
	НаборИсточниковДанных = СоставМонитора.НаборИсточниковДанных;
	
	ВариантыОтображенияВариантовАнализа = Перечисления.ВариантыОтображенияВариантовАнализа;
	ЗоныВнимания = Перечисления.ЗоныВниманияВариантовАнализа;
	
	Если РежимАнализаВзаимосвязанных Тогда
		// Создадим элементы формы для анализируемого показателя
		ОтборАнализируемых = Новый Структура("Группа", Перечисления.ВидыПоискаСвязанныхПоказателей.ПустаяСсылка());
		Анализируемые = НаборИсточниковДанных.НайтиСтроки(ОтборАнализируемых);
		НарисоватьЭлементыМонитораСУчетомВидаАнализаВзаимосвязаных(Анализируемые, Перечисления.ВидыПоискаСвязанныхПоказателей.ПустаяСсылка());
		
		// Создадим элементы формы для взаимосвязанных показателей
		ОтборЗависимых = Новый Структура("Группа", Перечисления.ВидыПоискаСвязанныхПоказателей.ЗависимыеПоказатели);
		Зависимые = НаборИсточниковДанных.НайтиСтроки(ОтборЗависимых);
		
		Если Зависимые.Количество() > 0 Тогда
			 НарисоватьЭлементыМонитораСУчетомВидаАнализаВзаимосвязаных(Зависимые, Перечисления.ВидыПоискаСвязанныхПоказателей.ЗависимыеПоказатели);
			 
		КонецЕсли;
		
		ОтборВлияющих = Новый Структура("Группа", Перечисления.ВидыПоискаСвязанныхПоказателей.ВлияющиеПоказатели);
		Влияющие  = НаборИсточниковДанных.НайтиСтроки(ОтборВлияющих);
		
		Если Влияющие.Количество() > 0 Тогда
			 НарисоватьЭлементыМонитораСУчетомВидаАнализаВзаимосвязаных(Влияющие, Перечисления.ВидыПоискаСвязанныхПоказателей.ВлияющиеПоказатели);
			 
		КонецЕсли;
		
	Иначе 
		Если СтруктураНастроек.ВариантГруппировкиПоказателей = "ПоВажности" Тогда
			Отбор = Новый Структура("ЗонаВнимания, ВариантОтображения", ЗоныВнимания.КритическоеСостояние, ВариантыОтображенияВариантовАнализа.Кратко);
			ПоказателиКритическоеСостояниеКратко = НаборИсточниковДанных.НайтиСтроки(Отбор);
			
			Отбор = Новый Структура("ЗонаВнимания, ВариантОтображения", ЗоныВнимания.Важно, ВариантыОтображенияВариантовАнализа.Кратко);
			ПоказателиВажноКратко = НаборИсточниковДанных.НайтиСтроки(Отбор);
			
			Отбор = Новый Структура("ЗонаВнимания, ВариантОтображения", ЗоныВнимания.КСведению, ВариантыОтображенияВариантовАнализа.Кратко);
			ПоказателиКСведениюКратко = НаборИсточниковДанных.НайтиСтроки(Отбор);
			
			Отбор = Новый Структура("ЗонаВнимания, ВариантОтображения", ЗоныВнимания.РассчитанныеСОшибками, ВариантыОтображенияВариантовАнализа.Кратко);
			ПоказателиРассчитанныеСОшибкамиКратко = НаборИсточниковДанных.НайтиСтроки(Отбор);
			
			Отбор = Новый Структура("ВариантОтображения", ВариантыОтображенияВариантовАнализа.Кратко);
			НаборИсточниковДанныхНеКраткие = МониторингЦелевыхПоказателей.ПрименитьКТаблицеУсловиеНЕ(НаборИсточниковДанных.Скопировать(), Отбор);
			НаборИсточниковДанныхНеКраткие.Сортировать("Позиция");
			
			Отбор = Новый Структура("ЗонаВнимания", ЗоныВнимания.КритическоеСостояние);
			ПоказателиКритическоеСостояниеПодробно = НаборИсточниковДанныхНеКраткие.НайтиСтроки(Отбор);
			
			Отбор = Новый Структура("ЗонаВнимания", ЗоныВнимания.Важно);
			ПоказателиВажноПодробно = НаборИсточниковДанныхНеКраткие.НайтиСтроки(Отбор);
			
			Отбор = Новый Структура("ЗонаВнимания", ЗоныВнимания.КСведению);
			ПоказателиКСведениюПодробно = НаборИсточниковДанныхНеКраткие.НайтиСтроки(Отбор);
			
			Отбор = Новый Структура("ЗонаВнимания", ЗоныВнимания.РассчитанныеСОшибками);
			ПоказателиРассчитанныеСОшибкамиПодробно = НаборИсточниковДанныхНеКраткие.НайтиСтроки(Отбор);
			
			Если ПоказателиКритическоеСостояниеКратко.Количество() > 0 Тогда
				НарисоватьЭлементыМонитораСУчетомЗоныВнимания(ПоказателиКритическоеСостояниеКратко, ЗоныВнимания.КритическоеСостояние);
				
			КонецЕсли;
			Если ПоказателиКритическоеСостояниеПодробно.Количество() > 0 Тогда
				НарисоватьЭлементыМонитораСУчетомЗоныВнимания(ПоказателиКритическоеСостояниеПодробно, ЗоныВнимания.КритическоеСостояние);
				
			КонецЕсли;
			
			Если ПоказателиВажноКратко.Количество() > 0 Тогда
				НарисоватьЭлементыМонитораСУчетомЗоныВнимания(ПоказателиВажноКратко, ЗоныВнимания.Важно);
				
			КонецЕсли;
			Если ПоказателиВажноПодробно.Количество() > 0 Тогда
				НарисоватьЭлементыМонитораСУчетомЗоныВнимания(ПоказателиВажноПодробно, ЗоныВнимания.Важно);
				
			КонецЕсли;
			
			Если ПоказателиКСведениюКратко.Количество() > 0 Тогда
				НарисоватьЭлементыМонитораСУчетомЗоныВнимания(ПоказателиКСведениюКратко, ЗоныВнимания.КСведению);
				
			КонецЕсли;
			Если ПоказателиКСведениюПодробно.Количество() > 0 Тогда
				НарисоватьЭлементыМонитораСУчетомЗоныВнимания(ПоказателиКСведениюПодробно, ЗоныВнимания.КСведению);
				
			КонецЕсли;
			
			Если ПоказателиРассчитанныеСОшибкамиКратко.Количество() > 0 Тогда
				НарисоватьЭлементыМонитораСУчетомЗоныВнимания(ПоказателиРассчитанныеСОшибкамиКратко, ЗоныВнимания.РассчитанныеСОшибками);
				
			КонецЕсли;
			Если ПоказателиРассчитанныеСОшибкамиПодробно.Количество() > 0 Тогда
				НарисоватьЭлементыМонитораСУчетомЗоныВнимания(ПоказателиРассчитанныеСОшибкамиПодробно, ЗоныВнимания.РассчитанныеСОшибками);
				
			КонецЕсли;
			
		ИначеЕсли СтруктураНастроек.ВариантГруппировкиПоказателей = "ПоКатегориямЦелей" Тогда
			
			
			НаборГруппИсточниковДанныхТекстовые = НаборИсточниковДанных.Скопировать();
			НаборГруппИсточниковДанныхТекстовые.Колонки.Добавить("Количество", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(2,0));
			НаборГруппИсточниковДанныхТекстовые.Колонки.Добавить("КоличествоГруппСтрок", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(2,0));
			НаборГруппИсточниковДанныхТекстовые.ЗаполнитьЗначения(1, "Количество");
			НаборГруппИсточниковДанныхТекстовые.Свернуть("ВариантОтображения, Группа","Количество, КоличествоГруппСтрок");
			
			ОтборТекстовых = Новый Структура("ВариантОтображения", Перечисления.ВариантыОтображенияВариантовАнализа.Кратко);
			НаборГруппИсточниковДанныхТекстовые = МониторингЦелевыхПоказателей.ПрименитьКТаблицеУсловие(НаборГруппИсточниковДанныхТекстовые, ОтборТекстовых);
			
			Для Каждого ГруппаИсточникаДанных Из НаборГруппИсточниковДанныхТекстовые Цикл
				ГруппаИсточникаДанных.КоличествоГруппСтрок = ПолучитьКоличествоГруппСтрок(ГруппаИсточникаДанных.Количество);
				
			КонецЦикла;
			
			НаборГруппИсточниковДанныхПодробные = НаборИсточниковДанных.Скопировать();
			НаборГруппИсточниковДанныхПодробные.Колонки.Добавить("Количество", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(2,0));
			НаборГруппИсточниковДанныхПодробные.Колонки.Добавить("КоличествоГруппСтрок", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(2,0));
			НаборГруппИсточниковДанныхПодробные.ЗаполнитьЗначения(1, "Количество");
			НаборГруппИсточниковДанныхПодробные.Свернуть("ВариантОтображения, Группа","Количество, КоличествоГруппСтрок");
			
			НаборГруппИсточниковДанныхПодробные = МониторингЦелевыхПоказателей.ПрименитьКТаблицеУсловиеНЕ(НаборГруппИсточниковДанныхПодробные, ОтборТекстовых);
			НаборГруппИсточниковДанныхПодробные.Свернуть("Группа","Количество, КоличествоГруппСтрок");
			
			Для Каждого ГруппаИсточникаДанных Из НаборГруппИсточниковДанныхПодробные Цикл
				ГруппаИсточникаДанных.КоличествоГруппСтрок = ПолучитьКоличествоГруппСтрок(ГруппаИсточникаДанных.Количество);
				
			КонецЦикла;
			
			// Заполним текстовые показатели
			Для Каждого ГруппаТекстовогоПоказателя Из НаборГруппИсточниковДанныхТекстовые Цикл
				
				ГруппаПоказателя = ГруппаТекстовогоПоказателя.Группа;
				ИмяГруппы = ГруппаПоказателя.Наименование;
				ИдентификаторГруппы = СтрЗаменить(Строка(ГруппаПоказателя.УникальныйИдентификатор()),"-","_");
				
				ОтборПоГруппеТекстовые = Новый Структура("Группа, ВариантОтображения", ГруппаПоказателя, ВариантыОтображенияВариантовАнализа.Кратко);
				ТекстовыеПоказателиГруппы = НаборИсточниковДанных.НайтиСтроки(ОтборПоГруппеТекстовые);
				
				НарисоватьЭлементыМонитораСУчетомГрупп(ТекстовыеПоказателиГруппы, ИдентификаторГруппы);
				
			КонецЦикла;
			
			// Заполним диаграммы и таблицы
			Для Каждого ГруппаПодробногоПоказателя Из НаборГруппИсточниковДанныхПодробные Цикл
				
				ГруппаПоказателя = ГруппаПодробногоПоказателя.Группа;
				ИмяГруппы = ГруппаПоказателя.Наименование;
				ИдентификаторГруппы = СтрЗаменить(Строка(ГруппаПоказателя.УникальныйИдентификатор()),"-","_");
				
				НаборИсточниковДанныхНеТекстовые = МониторингЦелевыхПоказателей.ПрименитьКТаблицеУсловиеНЕ(НаборИсточниковДанных.Скопировать(), ОтборТекстовых);
				НаборИсточниковДанныхНеТекстовые.Сортировать("Позиция");
				
				ОтборПоГруппе = Новый Структура("Группа", ГруппаПоказателя);
				ПоказателиГруппы = НаборИсточниковДанныхНеТекстовые.НайтиСтроки(ОтборПоГруппе);
				
				НарисоватьЭлементыМонитораСУчетомГрупп(ПоказателиГруппы, ИдентификаторГруппы);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывает процедуры добавления элементов формы монитора в режиме анализа взаимосвязанных
// 
// Параметры:
//	КоллекцияВариантовАнализа - ТаблицаЗначений - содержит источники данных и их свойства
//	ВидПоискаВзаимосвязанных - ПеречислениеСсылка.ВидыПоискаСвязанныхПоказателей - значения зависимые, влияющие, взаимосвязанные, все
//
&НаСервере 
Процедура НарисоватьЭлементыМонитораСУчетомВидаАнализаВзаимосвязаных(КоллекцияВариантовАнализа, ВидПоискаВзаимосвязанных)
	
	ВидыПредставленияЧисел = Перечисления.ВидыПредставленияЧисел;
	ВариантыОтображения = Перечисления.ВариантыОтображенияВариантовАнализа;
	
	Если НЕ ВидПоискаВзаимосвязанных = Перечисления.ВидыПоискаСвязанныхПоказателей.ПустаяСсылка() Тогда
		ИмяВидаПоиска = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ВидПоискаВзаимосвязанных);
		
	Иначе
		ИмяВидаПоиска = НСтр("ru='АнализируемыйПоказатель'");
		
	КонецЕсли;
	
	Для Каждого ЭлементМонитора Из КоллекцияВариантовАнализа Цикл 
		
		ВариантАнализа = ЭлементМонитора.ВариантАнализа; 
		ИсточникДанных = ЭлементМонитора.ИсточникДанных.Получить();
		ДинамическиеСвойстваВариантаАнализа = ИсточникДанных.ДинамическиеСвойстваВариантаАнализа;
		ВариантОтображения = ВариантыОтображения.Диаграмма;
		ВидПредставленияЧисел = ВариантАнализа.КратностьЗначений;
		ИдентификаторЭлементаФормы = СтрЗаменить(Строка(ВариантАнализа.УникальныйИдентификатор()),"-","_");
		
		МножительРазмера = СтруктураНастроек.РазмерДиаграмм;
		
		ГруппаСтрокаЭлементов = Элементы["ГруппаКолонкаЭлементов" + ИмяВидаПоиска + "__" + "1"];
		ВысотаЭлемента = 10;
		
		ГруппаЭлемент = Элементы.Добавить("ЭлементДиаграмма__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), ГруппаСтрокаЭлементов);
		ГруппаЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаЭлемент.Отображение = ОтображениеОбычнойГруппы.Отступ;
		ГруппаЭлемент.ОтображатьЗаголовок = Ложь;
		
		Если ВидПредставленияЧисел = ВидыПредставленияЧисел.БезИзменений Тогда
			ГруппаЭлемент.Заголовок = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность)," (" + ДинамическиеСвойстваВариантаАнализа.Размерность + ")", "");
		Иначе
			ГруппаЭлемент.Заголовок = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + " (" + МониторингЦелевыхПоказателей.ПолучитьСтрокуПредставленияЧисел(ВариантАнализа) + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность), " " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ")";
		КонецЕсли;
		
		ШиринаЭлемента = 29;
		
		// Размеры
		ГруппаЭлемент.Ширина = ШиринаЭлемента;
		ГруппаЭлемент.Высота = ВысотаЭлемента * МножительРазмера;
		ГруппаЭлемент.РастягиватьПоВертикали = Ложь;
		
		// Наполним элемент контентом по шаблонам вариантов
		НарисоватьЭлементыПоказателя(ВариантАнализа, ИсточникДанных, ВариантОтображения, ГруппаЭлемент, ИдентификаторЭлементаФормы);
		
		// Заполним справочную информацию о показателе
		ГруппаСтрокаЭлементов = Элементы["ГруппаКолонкаЭлементов" + ИмяВидаПоиска + "__" + "2"];
		ВысотаЭлемента = 10;
		
		ГруппаЭлемент = Элементы.Добавить("ЭлементСправкаОПоказателе__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), ГруппаСтрокаЭлементов);
		ГруппаЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаЭлемент.Отображение = ОтображениеОбычнойГруппы.Отступ;
		ГруппаЭлемент.Заголовок = "Справка о показателе";
		
		ШиринаЭлемента = 29;
		
		// Размеры
		ГруппаЭлемент.Ширина = ШиринаЭлемента;
		ГруппаЭлемент.Высота = ВысотаЭлемента * МножительРазмера;
		ГруппаЭлемент.РастягиватьПоВертикали = Ложь;
		ГруппаЭлемент.ШрифтЗаголовка = Новый Шрифт(,, Истина);
		
		// Наполним элемент контентом по шаблонам вариантов
		НарисоватьПоляСправкиОПоказателе(ВариантАнализа, ИсточникДанных, ГруппаЭлемент, ИдентификаторЭлементаФормы);
		
	КонецЦикла;
	
КонецПроцедуры

// Вызывает процедуры добавления элементов формы монитора в группу в режиме вывода по категориям целей
// 
// Параметры:
//	КоллекцияВариантовАнализа - ТаблицаЗначений - содержит источники данных и их свойства
//	ИдентификаторГруппы - Строка - идентификатор группы, в которую будут добавлены элементы формы
//
&НаСервере 
Процедура НарисоватьЭлементыМонитораСУчетомГрупп(КоллекцияВариантовАнализа, ИдентификаторГруппы)
	
	ВидыПредставленияЧисел = Перечисления.ВидыПредставленияЧисел;
	ВариантыОтображения = Перечисления.ВариантыОтображенияВариантовАнализа;
	
	СчетчикСтрок = 1;
	СчетчикЭлементовСтроки = 1;
	КоличествоПоказателейВКоллекции = КоллекцияВариантовАнализа.Количество();
	КоличествоПоказателейВПоследнейСтроке = КоличествоПоказателейВКоллекции % СтруктураНастроек.КоличествоКолонокМонитораПоказателей;
	КоличествоНенарисованныхПоказателей = КоличествоПоказателейВКоллекции;
	
	Для Каждого ЭлементМонитора Из КоллекцияВариантовАнализа Цикл 
		
		ВариантАнализа = ЭлементМонитора.ВариантАнализа; 
		ИсточникДанных = ЭлементМонитора.ИсточникДанных.Получить();
		ДинамическиеСвойстваВариантаАнализа = ИсточникДанных.ДинамическиеСвойстваВариантаАнализа;
		ВариантОтображения = ЭлементМонитора.ВариантОтображения;
		ВидПредставленияЧисел = ВариантАнализа.КратностьЗначений;
		ИдентификаторЭлементаФормы = СтрЗаменить(Строка(ВариантАнализа.УникальныйИдентификатор()),"-","_");// + "__" + ЭлементМонитора.Позиция;
		
		Если ВариантОтображения = ВариантыОтображения.Кратко Тогда
			МножительРазмера = 1;
		Иначе
			МножительРазмера = СтруктураНастроек.РазмерДиаграмм;
		КонецЕсли;
		
		Если ВариантОтображения = ВариантыОтображения.Кратко Тогда
			ГруппаСтолбецЭлементов = Элементы["ГруппаКолонкаЭлементовКратко" + ИдентификаторГруппы + "__" + СчетчикЭлементовСтроки];
			ВысотаЭлемента = 1;
			
			Если ВидПредставленияЧисел = ВидыПредставленияЧисел.БезИзменений Тогда
				ЗаголовокЭлемента = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность)," (" + ДинамическиеСвойстваВариантаАнализа.Размерность + ")", "");
			Иначе
				ЗаголовокЭлемента = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + " (" + МониторингЦелевыхПоказателей.ПолучитьСтрокуПредставленияЧисел(ВариантАнализа) + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность), " " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ")";
			КонецЕсли;
			
			ГруппаЭлемент = ГоризонтальнаяГруппа("Элемент__" + ИдентификаторЭлементаФормы, ЗаголовокЭлемента, ГруппаСтолбецЭлементов);
			
			НарисоватьЭлементыПоказателя(ВариантАнализа, ИсточникДанных, ВариантОтображения, ГруппаЭлемент, ИдентификаторЭлементаФормы);
			
			КоличествоНенарисованныхПоказателей = КоличествоНенарисованныхПоказателей - 1;
			
			Если КоличествоНенарисованныхПоказателей = 0 И СчетчикЭлементовСтроки < СтруктураНастроек.КоличествоКолонокМонитораПоказателей Тогда
				Для СчВыравнивающихЭлементов = 1 По (СтруктураНастроек.КоличествоКолонокМонитораПоказателей - СчетчикЭлементовСтроки) Цикл 
					ГруппаСтрокаЭлементов = Элементы["ГруппаКолонкаЭлементовКратко" + ИдентификаторГруппы + "__" + (СчетчикЭлементовСтроки + СчВыравнивающихЭлементов)];
					
					ГруппаЭлемент = Элементы.Добавить("КонтейнерВыравнивающегоЭлемента__" + ИдентификаторЭлементаФормы + "__" + СчВыравнивающихЭлементов, Тип("ГруппаФормы"), ГруппаСтрокаЭлементов);
					ГруппаЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
					ГруппаЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
					ГруппаЭлемент.ОтображатьЗаголовок = Ложь;
					ГруппаЭлемент.Заголовок = "Контейнер выравнивающего элемента";
					
					НарисоватьПустойЭлемент(ГруппаЭлемент, ИдентификаторЭлементаФормы + "__" + СчВыравнивающихЭлементов, ВысотаЭлемента, 0, Истина);
					
				КонецЦикла;
				
			КонецЕсли;
			
			СчетчикЭлементовСтроки = СчетчикЭлементовСтроки + 1;
			
			Если СчетчикЭлементовСтроки > СтруктураНастроек.КоличествоКолонокМонитораПоказателей Тогда 
				СчетчикЭлементовСтроки = 1;
				
			КонецЕсли;
			
		Иначе
			ГруппаСтрокаЭлементов = Элементы["ГруппаКолонкаПодробныхЭлементов" + ИдентификаторГруппы + "__" + СчетчикЭлементовСтроки];
			ВысотаЭлемента = 10;
			
			ГруппаЭлемент = Элементы.Добавить("Элемент__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), ГруппаСтрокаЭлементов);
			ГруппаЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаЭлемент.Отображение = ОтображениеОбычнойГруппы.Отступ;
			ГруппаЭлемент.ОтображатьЗаголовок = Ложь;
			
			Если ВидПредставленияЧисел = ВидыПредставленияЧисел.БезИзменений Тогда
				ГруппаЭлемент.Заголовок = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + " (" + ДинамическиеСвойстваВариантаАнализа.Размерность + ")";
			Иначе
				ГруппаЭлемент.Заголовок = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + " (" + МониторингЦелевыхПоказателей.ПолучитьСтрокуПредставленияЧисел(ВариантАнализа) + " " + ДинамическиеСвойстваВариантаАнализа.Размерность + ")";
			КонецЕсли;
			
			Если СтруктураНастроек.КоличествоКолонокМонитораПоказателей < 2
				ИЛИ СтруктураНастроек.КоличествоКолонокМонитораПоказателей > 4 Тогда
				ШиринаЭлемента = 30;
			ИначеЕсли СтруктураНастроек.КоличествоКолонокМонитораПоказателей = 2 тогда
				ШиринаЭлемента = 29;
			ИначеЕсли СтруктураНастроек.КоличествоКолонокМонитораПоказателей = 3 тогда
				ШиринаЭлемента = 29;
			ИначеЕсли СтруктураНастроек.КоличествоКолонокМонитораПоказателей = 4 тогда
				ШиринаЭлемента = 28;
			КонецЕсли;
			
			// Размеры
			ГруппаЭлемент.Ширина = ШиринаЭлемента;
			ГруппаЭлемент.Высота = ВысотаЭлемента * МножительРазмера;
			ГруппаЭлемент.РастягиватьПоВертикали = Ложь;
			
			// Наполним элемент контентом по шаблонам вариантов
			НарисоватьЭлементыПоказателя(ВариантАнализа, ИсточникДанных, ВариантОтображения, ГруппаЭлемент, ИдентификаторЭлементаФормы);
			
			КоличествоНенарисованныхПоказателей = КоличествоНенарисованныхПоказателей - 1;
			
			Если КоличествоНенарисованныхПоказателей = 0 И СчетчикЭлементовСтроки < СтруктураНастроек.КоличествоКолонокМонитораПоказателей Тогда
				Для СчВыравнивающихЭлементов = 1 По (СтруктураНастроек.КоличествоКолонокМонитораПоказателей - СчетчикЭлементовСтроки) Цикл 
					ГруппаСтрокаЭлементов = Элементы["ГруппаКолонкаПодробныхЭлементов" + ИдентификаторГруппы + "__" + (СчетчикЭлементовСтроки + СчВыравнивающихЭлементов)];
					
					ГруппаЭлемент = Элементы.Добавить("КонтейнерВыравнивающегоЭлемента__" + ИдентификаторЭлементаФормы + "__" + СчВыравнивающихЭлементов, Тип("ГруппаФормы"), ГруппаСтрокаЭлементов);
					ГруппаЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
					ГруппаЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
					ГруппаЭлемент.ОтображатьЗаголовок = Ложь;
					ГруппаЭлемент.Заголовок = "Контейнер выравнивающего элемента";
					
					НарисоватьПустойЭлемент(ГруппаЭлемент, ИдентификаторЭлементаФормы + "__" + СчВыравнивающихЭлементов, ВысотаЭлемента, 0, Истина);
					
				КонецЦикла;
				
			КонецЕсли;
			
			СчетчикЭлементовСтроки = СчетчикЭлементовСтроки + 1;
			
			Если СчетчикЭлементовСтроки > СтруктураНастроек.КоличествоКолонокМонитораПоказателей Тогда 
				СчетчикЭлементовСтроки = 1;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Вызывает процедуры добавления элементов формы монитора в группу в режиме вывода по важности
// 
// Параметры:
//	КоллекцияВариантовАнализа - ТаблицаЗначений - содержит источники данных и их свойства
//	ЗонаВнимания - ПеречислениеСсылка.ЗоныВниманияВариантовАнализа - зона внимания/важности, в которую будут добавлены элменты формы
//
&НаСервере 
Процедура НарисоватьЭлементыМонитораСУчетомЗоныВнимания(КоллекцияВариантовАнализа, ЗонаВнимания)
	
	ВидыПредставленияЧисел = Перечисления.ВидыПредставленияЧисел;
	ВариантыОтображения = Перечисления.ВариантыОтображенияВариантовАнализа;
	ИмяЗоныВнимания = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ЗонаВнимания);
	
	СчетчикСтрок = 1;
	СчетчикЭлементовСтроки = 1;
	КоличествоПоказателейВКоллекции = КоллекцияВариантовАнализа.Количество();
	КоличествоНенарисованныхПоказателей = КоличествоПоказателейВКоллекции;
	
	Для Каждого ЭлементМонитора Из КоллекцияВариантовАнализа Цикл 
		
		ВариантАнализа = ЭлементМонитора.ВариантАнализа; 
		ИсточникДанных = ЭлементМонитора.ИсточникДанных.Получить();
		ДинамическиеСвойстваВариантаАнализа = ИсточникДанных.ДинамическиеСвойстваВариантаАнализа;
		ВариантОтображения = ЭлементМонитора.ВариантОтображения;
		ВидПредставленияЧисел = ВариантАнализа.КратностьЗначений;
		ИдентификаторЭлементаФормы = СтрЗаменить(Строка(ВариантАнализа.УникальныйИдентификатор()),"-","_");// + "__" + ЭлементМонитора.Позиция;
		
		Если ВариантОтображения = ВариантыОтображения.Кратко Тогда
			МножительРазмера = 1;
		Иначе
			МножительРазмера = СтруктураНастроек.РазмерДиаграмм;
		КонецЕсли;
		
		Если ВариантОтображения = ВариантыОтображения.Кратко Тогда
			
			ГруппаСтолбецЭлементов = Элементы["ГруппаКолонкаЭлементовКратко" + ИмяЗоныВнимания + "__" + СчетчикЭлементовСтроки];
			ВысотаЭлемента = 1;
			
			Если ВидПредставленияЧисел = ВидыПредставленияЧисел.БезИзменений Тогда
				ЗаголовокЭлемента = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность)," (" + ДинамическиеСвойстваВариантаАнализа.Размерность + ")", "");
			Иначе
				ЗаголовокЭлемента = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + " (" + МониторингЦелевыхПоказателей.ПолучитьСтрокуПредставленияЧисел(ВариантАнализа) + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность), " " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ")";
			КонецЕсли;
			
			
			ГруппаЭлемент = ГоризонтальнаяГруппа("Элемент__" + ИдентификаторЭлементаФормы, ЗаголовокЭлемента, ГруппаСтолбецЭлементов);
			
			НарисоватьЭлементыПоказателя(ВариантАнализа, ИсточникДанных, ВариантОтображения, ГруппаЭлемент, ИдентификаторЭлементаФормы);
			
			Если СтруктураНастроек.КоличествоКолонокМонитораПоказателей < 2
				ИЛИ СтруктураНастроек.КоличествоКолонокМонитораПоказателей > 4 Тогда
				ШиринаЭлемента = 30;
			ИначеЕсли СтруктураНастроек.КоличествоКолонокМонитораПоказателей = 2 тогда
				ШиринаЭлемента = 29;
			ИначеЕсли СтруктураНастроек.КоличествоКолонокМонитораПоказателей = 3 тогда
				ШиринаЭлемента = 29;
			ИначеЕсли СтруктураНастроек.КоличествоКолонокМонитораПоказателей = 4 тогда
				ШиринаЭлемента = 28;
			КонецЕсли;
			
			КоличествоНенарисованныхПоказателей = КоличествоНенарисованныхПоказателей - 1;
			
			Если КоличествоНенарисованныхПоказателей = 0 И СчетчикЭлементовСтроки < СтруктураНастроек.КоличествоКолонокМонитораПоказателей Тогда
				Для СчВыравнивающихЭлементов = 1 По (СтруктураНастроек.КоличествоКолонокМонитораПоказателей - СчетчикЭлементовСтроки) Цикл 
					ГруппаСтрокаЭлементов = Элементы["ГруппаКолонкаЭлементовКратко" + ИмяЗоныВнимания + "__" + (СчетчикЭлементовСтроки + СчВыравнивающихЭлементов)];
					
					ГруппаЭлемент = Элементы.Добавить("КонтейнерВыравнивающегоЭлемента__" + ИдентификаторЭлементаФормы + "__" + СчВыравнивающихЭлементов, Тип("ГруппаФормы"), ГруппаСтрокаЭлементов);
					ГруппаЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
					ГруппаЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
					ГруппаЭлемент.ОтображатьЗаголовок = Ложь;
					ГруппаЭлемент.Заголовок = "Контейнер выравнивающего элемента";
					
					НарисоватьПустойЭлемент(ГруппаЭлемент, ИдентификаторЭлементаФормы + "__" + СчВыравнивающихЭлементов, ВысотаЭлемента, 0, Истина);
					
				КонецЦикла;
				
			КонецЕсли;
			
			СчетчикЭлементовСтроки = СчетчикЭлементовСтроки + 1;
			
			Если СчетчикЭлементовСтроки > СтруктураНастроек.КоличествоКолонокМонитораПоказателей Тогда 
				СчетчикЭлементовСтроки = 1;
				
			КонецЕсли;
			
		Иначе
			
			ГруппаСтрокаЭлементов = Элементы["ГруппаКолонкаПодробныхЭлементов" + ИмяЗоныВнимания + "__" + СчетчикЭлементовСтроки];
			ВысотаЭлемента = 10;
			
			ГруппаЭлемент = Элементы.Добавить("Элемент__" + ИдентификаторЭлементаФормы, Тип("ГруппаФормы"), ГруппаСтрокаЭлементов);
			ГруппаЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаЭлемент.Отображение = ОтображениеОбычнойГруппы.Отступ;
			ГруппаЭлемент.ОтображатьЗаголовок = Ложь;
			
			Если ВидПредставленияЧисел = ВидыПредставленияЧисел.БезИзменений Тогда
				ГруппаЭлемент.Заголовок = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность)," (" + ДинамическиеСвойстваВариантаАнализа.Размерность + ")", "");
			Иначе
				ГруппаЭлемент.Заголовок = ВариантАнализа.Владелец.КраткоеНаименованиеЦелевогоПоказателя + " (" + МониторингЦелевыхПоказателей.ПолучитьСтрокуПредставленияЧисел(ВариантАнализа) + ?(НЕ ПустаяСтрока(ДинамическиеСвойстваВариантаАнализа.Размерность), " " + ДинамическиеСвойстваВариантаАнализа.Размерность, "") + ")";
			КонецЕсли;
			
			Если СтруктураНастроек.КоличествоКолонокМонитораПоказателей < 2
				ИЛИ СтруктураНастроек.КоличествоКолонокМонитораПоказателей > 4 Тогда
				ШиринаЭлемента = 30;
			ИначеЕсли СтруктураНастроек.КоличествоКолонокМонитораПоказателей = 2 тогда
				ШиринаЭлемента = 29;
			ИначеЕсли СтруктураНастроек.КоличествоКолонокМонитораПоказателей = 3 тогда
				ШиринаЭлемента = 29;
			ИначеЕсли СтруктураНастроек.КоличествоКолонокМонитораПоказателей = 4 тогда
				ШиринаЭлемента = 28;
			КонецЕсли;
			
			// Размеры
			ГруппаЭлемент.Ширина = ШиринаЭлемента;
			ГруппаЭлемент.Высота = ВысотаЭлемента * МножительРазмера;
			ГруппаЭлемент.РастягиватьПоВертикали = Ложь;
			
			// Наполним элемент контентом по шаблонам вариантов
			НарисоватьЭлементыПоказателя(ВариантАнализа, ИсточникДанных, ВариантОтображения, ГруппаЭлемент, ИдентификаторЭлементаФормы);
			
			КоличествоНенарисованныхПоказателей = КоличествоНенарисованныхПоказателей - 1;
			
			Если КоличествоНенарисованныхПоказателей = 0 И СчетчикЭлементовСтроки < СтруктураНастроек.КоличествоКолонокМонитораПоказателей Тогда
				Для СчВыравнивающихЭлементов = 1 По (СтруктураНастроек.КоличествоКолонокМонитораПоказателей - СчетчикЭлементовСтроки) Цикл 
					ГруппаСтрокаЭлементов = Элементы["ГруппаКолонкаПодробныхЭлементов" + ИмяЗоныВнимания + "__" + (СчетчикЭлементовСтроки + СчВыравнивающихЭлементов)];
					
					ГруппаЭлемент = Элементы.Добавить("КонтейнерВыравнивающегоЭлемента__" + ИдентификаторЭлементаФормы + "__" + СчВыравнивающихЭлементов, Тип("ГруппаФормы"), ГруппаСтрокаЭлементов);
					ГруппаЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
					ГруппаЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
					ГруппаЭлемент.ОтображатьЗаголовок = Ложь;
					ГруппаЭлемент.Заголовок = "Контейнер выравнивающего элемента";
					
					НарисоватьПустойЭлемент(ГруппаЭлемент, ИдентификаторЭлементаФормы + "__" + СчВыравнивающихЭлементов, ВысотаЭлемента, 0, Истина);
					
				КонецЦикла;
				
			КонецЕсли;
			
			СчетчикЭлементовСтроки = СчетчикЭлементовСтроки + 1;
			
			Если СчетчикЭлементовСтроки > СтруктураНастроек.КоличествоКолонокМонитораПоказателей Тогда 
				СчетчикЭлементовСтроки = 1;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Вызывает процедуры добавления элементов формы для переданного варианта анализа
// 
// Параметры:
//	ВариантАнализа - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей - вариант, для которого будут созданы элементы формы
//	ИсточникДанных - Структура, содержащая результаты расчета для варианта анализа
//	ВариантОтображения - ПеречислениеСсылка.ВариантыОтображенияВариантовАнализа - кратко, диаграмма, таблица
//	ГруппаЭлементов - ГруппаФормы - группа, в которую будут добавлены элементы
//	ИдентификаторЭлементаФормы - Строка - строквый GUID варианта анализа("-" заменены на "_")
//
&НаСервере
Процедура НарисоватьЭлементыПоказателя(ВариантАнализа, ИсточникДанных, ВариантОтображения, ГруппаЭлементов, ИдентификаторЭлементаФормы)
	
	Если ВариантОтображения = Перечисления.ВариантыОтображенияВариантовАнализа.Диаграмма Тогда
		
		НарисоватьДиаграмму(ВариантАнализа, ИсточникДанных, ГруппаЭлементов, ИдентификаторЭлементаФормы);
		
	ИначеЕсли ВариантОтображения = Перечисления.ВариантыОтображенияВариантовАнализа.Кратко Тогда
		
		НарисоватьКратко(ВариантАнализа, ИсточникДанных, ГруппаЭлементов, ИдентификаторЭлементаФормы);
		
	ИначеЕсли ВариантОтображения = Перечисления.ВариантыОтображенияВариантовАнализа.Таблица Тогда
		
		НарисоватьТаблицу(ВариантАнализа, ИсточникДанных, ГруппаЭлементов, ИдентификаторЭлементаФормы);
		
	КонецЕсли;
	
	ДобавитьКомандыВариантовАнализа(ВариантАнализа, ГруппаЭлементов, ИдентификаторЭлементаФормы, ВариантОтображения);
	
КонецПроцедуры

// Создает группы формы для последующего добавления в них элементов формы вариантов анализа
// 
// Параметры:
//	СоставМонитора - Структура - содержит набор источников данных и обобщенную информацию об источниках
//
&НаСервере
Процедура СоздатьБазовуюСтруктуруЭлементовФормы(СоставМонитора)
	
	СтатистикаПоСтрокам = СоставМонитора.СтатистикаПоСтрокам;
	НаборИсточниковДанных = СоставМонитора.НаборИсточниковДанных;
	
	ВариантыОтображенияВариантовАнализа = Перечисления.ВариантыОтображенияВариантовАнализа;
	ЗоныВнимания = Перечисления.ЗоныВниманияВариантовАнализа;
	
	ГруппаПоказатели = ВертикальнаяГруппа("ГруппаПоказатели", НСтр("ru='Показатели'"), Элементы.ОсновнаяСтраницаМонитора);
	
	Если РежимАнализаВзаимосвязанных Тогда
		ВариантАнализа = ПолучитьСвойстваВариантаАнализаПоИдентификатору(Параметры.ИдентификаторВариантаАнализа);
		НаименованиеВариантаАнализа = ПолучитьСвойстваВариантаАнализаПоИдентификатору(Параметры.ИдентификаторВариантаАнализа).Ссылка.Владелец.ЦелевойПоказатель;
		
		ГруппаАнализируемыйПоказатель = ГоризонтальнаяГруппа("ГруппаАнализируемыйПоказатель", НСтр("ru='Анализируемый показатель'") + " """ + НаименованиеВариантаАнализа + """", ГруппаПоказатели); 
		ГруппаАнализируемыйПоказатель.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
		ГруппаАнализируемыйПоказатель.ОтображатьЗаголовок = Истина;
		ГруппаАнализируемыйПоказатель.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
		
		ГруппаАнализируемыйПоказатель.ШрифтЗаголовка = Новый Шрифт(,, Истина);
		
		// Колонок в режиме анализа взаимосвязей всегда 2 - диаграмма, справка о показателей.
		Для Сч = 1 По 2 Цикл 
			ГруппаКолонкаЭлементов = ВертикальнаяГруппа("ГруппаКолонкаЭлементовАнализируемыйПоказатель" + "__" + Сч, НСтр("ru='Колонка №'") + Сч, ГруппаАнализируемыйПоказатель);
			
		КонецЦикла;
		
		ОтборЗависимых = Новый Структура("Группа", Перечисления.ВидыПоискаСвязанныхПоказателей.ЗависимыеПоказатели);
		Зависимые = НаборИсточниковДанных.НайтиСтроки(ОтборЗависимых);
		
		Если Зависимые.Количество() > 0 Тогда
			ИмяВидаПоиска = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(Перечисления.ВидыПоискаСвязанныхПоказателей.ЗависимыеПоказатели);
			
			ГруппаЗависимые = ГоризонтальнаяГруппа("Группа" + ИмяВидаПоиска, НСтр("ru='Зависимые показатели'"), ГруппаПоказатели); 
			ГруппаЗависимые.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
			ГруппаЗависимые.ОтображатьЗаголовок = Истина;
			ГруппаЗависимые.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
			
			ГруппаЗависимые.ШрифтЗаголовка = Новый Шрифт(,, Истина);
			ГруппаЗависимые.ЦветТекстаЗаголовка = WebЦвета.СинийСоСтальнымОттенком;
			
			// Колонок в режиме анализа взаимосвязей всегда 3 - диаграмма, целевое состояние, фактическое состояние.
			Для Сч = 1 По 2 Цикл 
				ГруппаКолонкаЭлементов = ВертикальнаяГруппа("ГруппаКолонкаЭлементов" + ИмяВидаПоиска + "__" + Сч, НСтр("ru='Колонка №'") + Сч, ГруппаЗависимые);
				
			КонецЦикла;
			
		КонецЕсли;
		
		ОтборВлияющих = Новый Структура("Группа", Перечисления.ВидыПоискаСвязанныхПоказателей.ВлияющиеПоказатели);
		Влияющие  = НаборИсточниковДанных.НайтиСтроки(ОтборВлияющих);
		
		Если Влияющие.Количество() > 0 Тогда
			ИмяВидаПоиска = МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(Перечисления.ВидыПоискаСвязанныхПоказателей.ВлияющиеПоказатели);
			
			ГруппаВлияющие = ГоризонтальнаяГруппа("Группа" + ИмяВидаПоиска, НСтр("ru='Влияющие показатели'"), ГруппаПоказатели); 
			ГруппаВлияющие.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
			ГруппаВлияющие.ОтображатьЗаголовок = Истина;
			ГруппаВлияющие.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
			
			ГруппаВлияющие.ШрифтЗаголовка = Новый Шрифт(,, Истина);
			ГруппаВлияющие.ЦветТекстаЗаголовка = WebЦвета.СинийСоСтальнымОттенком;
			
			// Колонок в режиме анализа взаимосвязей всегда 3 - диаграмма, целевое состояние, фактическое состояние.
			Для Сч = 1 По 2 Цикл 
				ГруппаКолонкаЭлементов = ВертикальнаяГруппа("ГруппаКолонкаЭлементов" + ИмяВидаПоиска + "__" + Сч, НСтр("ru='Колонка №'") + Сч, ГруппаВлияющие);
				
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		ГруппаПоказателиКратко = ВертикальнаяГруппа("ГруппаПоказателиКратко", НСтр("ru='Показатели (кратко)'"), ГруппаПоказатели);
		ГруппаПоказателиПодробно = ВертикальнаяГруппа("ГруппаПоказателиПодробно", НСтр("ru='Показатели (подробно)'"), ГруппаПоказатели);
		
		Если СтруктураНастроек.ВариантГруппировкиПоказателей = "ПоВажности" Тогда
			
			// Заполним краткие показатели
			Если СтатистикаПоСтрокам.КритическоеСостояниеКратко > 0 Тогда
				
				ГруппаКритическоеСостояниеКратко = ГоризонтальнаяГруппа("ГруппаКритическоеСостояниеКратко", ЗоныВнимания.КритическоеСостояние, ГруппаПоказателиКратко); 
				ГруппаКритическоеСостояниеКратко.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
				ГруппаКритическоеСостояниеКратко.ОтображатьЗаголовок = Истина;
				ГруппаКритическоеСостояниеКратко.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
				
				ГруппаКритическоеСостояниеКратко.ШрифтЗаголовка = Новый Шрифт(,, Истина);
				ГруппаКритическоеСостояниеКратко.ЦветТекстаЗаголовка = WebЦвета.Кирпичный;
				
				ДобавитьГруппыКолонкиЭлементов(ГруппаКритическоеСостояниеКратко, ВариантыОтображенияВариантовАнализа.Кратко, 
				МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ЗоныВнимания.КритическоеСостояние));
				
			КонецЕсли;
			
			Если СтатистикаПоСтрокам.ВажноКратко > 0 Тогда
				
				ГруппаВажноКратко = ГоризонтальнаяГруппа("ГруппаВажноКратко", ЗоныВнимания.Важно, ГруппаПоказателиКратко); 
				ГруппаВажноКратко.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
				ГруппаВажноКратко.ОтображатьЗаголовок = Истина;
				ГруппаВажноКратко.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
				
				ГруппаВажноКратко.ШрифтЗаголовка = Новый Шрифт(,, Истина);
				ГруппаВажноКратко.ЦветТекстаЗаголовка = WebЦвета.ТемноОранжевый;
				
				ДобавитьГруппыКолонкиЭлементов(ГруппаВажноКратко, ВариантыОтображенияВариантовАнализа.Кратко, 
				МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ЗоныВнимания.Важно));
				
			КонецЕсли;
			
			Если СтатистикаПоСтрокам.КСведениюКратко > 0 Тогда
				
				ГруппаКСведениюКратко = ГоризонтальнаяГруппа("ГруппаКСведениюКратко", ЗоныВнимания.КСведению, ГруппаПоказателиКратко); 
				ГруппаКСведениюКратко.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
				ГруппаКСведениюКратко.ОтображатьЗаголовок = Истина;
				ГруппаКСведениюКратко.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
				
				ГруппаКСведениюКратко.ЦветТекстаЗаголовка = WebЦвета.ЦветМорскойВолны;
				
				ДобавитьГруппыКолонкиЭлементов(ГруппаКСведениюКратко, ВариантыОтображенияВариантовАнализа.Кратко, 
				МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ЗоныВнимания.КСведению));
				
			КонецЕсли;
			
			Если СтатистикаПоСтрокам.РассчитанныеСОшибкамиКратко > 0 Тогда
				
				ГруппаРассчитанныеСОшибкамиКратко = ГоризонтальнаяГруппа("ГруппаРассчитанныеСОшибкамиКратко", ЗоныВнимания.РассчитанныеСОшибками, ГруппаПоказателиКратко); 
				ГруппаРассчитанныеСОшибкамиКратко.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
				ГруппаРассчитанныеСОшибкамиКратко.ОтображатьЗаголовок = Истина;
				ГруппаРассчитанныеСОшибкамиКратко.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
				
				ГруппаРассчитанныеСОшибкамиКратко.ШрифтЗаголовка = Новый Шрифт(,, Истина);
				ГруппаРассчитанныеСОшибкамиКратко.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветОсобогоТекста;
				
				ДобавитьГруппыКолонкиЭлементов(ГруппаРассчитанныеСОшибкамиКратко, ВариантыОтображенияВариантовАнализа.Кратко, 
				МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ЗоныВнимания.РассчитанныеСОшибками));
				
			КонецЕсли;
			
			// Заполним диаграммы и таблицы
			Если СтатистикаПоСтрокам.КритическоеСостояниеПодробно > 0 Тогда
				
				ГруппаКритическоеСостояниеПодробно = ГоризонтальнаяГруппа("ГруппаКритическоеСостояниеПодробно", ЗоныВнимания.КритическоеСостояние, ГруппаПоказателиПодробно); 
				ГруппаКритическоеСостояниеПодробно.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
				ГруппаКритическоеСостояниеПодробно.ОтображатьЗаголовок = Истина;
				ГруппаКритическоеСостояниеПодробно.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
				ГруппаКритическоеСостояниеПодробно.РастягиватьПоГоризонтали = Истина;
				
				ГруппаКритическоеСостояниеПодробно.ШрифтЗаголовка = Новый Шрифт(,, Истина);
				ГруппаКритическоеСостояниеПодробно.ЦветТекстаЗаголовка = WebЦвета.Кирпичный;
				
				ДобавитьГруппыКолонкиЭлементов(ГруппаКритическоеСостояниеПодробно,, 
				МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ЗоныВнимания.КритическоеСостояние));
				
			КонецЕсли;
			
			Если СтатистикаПоСтрокам.ВажноПодробно > 0 Тогда
				
				ГруппаВажноПодробно = ГоризонтальнаяГруппа("ГруппаВажноПодробно", ЗоныВнимания.Важно, ГруппаПоказателиПодробно); 
				ГруппаВажноПодробно.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
				ГруппаВажноПодробно.ОтображатьЗаголовок = Истина;
				ГруппаВажноПодробно.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
				ГруппаВажноПодробно.РастягиватьПоГоризонтали = Истина;
				
				ГруппаВажноПодробно.ШрифтЗаголовка = Новый Шрифт(,, Истина);
				ГруппаВажноПодробно.ЦветТекстаЗаголовка = WebЦвета.ТемноОранжевый;
				
				ДобавитьГруппыКолонкиЭлементов(ГруппаВажноПодробно,, 
				МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ЗоныВнимания.Важно));
				
			КонецЕсли;
			
			Если СтатистикаПоСтрокам.КСведениюПодробно > 0 Тогда
				
				ГруппаКСведениюПодробно = ГоризонтальнаяГруппа("ГруппаКСведениюПодробно", ЗоныВнимания.КСведению, ГруппаПоказателиПодробно); 
				ГруппаКСведениюПодробно.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
				ГруппаКСведениюПодробно.ОтображатьЗаголовок = Истина;
				ГруппаКСведениюПодробно.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
				ГруппаКСведениюПодробно.РастягиватьПоГоризонтали = Истина;
				
				ГруппаКСведениюПодробно.ЦветТекстаЗаголовка = WebЦвета.ЦветМорскойВолны;
				
				ДобавитьГруппыКолонкиЭлементов(ГруппаКСведениюПодробно,, 
				МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ЗоныВнимания.КСведению));
				
			КонецЕсли;
			
			Если СтатистикаПоСтрокам.РассчитанныеСОшибкамиПодробно > 0 Тогда
				
				ГруппаРассчитанныеСОшибкамиПодробно = ГоризонтальнаяГруппа("ГруппаРассчитанныеСОшибкамиПодробно", ЗоныВнимания.РассчитанныеСОшибками, ГруппаПоказателиПодробно); 
				ГруппаРассчитанныеСОшибкамиПодробно.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
				ГруппаРассчитанныеСОшибкамиПодробно.ОтображатьЗаголовок = Истина;
				ГруппаРассчитанныеСОшибкамиПодробно.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
				ГруппаРассчитанныеСОшибкамиПодробно.РастягиватьПоГоризонтали = Истина;
				
				ГруппаРассчитанныеСОшибкамиПодробно.ШрифтЗаголовка = Новый Шрифт(,, Истина);
				ГруппаРассчитанныеСОшибкамиПодробно.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветОсобогоТекста;
				
				ДобавитьГруппыКолонкиЭлементов(ГруппаРассчитанныеСОшибкамиПодробно,, 
				МониторингЦелевыхПоказателей.ПолучитьИмяЗначенияПеречисления(ЗоныВнимания.РассчитанныеСОшибками));
				
			КонецЕсли;
			
		ИначеЕсли СтруктураНастроек.ВариантГруппировкиПоказателей = "ПоКатегориямЦелей" Тогда
			
			НаборГруппИсточниковДанныхТекстовые = НаборИсточниковДанных.Скопировать();
			НаборГруппИсточниковДанныхТекстовые.Колонки.Добавить("Количество", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(2,0));
			НаборГруппИсточниковДанныхТекстовые.Колонки.Добавить("КоличествоГруппСтрок", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(2,0));
			НаборГруппИсточниковДанныхТекстовые.ЗаполнитьЗначения(1, "Количество");
			НаборГруппИсточниковДанныхТекстовые.Свернуть("ВариантОтображения, Группа","Количество, КоличествоГруппСтрок");
			
			ОтборТекстовых = Новый Структура("ВариантОтображения", Перечисления.ВариантыОтображенияВариантовАнализа.Кратко);
			НаборГруппИсточниковДанныхТекстовые = МониторингЦелевыхПоказателей.ПрименитьКТаблицеУсловие(НаборГруппИсточниковДанныхТекстовые, ОтборТекстовых);
			
			Для Каждого ГруппаИсточникаДанных Из НаборГруппИсточниковДанныхТекстовые Цикл
				ГруппаИсточникаДанных.КоличествоГруппСтрок = ПолучитьКоличествоГруппСтрок(ГруппаИсточникаДанных.Количество);
				
			КонецЦикла;
			
			НаборГруппИсточниковДанныхПодробные = НаборИсточниковДанных.Скопировать();
			НаборГруппИсточниковДанныхПодробные.Колонки.Добавить("Количество", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(2,0));
			НаборГруппИсточниковДанныхПодробные.Колонки.Добавить("КоличествоГруппСтрок", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(2,0));
			НаборГруппИсточниковДанныхПодробные.ЗаполнитьЗначения(1, "Количество");
			НаборГруппИсточниковДанныхПодробные.Свернуть("ВариантОтображения, Группа","Количество, КоличествоГруппСтрок");
			
			НаборГруппИсточниковДанныхПодробные = МониторингЦелевыхПоказателей.ПрименитьКТаблицеУсловиеНЕ(НаборГруппИсточниковДанныхПодробные, ОтборТекстовых);
			НаборГруппИсточниковДанныхПодробные.Свернуть("Группа","Количество, КоличествоГруппСтрок");
			
			Для Каждого ГруппаИсточникаДанных Из НаборГруппИсточниковДанныхПодробные Цикл
				ГруппаИсточникаДанных.КоличествоГруппСтрок = ПолучитьКоличествоГруппСтрок(ГруппаИсточникаДанных.Количество);
				
			КонецЦикла;
			
			// Заполним текстовые показатели
			Для Каждого ГруппаТекстовогоПоказателя Из НаборГруппИсточниковДанныхТекстовые Цикл 
				ИмяГруппы = ГруппаТекстовогоПоказателя.Группа.Наименование;
				ИдентификаторГруппы = СтрЗаменить(Строка(ГруппаТекстовогоПоказателя.Группа.УникальныйИдентификатор()),"-","_");
				КоличествоГруппТекстовых = ГруппаИсточникаДанных.КоличествоГруппСтрок;
				
				ГруппаТекстовыеЗаголовок = ГоризонтальнаяГруппа("ГруппаТекстовыеЗаголовок__" + ИдентификаторГруппы, ИмяГруппы, ГруппаПоказателиКратко); 
				ГруппаТекстовыеЗаголовок.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
				ГруппаТекстовыеЗаголовок.ОтображатьЗаголовок = Истина;
				ГруппаТекстовыеЗаголовок.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
				
				ГруппаТекстовыеЗаголовок.ШрифтЗаголовка = Новый Шрифт(ГруппаТекстовыеЗаголовок.ШрифтЗаголовка,,,Истина);
				
				ДобавитьГруппыКолонкиЭлементов(ГруппаТекстовыеЗаголовок, ВариантыОтображенияВариантовАнализа.Кратко, 
				ИдентификаторГруппы);
				
			КонецЦикла;
			
			// Заполним таблицы и диаграммы
			Для Каждого ГруппаТабличногоИлиДиаграммногоПоказателя Из НаборГруппИсточниковДанныхПодробные Цикл 
				ИмяГруппы = ГруппаТабличногоИлиДиаграммногоПоказателя.Группа.Наименование;
				ИдентификаторГруппы = СтрЗаменить(Строка(ГруппаТабличногоИлиДиаграммногоПоказателя.Группа.УникальныйИдентификатор()),"-","_");
				КоличествоГруппТаблицИДиаграмм = ГруппаТабличногоИлиДиаграммногоПоказателя.КоличествоГруппСтрок;
				
				ГруппаТабличныеИДиаграммные = ГоризонтальнаяГруппа("ГруппаТабличныеИДиаграммные__" + ИдентификаторГруппы, ИмяГруппы, ГруппаПоказателиПодробно); 
				ГруппаТабличныеИДиаграммные.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
				ГруппаТабличныеИДиаграммные.ОтображатьЗаголовок = Истина;
				ГруппаТабличныеИДиаграммные.Отображение = ОтображениеОбычнойГруппы.РамкаГруппы;
				ГруппаТабличныеИДиаграммные.РастягиватьПоГоризонтали = Истина;
				
				ГруппаТабличныеИДиаграммные.ШрифтЗаголовка = Новый Шрифт(,, Истина);
				
				ДобавитьГруппыКолонкиЭлементов(ГруппаТабличныеИДиаграммные,, 
				ИдентификаторГруппы);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ЗаполнитьСписокВыбораОтбораПоСтатусу()
	
	СписокВыбора = Элементы.ОтборСтатус.СписокВыбора;
	
	СписокВыбора.Добавить(0, "Все");
	СписокВыбора.Добавить(1, "Критические");
	СписокВыбора.Добавить(2, "Важные");
	СписокВыбора.Добавить(3, "Критические и важные");
	СписокВыбора.Добавить(4, "К сведению");
	СписокВыбора.Добавить(5, "Рассчитанные с ошибкой");
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьОбработчикАвтообновления(ПериодАвтообновления = Неопределено)
	
	Если ПериодАвтообновления = Неопределено Тогда
		ПериодАвтообновления = СтруктураНастроек.ПериодАвтообновления * 60;
	КонецЕсли;
	
	Если ПериодАвтообновления > 0 Тогда
		ОтключитьОбработчикОжидания("ОбработчикОжидания_ОбновитьСоставМонитораКлиент");
		ПодключитьОбработчикОжидания("ОбработчикОжидания_ОбновитьСоставМонитораКлиент", ПериодАвтообновления);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьОбработчикПервогоЗапуска()
	
	ПериодПервогоЗапуска = 0.1;
	
	ПодключитьОбработчикОжидания("ОбработчикОжидания_ОбновитьСоставМонитораКлиент", ПериодПервогоЗапуска, Истина);
	
КонецПроцедуры

&НаСервере 
Процедура ОбновлениеСтруктурыНастроекСервер()
	
	СтруктураНастроек = МониторингЦелевыхПоказателей.ПолучитьНастройкиМонитораЦелевыхПоказателей();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСУчетомНеобходимостиУдаления(МассивСуществующихРеквизитов)
	
	НеудаляемыеРеквизиты = Новый СписокЗначений;
	НеудаляемыеРеквизиты.Добавить("ВариантыАнализаДляПечати");
	НеудаляемыеРеквизиты.Добавить("ВариантыАнализаСНарастающимИтогом");
	НеудаляемыеРеквизиты.Добавить("ВариантыАнализаСПодробностями");
	НеудаляемыеРеквизиты.Добавить("ДоступныеОтчеты");
	НеудаляемыеРеквизиты.Добавить("ОтборСтатус");
	НеудаляемыеРеквизиты.Добавить("ТекущийПользователь");
	НеудаляемыеРеквизиты.Добавить("РежимАнализаВзаимосвязанных");
	НеудаляемыеРеквизиты.Добавить("СоставМонитора");
	НеудаляемыеРеквизиты.Добавить("СоставМонитораНеопределен");
	НеудаляемыеРеквизиты.Добавить("СоставМонитораПустойПоОтбору");
	НеудаляемыеРеквизиты.Добавить("СтруктураНастроек");
	НеудаляемыеРеквизиты.Добавить("ТабличныеВариантыАнализаСИтогами");
	
	КоличествоНеУдаляемыхРеквизитов = НеудаляемыеРеквизиты.Количество();
	
	Для Счетчик = 1 По КоличествоНеУдаляемыхРеквизитов Цикл 
		МассивСуществующихРеквизитов.Удалить(0);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбработчикОжидания_ОбновитьСоставМонитораКлиент()
	
	ОбновитьСоставМонитораКлиент();
	
КонецПроцедуры

&НаСервере 
Процедура ОбновитьСоставМонитора(ОтборПоказателей = Неопределено, ПринудительноОбновитьДанные = Ложь)
	
	Элементы.ПояснениеПриЗапуске.Видимость = Ложь;
	
	// Удалим ранее созданные программно элементы формы
	ГруппаПоказатели = Элементы.Найти("ГруппаПоказатели");
	Если НЕ ГруппаПоказатели = Неопределено Тогда
		Элементы.Удалить(ГруппаПоказатели);
	КонецЕсли;
	
	// Удалим ранее созданные программно реквизиты
	МассивСуществующихРеквизитов = ПолучитьРеквизиты();
	
	ОбработатьСУчетомНеобходимостиУдаления(МассивСуществующихРеквизитов);
	
	Если МассивСуществующихРеквизитов.Количество() > 0 Тогда
		МассивПутейУдаляемыхРеквизитов = Новый Массив;
		Для Каждого СуществующийРеквизит Из МассивСуществующихРеквизитов Цикл 
			МассивПутейУдаляемыхРеквизитов.Добавить(СуществующийРеквизит.Имя);
		КонецЦикла;
		ИзменитьРеквизиты(, МассивПутейУдаляемыхРеквизитов);
		
	КонецЕсли;
	
	// Удалим ранее созданные программно команды
	МассивУдаляемыхКоманд = Новый Массив;
	Для Каждого Команда Из Команды Цикл 
		ИмяКоманды = Команда.Имя;
		
		Если Найти(ИмяКоманды, "Обновить") 
			ИЛИ Найти(ИмяКоманды, "СводныйОтчет")
			ИЛИ Найти(ИмяКоманды, "Настроить") Тогда
			Продолжить;
		КонецЕсли;
		
		МассивУдаляемыхКоманд.Добавить(ИмяКоманды);
		
	КонецЦикла;
	
	Для Каждого УдаляемаяКоманда Из МассивУдаляемыхКоманд Цикл
		Команды.Удалить(Команды.Найти(УдаляемаяКоманда));
		
	КонецЦикла;
	
	// Получить список состава монитора (показатель, тип, позиция)
	Если РежимАнализаВзаимосвязанных Тогда 
		
		СвойстваВариантаАнализа = ПолучитьСвойстваВариантаАнализаПоИдентификатору(Параметры.ИдентификаторВариантаАнализа);
		ВидПоиска = Перечисления.ВидыПоискаСвязанныхПоказателей[Параметры.ВидПоиска];
		
		АдресаДинамическихПараметров = ДанныеФормыВЗначение(Параметры.СоставМонитора, Тип("ТаблицаЗначений"));
		
		ПолученныйСоставМонитора = МониторингЦелевыхПоказателей.ПолучитьВзаимосвязанныеВариантыАнализа(ВидПоиска, СвойстваВариантаАнализа.Ссылка, ОтборСтатус, АдресаДинамическихПараметров);
		
	Иначе
		ПолученныйСоставМонитора = ПолучитьСоставМонитора(ОтборПоказателей, ПринудительноОбновитьДанные);
		
	КонецЕсли;
	
	ЗаполнитьСводнуюИнформациюПоИсточникамДанных(ПолученныйСоставМонитора, АдресаДинамическихПараметров);
		
	СоставМонитораНеопределен = (ПолученныйСоставМонитора.НаборИсточниковДанных = Неопределено);
	СоставМонитораПустойПоОтбору = (ПолученныйСоставМонитора.Пустой И Не СоставМонитораНеопределен);
	
	УстановитьВидимостьПредложенияДобавитьПоказателиВСоставМонитора(СоставМонитораНеопределен);
	УстановитьВидимостьСообщенияПустойРезультатОтбора(СоставМонитораПустойПоОтбору);
	
	Если НЕ СоставМонитораНеопределен И НЕ СоставМонитораПустойПоОтбору Тогда
		// Создать реквизиты формы по составу монитора
		// К имени элементов добавляется строка "__"+ID_Элемента, где
		// ID_Элемента = Показатель.УникальныйИдентификатор()
		НарисоватьЭлементыМонитора(ПолученныйСоставМонитора);
		
	КонецЕсли;
	
	ДоступныеОтчеты.Очистить();
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбновитьСоставМонитораКлиент(ПринудительноОбновитьДанные = Ложь)
	
	Состояние(НСтр("ru='Пожалуйста подождите, идет пересчет показателей...'"));
	
	ОбновитьСоставМонитора(, ПринудительноОбновитьДанные);
	
	ТекущийЭлемент = Элементы.ОсновнаяСтраницаМонитора;
	
	Состояние();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВариантОтчетаПоИдентификатору(Идентификатор)
	
	Перем ВариантОтчета;
	
	ВариантОтчета = ПолучитьСсылкуСправочникаПоИдентификатору("ВариантыОтчетов", Идентификатор);
	
	Возврат ВариантОтчета;
	
КонецФункции

&НаСервере 
Функция ТекстЗапросаВариантовОтчетов()
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ИдентификаторыМетаданных.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ИдентификаторыМетаданных
	|ИЗ
	|	Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыМетаданных
	|ГДЕ
	|	ИдентификаторыМетаданных.Ссылка В(&ДоступныеВариантыОтчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетыВариантовАнализа.ВариантОтчета КАК Ссылка,
	|	ВариантыОтчетов.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ВариантыАнализаЦелевыхПоказателей.ОтчетыДляРасшифровки КАК ОтчетыВариантовАнализа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|		ПО ОтчетыВариантовАнализа.ВариантОтчета = ВариантыОтчетов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИдентификаторыМетаданных КАК ИдентификаторыМетаданных
	|		ПО (ВариантыОтчетов.Отчет = ИдентификаторыМетаданных.Ссылка)
	|ГДЕ
	|	ОтчетыВариантовАнализа.Ссылка = &ВариантАнализа
	|	И НЕ ВариантыОтчетов.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВариантыОтчетов.Наименование";
	
	Возврат ТекстЗапроса;
КонецФункции

&НаСервере
Функция ПолучитьВариантыОтчетовПоказателя(ВариантАнализа)
	Перем ВариантыОтчетовПоказателя;
	
	ВариантыОтчетовПоказателя = Новый СписокЗначений;
	
	Если ДоступныеОтчеты.Количество() = 0 Тогда
		ДоступныеОтчеты.ЗагрузитьЗначения(ВариантыОтчетовПовтИсп.ОтчетыКонфигурацииДоступныеТекущемуПользователю());
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВариантовОтчетов();
	Запрос.УстановитьПараметр("ДоступныеВариантыОтчетов", ДоступныеОтчеты);
	Запрос.УстановитьПараметр("ВариантАнализа", ВариантАнализа);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаВариантов = РезультатЗапроса.Выбрать();
		Пока ВыборкаВариантов.Следующий() Цикл
			ВариантыОтчетовПоказателя.Добавить(СтрЗаменить(ВыборкаВариантов.Ссылка.УникальныйИдентификатор(), "-", "_"), ВыборкаВариантов.Наименование);	
		КонецЦикла;
	КонецЕсли;
	
	Возврат ВариантыОтчетовПоказателя;
КонецФункции

&НаСервере 
Функция ПолучитьДиаграммуПриОшибкахРасчета(ДиаграммаПриОшибках, ТекстОшибки, ЦветТекста)
	
	// Установим заголовок с ошибкой
	ДиаграммаПриОшибках.ОбластьЗаголовка.Верх = 0;
	ДиаграммаПриОшибках.ОбластьЗаголовка.Лево = 0;
	ДиаграммаПриОшибках.ОбластьЗаголовка.Низ = 1;
	ДиаграммаПриОшибках.ОбластьЗаголовка.Право = 1;
	ДиаграммаПриОшибках.ОбластьЗаголовка.Текст = ТекстОшибки;
	ДиаграммаПриОшибках.ОбластьЗаголовка.ЦветТекста = ЦветТекста;
	
	Возврат ДиаграммаПриОшибках;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИдентификаторВариантаОтчета(ИмяЭлемента)
	
	Перем Идентификатор;
	
	Идентификатор = "";
	
	ДваИдентификатора = Прав(ИмяЭлемента, СтрДлина(ИмяЭлемента) - Найти(ИмяЭлемента, "__") - 1);
	Идентификатор = Прав(ДваИдентификатора, Найти(ДваИдентификатора, "__") - 1); 
	
	Возврат Идентификатор;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИдентификаторДинамическогоПараметра(ИмяЭлемента)
	
	Перем Идентификатор;
	
	Идентификатор = "";
	
	ДваИдентификатора = Прав(ИмяЭлемента, СтрДлина(ИмяЭлемента) - Найти(ИмяЭлемента, "__") - 1);
	Идентификатор = Прав(ДваИдентификатора, СтрДлина(ДваИдентификатора) - Найти(ДваИдентификатора, "__") - 1); 
	
	Возврат Идентификатор;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИдентификаторЭлементаПоИмениБезПозиции(ИмяЭлемента)
	
	Перем Идентификатор;
	
	Идентификатор = "";
	
	ИдентификаторСПозицией = Прав(ИмяЭлемента, СтрДлина(ИмяЭлемента) - Найти(ИмяЭлемента, "__") - 1);
	Если Найти(ИдентификаторСПозицией, "__") > 0 Тогда
		Идентификатор = Лев(ИдентификаторСПозицией, Найти(ИдентификаторСПозицией, "__") - 1); 
	Иначе
		Идентификатор = ИдентификаторСПозицией
	КонецЕсли;
	
	Возврат Идентификатор;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИдентификаторЭлементаПоИмениСПозицией(ИмяЭлемента)
	
	Перем Идентификатор;
	
	Идентификатор = "";
	
	Идентификатор = Прав(ИмяЭлемента, СтрДлина(ИмяЭлемента) - Найти(ИмяЭлемента, "__") - 1);
	
	Возврат Идентификатор;
	
КонецФункции

&НаСервере
Функция ПолучитьКоличествоГруппСтрок(КоличествоВыводимыхЭлементов, МножительРазмера = 1)
	
	Перем КоличествоГруппСтрок;
	
	КоличествоГруппСтрок = 0;
	
	КоличествоНеделимыхЭлементовВСтроке = СтруктураНастроек.КоличествоКолонокМонитораПоказателей/МножительРазмера;
	
	ПредварительноеКоличествоСтрок = КоличествоВыводимыхЭлементов/?(КоличествоНеделимыхЭлементовВСтроке < 1, 1, Окр(КоличествоНеделимыхЭлементовВСтроке, 0, РежимОкругления.Окр15как10));
	ПредварительноеЦелоеКоличествоСтрок = Цел(ПредварительноеКоличествоСтрок);
	ПредварительноеДробноеКоличествоСтрок = ПредварительноеКоличествоСтрок - ПредварительноеЦелоеКоличествоСтрок;
	
	Если ПредварительноеДробноеКоличествоСтрок > 0 Тогда
		КоличествоГруппСтрок = ПредварительноеЦелоеКоличествоСтрок + 1;
		
	Иначе
		КоличествоГруппСтрок = ПредварительноеЦелоеКоличествоСтрок;
		
	КонецЕсли;
	
	Если КоличествоГруппСтрок = 0 И НЕ КоличествоВыводимыхЭлементов = 0 Тогда
		КоличествоГруппСтрок = 1;
	КонецЕсли;
	
	Возврат КоличествоГруппСтрок;
	
КонецФункции

&НаСервере
Функция ПолучитьСвойстваВариантаАнализаПоИдентификатору(Идентификатор)
	
	Перем СтруктураПоказателя;
	
	ВариантАнализа = ПолучитьСсылкуСправочникаПоИдентификатору("ВариантыАнализаЦелевыхПоказателей", Идентификатор);
	
	СтруктураПоказателя = Новый Структура("Ссылка, ПериодАнализа", ВариантАнализа, ВариантАнализа.ПериодАнализа.Получить());
	
	Возврат СтруктураПоказателя;
	
КонецФункции

&НаСервере
Функция ПолучитьСвойстваВариантаОтчета(ВариантОтчета)
	
	СвойстваВариантаОтчета = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	СвойстваВариантаОтчета = Новый Структура("КлючВарианта, КлючОбъекта", ВариантОтчета.КлючВарианта, ВариантОтчета.Отчет.ПолноеИмя);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СвойстваВариантаОтчета;
	
КонецФункции

&НаСервере
Функция ПолучитьСоставМонитора(ОтборВариантовАнализа = Неопределено, ПринудительноОбновитьДанные = Ложь)
	
	ПолученныйСоставМонитора			= Новый Структура("НаборИсточниковДанных, СтатистикаПоСтрокам, Пустой", , , Истина);
	НаборИсточниковДанных				= Новый ТаблицаЗначений;
	ТекущийПользователь					= Пользователи.ТекущийПользователь();
	ВариантыОтображенияВариантовАнализа	= Перечисления.ВариантыОтображенияВариантовАнализа;
	ЗоныВнимания						= Перечисления.ЗоныВниманияВариантовАнализа;
	
	ЗапросСостава = Новый Запрос;
	ЗапросСостава.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	ЗапросСостава.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	
	Если ОтборВариантовАнализа = Неопределено Тогда 
	
		Если НЕ СтруктураНастроек.ВариантыАнализаПользовательскиеНастройки = Неопределено
			И НЕ СтруктураНастроек.ВариантыАнализаПользовательскиеНастройки.Получить() = Неопределено Тогда
			ЗапросСостава.УстановитьПараметр("ВариантыАнализа", СтруктураНастроек.ВариантыАнализаПользовательскиеНастройки.Получить());
			ЗапросСостава.Текст = "ВЫБРАТЬ
			                      |	ПользовательскиеНастройкиВариантовАнализа.ВариантАнализа,
			                      |	ПользовательскиеНастройкиВариантовАнализа.Доступность,
			                      |	ПользовательскиеНастройкиВариантовАнализа.ВариантОтображения,
			                      |	ПользовательскиеНастройкиВариантовАнализа.Детали,
			                      |	ПользовательскиеНастройкиВариантовАнализа.ИтогТаблицы
			                      |ПОМЕСТИТЬ ПользовательскиеНастройкиВариантовАнализа
			                      |ИЗ
			                      |	&ВариантыАнализа КАК ПользовательскиеНастройкиВариантовАнализа
			                      |;
			                      |
			                      |////////////////////////////////////////////////////////////////////////////////
			                      |ВЫБРАТЬ
			                      |	ВариантыАнализа.Цель,
			                      |	ВариантыАнализа.ЦелевойПоказатель,
			                      |	ВариантыАнализа.ВариантАнализа,
			                      |	ВариантыАнализа.ВариантОтображения,
			                      |	ВариантыАнализа.ВыводитьПодробности,
			                      |	ВариантыАнализа.ВыводитьИтогТаблицы,
			                      |	ВариантыАнализа.СостояниеПоНарастающемуИтогу,
			                      |	ВариантыАнализа.Группа,
			                      |	ВариантыАнализа.КраткоеНаименованиеЦелевогоПоказателя
			                      |ИЗ
			                      |	(ВЫБРАТЬ
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец КАК Цель,
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.ЦелевойПоказатель КАК ЦелевойПоказатель,
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка КАК ВариантАнализа,
			                      |		ВЫБОР
			                      |			КОГДА ПользовательскиеНастройкиВариантовАнализа.Доступность ЕСТЬ NULL 
			                      |				ТОГДА ИСТИНА
			                      |			ИНАЧЕ ПользовательскиеНастройкиВариантовАнализа.Доступность
			                      |		КОНЕЦ КАК Доступность,
			                      |		ВЫБОР
			                      |			КОГДА ПользовательскиеНастройкиВариантовАнализа.ВариантОтображения ЕСТЬ NULL 
			                      |				ТОГДА ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.ВариантОтображения
			                      |			ИНАЧЕ ПользовательскиеНастройкиВариантовАнализа.ВариантОтображения
			                      |		КОНЕЦ КАК ВариантОтображения,
			                      |		ВЫБОР
			                      |			КОГДА ПользовательскиеНастройкиВариантовАнализа.Детали ЕСТЬ NULL 
			                      |				ТОГДА ЛОЖЬ
			                      |			ИНАЧЕ ПользовательскиеНастройкиВариантовАнализа.Детали
			                      |		КОНЕЦ КАК ВыводитьПодробности,
			                      |		ВЫБОР
			                      |			КОГДА ПользовательскиеНастройкиВариантовАнализа.ИтогТаблицы ЕСТЬ NULL 
			                      |				ТОГДА ЛОЖЬ
			                      |			ИНАЧЕ ПользовательскиеНастройкиВариантовАнализа.ИтогТаблицы
			                      |		КОНЕЦ КАК ВыводитьИтогТаблицы,
			                      |		ВЫБОР
			                      |			КОГДА ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.МетодРасчетаЗначений = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаВариантовАнализаЦелевыхПоказателей.СуммаНарастающимИтогом)
			                      |				ТОГДА ИСТИНА
			                      |			ИНАЧЕ ЛОЖЬ
			                      |		КОНЕЦ КАК СостояниеПоНарастающемуИтогу,
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.КатегорияЦели КАК Группа,
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.КраткоеНаименованиеЦелевогоПоказателя КАК КраткоеНаименованиеЦелевогоПоказателя
			                      |	ИЗ
			                      |		Справочник.ВариантыАнализаЦелевыхПоказателей.НастройкиДоступности КАК ВариантыАнализаЦелевыхПоказателейНастройкиДоступности
			                      |			ЛЕВОЕ СОЕДИНЕНИЕ ПользовательскиеНастройкиВариантовАнализа КАК ПользовательскиеНастройкиВариантовАнализа
			                      |			ПО ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка = ПользовательскиеНастройкиВариантовАнализа.ВариантАнализа
			                      |	ГДЕ
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Пользователь = &ТекущийПользователь
			                      |		И (НЕ ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.ПометкаУдаления)) КАК ВариантыАнализа
			                      |ГДЕ
			                      |	ВариантыАнализа.Доступность
			                      |
			                      |УПОРЯДОЧИТЬ ПО
			                      |	ВариантыАнализа.Цель.РеквизитДопУпорядочивания,
			                      |	ВариантыАнализа.ВариантАнализа.РеквизитДопУпорядочивания";
		Иначе
			ЗапросСостава.Текст = "ВЫБРАТЬ
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец КАК Цель,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.ЦелевойПоказатель КАК ЦелевойПоказатель,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка КАК ВариантАнализа,
			                      |	ЛОЖЬ КАК ВыводитьПодробности,
			                      |	ЛОЖЬ КАК ВыводитьИтогТаблицы,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.ВариантОтображения,
			                      |	ВЫБОР
			                      |		КОГДА ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.МетодРасчетаЗначений = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаВариантовАнализаЦелевыхПоказателей.СуммаНарастающимИтогом)
			                      |			ТОГДА ИСТИНА
			                      |		ИНАЧЕ ЛОЖЬ
			                      |	КОНЕЦ КАК СостояниеПоНарастающемуИтогу,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.КатегорияЦели КАК Группа,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.КраткоеНаименованиеЦелевогоПоказателя КАК КраткоеНаименованиеЦелевогоПоказателя
			                      |ИЗ
			                      |	Справочник.ВариантыАнализаЦелевыхПоказателей.НастройкиДоступности КАК ВариантыАнализаЦелевыхПоказателейНастройкиДоступности
			                      |ГДЕ
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Пользователь = &ТекущийПользователь
			                      |	И (НЕ ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.ПометкаУдаления)
			                      |
			                      |УПОРЯДОЧИТЬ ПО
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.РеквизитДопУпорядочивания,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.РеквизитДопУпорядочивания";
		КонецЕсли;
		
	Иначе
		ЗапросСостава.УстановитьПараметр("ОтборВариантовАнализа", ОтборВариантовАнализа);
		
		Если НЕ СтруктураНастроек.ВариантыАнализаПользовательскиеНастройки = Неопределено
			И НЕ СтруктураНастроек.ВариантыАнализаПользовательскиеНастройки.Получить() = Неопределено Тогда
			ЗапросСостава.УстановитьПараметр("ВариантыАнализа", СтруктураНастроек.ВариантыАнализаПользовательскиеНастройки.Получить());
			ЗапросСостава.Текст = "ВЫБРАТЬ
			                      |	ПользовательскиеНастройкиВариантовАнализа.ВариантАнализа,
			                      |	ПользовательскиеНастройкиВариантовАнализа.Доступность,
			                      |	ПользовательскиеНастройкиВариантовАнализа.ВариантОтображения,
			                      |	ПользовательскиеНастройкиВариантовАнализа.Детали,
			                      |	ПользовательскиеНастройкиВариантовАнализа.ИтогТаблицы
			                      |ПОМЕСТИТЬ ПользовательскиеНастройкиВариантовАнализа
			                      |ИЗ
			                      |	&ВариантыАнализа КАК ПользовательскиеНастройкиВариантовАнализа
			                      |ГДЕ
			                      |	ПользовательскиеНастройкиВариантовАнализа.ВариантАнализа В(&ОтборВариантовАнализа)
			                      |;
			                      |
			                      |////////////////////////////////////////////////////////////////////////////////
			                      |ВЫБРАТЬ
			                      |	ВариантыАнализа.Цель,
			                      |	ВариантыАнализа.ЦелевойПоказатель,
			                      |	ВариантыАнализа.ВариантАнализа,
			                      |	ВариантыАнализа.ВариантОтображения,
			                      |	ВариантыАнализа.ВыводитьПодробности,
			                      |	ВариантыАнализа.ВыводитьИтогТаблицы,
			                      |	ВариантыАнализа.СостояниеПоНарастающемуИтогу,
			                      |	ВариантыАнализа.Группа,
			                      |	ВариантыАнализа.КраткоеНаименованиеЦелевогоПоказателя
			                      |ИЗ
			                      |	(ВЫБРАТЬ
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец КАК Цель,
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.ЦелевойПоказатель КАК ЦелевойПоказатель,
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка КАК ВариантАнализа,
			                      |		ВЫБОР
			                      |			КОГДА ПользовательскиеНастройкиВариантовАнализа.Доступность ЕСТЬ NULL 
			                      |				ТОГДА ИСТИНА
			                      |			ИНАЧЕ ПользовательскиеНастройкиВариантовАнализа.Доступность
			                      |		КОНЕЦ КАК Доступность,
			                      |		ВЫБОР
			                      |			КОГДА ПользовательскиеНастройкиВариантовАнализа.ВариантОтображения ЕСТЬ NULL 
			                      |				ТОГДА ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.ВариантОтображения
			                      |			ИНАЧЕ ПользовательскиеНастройкиВариантовАнализа.ВариантОтображения
			                      |		КОНЕЦ КАК ВариантОтображения,
			                      |		ВЫБОР
			                      |			КОГДА ПользовательскиеНастройкиВариантовАнализа.Детали ЕСТЬ NULL 
			                      |				ТОГДА ЛОЖЬ
			                      |			ИНАЧЕ ПользовательскиеНастройкиВариантовАнализа.Детали
			                      |		КОНЕЦ КАК ВыводитьПодробности,
			                      |		ВЫБОР
			                      |			КОГДА ПользовательскиеНастройкиВариантовАнализа.ИтогТаблицы ЕСТЬ NULL 
			                      |				ТОГДА ЛОЖЬ
			                      |			ИНАЧЕ ПользовательскиеНастройкиВариантовАнализа.ИтогТаблицы
			                      |		КОНЕЦ КАК ВыводитьИтогТаблицы,
			                      |		ВЫБОР
			                      |			КОГДА ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.МетодРасчетаЗначений = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаВариантовАнализаЦелевыхПоказателей.СуммаНарастающимИтогом)
			                      |				ТОГДА ИСТИНА
			                      |			ИНАЧЕ ЛОЖЬ
			                      |		КОНЕЦ КАК СостояниеПоНарастающемуИтогу,
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.КатегорияЦели КАК Группа,
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.КраткоеНаименованиеЦелевогоПоказателя КАК КраткоеНаименованиеЦелевогоПоказателя
			                      |	ИЗ
			                      |		Справочник.ВариантыАнализаЦелевыхПоказателей.НастройкиДоступности КАК ВариантыАнализаЦелевыхПоказателейНастройкиДоступности
			                      |			ЛЕВОЕ СОЕДИНЕНИЕ ПользовательскиеНастройкиВариантовАнализа КАК ПользовательскиеНастройкиВариантовАнализа
			                      |			ПО ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка = ПользовательскиеНастройкиВариантовАнализа.ВариантАнализа
			                      |	ГДЕ
			                      |		ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Пользователь = &ТекущийПользователь
			                      |		И (НЕ ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.ПометкаУдаления)
			                      |		И ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка В(&ОтборВариантовАнализа)) КАК ВариантыАнализа
			                      |ГДЕ
			                      |	ВариантыАнализа.Доступность
			                      |
			                      |УПОРЯДОЧИТЬ ПО
			                      |	ВариантыАнализа.Цель.РеквизитДопУпорядочивания,
			                      |	ВариантыАнализа.ВариантАнализа.РеквизитДопУпорядочивания";
		Иначе
			ЗапросСостава.Текст = "ВЫБРАТЬ
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец КАК Цель,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.ЦелевойПоказатель КАК ЦелевойПоказатель,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка КАК ВариантАнализа,
			                      |	ЛОЖЬ КАК ВыводитьПодробности,
			                      |	ЛОЖЬ КАК ВыводитьИтогТаблицы,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.ВариантОтображения,
			                      |	ВЫБОР
			                      |		КОГДА ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.МетодРасчетаЗначений = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаВариантовАнализаЦелевыхПоказателей.СуммаНарастающимИтогом)
			                      |			ТОГДА ИСТИНА
			                      |		ИНАЧЕ ЛОЖЬ
			                      |	КОНЕЦ КАК СостояниеПоНарастающемуИтогу,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.КатегорияЦели КАК Группа,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.КраткоеНаименованиеЦелевогоПоказателя КАК КраткоеНаименованиеЦелевогоПоказателя
			                      |ИЗ
			                      |	Справочник.ВариантыАнализаЦелевыхПоказателей.НастройкиДоступности КАК ВариантыАнализаЦелевыхПоказателейНастройкиДоступности
			                      |ГДЕ
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Пользователь = &ТекущийПользователь
			                      |	И (НЕ ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.ПометкаУдаления)
			                      |	И ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка В(&ОтборВариантовАнализа)
			                      |
			                      |УПОРЯДОЧИТЬ ПО
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.Владелец.РеквизитДопУпорядочивания,
			                      |	ВариантыАнализаЦелевыхПоказателейНастройкиДоступности.Ссылка.РеквизитДопУпорядочивания";
		КонецЕсли;
		
	КонецЕсли;
	
	РезультатЗапросаСостава = ЗапросСостава.Выполнить();
	
	Если НЕ РезультатЗапросаСостава.Пустой() Тогда
		
		НаборИсточниковДанных = РезультатЗапросаСостава.Выгрузить();	
		
		// Дополним таблицу источниками данных, состоянием и трендом
		НаборИсточниковДанных.Колонки.Добавить("ИсточникДанных");
		НаборИсточниковДанных.Колонки.Добавить("ЗонаВнимания");
		НаборИсточниковДанных.Колонки.Добавить("Пустой");
		НаборИсточниковДанных.Колонки.Добавить("РасчетыНеактуальны");
		НаборИсточниковДанных.Колонки.Добавить("ОшибкаРасчета");
		НаборИсточниковДанных.Колонки.Добавить("ПорядокЗоныВнимания");
		НаборИсточниковДанных.Колонки.Добавить("Позиция");
		
		Позиция = 1;
		Для Каждого ЭлементНабора Из НаборИсточниковДанных Цикл 
			ВариантАнализа = ЭлементНабора.ВариантАнализа;
			
			// Получим данные варианта анализа
			ИсточникДанныхВариантаАнализа = МониторингЦелевыхПоказателей.ПолучитьИсточникДанныхВариантаАнализа(ВариантАнализа, ЭлементНабора.ВариантОтображения, ПринудительноОбновитьДанные);
			ЭлементНабора.ИсточникДанных = Новый ХранилищеЗначения(ИсточникДанныхВариантаАнализа);
			
			ЭлементНабора.Пустой = ИсточникДанныхВариантаАнализа.Пустой;
			ЭлементНабора.ОшибкаРасчета = ИсточникДанныхВариантаАнализа.ОшибкаРасчета;
			ЭлементНабора.РасчетыНеактуальны = НЕ ИсточникДанныхВариантаАнализа.РасчетыАктуальны;
			// Получим зону внимания
			ЭлементНабора.ЗонаВнимания = ИсточникДанныхВариантаАнализа.СвойстваЗоныАнализаИТренда.ЗонаВнимания;
			ЭлементНабора.ПорядокЗоныВнимания = ИсточникДанныхВариантаАнализа.СвойстваЗоныАнализаИТренда.ПорядокЗоныВнимания;
			ЭлементНабора.Позиция = Позиция;
			
			Позиция = Позиция + 1;
			
		КонецЦикла;
		
		// Установим отбор
		Если ОтборСтатус = 1 Тогда
			
			ОтборКритических = Новый Структура("ЗонаВнимания", ЗоныВнимания.КритическоеСостояние);
			НаборИсточниковДанных = НаборИсточниковДанных.Скопировать(ОтборКритических);
			
		ИначеЕсли ОтборСтатус = 2 Тогда
			
			ОтборВажных = Новый Структура("ЗонаВнимания", ЗоныВнимания.Важно);
			НаборИсточниковДанных = НаборИсточниковДанных.Скопировать(ОтборВажных);
			
		ИначеЕсли ОтборСтатус = 3 Тогда
			
			ОтборКСведению = Новый Структура("ЗонаВнимания", ЗоныВнимания.КСведению);
			ЭлементыМонитораКУдалению = НаборИсточниковДанных.НайтиСтроки(ОтборКСведению);
			
			Для Каждого ЭлементМонитораКУдалению Из ЭлементыМонитораКУдалению Цикл 
				
				НаборИсточниковДанных.Удалить(ЭлементМонитораКУдалению);
				
			КонецЦикла;
			
			ОтборПоРассчитаннымСОшибками = Новый Структура("ЗонаВнимания", ЗоныВнимания.РассчитанныеСОшибками);
			ЭлементыМонитораКУдалению = НаборИсточниковДанных.НайтиСтроки(ОтборПоРассчитаннымСОшибками);
			
			Для Каждого ЭлементМонитораКУдалению Из ЭлементыМонитораКУдалению Цикл 
				
				НаборИсточниковДанных.Удалить(ЭлементМонитораКУдалению);
				
			КонецЦикла;
			
		ИначеЕсли ОтборСтатус = 4 Тогда
			
			ОтборКСведению = Новый Структура("ЗонаВнимания", ЗоныВнимания.КСведению);
			НаборИсточниковДанных = НаборИсточниковДанных.Скопировать(ОтборКСведению);
			
		ИначеЕсли ОтборСтатус = 5 Тогда
			
			ОтборПоРассчитаннымСОшибкой = Новый Структура("ЗонаВнимания", ЗоныВнимания.РассчитанныеСОшибками);
			НаборИсточниковДанных = НаборИсточниковДанных.Скопировать(ОтборПоРассчитаннымСОшибкой);
			
		КонецЕсли;
		
		ПолученныйСоставМонитора.Пустой = (НаборИсточниковДанных.Количество() = 0);
		
		ПолученныйСоставМонитора.Вставить("НаборИсточниковДанных", НаборИсточниковДанных);
		
		ПолученныйСоставМонитора.Вставить("СтатистикаПоСтрокам", МониторингЦелевыхПоказателей.СтатистикаИсточниковДанныхПоЗонамВнимания(НаборИсточниковДанных, Истина));
		
	КонецЕсли;
	
	Возврат ПолученныйСоставМонитора;
	
КонецФункции

&НаСервере
Функция ПолучитьСсылкуПеречисленияПоИдентификатору(ИмяОбъекта, Идентификатор)
	
	Перем ИскомаяСсылка;
	
	ИскомаяСсылка = Перечисления[ИмяОбъекта][Идентификатор];
	
	Возврат ИскомаяСсылка;
	
КонецФункции

&НаСервере
Функция ПолучитьСсылкуСправочникаПоИдентификатору(ИмяОбъекта, Идентификатор)
	
	Перем ИскомаяСсылка;
	
	УникальныйИдентификаторПоказателя = Новый УникальныйИдентификатор(СтрЗаменить(Идентификатор, "_", "-"));
	ИскомаяСсылка = Справочники[ИмяОбъекта].ПолучитьСсылку(УникальныйИдентификаторПоказателя);
	
	Возврат ИскомаяСсылка;
	
КонецФункции

&НаКлиенте
Функция ПолучитьЧистоеИмяКоманды(ИмяКоманды)
	
	Перем ЧистоеИмяКоманды;
	
	ЧистоеИмяКоманды = Лев(ИмяКоманды, Найти(ИмяКоманды, "__") - 1);
	
	Возврат ЧистоеИмяКоманды;
	
КонецФункции

&НаСервере 
Процедура УстановитьВидимостьПредложенияДобавитьПоказателиВСоставМонитора(Видимость)
	
	Элементы.ГруппаПриПустомСоставеМонитора.Видимость = Видимость;
	Элементы.ФормаСводныйОтчет.Доступность = НЕ Видимость;
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьВидимостьСообщенияПустойРезультатОтбора(Видимость)
	
	Элементы.ГруппаПриПустомРезультатеОтбора.Видимость = Видимость;
	
КонецПроцедуры

