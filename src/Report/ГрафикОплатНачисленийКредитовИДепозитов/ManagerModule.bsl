#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// СтандартныеПодсистемы.ВариантыОтчетов

// Методы расширения общей формы отчетов
//

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ФормаОтчетаПереопределяемый.ПередЗагрузкойВариантаНаСервере
//
Процедура ПередЗагрузкойВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроек = Отчет.КомпоновщикНастроек;	
	
	// Установка значений по умолчанию
	УстановитьФиксированнуюНастройку(КомпоновщикНастроек, "ВариантГрафика");
	
	НовыеНастройкиКД = КомпоновщикНастроек.Настройки;
КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ФормаОтчетаПереопределяемый.ПередЗагрузкойПользовательскихНастроекНаСервере
//
Процедура ПередЗагрузкойПользовательскихНастроекНаСервере(ЭтаФорма, НовыеПользовательскиеНастройкиКД) Экспорт
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроек = Отчет.КомпоновщикНастроек;
	
	// Установка значений по умолчанию
	УстановитьФиксированнуюНастройку(КомпоновщикНастроек, "ВариантГрафика");
	
	НовыеПользовательскиеНастройкиКД = КомпоновщикНастроек.ПользовательскиеНастройки;
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура УстановитьФиксированнуюНастройку(КомпоновщикНастроек, ИмяНастройки)
	
	ФиксНастройка = ФиксированнаяНастройкаПараметра(КомпоновщикНастроек, ИмяНастройки);
		
	ПользНастройка = ПользовательскаяНастройкаПараметра(КомпоновщикНастроек, ИмяНастройки);
	ПользНастройка.Использование = Истина;
	ПользНастройка.Значение = ФиксНастройка.Значение;
	Если Найти(ИмяНастройки, "ВариантГрафика") > 0 
		И ФиксНастройка.Значение = Неопределено Тогда
		ПользНастройка.Значение = Справочники.ВариантыГрафиковКредитовИДепозитов.ПустаяСсылка();
	КонецЕсли;		
	ФиксНастройка.Использование = Ложь;
	
КонецПроцедуры

Функция ФиксированнаяНастройкаПараметра(КомпоновщикНастроек, ИмяПараметра)

	ПараметрДанных = КомпоновщикНастроек.ФиксированныеНастройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	
	Возврат ПараметрДанных;

КонецФункции

Функция ПользовательскаяНастройкаПараметра(КомпоновщикНастроек, ИмяПараметра)

	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки;
		Иначе
			Возврат ПараметрДанных;
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;

КонецФункции

// Конец СтандартныеПодсистемы.ВариантыОтчетов
 
#КонецЕсли