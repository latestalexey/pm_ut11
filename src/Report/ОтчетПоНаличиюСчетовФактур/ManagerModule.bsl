

// Формирует табличную часть отчета
Процедура СформироватьОтчет(ПараметрыОтчета, АдресХранилища)Экспорт 
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ТаблицаСчетовФактур = УчетНДСПереопределяемый.ПреобразоватьДокументыВСчетаФактуры(ПараметрыОтчета.СписокДокументов);
	Фильтр = ТаблицаСчетовФактур.ВыгрузитьКолонку("СчетФактура");
	
	Результат = УчетНДСПереопределяемый.ОпределитьНаличиеСчетовФактурПолученных(
		?(НЕ ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода), Неопределено, ПараметрыОтчета.НачалоПериода), 
		?(НЕ ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода), Неопределено, ПараметрыОтчета.КонецПериода),
		?(НЕ ЗначениеЗаполнено(ПараметрыОтчета.Организация), Неопределено, ПараметрыОтчета.Организация),
		?((Фильтр.Количество() > 0 И ПараметрыОтчета.ВсеКромеСпискаДокументов <> Неопределено), Фильтр, Неопределено),
		?(ПараметрыОтчета.ВсеКромеСпискаДокументов = Неопределено, Истина, ПараметрыОтчета.ВсеКромеСпискаДокументов),
			ПараметрыОтчета.НаличиеСчетаФактуры); 
	
	СформироватьОтчетПоНаличиюСчетовФактур(ПараметрыОтчета, ТабличныйДокумент, Результат);
	
	ПоместитьВоВременноеХранилище(ТабличныйДокумент, АдресХранилища);
	
КонецПроцедуры

// Процедура формирует табличный документ с отчетом по наличию счетов-фактур.
//
// Параметры:
//  ДокументРезультат - табличный документ, в который выводится
//                      отчет по наличию счетов-фактур
//  ТаблицаРезультат - таблица значений, содержащая данные для построения
//                     отчета
//
// Возвращаемое значение:
//  Нет.
//
Процедура СформироватьОтчетПоНаличиюСчетовФактур(ПараметрыОтчета, ДокументРезультат, ТаблицаРезультат) Экспорт
	
	ДокументРезультат.Очистить();
	
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ДокументРезультат.ПолеСверху         = 10;
	ДокументРезультат.ПолеСнизу          = 10;
	ДокументРезультат.ПолеСлева          = 20;
	ДокументРезультат.ПолеСправа         = 20;
	
	ДокументРезультат.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОтчетПоНаличиюСчетовФактур";
	
	// Получим макет и области макета.
	Макет = ПолучитьМакет("ОтчетПоНаличиюСчетовФактур");
	
	ЗаголовокОтчета = Макет.ПолучитьОбласть("Заголовок");
	Шапка           = Макет.ПолучитьОбласть("Шапка");
	Строка          = Макет.ПолучитьОбласть("Строка");
	Подвал          = Макет.ПолучитьОбласть("Подвал");
	
	// Выведем заголовок отчета.
	СведенияОбОрганизации = БухгалтерскийУчетВызовСервераПереопределяемый.СведенияОЮрФизЛице(ПараметрыОтчета.Организация, ?(НЕ ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода), ТекущаяДата(), ПараметрыОтчета.КонецПериода));	
	
	ЗаголовокОтчета.Параметры.НаименованиеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) И НЕ ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		ЗаголовокОтчета.Параметры.ОписаниеПериода = "С " + Формат(ПараметрыОтчета.НачалоПериода, "ДЛФ=DD");
	ИначеЕсли НЕ ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) И ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		ЗаголовокОтчета.Параметры.ОписаниеПериода = "По " + Формат(ПараметрыОтчета.КонецПериода, "ДЛФ=DD");
	ИначеЕсли ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) И ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		ЗаголовокОтчета.Параметры.ОписаниеПериода = ПредставлениеПериода(НачалоДня(ПараметрыОтчета.НачалоПериода), КонецДня(ПараметрыОтчета.КонецПериода), "ФП=Истина");
	Иначе
		ЗаголовокОтчета.Параметры.ОписаниеПериода = "(Без ограничения периода)";
	КонецЕсли;
	
	ДокументРезультат.Вывести(ЗаголовокОтчета);
	
	// Выведем шапку таблицы.
	ДокументРезультат.Вывести(Шапка);
	
	// Выведем строки таблицы.
	Для Каждого СтрокаРезультат Из ТаблицаРезультат Цикл
		
		Строка.Параметры.Заполнить(СтрокаРезультат);
		
		Строка.Параметры.СчетФактураПроведен = ?(СтрокаРезультат.СчетФактураПроведен = Истина, "●", "");
		
		Строка.Параметры.НомерСтроки = ТаблицаРезультат.Индекс(СтрокаРезультат) + 1;
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(Строка);
		СтрокаСПодвалом.Добавить(Подвал);
		
		Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ДокументРезультат, СтрокаСПодвалом) Тогда
			
			ДокументРезультат.Вывести(Подвал);
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			ДокументРезультат.Вывести(Шапка);
			
		КонецЕсли;
		
		ДокументРезультат.Вывести(Строка);
		
	КонецЦикла;
	
	ДокументРезультат.Вывести(Подвал);
	
	ДокументРезультат.ТолькоПросмотр = Истина;
	
КонецПроцедуры

