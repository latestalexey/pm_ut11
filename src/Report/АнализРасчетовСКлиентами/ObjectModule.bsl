#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НастройкаВариантКлассификацииЗадолженности = НастройкаПараметра("ВариантКлассификацииЗадолженности");
	НастройкаВариантКлассификацииЗадолженности.Использование = Истина;
	
	Если ТипЗнч(НастройкаВариантКлассификацииЗадолженности.Значение) <> Тип("СправочникСсылка.ВариантыКлассификацииЗадолженности") Тогда
		НастройкаВариантКлассификацииЗадолженности.Значение = Справочники.ВариантыКлассификацииЗадолженности.ПустаяСсылка();
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(НастройкаВариантКлассификацииЗадолженности.Значение) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 2
		|	ВариантыКлассификацииЗадолженности.Ссылка КАК ВариантыКлассификации
		|ИЗ
		|	Справочник.ВариантыКлассификацииЗадолженности КАК ВариантыКлассификацииЗадолженности
		|ГДЕ
		|	(НЕ ВариантыКлассификацииЗадолженности.ПометкаУдаления)";
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Если Выборка.Количество() = 1 Тогда
			Выборка.Следующий();
			НастройкаВариантКлассификацииЗадолженности.Значение = Выборка.ВариантыКлассификации;
		Иначе
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не выбран вариант классификации задолженности!";
			Сообщение.Сообщить();
			
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	НастройкаВариантКалендарь = НастройкаПараметра("Календарь");
	НастройкаВариантКалендарь.Использование = Истина;
	НастройкаВариантКалендарь.Значение = НастройкаВариантКлассификацииЗадолженности.Значение.Календарь;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СегментыСервер.ВключитьОтборПоСегментуПартнеровВСКД(КомпоновщикНастроек);
	
	НастройкаДатаОтчета = НастройкаПараметра("ДатаОтчета");
	НастройкаДатаОтчета.Использование = Истина;
	Если ТипЗнч(НастройкаДатаОтчета.Значение) = Тип("СтандартнаяДатаНачала") Тогда
		Если НЕ ЗначениеЗаполнено(НастройкаДатаОтчета.Значение.Дата) Тогда
			НастройкаДатаОтчета.Значение.Дата = КонецДня(ТекущаяДата());
		КонецЕсли;
	Иначе
		Если НЕ ЗначениеЗаполнено(НастройкаДатаОтчета.Значение) Тогда
			НастройкаДатаОтчета.Значение = КонецДня(ТекущаяДата());
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Прочее

Функция НастройкаПараметра(ИмяПараметра)

	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки;
		Иначе
			Возврат ПараметрДанных;
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;

КонецФункции

#КонецЕсли