#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// СтандартныеПодсистемы.ВариантыОтчетов

// Методы расширения общей формы отчетов
//

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ФормаОтчетаПереопределяемый.ПередЗагрузкойВариантаНаСервере
//
Процедура ПередЗагрузкойВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроек = Отчет.КомпоновщикНастроек;	
	
	// Установка значений по умолчанию
	НастроитьОтчет(КомпоновщикНастроек);
	
	НовыеНастройкиКД = КомпоновщикНастроек.Настройки;
КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ФормаОтчетаПереопределяемый.ПередЗагрузкойПользовательскихНастроекНаСервере
//
Процедура ПередЗагрузкойПользовательскихНастроекНаСервере(ЭтаФорма, НовыеПользовательскиеНастройкиКД) Экспорт
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроек = Отчет.КомпоновщикНастроек;
	
	// Установка значений по умолчанию
	НастроитьОтчет(КомпоновщикНастроек);
	
	НовыеПользовательскиеНастройкиКД = КомпоновщикНастроек.ПользовательскиеНастройки;
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура НастроитьОтчет(КомпоновщикНастроек)
	
	ФиксНастройка = ФиксированнаяНастройкаПараметра(КомпоновщикНастроек, "Договор");	
	ПользНастройка = ПользовательскаяНастройкаПараметра(КомпоновщикНастроек, "Договор");
		
	ПользНастройка = ПользовательскаяНастройкаПараметра(КомпоновщикНастроек, "Договор");
	ПользНастройка.Использование = Истина;
	ПользНастройка.Значение = ФиксНастройка.Значение;
	
	ФиксНастройка.Использование = Ложь;
	
	ВалютаДоговора = Справочники.Валюты.ПустаяСсылка();
	Если ПользНастройка <> Неопределено И ПользНастройка.Значение <> Неопределено Тогда
		ВалютаДоговора = ПользНастройка.Значение.ВалютаВзаиморасчетов;
	КонецЕсли;
	
	ПараметрВалюты = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Валюта"));	
	Если ПараметрВалюты <> Неопределено Тогда
		ПараметрВалюты.Значение = ВалютаДоговора;
		ПараметрВалюты.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция ФиксированнаяНастройкаПараметра(КомпоновщикНастроек, ИмяПараметра)

	ПараметрДанных = КомпоновщикНастроек.ФиксированныеНастройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	
	Возврат ПараметрДанных;

КонецФункции

Функция ПользовательскаяНастройкаПараметра(КомпоновщикНастроек, ИмяПараметра)

	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки;
		Иначе
			Возврат ПараметрДанных;
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;

КонецФункции

// Конец СтандартныеПодсистемы.ВариантыОтчетов
 
#КонецЕсли