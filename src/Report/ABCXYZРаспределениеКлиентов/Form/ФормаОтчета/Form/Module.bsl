////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Расшифровка") И Параметры.Расшифровка = Неопределено Тогда
		РасшифровкаНеИспользуется = Истина;
	Иначе
		ПараметрыРасшифровки = Параметры.Расшифровка;
		ПрименяемыеНастройки = Отчет.КомпоновщикНастроек.ФиксированныеНастройки;

		//установить параметры данных
		ПрименяемыеНастройки.ПараметрыДанных.УстановитьЗначениеПараметра(
			"ТекущийПериод", ПараметрыРасшифровки.ТекущийПериод
		);
		ПрименяемыеНастройки.ПараметрыДанных.УстановитьЗначениеПараметра(
			"ТипПараметраКлассификации", ПараметрыРасшифровки.ТипПараметраКлассификации
		);

		//установить отбор
		ОтборРасшифровки = ПрименяемыеНастройки.Отбор;
		Если ПараметрыРасшифровки.Свойство("Вопросы") Тогда

			ПолеXYZ = Новый ПолеКомпоновкиДанных("КлассXYZ");
			ПолеABC = Новый ПолеКомпоновкиДанных("КлассABC");

			ГруппаОтбораНе = СоздатьГруппуОтбора(ОтборРасшифровки, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе);
			ГруппаОтбораИли = СоздатьГруппуОтбора(ГруппаОтбораНе, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
			ГруппаОтбораИ = СоздатьГруппуОтбора(ГруппаОтбораИли, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
			СоздатьЭлементОтбора(ГруппаОтбораИ, ПолеABC, Перечисления.ABCКлассификация.AКласс);
			ГруппаОтбораИлиXYZ = СоздатьГруппуОтбора(ГруппаОтбораИ, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
			СоздатьЭлементОтбора(ГруппаОтбораИлиXYZ, ПолеXYZ, Перечисления.XYZКлассификация.XКласс);
			СоздатьЭлементОтбора(ГруппаОтбораИлиXYZ, ПолеXYZ, Перечисления.XYZКлассификация.ZКласс);
			ГруппаОтбораИ = СоздатьГруппуОтбора(ГруппаОтбораИли, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
			СоздатьЭлементОтбора(ГруппаОтбораИ, ПолеABC, Перечисления.ABCКлассификация.CКласс);
			СоздатьЭлементОтбора(ГруппаОтбораИ, ПолеXYZ, Перечисления.XYZКлассификация.XКласс);
			СоздатьЭлементОтбора(ГруппаОтбораИли, ПолеXYZ, Перечисления.XYZКлассификация.НеКлассифицирован);
			СоздатьЭлементОтбора(ГруппаОтбораИли, ПолеXYZ, Перечисления.XYZКлассификация.ПустаяСсылка());

		Иначе
			Для Каждого Элемент Из ПараметрыРасшифровки.Отбор Цикл
				СоздатьЭлементОтбора(
					ОтборРасшифровки, Новый ПолеКомпоновкиДанных(Элемент.Ключ), Элемент.Значение
				);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)

	Если РасшифровкаНеИспользуется Тогда
		Возврат;
	КонецЕсли;
	
	//установить заголовки
	ПараметрыРасшифровки = Параметры.Расшифровка;
	Настройки = Отчет.КомпоновщикНастроек.Настройки;
	Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("Заголовок", ПараметрыРасшифровки.Заголовок);
	Выбор = Настройки.Выбор.Элементы;
	ПолеТипПараметра = Новый ПолеКомпоновкиДанных("ЗначениеПараметраКлассификации");
	Для Каждого Элемент Из Выбор Цикл
		Если Элемент.Поле = ПолеТипПараметра Тогда
			Элемент.Заголовок = ПараметрыРасшифровки.ТипПараметраКлассификации;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если РасшифровкаНеИспользуется Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru='Отказ'"), ,
			НСтр("ru='Отчет может быть использован только как расшифровка анализа клиентской базы (BCG)'"),
			БиблиотекаКартинок.Информация32
		);
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция СоздатьГруппуОтбора(Родитель, ТипГруппы)

	ГруппаОтбора = Родитель.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппы;
	ГруппаОтбора.Использование = Истина;
	Возврат ГруппаОтбора;

КонецФункции

&НаСервереБезКонтекста
Процедура СоздатьЭлементОтбора(Родитель, Поле, Значение)

	ЭлементОтбора = Родитель.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Поле;
	ЭлементОтбора.ПравоеЗначение = Значение;
	ЭлементОтбора.Использование  = Истина;

КонецПроцедуры



