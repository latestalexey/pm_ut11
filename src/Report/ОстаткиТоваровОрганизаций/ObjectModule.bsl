#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДокументОбъект = Документ.ПолучитьОбъект();
	
	МенеджерВременныхТаблиц = ДокументОбъект.ВременныеТаблицыДанныхДокумента();
	ДокументОбъект.СформироватьВременнуюТаблицуТоваровИАналитики(МенеджерВременныхТаблиц);
	ДокументОбъект.СформироватьДоступныеВидыЗапасов(МенеджерВременныхТаблиц);
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
		ПоТоварамКОформлению = Документы.ПередачаТоваровМеждуОрганизациями.ПолучитьРеквизитыДокумента(Документ).ПоТоварамКОформлению;
	Иначе
		ПоТоварамКОформлению = Ложь;
	КонецЕсли;
	
	Если ПоТоварамКОформлению Тогда
		ДатаОстатков = ?(ЗначениеЗаполнено(ДокументОбъект.КонецПериода), КонецДня(ДокументОбъект.КонецПериода), КонецМесяца(ДокументОбъект.Дата));
		ЗапасыСервер.ТаблицаОстатковТоваровКПередаче(
			Документ,
			ДокументОбъект.Организация,
			ДокументОбъект.Склад,
			ДатаОстатков,
			МенеджерВременныхТаблиц,
			Истина // ПоВсемВидамЗапасов
		);
	Иначе
		ДатаОстатков = КонецМесяца(ДокументОбъект.Дата);
		ЗапасыСервер.ТаблицаОстатковТоваровОрганизаций(
			Документ,
			ДокументОбъект.Организация,
			ДатаОстатков,
			ДокументОбъект.ДополнительныеСвойства,
			МенеджерВременныхТаблиц,
			Истина // ПоВсемВидамЗапасов
		);
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Налогообложение.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС) КАК НалогообложениеНДС
	|	
	|ПОМЕСТИТЬ НалогообложениеОрганизаций
	|ИЗ
	|	РегистрСведений.СистемыНалогообложенияОрганизаций.СрезПоследних(&Дата, ) КАК Налогообложение
	|ГДЕ
	|	Налогообложение.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ТаблицаТоваров.НалогообложениеНДС,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Склад,
	|	ТаблицаТоваров.Количество,
	|
	|	ТаблицаТоваров.Подразделение,
	|	ТаблицаТоваров.Менеджер,
	|	ТаблицаТоваров.Сделка,
	|	ТаблицаТоваров.Назначение,
	|	ТаблицаТоваров.Партнер,
	|	ТаблицаТоваров.Соглашение
	|ИЗ
	|	ТаблицаТоваровИАналитики КАК ТаблицаТоваров
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Организация,
	|	ТаблицаОстатков.Номенклатура,
	|	ТаблицаОстатков.Характеристика,
	|	ТаблицаОстатков.Склад,
	|	ТаблицаОстатков.ВидЗапасовВладельца КАК ВидЗапасов,
	|	ТаблицаОстатков.НомерГТД КАК НомерГТД,
	|	ТаблицаОстатков.КоличествоОстаток,
	|	ВЫБОР КОГДА Не ДоступныеВидыЗапасов.ВидЗапасов ЕСТЬ NULL ТОГДА
	|		ВЫБОР КОГДА &ЗапретитьОперацииСТоварамиБезНомеровГТД
	|			И ТаблицаОстатков.Номенклатура.ВестиУчетПоГТД
	|			И ТаблицаОстатков.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|		ТОГДА
	|			0
	|		ИНАЧЕ
	|			ТаблицаОстатков.КоличествоОстаток
	|		КОНЕЦ
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК ДоступныйОстаток,
	|
	|	ТаблицаОстатков.ВидЗапасовВладельца.Подразделение КАК Подразделение,
	|	ТаблицаОстатков.ВидЗапасовВладельца.Менеджер КАК Менеджер,
	|	ТаблицаОстатков.ВидЗапасовВладельца.Сделка КАК Сделка,
	|	ТаблицаОстатков.ВидЗапасовВладельца.Соглашение КАК Соглашение,
	|
	|	ВЫБОР КОГДА ТаблицаОстатков.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|		ТаблицаОстатков.ВидЗапасовВладельца.Комитент
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	КОНЕЦ КАК Партнер,
	|
	|	ВЫБОР КОГДА ТаблицаОстатков.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|		ТаблицаОстатков.ВидЗапасовВладельца.НалогообложениеНДС
	|	ИНАЧЕ
	|		ЕСТЬNULL(НалогообложениеОрганизаций.НалогообложениеНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС))
	|	КОНЕЦ КАК НалогообложениеНДС,
	|
	|	ВЫБОР КОГДА Не ДоступныеВидыЗапасов.ВидЗапасов ЕСТЬ NULL И Не ТаблицаТоваров.Назначение ЕСТЬ NULL ТОГДА
	|		ВЫБОР КОГДА &ЗапретитьОперацииСТоварамиБезНомеровГТД
	|			И ТаблицаОстатков.Номенклатура.ВестиУчетПоГТД
	|			И ТаблицаОстатков.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|		ТОГДА
	|			Ложь
	|		ИНАЧЕ
	|			Истина
	|		КОНЕЦ
	|	ИНАЧЕ
	|		Ложь
	|	КОНЕЦ КАК ДоступенДляДокумента
	|ИЗ
	|	ТаблицаОстатков КАК ТаблицаОстатков
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаТоваровИАналитики КАК ТаблицаТоваров
	|	ПО
	|		ТаблицаОстатков.Номенклатура = ТаблицаТоваров.Номенклатура
	|		И ТаблицаОстатков.Характеристика = ТаблицаТоваров.Характеристика
	|		И ТаблицаОстатков.Склад = ТаблицаТоваров.Склад
	|		И ТаблицаОстатков.Назначение = ТаблицаТоваров.Назначение
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|	ПО
	|		(Не &ПоТоварамКОформлению И ТаблицаОстатков.ВидЗапасовВладельца = ДоступныеВидыЗапасов.ВидЗапасов)
	|		ИЛИ
	|		(&ПоТоварамКОформлению И ТаблицаОстатков.ВидЗапасовПолучателя = ДоступныеВидыЗапасов.ВидЗапасовПродавца)
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		НалогообложениеОрганизаций КАК НалогообложениеОрганизаций
	|	ПО
	|		ТаблицаОстатков.Организация = НалогообложениеОрганизаций.Организация
	|ГДЕ
	|	ТаблицаОстатков.КоличествоОстаток > 0
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|");
	Запрос.УстановитьПараметр("Дата", ДокументОбъект.Дата);
	Запрос.УстановитьПараметр("Организация", ДокументОбъект.Организация);
	Запрос.УстановитьПараметр("ПоТоварамКОформлению", ПоТоварамКОформлению);
	Запрос.УстановитьПараметр("ЗапретитьОперацииСТоварамиБезНомеровГТД", ПолучитьФункциональнуюОпцию("ЗапретитьОформлениеОперацийСИмпортнымиТоварамиБезНомеровГТД"));
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// МассивРезультатов[0] - НалогообложениеОрганизаций
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();
	ТаблицаОстатков = МассивРезультатов[2].Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура("ТаблицаТоваров, ТаблицаОстатков",
		ТаблицаТоваров,
		ТаблицаОстатков
	);
	
	ПараметрНаДату = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НаДату");
	Если ПараметрНаДату <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрНаДату.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			ПараметрПользовательскойНастройки.Значение = ДатаОстатков;
		Иначе
			ПараметрНаДату.Значение = ДатаОстатков;
		КонецЕсли;
	КонецЕсли;
	
	// Подготовим и выведем отчет.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных, 
		НастройкиКомпоновкиДанных,
		ДанныеРасшифровки
	);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(
		МакетКомпоновки,
		ВнешниеНаборыДанных,
		ДанныеРасшифровки
	);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	ПроцессорВывода.ЗакончитьВывод();
	
КонецПроцедуры

#КонецЕсли