////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ДокументВозврата = Параметры.ДокументВозврата;
	Дата = Параметры.Дата;
	Валюта = Параметры.Валюта;
	ЦенаВключаетНДС = Параметры.ЦенаВключаетНДС;
	Партнер = Параметры.Партнер;
	Соглашение = Параметры.Соглашение;
	Организация = Параметры.Организация;
	Контрагент = Параметры.Контрагент;
	НалогообложениеНДС =  Параметры.НалогообложениеНДС;
	
	Период = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(,"ПериодПодбораТоваровПоПоступлениям");
	Если Период.ДатаОкончания = Дата(1, 1, 1) Тогда
		Период.Вариант = ВариантСтандартногоПериода.Месяц;
	КонецЕсли; 
	
	ЗаполнитьТаблицуТоваров();
	РассчитатьИтогиПоТаблицеТоваров();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта
	);
	
	ИспользоватьМногооборотнуюТару = ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару");
	
	Если ИспользоватьМногооборотнуюТару И Параметры.ПоказыватьТару Тогда
		
		ЗаполнитьТаблицуТары();
		РассчитатьИтогиПоТаблицеТары();
		ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
			ТараИнформационнаяНадпись,
			ТараВыбраноКоличество,
			ТараВыбраноСумма,
			Валюта
		);
		
		Если Не Параметры.ПоказыватьТовары Тогда
			Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТара;
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТовары;
		КонецЕсли;
		
	Иначе
		
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТовары;
		
	КонецЕсли;
		
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("ТаблицаТары");
	МассивЭлементов.Добавить("ТараИнформационнаяНадписьОтборы");
	МассивЭлементов.Добавить("ТараИнформационнаяНадпись");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", ИспользоватьМногооборотнуюТару И Параметры.ПоказыватьТару);
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("ПериодВариант");
	МассивЭлементов.Добавить("ПериодДатаНачала");
	МассивЭлементов.Добавить("ПериодДатаОкончания");
	МассивЭлементов.Добавить("Обновить");
	МассивЭлементов.Добавить("ТаблицаТоваров");
	МассивЭлементов.Добавить("ТоварыИнформационнаяНадпись");
	МассивЭлементов.Добавить("ТоварыИнформационнаяНадписьОтборы");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", Параметры.ПоказыватьТовары);
	
	ПодборТоваровКлиентСервер.СформироватьЗаголовокФормыПодбора(Заголовок, ДокументВозврата);
	СформироватьИнформационнуюНадписьОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ПеренестиВДокумент И (ТоварыВыбраноКоличество > 0 Или ТараВыбраноКоличество > 0)  Тогда 
		
		Если ТоварыВыбраноКоличество > 0 И ТараВыбраноКоличество > 0 Тогда
			ТекстВопроса = НСтр("ru = 'Подобранные товары и многооборотная тара не перенесены в документ. Перенести?'")
		ИначеЕсли ТоварыВыбраноКоличество > 0 Тогда
			ТекстВопроса = НСтр("ru = 'Подобранные товары не перенесены в документ. Перенести?'")
		ИначеЕсли ТараВыбраноКоличество > 0 Тогда
			ТекстВопроса = НСтр("ru = 'Подобранная многооборотная тара не перенесена в документ. Перенести?'")
		КонецЕсли;
		
		ОтветНаВопрос = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
		Если ОтветНаВопрос = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			ПеренестиВДокумент = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ПеренестиВДокумент Тогда
		
		АдресТоваровВХранилище = ПоместитьТоварыВХранилище();
		Структура = Новый Структура("АдресТоваровВХранилище", АдресТоваровВХранилище);
		ПеренестиВДокумент = Истина;
		ОповеститьОВыборе(Структура);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(,"ПериодПодбораТоваровПоПоступлениям", Период);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ТОВАРЫ

&НаКлиенте
Процедура ТаблицаТоваровВыбранПриИзменении(Элемент)
	
	Данные = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	ЗнакСложения = ?(Данные.Выбран, 1, -1);
	ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + ЗнакСложения;
	ТоварыВыбраноСумма = ТоварыВыбраноСумма + Данные.СуммаСНДС*ЗнакСложения;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТарыВыбранПриИзменении(Элемент)
	
	Данные = Элементы.ТаблицаТары.ТекущиеДанные;
	
	ЗнакСложения = ?(Данные.Выбран, 1, -1);
	ТараВыбраноКоличество = ТараВыбраноКоличество + ЗнакСложения;
	ТараВыбраноСумма = ТараВыбраноСумма + Данные.Сумма*ЗнакСложения;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПеренестиВДокументВыполнить()
	
	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТовары()
	
	ВыбратьВсеТоварыНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьТовары()
	
	ИсключитьВсеТоварыНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделенныеТовары(Команда)
	
	МассивСтрок = Элементы.ТаблицаТоваров.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		Если Не ТаблицаТоваров[НомерСтроки].Выбран Тогда
			ТаблицаТоваров[НомерСтроки].Выбран = Истина;
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма + ТаблицаТоваров[НомерСтроки].СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма, 
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделенныеТовары(Команда)
	
	МассивСтрок = Элементы.ТаблицаТоваров.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		Если ТаблицаТоваров[НомерСтроки].Выбран Тогда
			ТаблицаТоваров[НомерСтроки].Выбран = Ложь;
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество - 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма - ТаблицаТоваров[НомерСтроки].СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТару()
	
	ВыбратьВсюТаруНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьТару()
	
	ИсключитьВсюТаруНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделеннуюТару(Команда)
	
	МассивСтрок = Элементы.ТаблицаТары.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		Если Не ТаблицаТары[НомерСтроки].Выбран Тогда
			ТаблицаТары[НомерСтроки].Выбран = Истина;
			ТараВыбраноКоличество = ТараВыбраноКоличество + 1;
			ТараВыбраноСумма = ТараВыбраноСумма + ТаблицаТары[НомерСтроки].Сумма;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделеннуюТару(Команда)
	
	МассивСтрок = Элементы.ТаблицаТары.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		Если ТаблицаТары[НомерСтроки].Выбран Тогда
			ТаблицаТары[НомерСтроки].Выбран = Ложь;
			ТараВыбраноКоличество = ТараВыбраноКоличество - 1;
			ТараВыбраноСумма = ТараВыбраноСумма - ТаблицаТары[НомерСтроки].Сумма
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТовары(Команда)
	
	ЗаполнитьТаблицуТоваров();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТару(Команда)
	
	ЗаполнитьТаблицуТары();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодВариантПриИзменении(Элемент)
	
	ЗаполнитьТаблицуТоваров();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Функция ПоместитьТоварыВХранилище()
	
	Товары = ТаблицаТоваров.Выгрузить(ТаблицаТоваров.НайтиСтроки(Новый Структура("Выбран", Истина)));
	Для Каждого ТекСтрока Из ТаблицаТары Цикл
		Если ТекСтрока.Выбран Тогда
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			НоваяСтрока.КоличествоУпаковок = ТекСтрока.Количество;
			НоваяСтрока.СуммаСНДС = ТекСтрока.Сумма;
			НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
		КонецЕсли;
	КонецЦикла;
	ВозвращаемаяСтруктура = Новый Структура("Товары", Товары);
	АдресТоваровВХранилище = ПоместитьВоВременноеХранилище(ВозвращаемаяСтруктура, УникальныйИдентификатор);
	
	Возврат АдресТоваровВХранилище;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуТоваров()
	
	ТаблицаТоваров.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДокументыПоступления
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ПоступлениеТоваровУслуг.Проведен
	|	И ПоступлениеТоваровУслуг.Контрагент = &Контрагент
	|	И ПоступлениеТоваровУслуг.Организация = &Организация
	|	И ПоступлениеТоваровУслуг.Соглашение = &Соглашение
	|	И ПоступлениеТоваровУслуг.НалогообложениеНДС = &НалогообложениеНДС
	|	И ПоступлениеТоваровУслуг.Партнер = &Партнер
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(КорректировкаПоступления.Дата) КАК Дата,
	|	ДокументыПоступления.Ссылка КАК СсылкаПоступления
	|ПОМЕСТИТЬ ДанныеКорректировки
	|ИЗ
	|	ДокументыПоступления КАК ДокументыПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|		ПО (КорректировкаПоступления.ДокументОснование = ДокументыПоступления.Ссылка)
	|ГДЕ
	|	КорректировкаПоступления.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыПоступления.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СсылкаПоступления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(КорректировкаПоступления.Ссылка) КАК Ссылка,
	|	МАКСИМУМ(КорректировкаПоступления.Ссылка.Дата) КАК Дата,
	|	КорректировкаПоступления.Ссылка.ДокументОснование КАК СсылкаПоступления
	|ПОМЕСТИТЬ ДанныеПоследнейКорректировки
	|ИЗ
	|	Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|ГДЕ
	|	(КорректировкаПоступления.Ссылка.ДокументОснование, КорректировкаПоступления.Ссылка.Дата) В
	|			(ВЫБРАТЬ
	|				ДанныеКорректировки.СсылкаПоступления КАК СсылкаПоступления,
	|				ДанныеКорректировки.Дата КАК Дата
	|			ИЗ
	|				ДанныеКорректировки)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступления.Ссылка.ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	СсылкаПоступления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|			ТОГДА ДокументыПоступления.Ссылка
	|		ИНАЧЕ ДанныеПоследнейКорректировки.Ссылка
	|	КОНЕЦ КАК Ссылка
	|ПОМЕСТИТЬ СсылкиНаПоступления
	|ИЗ
	|	ДокументыПоступления КАК ДокументыПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ДокументыПоступления.Ссылка = ДанныеПоследнейКорректировки.СсылкаПоступления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КорректировкаПоступленияТовары.Номенклатура КАК Номенклатура,
	|	КорректировкаПоступленияТовары.Характеристика КАК Характеристика,
	|	СУММА(КорректировкаПоступленияТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(КорректировкаПоступленияТовары.Количество) КАК Количество,
	|	КорректировкаПоступленияТовары.Цена КАК Цена,
	|	КорректировкаПоступленияТовары.Ссылка.ДокументОснование КАК ДокументПоступления,
	|	КорректировкаПоступленияТовары.Упаковка,
	|	КорректировкаПоступленияТовары.Сумма КАК Сумма,
	|	КорректировкаПоступленияТовары.СтавкаНДС КАК СтавкаНДС,
	|	КорректировкаПоступленияТовары.СуммаНДС КАК СуммаНДС,
	|	КорректировкаПоступленияТовары.СуммаСНДС КАК СуммаСНДС,
	|	КорректировкаПоступленияТовары.Склад КАК Склад,
	|	NULL КАК СуммаРучнойСкидки,
	|	NULL КАК ПроцентРучнойСкидки,
	|	КорректировкаПоступленияТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	КорректировкаПоступленияТовары.Ссылка.Валюта КАК Валюта,
	|	КорректировкаПоступленияТовары.Ссылка.ДокументОснование.Дата КАК ДатаПоступления,
	|	КорректировкаПоступленияТовары.Ссылка.ДокументОснование.Номер КАК НомерПоступления,
	|	КорректировкаПоступленияТовары.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару
	|ПОМЕСТИТЬ ДанныеДокументовПоступления
	|ИЗ
	|	СсылкиНаПоступления КАК СсылкиНаПоступления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|		ПО (КорректировкаПоступленияТовары.Ссылка = СсылкиНаПоступления.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияТовары.Характеристика,
	|	КорректировкаПоступленияТовары.Цена,
	|	КорректировкаПоступленияТовары.Ссылка.ДокументОснование,
	|	КорректировкаПоступленияТовары.Упаковка,
	|	КорректировкаПоступленияТовары.Сумма,
	|	КорректировкаПоступленияТовары.СтавкаНДС,
	|	КорректировкаПоступленияТовары.СуммаНДС,
	|	КорректировкаПоступленияТовары.СуммаСНДС,
	|	КорректировкаПоступленияТовары.Склад,
	|	КорректировкаПоступленияТовары.Ссылка.ЦенаВключаетНДС,
	|	КорректировкаПоступленияТовары.Ссылка.Валюта,
	|	КорректировкаПоступленияТовары.Ссылка.ДокументОснование.Дата,
	|	КорректировкаПоступленияТовары.Ссылка.ДокументОснование.Номер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Характеристика КАК Характеристика,
	|	СУММА(ПоступлениеТоваровУслугТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ПоступлениеТоваровУслугТовары.Количество) КАК Количество,
	|	ПоступлениеТоваровУслугТовары.Цена КАК Цена,
	|	ПоступлениеТоваровУслугТовары.Ссылка КАК ДокументПоступления,
	|	ПоступлениеТоваровУслугТовары.Упаковка КАК Упаковка,
	|	ПоступлениеТоваровУслугТовары.Сумма КАК Сумма,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаНДС КАК СуммаНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаСНДС КАК СуммаСНДС,
	|	ПоступлениеТоваровУслугТовары.Склад КАК Склад,
	|	ПоступлениеТоваровУслугТовары.СуммаРучнойСкидки КАК СуммаРучнойСкидки,
	|	ПоступлениеТоваровУслугТовары.ПроцентРучнойСкидки КАК ПроцентРучнойСкидки,
	|	ПоступлениеТоваровУслугТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Валюта КАК Валюта,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Дата КАК ДатаПоступления,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Номер КАК НомерПоступления,
	|	ПоступлениеТоваровУслугТовары.Ссылка.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару
	|ИЗ
	|	СсылкиНаПоступления КАК СсылкиНаПоступления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|		ПО (ПоступлениеТоваровУслугТовары.Ссылка = СсылкиНаПоступления.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Характеристика,
	|	ПоступлениеТоваровУслугТовары.Цена,
	|	ПоступлениеТоваровУслугТовары.Ссылка,
	|	ПоступлениеТоваровУслугТовары.Сумма,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаСНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаРучнойСкидки,
	|	ПоступлениеТоваровУслугТовары.ПроцентРучнойСкидки,
	|	ПоступлениеТоваровУслугТовары.Склад,
	|	ПоступлениеТоваровУслугТовары.Упаковка,
	|	ПоступлениеТоваровУслугТовары.Ссылка.ЦенаВключаетНДС,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Валюта,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Дата,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Номер
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	ДокументПоступления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПроверяемыеТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаПроверяемыеТовары.Характеристика КАК Характеристика,
	|	СУММА(ТаблицаПроверяемыеТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ТаблицаПроверяемыеТовары.Количество) КАК Количество,
	|	ТаблицаПроверяемыеТовары.ДокументПоступления,
	|	СУММА(ТаблицаПроверяемыеТовары.Сумма) КАК Сумма,
	|	СУММА(ТаблицаПроверяемыеТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ТаблицаПроверяемыеТовары.СуммаСНДС) КАК СуммаСНДС
	|ПОМЕСТИТЬ ДанныеДокументовВозврата
	|ИЗ
	|	ДокументыПоступления КАК ДокументыПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику.Товары КАК ТаблицаПроверяемыеТовары
	|		ПО (ТаблицаПроверяемыеТовары.ДокументПоступления = ДокументыПоступления.Ссылка)
	|	И ТаблицаПроверяемыеТовары.Ссылка <> &ДокументВозврата
	|ГДЕ
	|	ТаблицаПроверяемыеТовары.Ссылка.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПроверяемыеТовары.Номенклатура,
	|	ТаблицаПроверяемыеТовары.Характеристика,
	|	ТаблицаПроверяемыеТовары.ДокументПоступления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(ЕСТЬNULL(ДанныеДокументовПоступления.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)) КАК Количество,
	|	ДанныеДокументовПоступления.Номенклатура КАК Номенклатура,
	|	ДанныеДокументовПоступления.Характеристика КАК Характеристика,
	|	СУММА(
	|			ВЫБОР
	|				КОГДА ДанныеДокументовПоступления.Упаковка ЕСТЬ NULL 
	|						ИЛИ ДанныеДокументовПоступления.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|					ТОГДА ЕСТЬNULL(ДанныеДокументовПоступления.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)
	|				ИНАЧЕ (ЕСТЬNULL(ДанныеДокументовПоступления.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)) / ДанныеДокументовПоступления.Упаковка.Коэффициент
	|			КОНЕЦ
	|	) КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(ДанныеДокументовПоступления.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)) = 0
	|			ТОГДА ДанныеДокументовПоступления.Цена
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДанныеДокументовПоступления.Упаковка ЕСТЬ NULL 
	|					ИЛИ ДанныеДокументовПоступления.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|				ТОГДА СУММА(ДанныеДокументовПоступления.Сумма / (ЕСТЬNULL(ДанныеДокументовПоступления.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)))
	|				ИНАЧЕ СУММА(ДанныеДокументовПоступления.Сумма / ((ЕСТЬNULL(ДанныеДокументовПоступления.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)) / ДанныеДокументовПоступления.Упаковка.Коэффициент))
	|		КОНЕЦ
	|	КОНЕЦ КАК Цена,
	|	ДанныеДокументовПоступления.Упаковка КАК Упаковка,
	|	СУММА(ДанныеДокументовПоступления.Сумма - ЕСТЬNULL(ДанныеДокументовВозврата.Сумма, 0)) КАК Сумма,
	|	СУММА(ДанныеДокументовПоступления.СуммаНДС - ЕСТЬNULL(ДанныеДокументовВозврата.СуммаНДС, 0)) КАК СуммаНДС,
	|	СУММА(ДанныеДокументовПоступления.СуммаСНДС - ЕСТЬNULL(ДанныеДокументовВозврата.СуммаСНДС, 0)) КАК СуммаСНДС,
	|	ДанныеДокументовПоступления.Склад КАК Склад,
	|	ДанныеДокументовПоступления.ДатаПоступления КАК ДатаПоступления,
	|	ДанныеДокументовПоступления.НомерПоступления КАК НомерПоступления,
	|	ДанныеДокументовПоступления.СтавкаНДС КАК СтавкаНДС,
	|	ДанныеДокументовПоступления.ДокументПоступления КАК ДокументПоступления,
	|	ДанныеДокументовПоступления.ДокументПоступления.Валюта КАК ВалютаПоступления,
	|	ДанныеДокументовПоступления.ДокументПоступления.ЦенаВключаетНДС КАК ЦенаВключаетНДС
	|ИЗ
	|	ДанныеДокументовПоступления КАК ДанныеДокументовПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументовВозврата КАК ДанныеДокументовВозврата
	|		ПО ДанныеДокументовПоступления.Номенклатура = ДанныеДокументовВозврата.Номенклатура
	|			И ДанныеДокументовПоступления.Характеристика = ДанныеДокументовВозврата.Характеристика
	|			И (ДанныеДокументовВозврата.ДокументПоступления = ДанныеДокументовПоступления.ДокументПоступления)
	|ГДЕ
	|	ЕСТЬNULL(ДанныеДокументовПоступления.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество,0) > 0
	|	И ВЫБОР 
	|		КОГДА НЕ ДанныеДокументовПоступления.ВернутьМногооборотнуюТару ТОГДА
	|			ДанныеДокументовПоступления.Номенклатура.ТипНоменклатуры В
	|				(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИНАЧЕ ДанныеДокументовПоступления.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
	|	КОНЕЦ
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокументовПоступления.Номенклатура,
	|	ДанныеДокументовПоступления.Характеристика,
	|	ДанныеДокументовПоступления.Цена,
	|	ДанныеДокументовПоступления.Упаковка,
	|	ДанныеДокументовПоступления.Склад,
	|	ДанныеДокументовПоступления.ДатаПоступления,
	|	ДанныеДокументовПоступления.НомерПоступления,
	|	ДанныеДокументовПоступления.СтавкаНДС,
	|	ДанныеДокументовПоступления.ДокументПоступления,
	|	ДанныеДокументовПоступления.ДокументПоступления.Валюта,
	|	ДанныеДокументовПоступления.ДокументПоступления.ЦенаВключаетНДС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПоступления УБЫВ";

	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Соглашение", Соглашение);
	Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
	Запрос.УстановитьПараметр("ДокументВозврата", ДокументВозврата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если Валюта <> Выборка.ВалютаПоступления Тогда
			КурсНовойВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);
			КурсСтаройВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Выборка.ВалютаПоступления, Дата);
			
			НоваяСтрока.Цена = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(
				НоваяСтрока.Цена,
				Выборка.ВалютаПоступления,Валюта,
				КурсСтаройВалюты.Курс,КурсНовойВалюты.Курс,
				КурсСтаройВалюты.Кратность,КурсНовойВалюты.Кратность
			);

			Ценообразование.ПересчитатьСуммыВСтроке(
				НоваяСтрока,
				Ложь,
				Ложь,
				Истина,
				ЦенаВключаетНДС
			);
		КонецЕсли;

		Если ЦенаВключаетНДС <> Выборка.ЦенаВключаетНДС Тогда
			Ценообразование.ПересчитатьСуммыВСтрокеПоСуммеСНДС(НоваяСтрока, ЦенаВключаетНДС, Ложь, Истина, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТары()
	
	ТаблицаТары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПринятаяВозвратнаяТараОстатки.Номенклатура        КАК Номенклатура,
	|	ПринятаяВозвратнаяТараОстатки.Характеристика      КАК Характеристика,
	|	ПринятаяВозвратнаяТараОстатки.Партнер             КАК Партнер,
	|	ПринятаяВозвратнаяТараОстатки.ДокументПоступления КАК ДокументПоступления,
	|	ПринятаяВозвратнаяТараОстатки.КоличествоОстаток   КАК Количество,
	|	ПринятаяВозвратнаяТараОстатки.СуммаОстаток        КАК Сумма
	|ПОМЕСТИТЬ
	|	втПринятаяВозвратнаяТара
	|ИЗ
	|	РегистрНакопления.ПринятаяВозвратнаяТара.Остатки(
	|			,
	|			Партнер = &Партнер
	|			И ДокументПоступления.Организация = &Организация
	|			И ДокументПоступления.Контрагент = &Контрагент
	|			И ДокументПоступления.Организация = &Организация
	|			И ДокументПоступления.Соглашение = &Соглашение
	|			И ДокументПоступления.Валюта = &Валюта
	|			И ДокументПоступления.НалогообложениеНДС = &НалогообложениеНДС
	|			И ДокументПоступления.ЦенаВключаетНДС = &ЦенаВключаетНДС
	|		) КАК ПринятаяВозвратнаяТараОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПринятаяВозвратнаяТараДвижения.Номенклатура        КАК Номенклатура,
	|	ПринятаяВозвратнаяТараДвижения.Характеристика      КАК Характеристика,
	|	ПринятаяВозвратнаяТараДвижения.Партнер             КАК Партнер,
	|	ПринятаяВозвратнаяТараДвижения.ДокументПоступления КАК ДокументПоступления,
	|	ВЫБОР КОГДА ПринятаяВозвратнаяТараДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			-ПринятаяВозвратнаяТараДвижения.Количество
	|		ИНАЧЕ
	|			ПринятаяВозвратнаяТараДвижения.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР КОГДА ПринятаяВозвратнаяТараДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			-ПринятаяВозвратнаяТараДвижения.Сумма
	|		ИНАЧЕ
	|			ПринятаяВозвратнаяТараДвижения.Сумма
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	РегистрНакопления.ПринятаяВозвратнаяТара КАК ПринятаяВозвратнаяТараДвижения
	|ГДЕ
	|	ПринятаяВозвратнаяТараДвижения.Регистратор = &ДокументВозврата
	|;
	|ВЫБРАТЬ
	|	втПринятаяВозвратнаяТара.Номенклатура              КАК Номенклатура,
	|	втПринятаяВозвратнаяТара.Характеристика            КАК Характеристика,
	|	втПринятаяВозвратнаяТара.Партнер                   КАК Партнер,
	|	втПринятаяВозвратнаяТара.ДокументПоступления       КАК ДокументПоступления,
	|	втПринятаяВозвратнаяТара.ДокументПоступления.Номер КАК НомерПоступления,
	|	втПринятаяВозвратнаяТара.ДокументПоступления.Дата  КАК ДатаПоступления,
	|	втПринятаяВозвратнаяТара.ДокументПоступления.ДатаВозвратаМногооборотнойТары КАК ДатаВозврата,
	|	СУММА(втПринятаяВозвратнаяТара.Количество)         КАК Количество,
	|	СУММА(втПринятаяВозвратнаяТара.Сумма)              КАК Сумма,
	|	ВЫБОР
	|		КОГДА
	|			СУММА(втПринятаяВозвратнаяТара.Количество) = 0
	|		ТОГДА
	|			СУММА(втПринятаяВозвратнаяТара.Сумма)
	|		ИНАЧЕ
	|			СУММА(втПринятаяВозвратнаяТара.Сумма) / СУММА(втПринятаяВозвратнаяТара.Количество)
	|	КОНЕЦ КАК Цена
	|ИЗ
	|	втПринятаяВозвратнаяТара
	|СГРУППИРОВАТЬ ПО
	|	втПринятаяВозвратнаяТара.Номенклатура,
	|	втПринятаяВозвратнаяТара.Характеристика,
	|	втПринятаяВозвратнаяТара.Партнер,
	|	втПринятаяВозвратнаяТара.ДокументПоступления
	|ИМЕЮЩИЕ
	|	СУММА(втПринятаяВозвратнаяТара.Количество) > 0
	|УПОРЯДОЧИТЬ ПО
	|	ДатаВозврата ВОЗР,
	|	Номенклатура.Наименование ВОЗР,
	|	Характеристика.Наименование ВОЗР
	|";

	Запрос.УстановитьПараметр("Партнер",            Партнер);
	Запрос.УстановитьПараметр("Контрагент",         Контрагент);
	Запрос.УстановитьПараметр("Организация",        Организация);
	Запрос.УстановитьПараметр("Соглашение",         Соглашение);
	Запрос.УстановитьПараметр("Валюта",             Валюта);
	Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",    ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ДокументВозврата",   ДокументВозврата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиПоТаблицеТоваров()
	
	ТоварыВыбраноКоличество = 0;
	ТоварыВыбраноСумма = 0;
	
	Для н=0 По ТаблицаТоваров.Количество()-1 Цикл
		
		Если ТаблицаТоваров[н].Выбран Тогда
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма + ТаблицаТоваров[н].СуммаСНДС;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиПоТаблицеТары()
	
	ТараВыбраноКоличество = 0;
	ТараВыбраноСумма = 0;
	
	Для н=0 По ТаблицаТары.Количество()-1 Цикл
		
		Если ТаблицаТары[н].Выбран Тогда
			ТараВыбраноКоличество = ТараВыбраноКоличество + 1;
			ТараВыбраноСумма = ТараВыбраноСумма + ТаблицаТары[н].Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсеТоварыНаСервере()
	
	Для н=0 По ТаблицаТоваров.Количество()-1 Цикл
		Если Не ТаблицаТоваров[н].Выбран Тогда
			ТаблицаТоваров[н].Выбран = Истина;
		КонецЕсли;
	КонецЦикла;
	ТоварыВыбраноКоличество = ТаблицаТоваров.Количество();
	ТоварыВыбраноСумма = ТаблицаТоваров.Итог("СуммаСНДС");
	
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьВсеТоварыНаСервере()
	
	Для н=0 По ТаблицаТоваров.Количество()-1 Цикл
		Если ТаблицаТоваров[н].Выбран Тогда
			ТаблицаТоваров[н].Выбран = Ложь;
		КонецЕсли;
	КонецЦикла;
	ТоварыВыбраноКоличество = 0;
	ТоварыВыбраноСумма = 0;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсюТаруНаСервере()
	
	Для н=0 По ТаблицаТары.Количество()-1 Цикл
		Если Не ТаблицаТары[н].Выбран Тогда
			ТаблицаТары[н].Выбран = Истина;
		КонецЕсли;
	КонецЦикла;
	ТараВыбраноКоличество = ТаблицаТары.Количество();
	ТараВыбраноСумма = ТаблицаТары.Итог("Сумма");
	
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьВсюТаруНаСервере()
	
	Для н=0 По ТаблицаТары.Количество()-1 Цикл
		Если ТаблицаТары[н].Выбран Тогда
			ТаблицаТары[н].Выбран = Ложь;
		КонецЕсли;
	КонецЦикла;
	ТараВыбраноКоличество = 0;
	ТараВыбраноСумма = 0;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьИнформационнуюНадписьОтборы()
	
	ТоварыИнформационнаяНадписьОтборы = НСтр("ru='Отбор по: %Организация%, %Партнер%, %Контрагент%, %Соглашение%, %НалогообложениеНДС%'");
	ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Организация%", Организация);
	ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Партнер%", Партнер);
	ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Контрагент%", Контрагент);
	ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Соглашение%", Соглашение);
	ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%НалогообложениеНДС%", НалогообложениеНДС);
	
	ТараИнформационнаяНадписьОтборы = НСтр("ru='Отбор по: %Организация%, %Партнер%, %Контрагент%, Валюта, %Соглашение%, %НалогообложениеНДС%'");
	ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%Организация%", Организация);
	ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%Партнер%", Партнер);
	ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%Контрагент%", Контрагент);
	ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%Соглашение%", Соглашение);
	ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%Валюта%", Валюта);
	ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%НалогообложениеНДС%", НалогообложениеНДС);
	
КонецПроцедуры
