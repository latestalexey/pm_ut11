////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПоступления = Параметры.ДокументПоступления;
	ДокументВозврата = Параметры.Документ;
	
	ПолучатьСерииНоменклатуры = Параметры.ПолучатьСерииНоменклатуры;
	
	Валюта = Параметры.Валюта;
	Дата = Параметры.Дата;
	ЦенаВключаетНДС = Параметры.ЦенаВключаетНДС;
	
	ЗаполнитьТаблицуТоваров();
	
	ПодборТоваровКлиентСервер.УстановитьЗаголовокФормыПодбора(ЭтаФорма);
	
	РассчитатьИтогиПоТаблицеТоваров();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ИнформационнаяНадпись, ВыбраноКоличество, ВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ПеренестиВДокумент И ВыбраноКоличество > 0 Тогда
		
		ОтветНаВопрос = Вопрос(НСтр("ru = 'Подобранные товары не перенесены в документ. Перенести?'"), РежимДиалогаВопрос.ДаНетОтмена);
		
		Если ОтветНаВопрос = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			ПеренестиВДокумент = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ПеренестиВДокумент Тогда
		
		АдресТоваровВХранилище = ПоместитьТоварыВХранилище();
		Структура = Новый Структура("АдресТоваровВХранилище", АдресТоваровВХранилище);
		ПеренестиВДокумент = Истина;
		ОповеститьОВыборе(Структура);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ТОВАРЫ

&НаКлиенте
Процедура ТаблицаТоваровВыбранПриИзменении(Элемент)
	
	Данные = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	ЗнакСложения = ?(Данные.Выбран, 1, -1);
	ВыбраноКоличество = ВыбраноКоличество + ЗнакСложения;
	ВыбраноСумма = ВыбраноСумма + Данные.СуммаСНДС*ЗнакСложения;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ИнформационнаяНадпись, ВыбраноКоличество, ВыбраноСумма, Валюта);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПеренестиВДокументВыполнить()
	
	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТовары()
	
	ВыбратьВсеТоварыНаСервере();
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ИнформационнаяНадпись, ВыбраноКоличество, ВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьТовары()
	
	ИсключитьВсеТоварыНаСервере();
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ИнформационнаяНадпись, ВыбраноКоличество, ВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделенныеТовары(Команда)
	
	МассивСтрок = Элементы.ТаблицаТоваров.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		Если Не ТаблицаТоваров[НомерСтроки].Выбран Тогда
			ТаблицаТоваров[НомерСтроки].Выбран = Истина;
			ВыбраноКоличество = ВыбраноКоличество + 1;
			ВыбраноСумма = ВыбраноСумма + ТаблицаТоваров[НомерСтроки].СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ИнформационнаяНадпись, ВыбраноКоличество, ВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделенныеТовары(Команда)
	
	МассивСтрок = Элементы.ТаблицаТоваров.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		Если ТаблицаТоваров[НомерСтроки].Выбран Тогда
			ТаблицаТоваров[НомерСтроки].Выбран = Ложь;
			ВыбраноКоличество = ВыбраноКоличество - 1;
			ВыбраноСумма = ВыбраноСумма - ТаблицаТоваров[НомерСтроки].СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ИнформационнаяНадпись, ВыбраноКоличество, ВыбраноСумма, Валюта);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура СформироватьТекстЗапросаПоСериямДокументаПоступления(ТекстЗапроса)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Товары.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(Товары.Количество КАК ЧИСЛО) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&ТаблицаТовары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура КАК Номенклатура,
	|	Серии.Характеристика КАК Характеристика,
	|	Серии.Серия,
	|	Серии.Количество
	|ПОМЕСТИТЬ СерииДокумента
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Серии КАК Серии
	|ГДЕ
	|	Серии.Ссылка = &Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ СгруппированныеТовары
	|ИЗ
	|	Товары КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СерииДокумента.Номенклатура КАК Номенклатура,
	|	СерииДокумента.Характеристика КАК Характеристика,
	|	СерииДокумента.Серия,
	|	СерииДокумента.Количество,
	|	СгруппированныеТовары.Количество КАК КоличествоВыбрано
	|ИЗ
	|	СерииДокумента КАК СерииДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СгруппированныеТовары КАК СгруппированныеТовары
	|		ПО (СгруппированныеТовары.Номенклатура = СерииДокумента.Номенклатура)
	|			И (СгруппированныеТовары.Характеристика = СерииДокумента.Характеристика)
	|ИТОГИ
	|	МАКСИМУМ(КоличествоВыбрано)
	|ПО
	|	Номенклатура,
	|	Характеристика";
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТоварыВХранилище()
	
	Товары = ТаблицаТоваров.Выгрузить(ТаблицаТоваров.НайтиСтроки(Новый Структура("Выбран", Истина)));
	
	Серии = Новый ТаблицаЗначений;
	Серии.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Серии.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Серии.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	Серии.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	
	Если ПолучатьСерииНоменклатуры Тогда
		
		Запрос = Новый Запрос;
		СформироватьТекстЗапросаПоСериямДокументаПоступления(Запрос.Текст);
		Запрос.УстановитьПараметр("ТаблицаТовары", Товары);
		Запрос.УстановитьПараметр("Документ", ДокументПоступления);
		
		ВыборкаПоНоменклатуре = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоНоменклатуре.Следующий() Цикл
			
			ВыборкаПоХарактеристикам = ВыборкаПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоХарактеристикам.Следующий() Цикл
				
				КоличествоКРаспределению = ВыборкаПоХарактеристикам.КоличествоВыбрано;
				Выборка = ВыборкаПоХарактеристикам.Выбрать();
				Пока Выборка.Следующий() Цикл
					
					СтрокаСерии = Серии.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаСерии, Выборка);
					КоличествоКРаспределению = КоличествоКРаспределению - СтрокаСерии.Количество;
					Если КоличествоКРаспределению < 0 Тогда
						СтрокаСерии.Количество = СтрокаСерии.Количество + КоличествоКРаспределению;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если КоличествоКРаспределению < 0 Тогда
					Продолжить;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ВозвращаемаяСтруктура = Новый Структура("Товары, Серии", Товары, Серии);
	
	АдресТоваровВХранилище = ПоместитьВоВременноеХранилище(ВозвращаемаяСтруктура, УникальныйИдентификатор);
	
	Возврат АдресТоваровВХранилище;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуТоваров()
	
	Если Не ЗначениеЗаполнено(ДокументПоступления) Тогда
		
		Если ТаблицаТоваров.Количество() > 0 Тогда
			ТаблицаТоваров.Очистить();
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	ВалютаПоступления = ДокументПоступления.Валюта;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", ДокументПоступления);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.НоменклатураПоставщика,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Упаковка,
	|	Товары.КоличествоУпаковок,
	|	Товары.Количество,
	|	Товары.Цена,
	|	Товары.ПроцентРучнойСкидки,
	|	Товары.СуммаРучнойСкидки,
	|	Товары.Сумма,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаНДС,
	|	Товары.СуммаСНДС,
	|	Товары.Склад
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Документ
	|	И Товары.Номенклатура.ТипНоменклатуры В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))";
	
	ПересчитыватьСтроку = (ЦенаВключаетНДС <> ДокументПоступления.ЦенаВключаетНДС);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Если ПересчитыватьСтроку Тогда
			Ценообразование.ПересчитатьСуммыВСтрокеПоСуммеСНДС(НоваяСтрока, ЦенаВключаетНДС, Ложь, Истина, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Валюта <> ВалютаПоступления Тогда
		
		КурсНовойВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);
		КурсСтаройВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаПоступления, Дата);
		Ценообразование.ПересчитатьСуммыТабличнойЧастиВВалюту(
			ТаблицаТоваров,
			ЦенаВключаетНДС,
			ВалютаПоступления,
			Валюта,
			КурсСтаройВалюты,
			КурсНовойВалюты,
			Ложь, Истина
		);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиПоТаблицеТоваров()
	
	ВыбраноКоличество = 0;
	ВыбраноСумма = 0;
	
	Для н=0 По ТаблицаТоваров.Количество()-1 Цикл
		
		Если ТаблицаТоваров[н].Выбран Тогда
			ВыбраноКоличество = ВыбраноКоличество + 1;
			ВыбраноСумма = ВыбраноСумма + ТаблицаТоваров[н].СуммаСНДС;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсеТоварыНаСервере()
	
	Для н=0 По ТаблицаТоваров.Количество()-1 Цикл
		Если Не ТаблицаТоваров[н].Выбран Тогда
			ТаблицаТоваров[н].Выбран = Истина;
		КонецЕсли;
	КонецЦикла;
	ВыбраноКоличество = ТаблицаТоваров.Количество();
	ВыбраноСумма = ТаблицаТоваров.Итог("СуммаСНДС");
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьВсеТоварыНаСервере()
	
	Для н=0 По ТаблицаТоваров.Количество()-1 Цикл
		Если ТаблицаТоваров[н].Выбран Тогда
			ТаблицаТоваров[н].Выбран = Ложь;
		КонецЕсли;
	КонецЦикла;
	ВыбраноКоличество = 0;
	ВыбраноСумма = 0;
	
КонецПроцедуры