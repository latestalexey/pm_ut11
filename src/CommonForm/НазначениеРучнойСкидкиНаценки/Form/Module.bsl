
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьОграниченияРучныхСкидок = (ПолучитьФункциональнуюОпцию("ИспользоватьОграниченияРучныхСкидокВПродажахПоПользователям")
	                                      ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьОграниченияРучныхСкидокВПродажахПоСоглашениям"))
	                                      И Параметры.ИспользоватьОграниченияРучныхСкидок;
	
	Если Не ИспользоватьОграниченияРучныхСкидок Тогда
		СкидкаНаценкаНеОграничена = НСтр("ru = 'Не ограничена'");
	КонецЕсли;
	Элементы.МаксимальнаяНаценка.Видимость = ИспользоватьОграниченияРучныхСкидок;
	Элементы.МаксимальнаяСкидка.Видимость = ИспользоватьОграниченияРучныхСкидок;
	Элементы.МаксимальныйПроцентСкидки.Видимость = ИспользоватьОграниченияРучныхСкидок;
	Элементы.МаксимальныйПроцентНаценки.Видимость = ИспользоватьОграниченияРучныхСкидок;
	Элементы.МаксимальнаяСкидкаНеОграничена.Видимость = Не ИспользоватьОграниченияРучныхСкидок;
	Элементы.МаксимальнаяНаценкаНеОграничена.Видимость = Не ИспользоватьОграниченияРучныхСкидок;
	
	СтруктураТаблиц = ПолучитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище);
	
	СуммаБезСкидок                   = СтруктураТаблиц.Ограничения.Итог("СуммаБезСкидки");
	СуммаАвтоматическойСкидкиНаценки = СтруктураТаблиц.Ограничения.Итог("СуммаАвтоматическойСкидки");
	СуммаРучнойСкидкиНаценки         = СтруктураТаблиц.Ограничения.Итог("СуммаРучнойСкидки");
	Валюта                           = Параметры.Валюта;
	
	СуммаДокумента = СуммаБезСкидок;
	СуммаДокументаСУчетомСкидкиАвтоматичскойСкидки = СуммаБезСкидок - СуммаАвтоматическойСкидкиНаценки;
	Если СуммаДокументаСУчетомСкидкиАвтоматичскойСкидки >= СуммаРучнойСкидкиНаценки Тогда
		СуммаДокументаСУчетомСкидки = СуммаДокументаСУчетомСкидкиАвтоматичскойСкидки - СуммаРучнойСкидкиНаценки;
	Иначе
		СуммаРучнойСкидкиНаценки = СуммаДокументаСУчетомСкидкиАвтоматичскойСкидки;
		СуммаДокументаСУчетомСкидки = 0;
	КонецЕсли;
	
	Если СуммаРучнойСкидкиНаценки < 0 Тогда
		СуммаРучнойСкидкиНаценки = -СуммаРучнойСкидкиНаценки;
		ВариантПредоставления = 2;
		Элементы.ВариантыПредоставления.ТекущаяСтраница = Элементы.ВариантыПредоставления.ПодчиненныеЭлементы.Наценка;
	Иначе
		СуммаРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки;
		ВариантПредоставления = 1;
		Элементы.ВариантыПредоставления.ТекущаяСтраница = Элементы.ВариантыПредоставления.ПодчиненныеЭлементы.Скидка;
	КонецЕсли;
	
	Если СуммаДокумента > 0 Тогда
		ПроцентРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки * 100 / СуммаДокумента;
	КонецЕсли;
	
	Если ИспользоватьОграниченияРучныхСкидок Тогда
		
		МаксимальнаяСуммаСкидки  = СтруктураТаблиц.Ограничения.Итог("МаксимальнаяСуммаРучнойСкидки");
		МаксимальнаяСуммаНаценки = СтруктураТаблиц.Ограничения.Итог("МаксимальнаяСуммаРучнойНаценки");
		
		Если СуммаДокумента <> 0 Тогда
			МаксимальныйПроцентСкидки  = Окр(МаксимальнаяСуммаСкидки * 100 / СуммаДокумента,15,2);
			МаксимальныйПроцентНаценки  = Окр(МаксимальнаяСуммаНаценки * 100 / СуммаДокумента,15,2);
		Иначе
			МаксимальныйПроцентСкидки  = 0;
			МаксимальныйПроцентНаценки = 0;
		КонецЕсли;
		
		МаксимальнаяСуммаСкидкиДляКонтроля = МаксимальнаяСуммаСкидки;
		МаксимальнаяСуммаНаценкиДляКонтроля = МаксимальнаяСуммаНаценки;
		
		
		
		
		
		Если МаксимальнаяСуммаСкидки > 0 Тогда
			Элементы.СуммаСкидки.КнопкаСпискаВыбора = Истина;
			Элементы.СуммаСкидки.СписокВыбора.Добавить(МаксимальнаяСуммаСкидки, Формат(МаксимальнаяСуммаСкидки, "ЧЦ=15; ЧДЦ=2"));
		КонецЕсли;
		
		Если МаксимальныйПроцентСкидки > 0 Тогда
			Элементы.ПроцентСкидки.КнопкаСпискаВыбора = Истина;
			Элементы.ПроцентСкидки.СписокВыбора.Добавить(МаксимальныйПроцентСкидки, Формат(МаксимальныйПроцентСкидки, "ЧЦ=15; ЧДЦ=2"));
		КонецЕсли;
		
		Если МаксимальнаяСуммаНаценки > 0 Тогда
			Элементы.СуммаНаценки.КнопкаСпискаВыбора = Истина;
			Элементы.СуммаНаценки.СписокВыбора.Добавить(МаксимальнаяСуммаНаценки, Формат(МаксимальнаяСуммаНаценки, "ЧЦ=15; ЧДЦ=2"));
		КонецЕсли;
		
		Если МаксимальныйПроцентНаценки > 0 Тогда
			Элементы.ПроцентНаценки.КнопкаСпискаВыбора = Истина;
			Элементы.ПроцентНаценки.СписокВыбора.Добавить(МаксимальныйПроцентНаценки, Формат(МаксимальныйПроцентНаценки, "ЧЦ=15; ЧДЦ=2"));
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ВариантПредоставления.СписокВыбора.Добавить(1, НСтр("ru = 'Скидка'"));
	Элементы.ВариантПредоставления.СписокВыбора.Добавить(2, НСтр("ru = 'Наценка'"));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	Если ИспользоватьОграниченияРучныхСкидок Тогда
		Если НЕ ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВариантПредоставления = 1 Тогда
		Закрыть(СуммаРучнойСкидкиНаценки);
	Иначе
		Закрыть(-СуммаРучнойСкидкиНаценки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ ШАПКИ

&НаКлиенте
Процедура ВариантПредоставленияПриИзменении(Элемент)
	
	Если ВариантПредоставления = 1 Тогда
		Элементы.ВариантыПредоставления.ТекущаяСтраница = Элементы.ВариантыПредоставления.ПодчиненныеЭлементы.Скидка;
		СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - СуммаРучнойСкидкиНаценки;
	Иначе
		Элементы.ВариантыПредоставления.ТекущаяСтраница = Элементы.ВариантыПредоставления.ПодчиненныеЭлементы.Наценка;
		СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки + СуммаРучнойСкидкиНаценки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаСкидкиПриИзменении(Элемент)
	
	Если СуммаДокумента > 0 Тогда
		ПроцентРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки * 100 / СуммаДокумента;
	КонецЕсли;
	СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - СуммаРучнойСкидкиНаценки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентСкидкиПриИзменении(Элемент)
	
	Если ПроцентРучнойСкидкиНаценки = МаксимальныйПроцентСкидки Тогда
		СуммаРучнойСкидкиНаценки = МаксимальнаяСуммаСкидки;
	Иначе
		СуммаРучнойСкидкиНаценки    = ПроцентРучнойСкидкиНаценки * СуммаДокумента / 100;
	КонецЕсли;
	
	СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - СуммаРучнойСкидкиНаценки;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаСУчетомСкидкиПриИзменении(Элемент)
	
	Если СуммаДокументаСУчетомСкидки > СуммаДокумента Тогда
		СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки;
		СуммаРучнойСкидкиНаценки = 0;
		ПроцентРучнойСкидкиНаценки = 0;
	Иначе
		СуммаРучнойСкидкиНаценки   = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - СуммаДокументаСУчетомСкидки;
		Если СуммаДокумента > 0 Тогда
			ПроцентРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки * 100 / СуммаДокумента;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СуммаНаценкиПриИзменении(Элемент)

	Если СуммаДокумента > 0 Тогда
		ПроцентРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки * 100 / СуммаДокумента;
	КонецЕсли;
	СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки + СуммаРучнойСкидкиНаценки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентНаценкиПриИзменении(Элемент)
	
	Если ПроцентРучнойСкидкиНаценки = МаксимальныйПроцентНаценки Тогда
		СуммаРучнойСкидкиНаценки = МаксимальнаяСуммаНаценки;
	Иначе
		СуммаРучнойСкидкиНаценки    = ПроцентРучнойСкидкиНаценки * СуммаДокумента / 100;
	КонецЕсли;
	
	СуммаДокументаСУчетомСкидки = СуммаДокумента + СуммаРучнойСкидкиНаценки - СуммаАвтоматическойСкидкиНаценки;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаСУчетомНаценкиПриИзменении(Элемент)
	
	Если СуммаДокументаСУчетомСкидки < СуммаДокумента - СуммаАвтоматическойСкидкиНаценки Тогда
		СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки;
		СуммаРучнойСкидкиНаценки = 0;
		ПроцентРучнойСкидкиНаценки = 0;
	Иначе
		СуммаРучнойСкидкиНаценки   = СуммаДокументаСУчетомСкидки - (СуммаДокумента - СуммаАвтоматическойСкидкиНаценки);
		Если СуммаДокумента > 0 Тогда
			ПроцентРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки * 100 / СуммаДокумента;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ВариантПредоставления = 1 Тогда
		Если СуммаРучнойСкидкиНаценки > МаксимальнаяСуммаСкидкиДляКонтроля Тогда
			
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Сумма скидки превышает максимальное возможное значение'")+": "+МаксимальнаяСуммаСкидкиДляКонтроля+" "+Валюта;
			Сообщение.Сообщить();
			
		КонецЕсли;
	КонецЕсли;
	
	Если ВариантПредоставления = 2 Тогда
		Если СуммаРучнойСкидкиНаценки > МаксимальнаяСуммаНаценкиДляКонтроля Тогда
			
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Сумма наценки превышает максимальное возможное значение'")+": "+МаксимальнаяСуммаНаценкиДляКонтроля+" "+Валюта;
			Сообщение.Сообщить();
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаСУчетомСкидкиНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();

	МаксимальноеТочностьОкругления = 100000;
	
	Варианты = Новый Массив;

	ТочностиОкругления = Новый Массив();
	ТочностьОкругления             = 0.01;
	ЧетноеПриращение               = Ложь;
	
	СоответствиеТочностей = Новый Соответствие;
	
	Пока ТочностьОкругления <= МаксимальноеТочностьОкругления Цикл
		
		Значение = ОкруглитьСумму(СуммаДокументаСУчетомСкидки, ТочностьОкругления, Истина);
		Если Варианты.Найти(Значение) = Неопределено Тогда
			Варианты.Добавить(Значение);
			СоответствиеТочностей.Вставить(Значение, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(+окр.до %1)", ТочностьОкругления));
		КонецЕсли;
		
		Значение = ОкруглитьСумму(СуммаДокументаСУчетомСкидки, ТочностьОкругления, Ложь);
		Если Варианты.Найти(Значение) = Неопределено Тогда
			Варианты.Добавить(Значение);
			СоответствиеТочностей.Вставить(Значение, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(-окр.до %1)", ТочностьОкругления));
		КонецЕсли;
	
		ТочностьОкругления = ТочностьОкругления * ?(ЧетноеПриращение, 2, 5);
		ЧетноеПриращение   = Не ЧетноеПриращение;
		
	КонецЦикла;
	
	Для Каждого Сумма Из Варианты Цикл
		
		Если Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - Сумма > МаксимальнаяСуммаСкидки Тогда
			Продолжить;
		КонецЕсли;
		
		Если СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - Сумма < 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СуммаДокументаСУчетомСкидки = Сумма Тогда
			Продолжить;
		КонецЕсли;
		
		Элемент.СписокВыбора.Добавить(Сумма, Формат(Сумма, "ЧДЦ=2")+ " " + СоответствиеТочностей.Получить(Сумма));
		
	КонецЦикла;
	
	Элемент.СписокВыбора.СортироватьПоЗначению();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаСУчетомНаценкиНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();

	МаксимальноеТочностьОкругления = 100000;
	
	Варианты = Новый Массив;

	ТочностиОкругления = Новый Массив();
	ТочностьОкругления             = 0.01;
	ЧетноеПриращение               = Ложь;
	
	СоответствиеТочностей = Новый Соответствие;
	
	Пока ТочностьОкругления <= МаксимальноеТочностьОкругления Цикл
		
		Значение = ОкруглитьСумму(СуммаДокументаСУчетомСкидки, ТочностьОкругления, Истина);
		Если Варианты.Найти(Значение) = Неопределено Тогда
			Варианты.Добавить(Значение);
			СоответствиеТочностей.Вставить(Значение, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(+окр.до %1)", ТочностьОкругления));
		КонецЕсли;
		
		Значение = ОкруглитьСумму(СуммаДокументаСУчетомСкидки, ТочностьОкругления, Ложь);
		Если Варианты.Найти(Значение) = Неопределено Тогда
			Варианты.Добавить(Значение);
			СоответствиеТочностей.Вставить(Значение, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(-окр.до %1)", ТочностьОкругления));
		КонецЕсли;
		
		ТочностьОкругления = ТочностьОкругления * ?(ЧетноеПриращение, 2, 5);
		ЧетноеПриращение   = Не ЧетноеПриращение;
		
	КонецЦикла;
	
	Для Каждого Сумма Из Варианты Цикл
		
		Если Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Сумма - (СуммаДокумента - СуммаАвтоматическойСкидкиНаценки) > МаксимальнаяСуммаНаценки Тогда
			Продолжить;
		КонецЕсли;
		
		Если Сумма - (СуммаДокумента - СуммаАвтоматическойСкидкиНаценки) < 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СуммаДокументаСУчетомСкидки = Сумма Тогда
			Продолжить;
		КонецЕсли;
		
		Элемент.СписокВыбора.Добавить(Сумма, Формат(Сумма, "ЧДЦ=2")+ " " + СоответствиеТочностей.Получить(Сумма));
		
	КонецЦикла;
	
	Элемент.СписокВыбора.СортироватьПоЗначению();

КонецПроцедуры

&НаКлиенте
Процедура СуммаСкидкиНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	
	Если МаксимальнаяСуммаСкидки > 0 Тогда
		Элемент.СписокВыбора.Добавить(МаксимальнаяСуммаСкидки, Формат(МаксимальнаяСуммаСкидки, "ЧЦ=15; ЧДЦ=2"));
	КонецЕсли;

	МаксимальноеТочностьОкругления = 100000;
	
	Варианты = Новый Массив;

	ТочностиОкругления = Новый Массив();
	ТочностьОкругления             = 0.01;
	ЧетноеПриращение               = Ложь;
	
	СоответствиеТочностей = Новый Соответствие;
	
	Пока ТочностьОкругления <= МаксимальноеТочностьОкругления Цикл
		
		Значение = ОкруглитьСумму(СуммаДокументаСУчетомСкидки, ТочностьОкругления, Истина);
		Если Варианты.Найти(Значение) = Неопределено Тогда
			Варианты.Добавить(Значение);
			СоответствиеТочностей.Вставить(Значение, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(окр.до %1)", Значение));
		КонецЕсли;
		
		Значение = ОкруглитьСумму(СуммаДокументаСУчетомСкидки, ТочностьОкругления, Ложь);
		Если Варианты.Найти(Значение) = Неопределено Тогда
			Варианты.Добавить(Значение);
			СоответствиеТочностей.Вставить(Значение, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(окр.до %1)", Значение));
		КонецЕсли;
		
		ТочностьОкругления = ТочностьОкругления * ?(ЧетноеПриращение, 2, 5);
		ЧетноеПриращение   = Не ЧетноеПриращение;
		
	КонецЦикла;
	
	Для Каждого Сумма Из Варианты Цикл
		
		Если Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СуммаСкидки =  СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - Сумма;
		
		Если СуммаСкидки > МаксимальнаяСуммаСкидки Тогда
			Продолжить;
		КонецЕсли;
		
		Если СуммаСкидки < 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СуммаДокументаСУчетомСкидки = Сумма Тогда
			Продолжить;
		КонецЕсли;
		
		Элемент.СписокВыбора.Добавить(СуммаСкидки, Формат(СуммаСкидки, "ЧДЦ=2; ЧН=0.00")+ " " + СоответствиеТочностей.Получить(Сумма));
		
	КонецЦикла;
	
	Элемент.СписокВыбора.СортироватьПоЗначению();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаНаценкиНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	
	Если МаксимальнаяСуммаСкидки > 0 Тогда
		Элемент.СписокВыбора.Добавить(МаксимальнаяСуммаСкидки, Формат(МаксимальнаяСуммаСкидки, "ЧЦ=15; ЧДЦ=2"));
	КонецЕсли;

	МаксимальноеТочностьОкругления = 100000;
	
	Варианты = Новый Массив;

	ТочностиОкругления = Новый Массив();
	ТочностьОкругления             = 0.01;
	ЧетноеПриращение               = Ложь;
	
	СоответствиеТочностей = Новый Соответствие;
	
	Пока ТочностьОкругления <= МаксимальноеТочностьОкругления Цикл
		
		Значение = ОкруглитьСумму(СуммаДокументаСУчетомСкидки, ТочностьОкругления, Истина);
		Если Варианты.Найти(Значение) = Неопределено Тогда
			Варианты.Добавить(Значение);
			СоответствиеТочностей.Вставить(Значение, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(окр.до %1)", Значение));
		КонецЕсли;
		
		Значение = ОкруглитьСумму(СуммаДокументаСУчетомСкидки, ТочностьОкругления, Ложь);
		Если Варианты.Найти(Значение) = Неопределено Тогда
			Варианты.Добавить(Значение);
			СоответствиеТочностей.Вставить(Значение, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(окр.до %1)", Значение));
		КонецЕсли;
		
		ТочностьОкругления = ТочностьОкругления * ?(ЧетноеПриращение, 2, 5);
		ЧетноеПриращение   = Не ЧетноеПриращение;
		
	КонецЦикла;
	
	Для Каждого Сумма Из Варианты Цикл
		
		Если Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СуммаНаценки = Сумма - (СуммаДокумента - СуммаАвтоматическойСкидкиНаценки);
		
		Если СуммаНаценки > МаксимальнаяСуммаНаценки Тогда
			Продолжить;
		КонецЕсли;
		
		Если СуммаНаценки < 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СуммаДокументаСУчетомСкидки = Сумма Тогда
			Продолжить;
		КонецЕсли;
		
		Элемент.СписокВыбора.Добавить(СуммаНаценки, Формат(СуммаНаценки, "ЧДЦ=2; ЧН=0.00")+ " " + СоответствиеТочностей.Получить(Сумма));
		
	КонецЦикла;
	
	Элемент.СписокВыбора.СортироватьПоЗначению();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Функция ОкруглитьСумму(Число, ТочностьОкругления, ОкруглятьВБольшуюСторону) Экспорт

	Перем Результат;
		
	// вычислим количество интервалов, входящих в число
	КоличествоИнтервалов = Число / ТочностьОкругления;
		
	// вычислим целое количество интервалов.
	КоличествоЦелыхИнтервалов = Цел(КоличествоИнтервалов);
		
	Если КоличествоИнтервалов = КоличествоЦелыхИнтервалов Тогда
		// Числа поделились нацело. Округлять не нужно.
		Результат = Число;
	Иначе
		Если ОкруглятьВБольшуюСторону Тогда
			// При порядке округления "0.05" 0.371 должно округлится до 0.4
			Результат = ТочностьОкругления * (КоличествоЦелыхИнтервалов + 1);
		Иначе
			// При порядке округления "0.05" 0.371 должно округлится до 0.35,
			// а 0.376 до 0.4
			Результат = ТочностьОкругления * Окр(КоличествоИнтервалов, 0, РежимОкругления.Окр15как20);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ОкруглитьСумму()

