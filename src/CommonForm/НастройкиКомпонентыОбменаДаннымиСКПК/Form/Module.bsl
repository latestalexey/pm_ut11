////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьСтатусы();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура УстановитьКомпоненту(Команда)
	
	УстановитьВнешнююКомпоненту("ОбщийМакет.КомпонентаОбменДаннымиСМобильнымиПриложениями");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьКомпоненту(Команда)
	
	Если глКомпонентаОбменаСМобильнымиПриложениями <> Неопределено Тогда
		ТекстСообщения = НСтр("ru='Компонента обмена данными с мобильными приложениями уже подключена'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	Иначе
		МобильныеПриложенияКлиент.ПодключитьВнешнююКомпонентуДляОбменаДаннымиСМобильнымиПриложениями();
	КонецЕсли;
	
	ОбновитьСтатусы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьКомпоненту(Команда)
	
	Если глКомпонентаОбменаСМобильнымиПриложениями = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Компонента обмена данными не подключена'");
		Предупреждение(ТекстСообщения);
	Иначе
		глКомпонентаОбменаСМобильнымиПриложениями.Отключить(0);
		глКомпонентаОбменаСМобильнымиПриложениями = Неопределено;
		ТекстСообщения = НСтр("ru='Отключение компоненты'");
		ТекстПояснения = НСтр("ru='Компонента обмена данными отключена'");
		ПоказатьОповещениеПользователя(ТекстСообщения,,ТекстПояснения, БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
	ОбновитьСтатусы();
	
КонецПроцедуры

&НаКлиенте
Процедура Настройки(Команда)
	
	ОткрытьФормуМодально("ОбщаяФорма.ПерсональныеНастройкиРаботыСКомпонентойОбменаДанными");
	ОбновитьСтатусы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюОСтатусе(Команда)
	
	ОбновитьСтатусы();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОбновитьСтатусы()
	
	Если глКомпонентаОбменаСМобильнымиПриложениями <> Неопределено Тогда
		
		КомпонентаПодключена = Истина;
		СтатусПодключения = "Компонента подключена";
		
		TCPIPПодключен = глКомпонентаОбменаСМобильнымиПриложениями.TCPIPПодключен;
		COMПортПодключен = глКомпонентаОбменаСМобильнымиПриложениями.COMПортПодключен;
		IRDAПодключен = глКомпонентаОбменаСМобильнымиПриложениями.IRDAПодключен;
		
		ПортTCPIP = глКомпонентаОбменаСМобильнымиПриложениями.ПортTCPIP;
		COMПорт = глКомпонентаОбменаСМобильнымиПриложениями.COMПорт;
		ИмяСервисаIRDA = глКомпонентаОбменаСМобильнымиПриложениями.ИмяСервисаIRDA;
		
		ВестиЖурналСобытий = глКомпонентаОбменаСМобильнымиПриложениями.ВестиЖурналСобытий;
		КаталогЖурналаСобытий = глКомпонентаОбменаСМобильнымиПриложениями.КаталогЖурналаСобытий;
		
	Иначе
		
		КомпонентаПодключена = Ложь;
		СтатусПодключения = "Компонента не подключена";
		
		TCPIPПодключен = Ложь;
		COMПортПодключен = Ложь;
		IRDAПодключен = Ложь;
		
		ПортTCPIP = 0;
		COMПорт=0;
		ИмяСервисаIRDA = "";
		
		ВестиЖурналСобытий = Ложь;
		КаталогЖурналаСобытий = "";
		
	КонецЕсли;
	
КонецПроцедуры