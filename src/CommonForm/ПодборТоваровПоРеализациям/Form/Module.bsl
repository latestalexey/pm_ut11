////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ДокументВозврата = Параметры.ДокументВозврата;
	Валюта = Параметры.Валюта;
	Дата = Параметры.Дата;
	ЦенаВключаетНДС = Параметры.ЦенаВключаетНДС;
	Партнер = Параметры.Партнер;
	Соглашение = Параметры.Соглашение;
	Организация = Параметры.Организация;
	Контрагент = Параметры.Контрагент;
	НалогообложениеНДС = Параметры.НалогообложениеНДС;
	ВозвратОтРозничногоПокупателя = Параметры.ВозвратОтРозничногоПокупателя;
	ЧекККМ = Параметры.ЧекККМ;
	
	Период = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(,"ПериодПодбораТоваровПоРеализациям");
	Если Период.ДатаОкончания = Дата(1, 1, 1) Тогда
		Период.Вариант = ВариантСтандартногоПериода.Месяц;
	КонецЕсли;
	
	ЗаполнитьТаблицуТоваров();
	РассчитатьИтогиПоТаблицеТоваров();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта
	);
	
	ИспользоватьМногооборотнуюТару = ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару");
	
	Если ИспользоватьМногооборотнуюТару И Параметры.ПоказыватьТару Тогда
		
		ЗаполнитьТаблицуТары();
		РассчитатьИтогиПоТаблицеТары();
		ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
			ТараИнформационнаяНадпись,
			ТараВыбраноКоличество,
			ТараВыбраноСумма,
			Валюта
		);
		
		Если Не Параметры.ПоказыватьТовары Тогда
			Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТара;
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТовары;
		КонецЕсли;
		
	Иначе
		
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТовары;
		
	КонецЕсли;
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("ТаблицаТары");
	МассивЭлементов.Добавить("ТараИнформационнаяНадписьОтборы");
	МассивЭлементов.Добавить("ТараИнформационнаяНадпись");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", ИспользоватьМногооборотнуюТару И Параметры.ПоказыватьТару);
		
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("ПериодВариант");
	МассивЭлементов.Добавить("ПериодДатаНачала");
	МассивЭлементов.Добавить("ПериодДатаОкончания");
	МассивЭлементов.Добавить("Обновить");
	МассивЭлементов.Добавить("ТаблицаТоваров");
	МассивЭлементов.Добавить("ТоварыИнформационнаяНадпись");
	МассивЭлементов.Добавить("ТоварыИнформационнаяНадписьОтборы");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", Параметры.ПоказыватьТовары);
	
	ПодборТоваровКлиентСервер.СформироватьЗаголовокФормыПодбора(Заголовок, ДокументВозврата);
	СформироватьИнформационнуюНадписьОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ПеренестиВДокумент И ТоварыВыбраноКоличество > 0 Тогда
		
		ОтветНаВопрос = Вопрос(НСтр("ru = 'Подобранные товары не перенесены в документ. Перенести?'"), РежимДиалогаВопрос.ДаНетОтмена);
		
		Если ОтветНаВопрос = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			ПеренестиВДокумент = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ПереноситьЧек = ПроверитьВозможностьПереносаЧекаККМ();
	Если ПеренестиВДокумент И НЕ ПереноситьЧек Тогда
		ВариантыОтветов = Новый СписокЗначений;
		ВариантыОтветов.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Перенести без чека ККМ'"));
		ВариантыОтветов.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Вернуться'"));
		
		Результат = Вопрос(НСтр("ru = 'Выбраны товары из разных чеков ККМ. Чек ККМ не будет заполнен. Перенести товары без чека ККМ?'"), ВариантыОтветов);
		
		Если Результат = КодВозвратаДиалога.Нет Тогда
			ПеренестиВДокумент = Ложь;
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ПеренестиВДокумент Тогда
		
		АдресТоваровВХранилище = ПоместитьТоварыВХранилище();
		Структура = Новый Структура("АдресТоваровВХранилище", АдресТоваровВХранилище);
		ПеренестиВДокумент = Истина;
		ОповеститьОВыборе(Структура);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(,"ПериодПодбораТоваровПоРеализациям", Период);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ТОВАРЫ

&НаКлиенте
Процедура ТаблицаТоваровВыбранПриИзменении(Элемент)
	
	Данные = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	ЗнакСложения = ?(Данные.Выбран, 1, -1);
	ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + ЗнакСложения;
	ТоварыВыбраноСумма = ТоварыВыбраноСумма + Данные.СуммаСНДС*ЗнакСложения;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ТоварыИнформационнаяНадпись, ТоварыВыбраноКоличество, ТоварыВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТарыВыбранПриИзменении(Элемент)
	
	Данные = Элементы.ТаблицаТары.ТекущиеДанные;
	
	ЗнакСложения = ?(Данные.Выбран, 1, -1);
	ТараВыбраноКоличество = ТараВыбраноКоличество + ЗнакСложения;
	ТараВыбраноСумма = ТараВыбраноСумма + Данные.Сумма*ЗнакСложения;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПеренестиВДокументВыполнить()
	
	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТовары()
	
	ВыбратьВсеТоварыНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ТоварыИнформационнаяНадпись, ТоварыВыбраноКоличество, ТоварыВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьТовары()
	
	ИсключитьВсеТоварыНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ТоварыИнформационнаяНадпись, ТоварыВыбраноКоличество, ТоварыВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделенныеТовары(Команда)
	
	МассивСтрок = Элементы.ТаблицаТоваров.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		Если Не ТаблицаТоваров[НомерСтроки].Выбран Тогда
			ТаблицаТоваров[НомерСтроки].Выбран = Истина;
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма + ТаблицаТоваров[НомерСтроки].СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ТоварыИнформационнаяНадпись, ТоварыВыбраноКоличество, ТоварыВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделенныеТовары(Команда)
	
	МассивСтрок = Элементы.ТаблицаТоваров.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		Если ТаблицаТоваров[НомерСтроки].Выбран Тогда
			ТаблицаТоваров[НомерСтроки].Выбран = Ложь;
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество - 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма - ТаблицаТоваров[НомерСтроки].СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ТоварыИнформационнаяНадпись, ТоварыВыбраноКоличество, ТоварыВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТару()
	
	ВыбратьВсюТаруНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьТару()
	
	ИсключитьВсюТаруНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделеннуюТару(Команда)
	
	МассивСтрок = Элементы.ТаблицаТары.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		Если Не ТаблицаТары[НомерСтроки].Выбран Тогда
			ТаблицаТары[НомерСтроки].Выбран = Истина;
			ТараВыбраноКоличество = ТараВыбраноКоличество + 1;
			ТараВыбраноСумма = ТараВыбраноСумма + ТаблицаТары[НомерСтроки].Сумма;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделеннуюТару(Команда)
	
	МассивСтрок = Элементы.ТаблицаТары.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		Если ТаблицаТары[НомерСтроки].Выбран Тогда
			ТаблицаТары[НомерСтроки].Выбран = Ложь;
			ТараВыбраноКоличество = ТараВыбраноКоличество - 1;
			ТараВыбраноСумма = ТараВыбраноСумма - ТаблицаТары[НомерСтроки].Сумма
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТовары(Команда)
	
	ЗаполнитьТаблицуТоваров();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТару(Команда)
	
	ЗаполнитьТаблицуТары();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодВариантПриИзменении(Элемент)
	
	ЗаполнитьТаблицуТоваров();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Функция ПоместитьТоварыВХранилище()
	
	Товары = ТаблицаТоваров.Выгрузить(ТаблицаТоваров.НайтиСтроки(Новый Структура("Выбран", Истина)));
	СтруктураШапки = Новый Структура("ЧекККМ,Партнер",
										Документы.ЧекККМ.ПустаяСсылка(),
										Партнер);
	
	Если ВозвратОтРозничногоПокупателя Тогда
		
		ТаблицаОснований = ТаблицаТоваров.Выгрузить(ТаблицаТоваров.НайтиСтроки(Новый Структура("Выбран", Истина)));
		ТаблицаОснований.Свернуть("ДокументРеализации,Партнер");
		
		Если ТаблицаОснований.Количество() = 1 Тогда
			СтрокаТаблицы = ТаблицаОснований.Получить(0);
			СтруктураШапки.ЧекККМ = СтрокаТаблицы.ДокументРеализации;
			СтруктураШапки.Партнер = СтрокаТаблицы.Партнер;
		КонецЕсли;
		
	Иначе
		
		Для Каждого ТекСтрока Из ТаблицаТары Цикл
			Если ТекСтрока.Выбран Тогда
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
				НоваяСтрока.КоличествоУпаковок = ТекСтрока.Количество;
				НоваяСтрока.СуммаСНДС = ТекСтрока.Сумма;
				НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли; 
	
	АдресТоваровВХранилище = ПоместитьВоВременноеХранилище(Новый Структура("Товары,СтруктураШапки", Товары, СтруктураШапки), УникальныйИдентификатор);
	
	Возврат АдресТоваровВХранилище;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуТоваров()
	
	ТаблицаТоваров.Очистить();
	
	Запрос = Новый Запрос;
	Если НЕ ВозвратОтРозничногоПокупателя Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ДокументыРеализации
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И РеализацияТоваровУслуг.Проведен
		|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
		|	И РеализацияТоваровУслуг.Организация = &Организация
		|	И РеализацияТоваровУслуг.Соглашение = &Соглашение
		|	И РеализацияТоваровУслуг.НалогообложениеНДС = &НалогообложениеНДС
		|	И РеализацияТоваровУслуг.Партнер = &Партнер
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МАКСИМУМ(КорректировкаРеализации.Дата) КАК Дата,
		|	ДокументыРеализации.Ссылка КАК СсылкаРеализации
		|ПОМЕСТИТЬ ДанныеКорректировки
		|ИЗ
		|	ДокументыРеализации КАК ДокументыРеализации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|		ПО (КорректировкаРеализации.ДокументОснование = ДокументыРеализации.Ссылка)
		|ГДЕ
		|	КорректировкаРеализации.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументыРеализации.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СсылкаРеализации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МАКСИМУМ(КорректировкаРеализации.Ссылка) КАК Ссылка,
		|	МАКСИМУМ(КорректировкаРеализации.Ссылка.Дата) КАК Дата,
		|	КорректировкаРеализации.Ссылка.ДокументОснование КАК СсылкаРеализации
		|ПОМЕСТИТЬ ДанныеПоследнейКорректировки
		|ИЗ
		|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|ГДЕ
		|	(КорректировкаРеализации.Ссылка.ДокументОснование, КорректировкаРеализации.Ссылка.Дата) В
		|			(ВЫБРАТЬ
		|				ДанныеКорректировки.СсылкаРеализации КАК СсылкаРеализации,
		|				ДанныеКорректировки.Дата КАК Дата
		|			ИЗ
		|				ДанныеКорректировки)
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаРеализации.Ссылка.ДокументОснование
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	СсылкаРеализации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
		|			ТОГДА ДокументыРеализации.Ссылка
		|		ИНАЧЕ ДанныеПоследнейКорректировки.Ссылка
		|	КОНЕЦ КАК Ссылка
		|ПОМЕСТИТЬ СсылкиНаРеализации
		|ИЗ
		|	ДокументыРеализации КАК ДокументыРеализации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
		|		ПО ДокументыРеализации.Ссылка = ДанныеПоследнейКорректировки.СсылкаРеализации
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КорректировкаРеализацииТовары.Номенклатура КАК Номенклатура,
		|	КорректировкаРеализацииТовары.Характеристика КАК Характеристика,
		|	СУММА(КорректировкаРеализацииТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(КорректировкаРеализацииТовары.Количество) КАК Количество,
		|	КорректировкаРеализацииТовары.Цена КАК Цена,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование КАК ДокументРеализации,
		|	КорректировкаРеализацииТовары.Упаковка КАК Упаковка,
		|	КорректировкаРеализацииТовары.ВидЦены КАК ВидЦены,
		|	КорректировкаРеализацииТовары.Сумма КАК Сумма,
		|	КорректировкаРеализацииТовары.СтавкаНДС КАК СтавкаНДС,
		|	КорректировкаРеализацииТовары.СуммаНДС КАК СуммаНДС,
		|	КорректировкаРеализацииТовары.СуммаСНДС КАК СуммаСНДС,
		|	КорректировкаРеализацииТовары.Склад КАК Склад,
		|	КорректировкаРеализацииТовары.Упаковка,
		|	NULL КАК СуммаРучнойСкидки,
		|	NULL КАК СуммаАвтоматическойСкидки,
		|	NULL КАК ПроцентРучнойСкидки,
		|	NULL КАК ПроцентАвтоматическойСкидки,
		|	КорректировкаРеализацииТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	КорректировкаРеализацииТовары.Ссылка.Валюта КАК Валюта,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.Дата КАК ДатаРеализации,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.Номер КАК НомерРеализации,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару
		|ПОМЕСТИТЬ ДанныеДокументовРеализации
		|ИЗ
		|	СсылкиНаРеализации КАК СсылкиНаРеализации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
		|		ПО (КорректировкаРеализацииТовары.Ссылка = СсылкиНаРеализации.Ссылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаРеализацииТовары.Номенклатура,
		|	КорректировкаРеализацииТовары.Характеристика,
		|	КорректировкаРеализацииТовары.Цена,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование,
		|	КорректировкаРеализацииТовары.Упаковка,
		|	КорректировкаРеализацииТовары.ВидЦены,
		|	КорректировкаРеализацииТовары.Сумма,
		|	КорректировкаРеализацииТовары.СтавкаНДС,
		|	КорректировкаРеализацииТовары.СуммаНДС,
		|	КорректировкаРеализацииТовары.СуммаСНДС,
		|	КорректировкаРеализацииТовары.Склад,
		|	КорректировкаРеализацииТовары.Ссылка.ЦенаВключаетНДС,
		|	КорректировкаРеализацииТовары.Ссылка.Валюта,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.Дата,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.Номер
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	РеализацияТоваровУслугТовары.Характеристика КАК Характеристика,
		|	СУММА(РеализацияТоваровУслугТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(РеализацияТоваровУслугТовары.Количество) КАК Количество,
		|	РеализацияТоваровУслугТовары.Цена КАК Цена,
		|	РеализацияТоваровУслугТовары.Ссылка КАК ДокументРеализации,
		|	РеализацияТоваровУслугТовары.Упаковка КАК Упаковка,
		|	РеализацияТоваровУслугТовары.ВидЦены КАК ВидЦены,
		|	РеализацияТоваровУслугТовары.Сумма КАК Сумма,
		|	РеализацияТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
		|	РеализацияТоваровУслугТовары.СуммаНДС КАК СуммаНДС,
		|	РеализацияТоваровУслугТовары.СуммаСНДС КАК СуммаСНДС,
		|	РеализацияТоваровУслугТовары.Склад КАК Склад,
		|	РеализацияТоваровУслугТовары.Упаковка,
		|	РеализацияТоваровУслугТовары.СуммаРучнойСкидки КАК СуммаРучнойСкидки,
		|	РеализацияТоваровУслугТовары.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
		|	РеализацияТоваровУслугТовары.ПроцентРучнойСкидки КАК ПроцентРучнойСкидки,
		|	РеализацияТоваровУслугТовары.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
		|	РеализацияТоваровУслугТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	РеализацияТоваровУслугТовары.Ссылка.Валюта КАК Валюта,
		|	РеализацияТоваровУслугТовары.Ссылка.Дата КАК ДатаРеализации,
		|	РеализацияТоваровУслугТовары.Ссылка.Номер КАК НомерРеализации,
		|	РеализацияТоваровУслугТовары.Ссылка.ВернутьМногооборотнуюТару
		|ИЗ
		|	СсылкиНаРеализации КАК СсылкиНаРеализации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|		ПО (РеализацияТоваровУслугТовары.Ссылка = СсылкиНаРеализации.Ссылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровУслугТовары.Номенклатура,
		|	РеализацияТоваровУслугТовары.Характеристика,
		|	РеализацияТоваровУслугТовары.Цена,
		|	РеализацияТоваровУслугТовары.Ссылка,
		|	РеализацияТоваровУслугТовары.Упаковка,
		|	РеализацияТоваровУслугТовары.ВидЦены,
		|	РеализацияТоваровУслугТовары.Сумма,
		|	РеализацияТоваровУслугТовары.СтавкаНДС,
		|	РеализацияТоваровУслугТовары.СуммаНДС,
		|	РеализацияТоваровУслугТовары.СуммаСНДС,
		|	РеализацияТоваровУслугТовары.СуммаРучнойСкидки,
		|	РеализацияТоваровУслугТовары.СуммаАвтоматическойСкидки,
		|	РеализацияТоваровУслугТовары.ПроцентРучнойСкидки,
		|	РеализацияТоваровУслугТовары.ПроцентАвтоматическойСкидки,
		|	РеализацияТоваровУслугТовары.Склад,
		|	РеализацияТоваровУслугТовары.Упаковка,
		|	РеализацияТоваровУслугТовары.Ссылка.ЦенаВключаетНДС,
		|	РеализацияТоваровУслугТовары.Ссылка.Валюта,
		|	РеализацияТоваровУслугТовары.Ссылка.Дата,
		|	РеализацияТоваровУслугТовары.Ссылка.Номер
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	ДокументРеализации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПроверяемыеТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаПроверяемыеТовары.Характеристика КАК Характеристика,
		|	СУММА(ТаблицаПроверяемыеТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ТаблицаПроверяемыеТовары.Количество) КАК Количество,
		|	ТаблицаПроверяемыеТовары.ДокументРеализации КАК ДокументРеализации,
		|	СУММА(ТаблицаПроверяемыеТовары.Сумма) КАК Сумма,
		|	СУММА(ТаблицаПроверяемыеТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ТаблицаПроверяемыеТовары.СуммаСНДС) КАК СуммаСНДС
		|ПОМЕСТИТЬ ДанныеДокументовВозврата
		|ИЗ
		|	ДокументыРеализации КАК ДокументыРеализации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ТаблицаПроверяемыеТовары
		|		ПО (ТаблицаПроверяемыеТовары.ДокументРеализации = ДокументыРеализации.Ссылка)
		|ГДЕ
		|	ТаблицаПроверяемыеТовары.Ссылка.Проведен
		|	И ТаблицаПроверяемыеТовары.Ссылка <> &ДокументВозврата
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПроверяемыеТовары.Номенклатура,
		|	ТаблицаПроверяемыеТовары.Характеристика,
		|	ТаблицаПроверяемыеТовары.ДокументРеализации
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ЕСТЬNULL(ДанныеДокументовРеализации.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)) КАК Количество,
		|	ДанныеДокументовРеализации.Номенклатура КАК Номенклатура,
		|	ДанныеДокументовРеализации.Характеристика КАК Характеристика,
		|	СУММА(
		|			ВЫБОР
		|				КОГДА ДанныеДокументовРеализации.Упаковка ЕСТЬ NULL 
		|						ИЛИ ДанныеДокументовРеализации.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
		|					ТОГДА ЕСТЬNULL(ДанныеДокументовРеализации.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)
		|				ИНАЧЕ (ЕСТЬNULL(ДанныеДокументовРеализации.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)) / ДанныеДокументовРеализации.Упаковка.Коэффициент
		|			КОНЕЦ
		|	) КАК КоличествоУпаковок,
		|	ВЫБОР
		|		КОГДА СУММА(ЕСТЬNULL(ДанныеДокументовРеализации.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)) = 0
		|			ТОГДА ДанныеДокументовРеализации.Цена
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ДанныеДокументовРеализации.Упаковка ЕСТЬ NULL 
		|					ИЛИ ДанныеДокументовРеализации.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
		|				ТОГДА СУММА(ДанныеДокументовРеализации.Сумма / (ЕСТЬNULL(ДанныеДокументовРеализации.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)))
		|				ИНАЧЕ СУММА(ДанныеДокументовРеализации.Сумма / ((ЕСТЬNULL(ДанныеДокументовРеализации.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0)) / ДанныеДокументовРеализации.Упаковка.Коэффициент))
		|		КОНЕЦ
		|	КОНЕЦ КАК Цена,
		|	ДанныеДокументовРеализации.Упаковка КАК Упаковка,
		|	СУММА(ДанныеДокументовРеализации.Сумма - ЕСТЬNULL(ДанныеДокументовВозврата.Сумма, 0)) КАК Сумма,
		|	СУММА(ДанныеДокументовРеализации.СуммаНДС - ЕСТЬNULL(ДанныеДокументовВозврата.СуммаНДС, 0)) КАК СуммаНДС,
		|	СУММА(ДанныеДокументовРеализации.СуммаСНДС - ЕСТЬNULL(ДанныеДокументовВозврата.СуммаСНДС, 0)) КАК СуммаСНДС,
		|	ДанныеДокументовРеализации.Склад КАК Склад,
		|	ДанныеДокументовРеализации.ДатаРеализации КАК ДатаРеализации,
		|	ДанныеДокументовРеализации.НомерРеализации КАК НомерРеализации,
		|	ДанныеДокументовРеализации.СтавкаНДС КАК СтавкаНДС,
		|	ДанныеДокументовРеализации.ДокументРеализации КАК ДокументРеализации,
		|	ДанныеДокументовРеализации.ДокументРеализации.Валюта КАК ВалютаРеализации,
		|	ДанныеДокументовРеализации.ДокументРеализации.ЦенаВключаетНДС КАК ЦенаВключаетНДС
		|ИЗ
		|	ДанныеДокументовРеализации КАК ДанныеДокументовРеализации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументовВозврата КАК ДанныеДокументовВозврата
		|		ПО ДанныеДокументовРеализации.Номенклатура = ДанныеДокументовВозврата.Номенклатура
		|			И ДанныеДокументовРеализации.Характеристика = ДанныеДокументовВозврата.Характеристика
		|			И (ДанныеДокументовВозврата.ДокументРеализации = ДанныеДокументовРеализации.ДокументРеализации)
		|ГДЕ
		|	ЕСТЬNULL(ДанныеДокументовРеализации.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество,0) > 0
		|	И ВЫБОР 
		|		КОГДА НЕ ДанныеДокументовРеализации.ВернутьМногооборотнуюТару ТОГДА
		|			ДанныеДокументовРеализации.Номенклатура.ТипНоменклатуры В
		|				(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|		ИНАЧЕ ДанныеДокументовРеализации.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
		|	КОНЕЦ
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДокументовРеализации.Номенклатура,
		|	ДанныеДокументовРеализации.Характеристика,
		|	ДанныеДокументовРеализации.Цена,
		|	ДанныеДокументовРеализации.Упаковка,
		|	ДанныеДокументовРеализации.Склад,
		|	ДанныеДокументовРеализации.ДатаРеализации,
		|	ДанныеДокументовРеализации.НомерРеализации,
		|	ДанныеДокументовРеализации.СтавкаНДС,
		|	ДанныеДокументовРеализации.ДокументРеализации,
		|	ДанныеДокументовРеализации.ДокументРеализации.Валюта,
		|	ДанныеДокументовРеализации.ДокументРеализации.ЦенаВключаетНДС
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаРеализации УБЫВ";
		
		Запрос.УстановитьПараметр("Партнер", Партнер);
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
		Запрос.УстановитьПараметр("Соглашение", Соглашение);
		Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
		Запрос.УстановитьПараметр("ДокументВозврата", ДокументВозврата);
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЧекККМТовары.Ссылка КАК ДокументРеализации,
		|	ЧекККМТовары.Номенклатура КАК Номенклатура,
		|	ЧекККМТовары.Характеристика КАК Характеристика,
		|	ЧекККМТовары.Упаковка КАК Упаковка,
		|	СУММА(ЧекККМТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ЧекККМТовары.Количество) КАК Количество,
		|	ВЫБОР
		|		КОГДА СУММА(ЧекККМТовары.СуммаРучнойСкидки) + СУММА(ЧекККМТовары.СуммаАвтоматическойСкидки) = 0
		|				ИЛИ СУММА(ЧекККМТовары.КоличествоУпаковок) = 0
		|			ТОГДА ЧекККМТовары.Цена
		|		ИНАЧЕ СУММА(ЧекККМТовары.Сумма) / СУММА(ЧекККМТовары.КоличествоУпаковок)
		|	КОНЕЦ КАК Цена,
		|	СУММА(ЧекККМТовары.Сумма) КАК Сумма,
		|	ЧекККМТовары.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ЧекККМТовары.СуммаНДС) КАК СуммаНДС,
		|	ЧекККМТовары.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
		|	СУММА(ЧекККМТовары.СуммаАвтоматическойСкидки) КАК СуммаАвтоматическойСкидки,
		|	ЧекККМТовары.ПроцентРучнойСкидки КАК ПроцентРучнойСкидки,
		|	СУММА(ЧекККМТовары.СуммаРучнойСкидки) КАК СуммаРучнойСкидки,
		|	ЧекККМТовары.Ссылка.Дата КАК ДатаРеализации,
		|	ЧекККМТовары.Ссылка.Номер КАК НомерРеализации,
		|	ЧекККМТовары.Ссылка.Валюта КАК ВалютаРеализации,
		|	ЧекККМТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	ЧекККМТовары.Ссылка.Партнер КАК Партнер
		|ПОМЕСТИТЬ ЧекиККМ
		|ИЗ
		|	Документ.ЧекККМ.Товары КАК ЧекККМТовары
		|ГДЕ
		|	ЧекККМТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ЧекККМТовары.Ссылка.НалогообложениеНДС = &НалогообложениеНДС
		|	И ЧекККМТовары.Ссылка.Проведен
		|	И ЧекККМТовары.Ссылка.Организация = &Организация
		|	И ЧекККМТовары.Ссылка.Партнер = &Партнер
		|	И (ЧекККМТовары.Ссылка = &ЧекККМ ИЛИ &ЧекККМ = ЗНАЧЕНИЕ(Документ.ЧекККМ.ПустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	ЧекККМТовары.Ссылка,
		|	ЧекККМТовары.Номенклатура,
		|	ЧекККМТовары.Характеристика,
		|	ЧекККМТовары.Упаковка,
		|	ЧекККМТовары.Цена,
		|	ЧекККМТовары.СтавкаНДС,
		|	ЧекККМТовары.ПроцентАвтоматическойСкидки,
		|	ЧекККМТовары.ПроцентРучнойСкидки,
		|	ЧекККМТовары.Ссылка.Дата,
		|	ЧекККМТовары.Ссылка.Номер,
		|	ЧекККМТовары.Ссылка.Валюта,
		|	ЧекККМТовары.Ссылка.ЦенаВключаетНДС,
		|	ЧекККМТовары.Ссылка.Партнер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЧекККМВозвратТовары.Ссылка КАК ДокументРеализации,
		|	ЧекККМВозвратТовары.Номенклатура КАК Номенклатура,
		|	ЧекККМВозвратТовары.Характеристика КАК Характеристика,
		|	ЧекККМВозвратТовары.Упаковка КАК Упаковка,
		|	СУММА(ЧекККМВозвратТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ЧекККМВозвратТовары.Количество) КАК Количество,
		|	ЧекККМВозвратТовары.Цена КАК Цена,
		|	СУММА(ЧекККМВозвратТовары.Сумма) КАК Сумма,
		|	ЧекККМВозвратТовары.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ЧекККМВозвратТовары.СуммаНДС) КАК СуммаНДС,
		|	ЧекККМВозвратТовары.Ссылка.Дата КАК ДатаРеализации,
		|	ЧекККМВозвратТовары.Ссылка.Номер КАК НомерРеализации,
		|	ЧекККМВозвратТовары.Ссылка.Валюта КАК ВалютаРеализации,
		|	ЧекККМВозвратТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	ЧекККМВозвратТовары.Ссылка.Партнер КАК Партнер
		|ПОМЕСТИТЬ ЧекиККМВозврат
		|ИЗ
		|	Документ.ЧекККМВозврат.Товары КАК ЧекККМВозвратТовары
		|ГДЕ
		|	ЧекККМВозвратТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ЧекККМВозвратТовары.Ссылка.НалогообложениеНДС = &НалогообложениеНДС
		|	И ЧекККМВозвратТовары.Ссылка.Проведен
		|	И ЧекККМВозвратТовары.Ссылка.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	ЧекККМВозвратТовары.Упаковка,
		|	ЧекККМВозвратТовары.Номенклатура,
		|	ЧекККМВозвратТовары.СтавкаНДС,
		|	ЧекККМВозвратТовары.Ссылка,
		|	ЧекККМВозвратТовары.Характеристика,
		|	ЧекККМВозвратТовары.Цена,
		|	ЧекККМВозвратТовары.Ссылка.Дата,
		|	ЧекККМВозвратТовары.Ссылка.Номер,
		|	ЧекККМВозвратТовары.Ссылка.Валюта,
		|	ЧекККМВозвратТовары.Ссылка.ЦенаВключаетНДС,
		|	ЧекККМВозвратТовары.Ссылка.Партнер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПроверяемыеТовары.Номенклатура,
		|	ТаблицаПроверяемыеТовары.Характеристика,
		|	СУММА(ТаблицаПроверяемыеТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ТаблицаПроверяемыеТовары.Количество) КАК Количество,
		|	ТаблицаПроверяемыеТовары.ДокументРеализации КАК ДокументРеализации,
		|	ТаблицаПроверяемыеТовары.Ссылка КАК Ссылка,
		|	СУММА(ТаблицаПроверяемыеТовары.Сумма) КАК Сумма,
		|	СУММА(ТаблицаПроверяемыеТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ТаблицаПроверяемыеТовары.СуммаСНДС) КАК СуммаСНДС
		|ПОМЕСТИТЬ ДанныеВозврата
		|ИЗ
		|	ЧекиККМ КАК ЧекиККМ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ТаблицаПроверяемыеТовары
		|		ПО ЧекиККМ.ДокументРеализации = ТаблицаПроверяемыеТовары.Ссылка.ЧекККМ
		|ГДЕ
		|	ТаблицаПроверяемыеТовары.Ссылка.Проведен
		|	И ТаблицаПроверяемыеТовары.Ссылка <> &ДокументВозврата
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПроверяемыеТовары.Номенклатура,
		|	ТаблицаПроверяемыеТовары.Характеристика,
		|	ТаблицаПроверяемыеТовары.ДокументРеализации,
		|	ТаблицаПроверяемыеТовары.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЧекиККМ.ДокументРеализации КАК ДокументРеализации,
		|	ЧекиККМ.Номенклатура КАК Номенклатура,
		|	ЧекиККМ.Характеристика КАК Характеристика,
		|	ЧекиККМ.Упаковка КАК Упаковка,
		|	ВЫБОР
		|		КОГДА ЧекиККМ.Упаковка ЕСТЬ NULL 
		|				ИЛИ ЧекиККМ.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
		|			ТОГДА ЧекиККМ.Количество - ЕСТЬNULL(ДанныеВозврата.Количество, 0) - ЕСТЬNULL(ЧекиККМВозврат.Количество, 0)
		|		ИНАЧЕ (ЧекиККМ.Количество - ЕСТЬNULL(ДанныеВозврата.Количество, 0) - ЕСТЬNULL(ЧекиККМВозврат.Количество, 0)) / ЧекиККМ.Упаковка.Коэффициент
		|	КОНЕЦ КАК КоличествоУпаковок,
		|	ЧекиККМ.Количество - ЕСТЬNULL(ДанныеВозврата.Количество, 0) - ЕСТЬNULL(ЧекиККМВозврат.Количество, 0) КАК Количество,
		|	ВЫБОР
		|		КОГДА ЧекиККМ.Количество - ЕСТЬNULL(ДанныеВозврата.Количество, 0) - ЕСТЬNULL(ЧекиККМВозврат.Количество, 0) = 0
		|			ТОГДА ЧекиККМ.Цена
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ЧекиККМ.Упаковка ЕСТЬ NULL 
		|						ИЛИ ЧекиККМ.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
		|					ТОГДА ЧекиККМ.Сумма / (ЧекиККМ.Количество - ЕСТЬNULL(ДанныеВозврата.Количество, 0) - ЕСТЬNULL(ЧекиККМВозврат.Количество, 0))
		|				ИНАЧЕ ЧекиККМ.Сумма / ((ЧекиККМ.Количество - ЕСТЬNULL(ДанныеВозврата.Количество, 0) - ЕСТЬNULL(ЧекиККМВозврат.Количество, 0)) / ЧекиККМ.Упаковка.Коэффициент)
		|			КОНЕЦ
		|	КОНЕЦ КАК Цена,
		|	ЧекиККМ.Сумма - ЕСТЬNULL(ДанныеВозврата.Сумма, 0) - ЕСТЬNULL(ЧекиККМВозврат.Сумма, 0) КАК Сумма,
		|	ЧекиККМ.СтавкаНДС КАК СтавкаНДС,
		|	ЧекиККМ.СуммаНДС - ЕСТЬNULL(ДанныеВозврата.СуммаНДС, 0) - ЕСТЬNULL(ЧекиККМВозврат.СуммаНДС, 0) КАК СуммаНДС,
		|	ЧекиККМ.ДатаРеализации КАК ДатаРеализации,
		|	ЧекиККМ.НомерРеализации КАК НомерРеализации,
		|	ЧекиККМ.ВалютаРеализации КАК ВалютаРеализации,
		|	ЧекиККМ.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	ЧекиККМ.Партнер КАК Партнер,
		|	ЧекиККМ.ДокументРеализации.Склад КАК Склад
		|ИЗ
		|	ЧекиККМ КАК ЧекиККМ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЧекиККМВозврат КАК ЧекиККМВозврат
		|		ПО ЧекиККМ.ДокументРеализации = ЧекиККМВозврат.ДокументРеализации.ЧекККМ
		|			И ЧекиККМ.Номенклатура = ЧекиККМВозврат.Номенклатура
		|			И ЧекиККМ.Характеристика = ЧекиККМВозврат.Характеристика
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеВозврата КАК ДанныеВозврата
		|		ПО ЧекиККМ.ДокументРеализации = ДанныеВозврата.Ссылка.ЧекККМ
		|			И ЧекиККМ.Номенклатура = ДанныеВозврата.Номенклатура
		|			И ЧекиККМ.Номенклатура = ДанныеВозврата.Характеристика
		|ГДЕ
		|	ЧекиККМ.Количество - ЕСТЬNULL(ДанныеВозврата.Количество, 0) - ЕСТЬNULL(ЧекиККМВозврат.Количество, 0) > 0
		|	И ЧекиККМ.Номенклатура.ТипНоменклатуры В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|УПОРЯДОЧИТЬ ПО
		|	ДатаРеализации УБЫВ";
		
		Запрос.УстановитьПараметр("Партнер", Партнер);
		Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
		Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ЧекККМ", ЧекККМ);
		Запрос.УстановитьПараметр("ДокументВозврата", ДокументВозврата);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если Валюта <> Выборка.ВалютаРеализации Тогда
			
			КурсНовойВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);
			КурсСтаройВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Выборка.ВалютаРеализации, Дата);
			
			НоваяСтрока.Цена = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(
				НоваяСтрока.Цена,
				Выборка.ВалютаРеализации,Валюта,
				КурсСтаройВалюты.Курс,КурсНовойВалюты.Курс,
				КурсСтаройВалюты.Кратность,КурсНовойВалюты.Кратность
			);
				
		КонецЕсли;
		
		Ценообразование.ПересчитатьСуммыВСтроке(
				НоваяСтрока,
				Истина,
				Ложь,
				Истина,
				ЦенаВключаетНДС
			);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТары()
	
	ТаблицаТары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПереданнаяВозвратнаяТараОстатки.Номенклатура      КАК Номенклатура,
	|	ПереданнаяВозвратнаяТараОстатки.Характеристика    КАК Характеристика,
	|	ПереданнаяВозвратнаяТараОстатки.Партнер           КАК Партнер,
	|	ПереданнаяВозвратнаяТараОстатки.ДокументПередачи  КАК ДокументПередачи,
	|	ПереданнаяВозвратнаяТараОстатки.КоличествоОстаток КАК Количество,
	|	ПереданнаяВозвратнаяТараОстатки.СуммаОстаток      КАК Сумма
	|ПОМЕСТИТЬ
	|	втПереданнаяВозвратнаяТара
	|ИЗ
	|	РегистрНакопления.ПереданнаяВозвратнаяТара.Остатки(
	|			,
	|			Партнер = &Партнер
	|			И ДокументПередачи.Организация = &Организация
	|			И ДокументПередачи.Контрагент = &Контрагент
	|			И ДокументПередачи.Организация = &Организация
	|			И ДокументПередачи.Соглашение = &Соглашение
	|			И ДокументПередачи.Валюта = &Валюта
	|			И ДокументПередачи.НалогообложениеНДС = &НалогообложениеНДС
	|			И ДокументПередачи.ЦенаВключаетНДС = &ЦенаВключаетНДС
	|		) КАК ПереданнаяВозвратнаяТараОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПереданнаяВозвратнаяТараДвижения.Номенклатура     КАК Номенклатура,
	|	ПереданнаяВозвратнаяТараДвижения.Характеристика   КАК Характеристика,
	|	ПереданнаяВозвратнаяТараДвижения.Партнер          КАК Партнер,
	|	ПереданнаяВозвратнаяТараДвижения.ДокументПередачи КАК ДокументПередачи,
	|	ВЫБОР КОГДА ПереданнаяВозвратнаяТараДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			-ПереданнаяВозвратнаяТараДвижения.Количество
	|		ИНАЧЕ
	|			ПереданнаяВозвратнаяТараДвижения.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР КОГДА ПереданнаяВозвратнаяТараДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			-ПереданнаяВозвратнаяТараДвижения.Сумма
	|		ИНАЧЕ
	|			ПереданнаяВозвратнаяТараДвижения.Сумма
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	РегистрНакопления.ПереданнаяВозвратнаяТара КАК ПереданнаяВозвратнаяТараДвижения
	|ГДЕ
	|	ПереданнаяВозвратнаяТараДвижения.Регистратор = &ДокументВозврата
	|;
	|ВЫБРАТЬ
	|	втПереданнаяВозвратнаяТара.Номенклатура           КАК Номенклатура,
	|	втПереданнаяВозвратнаяТара.Характеристика         КАК Характеристика,
	|	втПереданнаяВозвратнаяТара.Партнер                КАК Партнер,
	|	втПереданнаяВозвратнаяТара.ДокументПередачи       КАК ДокументРеализации,
	|	втПереданнаяВозвратнаяТара.ДокументПередачи.Номер КАК НомерРеализации,
	|	втПереданнаяВозвратнаяТара.ДокументПередачи.Дата  КАК ДатаРеализации,
	|	втПереданнаяВозвратнаяТара.ДокументПередачи.ДатаВозвратаМногооборотнойТары КАК ДатаВозврата,
	|	СУММА(втПереданнаяВозвратнаяТара.Количество)      КАК Количество,
	|	СУММА(втПереданнаяВозвратнаяТара.Сумма)           КАК Сумма,
	|	ВЫБОР
	|		КОГДА
	|			СУММА(втПереданнаяВозвратнаяТара.Количество) = 0
	|		ТОГДА
	|			СУММА(втПереданнаяВозвратнаяТара.Сумма)
	|		ИНАЧЕ
	|			СУММА(втПереданнаяВозвратнаяТара.Сумма) / СУММА(втПереданнаяВозвратнаяТара.Количество)
	|	КОНЕЦ КАК Цена
	|ИЗ
	|	втПереданнаяВозвратнаяТара
	|СГРУППИРОВАТЬ ПО
	|	втПереданнаяВозвратнаяТара.Номенклатура,
	|	втПереданнаяВозвратнаяТара.Характеристика,
	|	втПереданнаяВозвратнаяТара.Партнер,
	|	втПереданнаяВозвратнаяТара.ДокументПередачи
	|ИМЕЮЩИЕ
	|	СУММА(втПереданнаяВозвратнаяТара.Количество) > 0
	|УПОРЯДОЧИТЬ ПО
	|	ДатаВозврата ВОЗР,
	|	Номенклатура.Наименование ВОЗР,
	|	Характеристика.Наименование ВОЗР
	|";

	Запрос.УстановитьПараметр("Партнер",            Партнер);
	Запрос.УстановитьПараметр("Контрагент",         Контрагент);
	Запрос.УстановитьПараметр("Организация",        Организация);
	Запрос.УстановитьПараметр("Соглашение",         Соглашение);
	Запрос.УстановитьПараметр("Валюта",             Валюта);
	Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",    ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ДокументВозврата",   ДокументВозврата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиПоТаблицеТоваров()
	
	ТоварыВыбраноКоличество = 0;
	ТоварыВыбраноСумма = 0;
	
	Для н=0 По ТаблицаТоваров.Количество()-1 Цикл
		
		Если ТаблицаТоваров[н].Выбран Тогда
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма + ТаблицаТоваров[н].СуммаСНДС;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиПоТаблицеТары()
	
	ТараВыбраноКоличество = 0;
	ТараВыбраноСумма = 0;
	
	Для н=0 По ТаблицаТары.Количество()-1 Цикл
		
		Если ТаблицаТары[н].Выбран Тогда
			ТараВыбраноКоличество = ТараВыбраноКоличество + 1;
			ТараВыбраноСумма = ТараВыбраноСумма + ТаблицаТары[н].Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсеТоварыНаСервере()
	
	Для н=0 По ТаблицаТоваров.Количество()-1 Цикл
		Если Не ТаблицаТоваров[н].Выбран Тогда
			ТаблицаТоваров[н].Выбран = Истина;
		КонецЕсли;
	КонецЦикла;
	ТоварыВыбраноКоличество = ТаблицаТоваров.Количество();
	ТоварыВыбраноСумма = ТаблицаТоваров.Итог("СуммаСНДС");
	
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьВсеТоварыНаСервере()
	
	Для н=0 По ТаблицаТоваров.Количество()-1 Цикл
		Если ТаблицаТоваров[н].Выбран Тогда
			ТаблицаТоваров[н].Выбран = Ложь;
		КонецЕсли;
	КонецЦикла;
	ТоварыВыбраноКоличество = 0;
	ТоварыВыбраноСумма = 0;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсюТаруНаСервере()
	
	Для н=0 По ТаблицаТары.Количество()-1 Цикл
		Если Не ТаблицаТары[н].Выбран Тогда
			ТаблицаТары[н].Выбран = Истина;
		КонецЕсли;
	КонецЦикла;
	ТараВыбраноКоличество = ТаблицаТары.Количество();
	ТараВыбраноСумма = ТаблицаТары.Итог("Сумма");
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьВсюТаруНаСервере()
	
	Для н=0 По ТаблицаТары.Количество()-1 Цикл
		Если ТаблицаТары[н].Выбран Тогда
			ТаблицаТары[н].Выбран = Ложь;
		КонецЕсли;
	КонецЦикла;
	ТараВыбраноКоличество = 0;
	ТараВыбраноСумма = 0;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВозможностьПереносаЧекаККМ()
	
	Если ВозвратОтРозничногоПокупателя Тогда
		ТаблицаОснований = ТаблицаТоваров.Выгрузить(ТаблицаТоваров.НайтиСтроки(Новый Структура("Выбран", Истина)));
		ТаблицаОснований.Свернуть("ДокументРеализации");
		Если ТаблицаОснований.Количество() <= 1 Тогда
			Возврат Истина
		Иначе
			Возврат Ложь
		КонецЕсли;
	Иначе
		Возврат Истина
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СформироватьИнформационнуюНадписьОтборы()
	
	Если ВозвратОтРозничногоПокупателя Тогда
		
		Если ЧекККМ <> Документы.ЧекККМ.ПустаяСсылка() Тогда
			ТоварыИнформационнаяНадписьОтборы = НСтр("ru='Отбор по: %Организация%, %Партнер%, %НалогообложениеНДС%, %ЧекККМ%'");
			ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Организация%", Организация);
			ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Партнер%", Партнер);
			ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%НалогообложениеНДС%", НалогообложениеНДС);
			ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%ЧекККМ%", ЧекККМ);
		Иначе
			ТоварыИнформационнаяНадписьОтборы = НСтр("ru='Отбор по: %Организация%, %НалогообложениеНДС%'");
			ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Организация%", Организация);
			ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%НалогообложениеНДС%", НалогообложениеНДС);
		КонецЕсли;
		
	Иначе
		
		ТоварыИнформационнаяНадписьОтборы = НСтр("ru='Отбор по: %Организация%, %Партнер%, %Контрагент%, %Соглашение%, %НалогообложениеНДС%'");
		ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Организация%", Организация);
		ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Партнер%", Партнер);
		ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Контрагент%", Контрагент);
		ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%Соглашение%", Соглашение);
		ТоварыИнформационнаяНадписьОтборы = СтрЗаменить(ТоварыИнформационнаяНадписьОтборы,"%НалогообложениеНДС%", НалогообложениеНДС);
		
		ТараИнформационнаяНадписьОтборы = НСтр("ru='Отбор по: %Организация%, %Партнер%, %Контрагент%, Валюта, %Соглашение%, %НалогообложениеНДС%'");
		ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%Организация%", Организация);
		ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%Партнер%", Партнер);
		ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%Контрагент%", Контрагент);
		ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%Соглашение%", Соглашение);
		ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%Валюта%", Валюта);
		ТараИнформационнаяНадписьОтборы = СтрЗаменить(ТараИнформационнаяНадписьОтборы,"%НалогообложениеНДС%", НалогообложениеНДС);
		
	КонецЕсли;
	
КонецПроцедуры
