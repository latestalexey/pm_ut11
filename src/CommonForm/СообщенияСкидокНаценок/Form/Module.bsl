
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КартаЛояльности = Параметры.КартаЛояльности;
	ВидЗаменяемойКартыЛояльности = Параметры.КартаЛояльности.Владелец;
	
	Партнер    = Параметры.Партнер;
	Контрагент = Параметры.Контрагент;
	Соглашение = Параметры.Соглашение;
	
	Для Каждого СтрокаТЧ Из Параметры.Сообщения Цикл
		НовоеСообщение = ТаблицаСообщений.Добавить();
		НовоеСообщение.СкидкаНаценка  = СтрокаТЧ.СкидкаНаценка;
		НовоеСообщение.ТекстСообщения = НовоеСообщение.СкидкаНаценка.ТекстСообщения;
		НовоеСообщение.ВидКарты       = НовоеСообщение.СкидкаНаценка.ВидКартыЛояльности;
		НовоеСообщение.НапомнитьПозже = СтрокаТЧ.НапомнитьПозже;
	КонецЦикла;
	
	НомерСообщения = 1;
	ВсегоСообщений = ТаблицаСообщений.Количество();
	
	ТаблицаСообщений.Сортировать("НапомнитьПозже Убыв,СкидкаНаценка Убыв");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОтобразитьТекущееСообщение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ТаблицаСообщений[НомерСообщения - 1].НапомнитьПозже = НапомнитьПозже;
	
	МассивСообщений = Новый Массив;
	Для Каждого СтрокаТЧ Из ТаблицаСообщений Цикл
		МассивСообщений.Добавить(Новый Структура("СкидкаНаценка, НапомнитьПозже", СтрокаТЧ.СкидкаНаценка, СтрокаТЧ.НапомнитьПозже));
	КонецЦикла;
	
	Оповестить("ПолученыСообщения", Новый Структура("Сообщения, ФормаВладелец", МассивСообщений, ВладелецФормы.УникальныйИдентификатор), Неопределено);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура НапомнитьПозжеПриИзменении(Элемент)
	
	ТаблицаСообщений[НомерСообщения - 1].ИзмененоПользователем = Истина;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СледующееСообщение(Команда)
	
	ТаблицаСообщений[НомерСообщения - 1].НапомнитьПозже = НапомнитьПозже;
	
	НомерСообщения = НомерСообщения + 1;
	
	ОтобразитьТекущееСообщение();

КонецПроцедуры

&НаКлиенте
Процедура ПредыдущееСообщение(Команда)
	
	ТаблицаСообщений[НомерСообщения - 1].НапомнитьПозже = НапомнитьПозже;
	
	НомерСообщения = НомерСообщения - 1;
	
	ОтобразитьТекущееСообщение();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыдатьКарту(Команда)
	
	СтрокаТЧ = ТаблицаСообщений[НомерСообщения - 1];
	
	ПараметрыОткрытия = Новый Структура("Режим, ТребуемыйВидНовойКарты, ЗаменяемаяКарта, Партнер, Контрагент, Соглашение");
	ПараметрыОткрытия.ТребуемыйВидНовойКарты = СтрокаТЧ.ВидКарты;
	Если Не ЗначениеЗаполнено(КартаЛояльности) Тогда
		ПараметрыОткрытия.Режим = "РегистрацияНовойКартыЛояльности";
	Иначе
		ПараметрыОткрытия.Режим = "ЗаменаКартыЛояльности";
		ПараметрыОткрытия.ЗаменяемаяКарта = КартаЛояльности;
	КонецЕсли;
	ПараметрыОткрытия.Партнер    = Партнер;
	ПараметрыОткрытия.Контрагент = Контрагент;
	ПараметрыОткрытия.Соглашение = Соглашение;
	
	ВыбранноеЗначение = ОткрытьФорму("Справочник.КартыЛояльности.Форма.ПомощникРегистрации", ПараметрыОткрытия, ВладелецФормы);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ОтобразитьТекущееСообщение()
	
	СтрокаТЧ = ТаблицаСообщений[НомерСообщения - 1];
	Если ЗначениеЗаполнено(СтрокаТЧ.ВидКарты) Тогда
		Если ЗначениеЗаполнено(КартаЛояльности) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Заменить карту лояльности ""%1"" на карту ""%2""'"), ВидЗаменяемойКартыЛояльности, СтрокаТЧ.ВидКарты);
		Иначе
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Выдать карту лояльности ""%1""'"), СтрокаТЧ.ВидКарты);
		КонецЕсли;
	Иначе
		ТекстСообщения = СтрокаТЧ.ТекстСообщения;
	КонецЕсли;
	Если Не СтрокаТЧ.ИзмененоПользователем Тогда
		СтрокаТЧ.НапомнитьПозже = Ложь;
	КонецЕсли;
	НапомнитьПозже = СтрокаТЧ.НапомнитьПозже;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сообщение %1 из %2'"), НомерСообщения, ВсегоСообщений);
	
	Элементы.ЗарегистрироватьКартуЛояльности.Видимость = ЗначениеЗаполнено(СтрокаТЧ.ВидКарты) И Пользователи.РолиДоступны("ДобавлениеИзменениеКартЛояльности");
	Элементы.ПредыдущееСообщение.Видимость = НомерСообщения > 1;
	Элементы.СледующееСообщение.Видимость  = Не НомерСообщения = ТаблицаСообщений.Количество();
	
КонецПроцедуры
