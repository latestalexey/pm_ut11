
&НаКлиенте
Процедура Выбрать(Команда)
	Если ИндексВыбора=0 Тогда
		Документ = "Реализация товаров и услуг";
	ИначеЕсли ИндексВыбора=1 Тогда
		Документ = "Акт выполненных работ";
	КонецЕсли;	
	Оповестить("ВыбранТипДокументаПродажи",Документ);
	Этаформа.Закрыть();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьАктыВыполненныхРаботПоНесколькимЗаказам 	= ПолучитьФункциональнуюОпцию("ИспользоватьАктыВыполненныхРаботПоНесколькимЗаказам");
	ИспользоватьРеализациюПоНесколькимЗаказам 		= ПолучитьФункциональнуюОпцию("ИспользоватьРеализациюПоНесколькимЗаказам");
	
	//определение количества позиций с различными вариантами оформления услуг
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказыКлиентовОстатки.Номенклатура КАК Номенклатура,
	               |	ЗаказыКлиентовОстатки.ЗаказКлиента КАК ЗаказКлиента,
	               |	ЗаказыКлиентовОстатки.КодСтроки КАК КодСтроки
	               |ИЗ
	               |	РегистрНакопления.ЗаказыКлиентов.Остатки(
	               |			&ДатаОтгрузки,
	               |			ЗаказКлиента В (&МассивЗаказовДляАктовВыполненныхРабот)
	               |				И Номенклатура.ВариантОформленияПродажи = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияПродажи.АктВыполненныхРабот)) КАК ЗаказыКлиентовОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказыКлиентовОстатки.Номенклатура КАК Номенклатура,
	               |	ЗаказыКлиентовОстатки.ЗаказКлиента КАК ЗаказКлиента,
	               |	ЗаказыКлиентовОстатки.КодСтроки КАК КодСтроки
	               |ИЗ
	               |	РегистрНакопления.ЗаказыКлиентов.Остатки(
	               |			&ДатаОтгрузки,
	               |			ЗаказКлиента В (&МассивЗаказовДляРеализации)
	               |				И (Склад = &Склад
	               |					ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	               |				И Номенклатура.ВариантОформленияПродажи = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияПродажи.РеализацияТоваровУслуг)) КАК ЗаказыКлиентовОстатки";
		   
	Запрос.УстановитьПараметр("МассивЗаказовДляАктовВыполненныхРабот", Параметры.СписокЗаказовДляАктовВыполненныхРабот.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("МассивЗаказовДляРеализации", Параметры.СписокЗаказовДляРеализации.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("ДатаОтгрузки", КонецДня(Параметры.ДатаОтгрузки));
	Запрос.УстановитьПараметр("Склад", Параметры.Склад);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоАктВыполненныхРабот 		 = РезультатЗапроса[0].Выгрузить().Количество();
	КоличествоРеализацияТоваровУслуг = РезультатЗапроса[1].Выгрузить().Количество();
	ТекстРеализация = НСтр("ru = 'Реализация товаров и услуг (%количество% поз.)'");
	ТекстАктВыполненныхРабот = НСтр("ru = 'Акт выполненных работ (%количество% поз.)'");
	Элементы.ИндексВыбора.СписокВыбора.Добавить(0, СтрЗаменить(ТекстРеализация,"%количество%",Формат(КоличествоРеализацияТоваровУслуг,"ЧГ=")));
	Элементы.ИндексВыбора.СписокВыбора.Добавить(1, СтрЗаменить(ТекстАктВыполненныхРабот,"%количество%",Формат(КоличествоАктВыполненныхРабот,"ЧГ=")));
	Если Параметры.СписокЗаказовДляРеализации.Количество()>1 ИЛИ Параметры.СписокЗаказовДляАктовВыполненныхРабот.Количество()>1 Тогда
		Элементы.Декорация1.Заголовок = СтрЗаменить(Элементы.Декорация1.Заголовок,НСтр("ru = 'заказе'"),НСтр("ru = 'заказах'"));
	КонецЕсли;
		
КонецПроцедуры
