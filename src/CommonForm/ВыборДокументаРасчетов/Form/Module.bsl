
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоРасчетыСКлиентами = Ложь;
	Если ТипЗнч(Параметры.Отбор) = Тип("Структура") Тогда
		Если Параметры.Отбор.Свойство("ЭтоРасчетыСКлиентами") Тогда
			ЭтоРасчетыСКлиентами = Параметры.Отбор.ЭтоРасчетыСКлиентами;
		КонецЕсли;
	КонецЕсли;
	
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеПодразделенияВыделенныеНаБаланс")
		И Параметры.Отбор.Свойство("Организация") Тогда
		
		Организация = Параметры.Отбор.Организация;
		
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ГоловнаяОрганизация = &Организация
		|	И (Организации.ДопускаютсяВзаиморасчетыСКлиентамиЧерезГоловнуюОрганизацию
		|				И &ЭтоРасчетыСКлиентами
		|			ИЛИ Организации.ДопускаютсяВзаиморасчетыСПоставщикамиЧерезГоловнуюОрганизацию
		|				И НЕ &ЭтоРасчетыСКлиентами)");
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ЭтоРасчетыСКлиентами", ЭтоРасчетыСКлиентами);
		ДоступныеОрганизации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		ДоступныеОрганизации.Добавить(Организация);
		
		Параметры.Отбор.Организация = ДоступныеОрганизации;
		
	КонецЕсли;
	
	Если ЭтоРасчетыСКлиентами Тогда
		Список.ТекстЗапроса = ВзаиморасчетыСервер.ПолучитьТекстЗапросаДокументыРасчетовСКлиентами(Параметры.ВыборОснованияПлатежа);
		Если Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВыборОснованияПлатежа")) <> Неопределено Тогда
			Список.Параметры.УстановитьЗначениеПараметра("ВыборОснованияПлатежа", Параметры.ВыборОснованияПлатежа);
		КонецЕсли;
	Иначе
		Список.ТекстЗапроса = ВзаиморасчетыСервер.ПолучитьТекстЗапросаДокументыРасчетовСПоставщиками();
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	ОповеститьОВыборе(Новый Структура("ОснованиеПлатежа, Заказ, Партнер, ВалютаВзаиморасчетов, СуммаВзаиморасчетов, Дата, Номер",
		СтрокаТаблицы.Ссылка, 
		СтрокаТаблицы.ОбъектРасчетов,
		СтрокаТаблицы.Партнер,
		СтрокаТаблицы.Валюта,
		0,
		СтрокаТаблицы.Дата,
		СтрокаТаблицы.Номер
	));
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыбратьДокумент(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		ОповеститьОВыборе(Новый Структура("ОснованиеПлатежа, Заказ, Партнер, ВалютаВзаиморасчетов, СуммаВзаиморасчетов, Дата, Номер",
			СтрокаТаблицы.Ссылка, 
			СтрокаТаблицы.ОбъектРасчетов,
			СтрокаТаблицы.Партнер,
			СтрокаТаблицы.Валюта,
			0,
			СтрокаТаблицы.Дата,
			СтрокаТаблицы.Номер
		));
	Иначе
		Закрыть();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокумент(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		ОткрытьЗначение(СтрокаТаблицы.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

