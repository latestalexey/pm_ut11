////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбменДаннымиСервер.ПроверитьВозможностьАдминистрированияОбменов();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пользователь = РегистрыСведений.ОбщиеНастройкиУзловИнформационныхБаз.ПользовательДляСинхронизацииДанных(Параметры.АвтономноеРабочееМесто);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Отказ = Ложь;
	
	Если ПустаяСтрока(Пароль) Тогда
		
		НСтрока = НСтр("ru = 'Не задан пароль.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтрока,,"Пароль",, Отказ);
		
	ИначеЕсли Пароль <> ПодтверждениеПароля Тогда
		
		НСтрока = НСтр("ru = 'Пароль и подтверждение пароля не совпадают.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтрока,, "ПодтверждениеПароля",, Отказ);
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьПользователя();
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подробно(Команда)
	
	ОткрытьЗначение(Пользователь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ИзменитьПользователя()
	
	ОбменДаннымиСервер.ПроверитьВозможностьАдминистрированияОбменов();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пользователь = РегистрыСведений.ОбщиеНастройкиУзловИнформационныхБаз.ПользовательДляСинхронизацииДанных(Параметры.АвтономноеРабочееМесто);
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		ВызватьИсключение НСтр("ru = 'Пользователя для синхронизации данных с текущим автономным рабочим местом не существует.'");
	КонецЕсли;
	
	ПользовательОбъект = Пользователь.ПолучитьОбъект();
	
	Если ПользовательОбъект = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Пользователя для синхронизации данных с текущим автономным рабочим местом не существует.'");
	КонецЕсли;
	
	ОписаниеПользователяИБ = Новый Структура;
	ОписаниеПользователяИБ.Вставить("Действие", "Записать");
	ОписаниеПользователяИБ.Вставить("Пароль", Пароль);
	
	ПользовательОбъект.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", ОписаниеПользователяИБ);
	
	ПользовательОбъект.Записать();
	
КонецПроцедуры
