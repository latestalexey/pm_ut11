
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОписаниеТиповМаркетинговоеМероприятие = Новый ОписаниеТипов("СправочникСсылка.МаркетинговыеМероприятия");
	ИспользоватьМаркетинговыеМероприятия = ПолучитьФункциональнуюОпцию("ИспользоватьМаркетинговыеМероприятия");
	ТипМаркетинговоеМероприятие = Тип("СправочникСсылка.МаркетинговыеМероприятия");
	
	Для каждого ДоступныйТип Из Метаданные.ПланыВидовХарактеристик.КаналыРекламныхВоздействий.Тип.Типы() Цикл
		
		Если (НЕ (ДоступныйТип = ТипМаркетинговоеМероприятие
			И НЕ ИспользоватьМаркетинговыеМероприятия))
			ИЛИ (Объект.ТипЗначения = ОписаниеТиповМаркетинговоеМероприятие
			И ДоступныйТип = ТипМаркетинговоеМероприятие) Тогда
			
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ДоступныйТип);
			Элементы.ТипЗначения.СписокВыбора.Добавить(Новый ОписаниеТипов(МассивТипов));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ТипЗначения = Элементы.ТипЗначения.СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(Объект.ТипЗначения) Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан тип воздействия.'"),"ТипЗначения","Объект.ТипЗначения");
		Отказ = Истина;
	
	КонецЕсли;
	
КонецПроцедуры

