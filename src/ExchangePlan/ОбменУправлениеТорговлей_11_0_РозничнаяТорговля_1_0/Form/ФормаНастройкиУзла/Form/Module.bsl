////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбменДаннымиСервер.ФормаНастройкиУзлаПриСозданииНаСервере(ЭтаФорма, "ОбменУправлениеТорговлей_11_0_РозничнаяТорговля_1_0");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ИспользоватьОтборПоОрганизациямПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоКассамПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоСкладамПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоДоговорамЭквайрингаПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоВидамЦенНоменклатурыПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КомандаОК(Команда)
	
	// Очистка неиспользуемых реквизитов
	
	Если НЕ ИспользоватьОтборПоВидамЦенНоменклатуры И ВидыЦенНоменклатуры.Количество() <> 0 Тогда
		ВидыЦенНоменклатуры.Очистить();
	ИначеЕсли ИспользоватьОтборПоВидамЦенНоменклатуры И ВидыЦенНоменклатуры.Количество() = 0 Тогда
		ИспользоватьОтборПоВидамЦенНоменклатуры = Ложь;
	КонецЕсли;
	
	Если НЕ ИспользоватьОтборПоДоговорамЭквайринга И ДоговорыЭквайринга.Количество() <> 0 Тогда
		ДоговорыЭквайринга.Очистить();
	ИначеЕсли ИспользоватьОтборПоДоговорамЭквайринга И ДоговорыЭквайринга.Количество() = 0 Тогда
		ИспользоватьОтборПоДоговорамЭквайринга = Ложь;
	КонецЕсли;
	
	Если НЕ ИспользоватьОтборПоКассам И Кассы.Количество() <> 0 Тогда
		Кассы.Очистить();
	ИначеЕсли ИспользоватьОтборПоКассам И Кассы.Количество() = 0 Тогда
		ИспользоватьОтборПоКассам = Ложь;
	КонецЕсли;
	
	Если НЕ ИспользоватьОтборПоОрганизациям И Организации.Количество() <> 0 Тогда
		Организации.Очистить();
	ИначеЕсли ИспользоватьОтборПоОрганизациям И Организации.Количество() = 0 Тогда
		ИспользоватьОтборПоОрганизациям = Ложь;
	КонецЕсли;
	
	Если НЕ ИспользоватьОтборПоСкладам И Склады.Количество() <> 0 Тогда
		Склады.Очистить();
	ИначеЕсли ИспользоватьОтборПоСкладам И Склады.Количество() = 0 Тогда
		ИспользоватьОтборПоСкладам = Ложь;
	КонецЕсли;
	
	ОбменДаннымиКлиент.ФормаНастройкиУзлаКомандаЗакрытьФорму(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Процедура УстановитьВидимость()
	
	НастройкаФильтровРегистрации = Элементы.Группа.ПодчиненныеЭлементы.НастройкаФильтровРегистрации;
	НастройкиОтборов = НастройкаФильтровРегистрации.ПодчиненныеЭлементы.НастройкиОтборов;
	
	ОтборПоОрганизациям 		= НастройкиОтборов.ПодчиненныеЭлементы.ОтборПоОрганизациям;
	ОтборПоСкладам				= НастройкиОтборов.ПодчиненныеЭлементы.ОтборПоСкладам;
	ОтборПоКассам				= НастройкиОтборов.ПодчиненныеЭлементы.ОтборПоКассам;
	ОтборПоВидамЦен				= НастройкиОтборов.ПодчиненныеЭлементы.ОтборПоВидамЦенНоменклатуры;
	ОтборПоДоговорамЭквайринга	= НастройкиОтборов.ПодчиненныеЭлементы.ОтборПоДоговорамЭквайринга;
	
	ОтборПоОрганизациям.Видимость			= ИспользоватьОтборПоОрганизациям;
	ОтборПоСкладам.Видимость				= ИспользоватьОтборПоСкладам;
	ОтборПоКассам.Видимость					= ИспользоватьОтборПоКассам;
	ОтборПоВидамЦен.Видимость				= ИспользоватьОтборПоВидамЦенНоменклатуры;
	ОтборПоДоговорамЭквайринга.Видимость	= ИспользоватьОтборПоДоговорамЭквайринга;
	
КонецПроцедуры