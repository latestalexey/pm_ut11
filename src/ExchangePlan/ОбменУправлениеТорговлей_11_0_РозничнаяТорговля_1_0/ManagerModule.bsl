#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Возвращает признак использования плана обмена для организации обмена в модели сервиса.
//  Если признак установлен, то в сервисе можно включить обмен данными
//  с использованием этого плана обмена.
//  Если признак не установлен, то план обмена будет использоваться только 
//  для обмена в локальном режиме работы конфигурации.
// 
Функция ПланОбменаИспользуетсяВМоделиСервиса() Экспорт
	
	Возврат Ложь;
	
КонецФункции

Функция ОбщиеДанныеУзлов() Экспорт
	
	Возврат "";
	
КонецФункции

Функция ПояснениеДляНастройкиПараметровУчета() Экспорт
	
	Возврат "";
	
КонецФункции

Функция ПояснениеДляНастройкиПараметровУчетаБазыКорреспондента() Экспорт
	
	Возврат "";
	
КонецФункции

Процедура ОбработчикПроверкиПараметровУчета(Отказ, Получатель, Сообщение) Экспорт
	
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции БСП

// Определяет несколько вариантов настройки расписания выполнения обмена данными;
// Рекомендуется указывать не более 3 вариантов;
// Эти варианты должны быть одинаковым в плане обмена источника и приемника.
// 
// Параметры:
//  Нет.
// 
// Возвращаемое значение:
//  ВариантыНастройки - СписокЗначений - список расписаний обмена данными
//
Функция ВариантыНастройкиРасписания() Экспорт
	
	Месяцы = Новый Массив;
	Месяцы.Добавить(1);
	Месяцы.Добавить(2);
	Месяцы.Добавить(3);
	Месяцы.Добавить(4);
	Месяцы.Добавить(5);
	Месяцы.Добавить(6);
	Месяцы.Добавить(7);
	Месяцы.Добавить(8);
	Месяцы.Добавить(9);
	Месяцы.Добавить(10);
	Месяцы.Добавить(11);
	Месяцы.Добавить(12);
	
	// Расписание №1
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(1);
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	
	Расписание1 = Новый РасписаниеРегламентногоЗадания;
	Расписание1.ДниНедели                = ДниНедели;
	Расписание1.ПериодПовтораВТечениеДня = 900; // 15 минут
	Расписание1.ПериодПовтораДней        = 1; // каждый день
	Расписание1.Месяцы                   = Месяцы;
	
	// Расписание №2
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(1);
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	ДниНедели.Добавить(6);
	ДниНедели.Добавить(7);
	
	Расписание2 = Новый РасписаниеРегламентногоЗадания;
	Расписание2.ВремяНачала              = Дата('00010101080000');
	Расписание2.ВремяКонца               = Дата('00010101200000');
	Расписание2.ПериодПовтораВТечениеДня = 3600; // каждый час
	Расписание2.ПериодПовтораДней        = 1; // каждый день
	Расписание2.ДниНедели                = ДниНедели;
	Расписание2.Месяцы                   = Месяцы;
	
	// Расписание №3
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	ДниНедели.Добавить(6);
	
	Расписание3 = Новый РасписаниеРегламентногоЗадания;
	Расписание3.ДниНедели         = ДниНедели;
	Расписание3.ВремяНачала       = Дата('00010101020000');
	Расписание3.ПериодПовтораДней = 1; // каждый день
	Расписание3.Месяцы            = Месяцы;
	
	// возвращаемое значение функции
	ВариантыНастройки = Новый СписокЗначений;
	
	ВариантыНастройки.Добавить(Расписание1, "Один раз в 15 минут, кроме субботы и воскресенья");
	ВариантыНастройки.Добавить(Расписание2, "Каждый час с 8:00 до 20:00, ежедневно");
	ВариантыНастройки.Добавить(Расписание3, "Каждую ночь в 2:00, кроме субботы и воскресенья");
	
	Возврат ВариантыНастройки;
	
КонецФункции

// Определяет версию платформы базы-приемника для создания СОМ-подключения;
// Возможные варианты возвращаемого значения: "V81"; "V82"
// 
// Параметры:
//  Нет.
// 
// Возвращаемое значение:
//  Строка, 3 - версия платформы базы-приемника (V81; V82)
//
Функция ВерсияПлатформыИнформационнойБазы() Экспорт
	
	Возврат "V82";
	
КонецФункции

Функция ПрефиксНастройкиОбменаДанными() Экспорт
	
	Возврат "Б";
	
КонецФункции

// Возвращает имя файла настроек по умолчанию;
// В этот файл будут выгружены настройки обмена для приемника;
// Это значение должно быть одинаковым в плане обмена источника и приемника.
// 
// Параметры:
//  Нет.
// 
// Возвращаемое значение:
//  Строка, 255 - имя файла по умолчанию для выгрузки настроек обмена данными
//
Функция ИмяФайлаНастроекДляПриемника() Экспорт
	
	Возврат "Настройки для обмена УТ11.0-РТ1.0";
	
КонецФункции

// Возвращает структуру отборов на узле плана обмена с установленными значениями по умолчанию;
// Структура настроек повторяет состав реквизитов шапки и табличных частей плана обмена;
// Для реквизитов шапки используются аналогичные по ключу и значению элементы структуры,
// а для табличных частей используются структуры,
// содержащие массивы значений полей табличных частей плана обмена.
// 
// Параметры:
//  Нет.
// 
// Возвращаемое значение:
//  СтруктураНастроек - Структура - структура отборов на узле плана обмена
// 
Функция НастройкаОтборовНаУзле() Экспорт
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ДатаНачалаВыгрузкиДокументов", НачалоГода(ТекущаяДата()));
	СтруктураНастроек.Вставить("ВыгружатьСебестоимость", 					Ложь);
	СтруктураНастроек.Вставить("ИспользоватьОтборПоВидамЦенНоменклатуры",	Ложь);
	СтруктураНастроек.Вставить("ИспользоватьОтборПоДоговорамЭквайринга",	Ложь);
	СтруктураНастроек.Вставить("ИспользоватьОтборПоКассам",					Ложь);
	СтруктураНастроек.Вставить("ИспользоватьОтборПоКонтрагентам",			Ложь);
	СтруктураНастроек.Вставить("ИспользоватьОтборПоНоменклатуре",			Ложь);
	СтруктураНастроек.Вставить("ИспользоватьОтборПоОрганизациям",			Ложь);
	СтруктураНастроек.Вставить("ИспользоватьОтборПоСкладам",				Ложь);
	СтруктураНастроек.Вставить("ИспользоватьОтборПоФизическимЛицам",		Ложь);
	
	СтруктураТабличнойЧастиВидыЦенНоменклатуры = Новый Структура("ВидЦенНоменклатуры", Новый Массив);
	СтруктураНастроек.Вставить("ВидыЦенНоменклатуры", СтруктураТабличнойЧастиВидыЦенНоменклатуры);
	
	СтруктураТабличнойЧастиДоговорыЭквайринга = Новый Структура("ДоговорЭквайринга", Новый Массив);
	СтруктураНастроек.Вставить("ДоговорыЭквайринга", СтруктураТабличнойЧастиДоговорыЭквайринга);
	
	СтруктураТабличнойЧастиКассы = Новый Структура("Касса", Новый Массив);
	СтруктураНастроек.Вставить("Кассы", СтруктураТабличнойЧастиКассы);
	
	СтруктураТабличнойЧастиКассыМагазинов = Новый Структура("Касса", Новый Массив);
	СтруктураНастроек.Вставить("КассыМагазинов", СтруктураТабличнойЧастиКассыМагазинов);
	
	СтруктураТабличнойЧастиОрганизации = Новый Структура("Организация", Новый Массив);
	СтруктураНастроек.Вставить("Организации", СтруктураТабличнойЧастиОрганизации);
	
	СтруктураТабличнойЧастиСклады = Новый Структура("Склад", Новый Массив);
	СтруктураНастроек.Вставить("Склады", СтруктураТабличнойЧастиСклады);
	
	СтруктураТабличнойЧастиСклады = Новый Структура("Склад", Новый Массив);
	СтруктураНастроек.Вставить("Склады", СтруктураТабличнойЧастиСклады);
	
	СтруктураТабличнойЧастиСоответствияАналитикиОбмена = Новый Структура("Магазин,ПодразделениеУТ", Новый Массив, Новый Массив);
	СтруктураНастроек.Вставить("СоответствияАналитикиОбмена", СтруктураТабличнойЧастиСоответствияАналитикиОбмена);
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Возвращает структуру значений по умолчению для узла;
// Структура настроек повторяет состав реквизитов шапки плана обмена;
// Для реквизитов шапки используются аналогичные по ключу и значению элементы структуры.
// 
// Параметры:
//  Нет.
// 
// Возвращаемое значение:
//  СтруктураНастроек - Структура - структура значений по умолчанию на узле плана обмена
// 
Функция ЗначенияПоУмолчаниюНаУзле() Экспорт
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("СтатьяДДС_ОплатаОтПокупателя");
	СтруктураНастроек.Вставить("СтатьяДДС_ВозвратДСПоставщиком");
	СтруктураНастроек.Вставить("СтатьяДДС_ПрочиеРасчетыСКонтрагентамиПоступление");
	СтруктураНастроек.Вставить("СтатьяДДС_ПрочийПриходДенежныхСредств");
	СтруктураНастроек.Вставить("СтатьяДДС_ОплатаПоставщику");
	СтруктураНастроек.Вставить("СтатьяДДС_ВозвратДСПокупателю");
	СтруктураНастроек.Вставить("СтатьяДДС_ПрочиеРасчетыСКонтрагентамиВыдача");
	СтруктураНастроек.Вставить("СтатьяДДС_ПрочийРасходДенежныхСредств");
	СтруктураНастроек.Вставить("СтатьяРасходов_ПоступлениеУслуг");
	
	СтруктураНастроек.Вставить("РежимВыгрузкиПриНеобходимости", Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости);
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Возвращает строку описания ограничений миграции данных для пользователя;
// Прикладной разработчик на основе установленных отборов на узле должен сформировать строку описания ограничений 
// удобную для восприятия пользователем.
// 
// Параметры:
//  НастройкаОтборовНаУзле - Структура - структура отборов на узле плана обмена,
//                                       полученная при помощи функции НастройкаОтборовНаУзле().
// 
// Возвращаемое значение:
//  Строка, Неогранич. - строка описания ограничений миграции данных для пользователя
//
Функция ОписаниеОграниченийПередачиДанных(НастройкаОтборовНаУзле) Экспорт
	
	// Ограничение по дате документов
	ТекстОписания = ?(ЗначениеЗаполнено(НастройкаОтборовНаУзле.ДатаНачалаВыгрузкиДокументов),
						 "Документы выгружаются начиная с " + Формат(НастройкаОтборовНаУзле.ДатаНачалаВыгрузкиДокументов, "ДФ=dd.MM.yyyy"),
						 "Документы выгружаются за весь период") + ";";
	
	// Настройка выгрузки себестоимости
	ТекстОписания = ТекстОписания + Символы.ПС + "Выгрузка себестоимости: " 
					+ Формат(НастройкаОтборовНаУзле.ВыгружатьСебестоимость, "БЛ=Выключено; БИ=Включено") + ";";
	
	// Отбор по организации
	Если НастройкаОтборовНаУзле.ИспользоватьОтборПоОрганизациям Тогда
		
		КоллекцияЗначений = НастройкаОтборовНаУзле.Организации.Организация;
		ТекстОписания = ТекстОписания + Символы.ПС + "Отбор по организациям: " 
						+ ОбщегоНазначенияУТ.ПолучитьСокращенноеПредставлениеКоллекцииЗначений(КоллекцияЗначений) + ";";
						
	КонецЕсли;
	
	// Отбор по кассам
	Если НастройкаОтборовНаУзле.ИспользоватьОтборПоКассам Тогда
		
		КоллекцияЗначений = НастройкаОтборовНаУзле.Кассы.Касса;
		ТекстОписания = ТекстОписания + Символы.ПС + "Отбор по кассам: " 
						+ ОбщегоНазначенияУТ.ПолучитьСокращенноеПредставлениеКоллекцииЗначений(КоллекцияЗначений) + ";";
						
	КонецЕсли;
	
	// Отбор по складам
	Если НастройкаОтборовНаУзле.ИспользоватьОтборПоСкладам Тогда
		
		КоллекцияЗначений = НастройкаОтборовНаУзле.Склады.Склад;
		ТекстОписания = ТекстОписания + Символы.ПС + "Отбор по складам: " 
						+ ОбщегоНазначенияУТ.ПолучитьСокращенноеПредставлениеКоллекцииЗначений(КоллекцияЗначений) + ";";
						
	КонецЕсли;
	
	// Отбор по договорам эквайринга
	Если НастройкаОтборовНаУзле.ИспользоватьОтборПоДоговорамЭквайринга Тогда
		
		КоллекцияЗначений = НастройкаОтборовНаУзле.ДоговорыЭквайринга.ДоговорЭквайринга;
		ТекстОписания = ТекстОписания + Символы.ПС + "Отбор по договорам эквайринга: " 
						+ ОбщегоНазначенияУТ.ПолучитьСокращенноеПредставлениеКоллекцииЗначений(КоллекцияЗначений) + ";";
						
	КонецЕсли;
	
	// Отбор по видам цен номенклатуры
	Если НастройкаОтборовНаУзле.ИспользоватьОтборПоВидамЦенНоменклатуры Тогда
		
		КоллекцияЗначений = НастройкаОтборовНаУзле.ВидыЦенНоменклатуры.ВидЦенНоменклатуры;
		ТекстОписания = ТекстОписания + Символы.ПС + "Отбор по видам цен номенклатуры: " 
						+ ОбщегоНазначенияУТ.ПолучитьСокращенноеПредставлениеКоллекцииЗначений(КоллекцияЗначений) + ";";
						
	КонецЕсли;
	
	Возврат ТекстОписания;
	
КонецФункции

// Возвращает строку описания значений по умолчанию для пользователя;
// Прикладной разработчик на основе установленных значений по умолчанию на узле должен сформировать строку описания 
// удобную для восприятия пользователем.
// 
// Параметры:
//  ЗначенияПоУмолчаниюНаУзле - Структура - структура значений по умолчанию на узле плана обмена,
//                                       полученная при помощи функции ЗначенияПоУмолчаниюНаУзле().
// 
// Возвращаемое значение:
//  Строка, Неогранич. - строка описания для пользователя значений по умолчанию
//
Функция ОписаниеЗначенийПоУмолчанию(ЗначенияПоУмолчаниюНаУзле) Экспорт
	
	ТекстОписания = "";
	
	ТекстОписания = ТекстОписания 
					+ "Статья ДДС при оплате от покупателя: "
					+ ?(ЗначениеЗаполнено(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ОплатаОтПокупателя),
						Строка(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ОплатаОтПокупателя), "не указана")
					+ ";" + Символы.ПС;
									  
	ТекстОписания = ТекстОписания 
					+ "Статья ДДС при возврате денежных средств поставщиком: "
					+ ?(ЗначениеЗаполнено(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ВозвратДСПоставщиком),
						Строка(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ВозвратДСПоставщиком), "не указана")
					+ ";" + Символы.ПС;
									  
	ТекстОписания = ТекстОписания 
					+ "Статья ДДС при прочих расчетах с контрагентами (поступление): "
					+ ?(ЗначениеЗаполнено(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ПрочиеРасчетыСКонтрагентамиПоступление),
						Строка(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ПрочиеРасчетыСКонтрагентамиПоступление), "не указана")
					+ ";" + Символы.ПС;
									  
	ТекстОписания = ТекстОписания 
					+ "Статья ДДС при прочем приходе денежных средств: "
					+ ?(ЗначениеЗаполнено(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ПрочийПриходДенежныхСредств),
						Строка(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ПрочийПриходДенежныхСредств), "не указана")
					+ ";" + Символы.ПС;
									  
	ТекстОписания = ТекстОписания 
					+ "Статья ДДС при оплате поставщику: "
					+ ?(ЗначениеЗаполнено(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ОплатаПоставщику),
						Строка(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ОплатаПоставщику), "не указана")
					+ ";" + Символы.ПС;
									  
	ТекстОписания = ТекстОписания 
					+ "Статья ДДС при возврате денежных средств покупателю: "
					+ ?(ЗначениеЗаполнено(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ВозвратДСПокупателю),
						Строка(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ВозвратДСПокупателю), "не указана")
					+ ";" + Символы.ПС;
									  
	ТекстОписания = ТекстОписания 
					+ "Статья ДДС при прочих расчетах с контрагентами (выдача): "
					+ ?(ЗначениеЗаполнено(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ПрочиеРасчетыСКонтрагентамиВыдача),
						Строка(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ПрочиеРасчетыСКонтрагентамиВыдача), "не указана")
					+ ";" + Символы.ПС;
									  
	ТекстОписания = ТекстОписания 
					+ "Статья ДДС при прочем расходе денежных средств: "
					+ ?(ЗначениеЗаполнено(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ПрочийРасходДенежныхСредств),
						Строка(ЗначенияПоУмолчаниюНаУзле.СтатьяДДС_ПрочийРасходДенежныхСредств), "не указана")
					+ ";" + Символы.ПС;
					
	ТекстОписания = ТекстОписания 
					+ "Статья расходов при поступлении услуг: "
					+ ?(ЗначениеЗаполнено(ЗначенияПоУмолчаниюНаУзле.СтатьяРасходов_ПоступлениеУслуг),
						Строка(ЗначенияПоУмолчаниюНаУзле.СтатьяРасходов_ПоступлениеУслуг), "не указана") + ";";
									  
	Возврат ТекстОписания;
	
КонецФункции

// Возвращает представление команды создания нового обмена данными.
//
// Возвращаемое значение:
//  Строка, Неогранич - представление команды, выводимое в пользовательском интерфейсе.
//
// Например:
//	Возврат НСтр("ru = 'Создать обмен в распределенной информационной базе'");
//
Функция ЗаголовокКомандыДляСозданияНовогоОбменаДанными() Экспорт
	
	Возврат "Создать обмен с конфигурацией ""Розница, ред. 1.0""";
	
КонецФункции

// Определяет, будет ли использоваться помощник для создания новых узлов плана обмена.
//
// Возвращаемое значение:
//  Булево - признак использования помощника.
//
Функция ИспользоватьПомощникСозданияОбменаДанными() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает пользовательскую форму для создания начального образа базы.
// Эта форма будет открыта после завершения настройки обмена с помощью помощника.
// Для планов обмена не РИБ функция возвращает пустую строку
//
// Возвращаемое значение:
//  Строка, Неогранич - имя формы
//
// Например:
//	Возврат "ПланОбмена._ДемоРаспределеннаяИнформационнаяБаза.Форма.ФормаСозданияНачальногоОбраза";
//
Функция ИмяФормыСозданияНачальногоОбраза() Экспорт
	
	Возврат "";
	
КонецФункции

// Возвращает массив используемых транспортов сообщений для этого плана обмена
//
// 1. Например, если план обмена поддерживает только два транспорта сообщений FILE и FTP,
// то тело функции следует определить следующим образом:
//
//	Результат = Новый Массив;
//	Результат.Добавить(Перечисления.ВидыТранспортаСообщенийОбмена.FILE);
//	Результат.Добавить(Перечисления.ВидыТранспортаСообщенийОбмена.FTP);
//	Возврат Результат;
//
// 2. Например, если план обмена поддерживает все транспорты сообщений, определенных в конфигурации,
// то тело функции следует определить следующим образом:
//
//	Возврат ОбменДаннымиСервер.ВсеТранспортыСообщенийОбменаКонфигурации();
//
// Возвращаемое значение:
//  Массив - массив содержит значения перечисления ВидыТранспортаСообщенийОбмена
//
Функция ИспользуемыеТранспортыСообщенийОбмена() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.ВидыТранспортаСообщенийОбмена.FILE);
	Результат.Добавить(Перечисления.ВидыТранспортаСообщенийОбмена.FTP);
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Функции для работы обмена через внешнее соединение

// Возвращает структуру отборов на узле плана обмена базы корреспондента с установленными значениями по умолчанию;
// Структура настроек повторяет состав реквизитов шапки и табличных частей плана обмена базы корреспондента;
// Для реквизитов шапки используются аналогичные по ключу и значению элементы структуры,
// а для табличных частей используются структуры,
// содержащие массивы значений полей табличных частей плана обмена.
// 
// Параметры:
//  Нет.
// 
// Возвращаемое значение:
//  СтруктураНастроек - Структура - структура отборов на узле плана обмена базы корреспондента
// 
Функция НастройкаОтборовНаУзлеБазыКорреспондента() Экспорт
	
	Возврат Новый Структура;
	
КонецФункции

// Возвращает структуру значений по умолчению для узла базы корреспондента;
// Структура настроек повторяет состав реквизитов шапки плана обмена базы корреспондента;
// Для реквизитов шапки используются аналогичные по ключу и значению элементы структуры.
// 
// Параметры:
//  Нет.
// 
// Возвращаемое значение:
//  СтруктураНастроек - Структура - структура значений по умолчанию на узле плана обмена базы корреспондента
//
Функция ЗначенияПоУмолчаниюНаУзлеБазыКорреспондента() Экспорт
	
	Возврат Новый Структура;
	
КонецФункции

// Возвращает строку описания ограничений миграции данных для базы корреспондента, которая отображается пользователю;
// Прикладной разработчик на основе установленных отборов на узле базы корреспондента должен сформировать строку описания ограничений 
// удобную для восприятия пользователем.
// 
// Параметры:
//  НастройкаОтборовНаУзле - Структура - структура отборов на узле плана обмена базы корреспондента,
//                                       полученная при помощи функции НастройкаОтборовНаУзлеБазыКорреспондента().
// 
// Возвращаемое значение:
//  Строка, Неогранич. - строка описания ограничений миграции данных для пользователя
//
Функция ОписаниеОграниченийПередачиДанныхБазыКорреспондента(НастройкаОтборовНаУзле) Экспорт
	
	Возврат "";
	
КонецФункции

// Возвращает строку описания значений по умолчанию для базы корреспондента, которая отображается пользователю;
// Прикладной разработчик на основе установленных значений по умолчанию на узле базы корреспондента должен сформировать строку описания 
// удобную для восприятия пользователем.
// 
// Параметры:
//  ЗначенияПоУмолчаниюНаУзле - Структура - структура значений по умолчанию на узле плана обмена базы корреспондента,
//                                       полученная при помощи функции ЗначенияПоУмолчаниюНаУзлеБазыКорреспондента().
// 
// Возвращаемое значение:
//  Строка, Неогранич. - строка описания для пользователя значений по умолчанию
//
Функция ОписаниеЗначенийПоУмолчаниюБазыКорреспондента(ЗначенияПоУмолчаниюНаУзле) Экспорт
	
	Возврат "";
	
КонецФункции

#КонецЕсли