////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбменДаннымиСервер.ФормаНастройкиУзловПриСозданииНаСервере(ЭтаФорма, Отказ);
	ПолучитьОписаниеКонтекста();
	
	// Задание значений по умолчанию
	Если НЕ ЗначениеЗаполнено(ДатаНачалаВыгрузкиДокументов) Тогда
		ДатаНачалаВыгрузкиДокументов = НачалоГода(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РежимВыгрузкиПриНеобходимости) Тогда
		РежимВыгрузкиПриНеобходимости = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;
	КонецЕсли;
	
	// Сформируем список выбора для реквизита "ПравилаСозданияДоговоровКонтрагентов"
	СписокВыбора = Элементы.ПравилаСозданияДоговоровКонтрагентов.СписокВыбора;
	Если НЕ (ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам"))
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПоступлениеПоНесколькимЗаказам")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьРеализациюПоНесколькимЗаказам")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьАктыВыполненныхРаботПоНесколькимЗаказам") Тогда
		СписокВыбора.Удалить(СписокВыбора.НайтиПоЗначению("ПоЗаказам"));
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСделкиСКлиентами") Тогда
		СписокВыбора.Удалить(СписокВыбора.НайтиПоЗначению("ПоСделкам"));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ИспользоватьОтборПоОрганизациям = Не ОбменДаннымиКлиент.ВТаблицеОтмеченыВсеЭлементы(Организации);
	ВыгружатьЦеныНоменклатуры = Ложь;
	Для Каждого Элемент Из ВидыЦенНоменклатуры Цикл
		Если Элемент.Использовать = Истина Тогда
			ВыгружатьЦеныНоменклатуры = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПолучитьОписаниеКонтекста();
	
	ОбменДаннымиКлиент.ФормаНастройкиУзловКомандаЗакрытьФорму(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВсеОрганизации(Команда)
	
	ВключитьОтключитьВсеЭлементыВТаблице(Истина, "Организации");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьВсеОрганизации(Команда)
	
	ВключитьОтключитьВсеЭлементыВТаблице(Ложь, "Организации");
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВсеВидыЦенНоменклатуры(Команда)
	
	ВключитьОтключитьВсеЭлементыВТаблице(Истина, "ВидыЦенНоменклатуры");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьВсеВидыЦенНоменклатуры(Команда)
	
	ВключитьОтключитьВсеЭлементыВТаблице(Ложь, "ВидыЦенНоменклатуры");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ВключитьОтключитьВсеЭлементыВТаблице(Включить, ИмяТаблицы)
	
	Для Каждого ЭлементКоллекции Из ЭтаФорма[ИмяТаблицы] Цикл
		
		ЭлементКоллекции.Использовать = Включить;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьОписаниеКонтекста()
	// дата начала выгрузки документов
	Если ЗначениеЗаполнено(ДатаНачалаВыгрузкиДокументов) Тогда
		ДатаНачалаВыгрузкиДокументовОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Данные будут синхронизироваться, начиная с %1'"),
			Формат(ДатаНачалаВыгрузкиДокументов, "ДЛФ=DD")
		);
	Иначе
		ДатаНачалаВыгрузкиДокументовОписание = НСтр("ru = 'Данные будут синхронизироваться за весь период ведения учета в программах'");
	КонецЕсли;
	
	// отбор по Организациям
	Если ИспользоватьОтборПоОрганизациям Тогда
		ОрганизацииОписание = НСтр("ru = 'Только по организациям:'") + Символы.ПС + ИспользуемыеЭлементы("Организации");
	Иначе
		ОрганизацииОписание = НСтр("ru = 'По всем организациям.'");
	КонецЕсли;
	
	// Выгрузка цен, номенклатуры
	Если ВыгружатьЦеныНоменклатуры Тогда
		ВидыЦенНоменклатурыОписание = НСтр("ru = 'Выгружать цены номенклатуры:'") + Символы.ПС + ИспользуемыеЭлементы("ВидыЦенНоменклатуры");
	Иначе
		ВидыЦенНоменклатурыОписание = НСтр("ru = 'Не выгружать цены номенлатуры'");
	КонецЕсли;
	
	ПравилаСозданияДоговоровКонтрагентовОписание = НСтр("ru = 'Договора формируются по правилу: '")+ПравилаСозданияДоговоровКонтрагентов;
	
	РежимВыгрузкиОписание = НСтр("ru = 'Справочники выгружаются в режиме: '")+РежимВыгрузкиПриНеобходимости;
	
	
	ОписаниеКонтекста = (""
		+ ДатаНачалаВыгрузкиДокументовОписание
		+ Символы.ПС
		+ ОрганизацииОписание
		+ Символы.ПС
		+ ВидыЦенНоменклатурыОписание
		+ Символы.ПС
		+ ПравилаСозданияДоговоровКонтрагентовОписание
		+ Символы.ПС
		+ РежимВыгрузкиОписание
	);
КонецПроцедуры

&НаСервере
Функция ИспользуемыеЭлементы(ИмяТаблицы)
	
	Возврат СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(
			ЭтаФорма[ИмяТаблицы].Выгрузить(Новый Структура("Использовать", Истина)).ВыгрузитьКолонку("Представление"),
			Символы.ПС
	);
	
КонецФункции

