#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ 

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не СкладыСервер.ИспользоватьАдресноеХранение(Склад,Помещение) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Упаковка");
	КонецЕсли;
	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ);
	
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Обработки.ПроверкаКоличестваТоваровВДокументе),Отказ);
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

Функция ЗавершитьПроверку(ПараметрыУказанияСерий, ИспользуетсяАдресноеХранение) Экспорт
	
	Если Товары.НайтиСтроки(Новый Структура("ЕстьНедобор",Истина)).Количество() = 0
		И Товары.НайтиСтроки(Новый Структура("ЕстьНеотгружаемые",Истина)).Количество() = 0 Тогда
		
		Попытка
			ДокументОбъектОрдер = Ордер.ПолучитьОбъект();
			ДокументОбъектОрдер.Заблокировать();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке().Описание);
			Возврат  Ложь;
		КонецПопытки;
		
		Если ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
			ДокументОбъектОрдер.Серии.Загрузить(СвернутьТаблицуСерийДляПереноса(ПараметрыУказанияСерий));
		КонецЕсли;
		
		
		Если ТипЗнч(Ордер) = Тип("ДокументСсылка.РасходныйОрдерНаТовары") Тогда
			ДокументОбъектОрдер.Статус = Перечисления.СтатусыРасходныхОрдеров.КОтгрузке;
		Иначе
			ДокументОбъектОрдер.Статус = Перечисления.СтатусыОрдеровНаПеремещение.КОтгрузке;
		КонецЕсли;
		
		Попытка
			
			ИмяОрдера = ДокументОбъектОрдер.Метаданные().Имя;
			ПараметрыУказанияСерийОрдера = НоменклатураСервер.ПараметрыУказанияСерий(ДокументОбъектОрдер,Документы[ИмяОрдера]);
			НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъектОрдер,ПараметрыУказанияСерийОрдера);
			
			ДокументОбъектОрдер.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке().Описание);
			Возврат Ложь;
		КонецПопытки;
		
	Иначе
		
		Излишки.Очистить();
		Недостачи.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|	ТаблицаТовары.Упаковка КАК Упаковка,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 0
		|				ИЛИ ТаблицаТовары.СтатусУказанияСерий = 2
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК УчитыватьОстаткиСерий,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаТовары.КоличествоНеОтгружать КАК КоличествоНеОтгружать,
		|	ТаблицаТовары.КоличествоУпаковокНеОтгружать КАК КоличествоУпаковокНеОтгружать,
		|	ТаблицаТовары.КоличествоВДокументе КАК КоличествоВДокументе,
		|	ТаблицаТовары.КоличествоУпаковокВДокументе КАК КоличествоУпаковокВДокументе
		|ПОМЕСТИТЬ ТаблицаТовары
		|ИЗ
		|	&ТаблицаТовары КАК ТаблицаТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСерии.Номенклатура КАК Номенклатура,
		|	ТаблицаСерии.Характеристика КАК Характеристика,
		|	ТаблицаСерии.Серия КАК Серия,
		|	ТаблицаСерии.Упаковка КАК Упаковка,
		|	ТаблицаСерии.Количество КАК Количество,
		|	ТаблицаСерии.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаСерии.КоличествоНеОтгружать КАК КоличествоНеОтгружать,
		|	ТаблицаСерии.КоличествоУпаковокНеОтгружать КАК КоличествоУпаковокНеОтгружать,
		|	ТаблицаСерии.КоличествоВДокументе КАК КоличествоВДокументе,
		|	ТаблицаСерии.КоличествоУпаковокВДокументе КАК КоличествоУпаковокВДокументе
		|ПОМЕСТИТЬ ТаблицаСерии
		|ИЗ
		|	&ТаблицаСерии КАК ТаблицаСерии
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.Характеристика,
		|	ТаблицаТовары.Упаковка,
		|	МАКСИМУМ(ТаблицаТовары.УчитыватьОстаткиСерий) КАК УчитыватьОстаткиСерий,
		|	СУММА(ТаблицаТовары.Количество) КАК Количество,
		|	СУММА(ТаблицаТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ТаблицаТовары.КоличествоНеОтгружать) КАК КоличествоНеОтгружать,
		|	СУММА(ТаблицаТовары.КоличествоУпаковокНеОтгружать) КАК КоличествоУпаковокНеОтгружать,
		|	СУММА(ТаблицаТовары.КоличествоВДокументе) КАК КоличествоВДокументе,
		|	СУММА(ТаблицаТовары.КоличествоУпаковокВДокументе) КАК КоличествоУпаковокВДокументе
		|ПОМЕСТИТЬ ТаблицаТоварыДляЗапроса
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТовары.Характеристика,
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСерии.Номенклатура,
		|	ТаблицаСерии.Характеристика,
		|	ТаблицаСерии.Серия,
		|	ТаблицаСерии.Упаковка,
		|	СУММА(ТаблицаСерии.Количество) КАК Количество,
		|	СУММА(ТаблицаСерии.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ТаблицаСерии.КоличествоНеОтгружать) КАК КоличествоНеОтгружать,
		|	СУММА(ТаблицаСерии.КоличествоУпаковокНеОтгружать) КАК КоличествоУпаковокНеОтгружать,
		|	СУММА(ТаблицаСерии.КоличествоВДокументе) КАК КоличествоВДокументе,
		|	СУММА(ТаблицаСерии.КоличествоУпаковокВДокументе) КАК КоличествоУпаковокВДокументе
		|ПОМЕСТИТЬ ТаблицаСерииДляЗапроса
		|ИЗ
		|	ТаблицаСерии КАК ТаблицаСерии
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаСерии.Номенклатура,
		|	ТаблицаСерии.Характеристика,
		|	ТаблицаСерии.Упаковка,
		|	ТаблицаСерии.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоварыДляЗапроса.Номенклатура,
		|	ТаблицаТоварыДляЗапроса.Характеристика,
		|	ТаблицаТоварыДляЗапроса.Упаковка,
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК Серия,
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.Количество, ТаблицаТоварыДляЗапроса.Количество) - ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоНеОтгружать, ТаблицаТоварыДляЗапроса.КоличествоНеОтгружать) КАК КоличествоКОтгрузке,
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковок, ТаблицаТоварыДляЗапроса.КоличествоУпаковок) - ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковокНеОтгружать, ТаблицаТоварыДляЗапроса.КоличествоУпаковокНеОтгружать) КАК КоличествоУпаковокКОтгрузке,
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоВДокументе, ТаблицаТоварыДляЗапроса.КоличествоВДокументе) КАК КоличествоВДокументе,
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковокВДокументе, ТаблицаТоварыДляЗапроса.КоличествоУпаковокВДокументе) КАК КоличествоУпаковокВДокументе,
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоВДокументе, ТаблицаТоварыДляЗапроса.КоличествоВДокументе) - (ЕСТЬNULL(ТаблицаСерииДляЗапроса.Количество, ТаблицаТоварыДляЗапроса.Количество) - ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоНеОтгружать, ТаблицаТоварыДляЗапроса.КоличествоНеОтгружать)) КАК КоличествоНедобор,
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковокВДокументе, ТаблицаТоварыДляЗапроса.КоличествоУпаковокВДокументе) - (ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковок, ТаблицаТоварыДляЗапроса.КоличествоУпаковок) - ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковокНеОтгружать, ТаблицаТоварыДляЗапроса.КоличествоУпаковокНеОтгружать)) КАК КоличествоУпаковокНедобор
		|ИЗ
		|	ТаблицаТоварыДляЗапроса КАК ТаблицаТоварыДляЗапроса
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСерииДляЗапроса КАК ТаблицаСерииДляЗапроса
		|		ПО ТаблицаТоварыДляЗапроса.Номенклатура = ТаблицаСерииДляЗапроса.Номенклатура
		|			И ТаблицаТоварыДляЗапроса.Характеристика = ТаблицаСерииДляЗапроса.Характеристика
		|			И ТаблицаТоварыДляЗапроса.Упаковка = ТаблицаСерииДляЗапроса.Упаковка
		|			И (ТаблицаТоварыДляЗапроса.УчитыватьОстаткиСерий)
		|ГДЕ
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковокВДокументе, ТаблицаТоварыДляЗапроса.КоличествоУпаковокВДокументе) - (ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковок, ТаблицаТоварыДляЗапроса.КоличествоУпаковок) - ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковокНеОтгружать, ТаблицаТоварыДляЗапроса.КоличествоУпаковокНеОтгружать)) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоварыДляЗапроса.Номенклатура,
		|	ТаблицаТоварыДляЗапроса.Характеристика,
		|	ТаблицаТоварыДляЗапроса.Упаковка,
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК Серия,
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоНеОтгружать, ТаблицаТоварыДляЗапроса.КоличествоНеОтгружать) КАК КоличествоНеОтгружать,
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковокНеОтгружать, ТаблицаТоварыДляЗапроса.КоличествоУпаковокНеОтгружать) КАК КоличествоУпаковокНеОтгружать
		|ИЗ
		|	ТаблицаТоварыДляЗапроса КАК ТаблицаТоварыДляЗапроса
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСерииДляЗапроса КАК ТаблицаСерииДляЗапроса
		|		ПО ТаблицаТоварыДляЗапроса.Номенклатура = ТаблицаСерииДляЗапроса.Номенклатура
		|			И ТаблицаТоварыДляЗапроса.Характеристика = ТаблицаСерииДляЗапроса.Характеристика
		|			И ТаблицаТоварыДляЗапроса.Упаковка = ТаблицаСерииДляЗапроса.Упаковка
		|ГДЕ
		|	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковокНеОтгружать, ТаблицаТоварыДляЗапроса.КоличествоУпаковокНеОтгружать) > 0";
		
		Если Не ИспользуетсяАдресноеХранение Тогда
			
			//Если адресное хранение не используеются, то серии связаны с товарами без упаковок,
			//поэтому свернем по упаковкам
			ТаблицаТоваров = Товары.Выгрузить();
			ТаблицаТоваров.Свернуть("Номенклатура, Характеристика, СтатусУказанияСерий", "Количество, КоличествоУпаковок, КоличествоВДокументе,
			|КоличествоУпаковокВДокументе , КоличествоНеОтгружать, КоличествоУпаковокНеОтгружать");
			
			ТаблицаТоваров.Колонки.Добавить("Упаковка", Новый ОписаниеТипов("СправочникСсылка.УпаковкиНоменклатуры"));
			
			ТаблицаТоваров.ЗагрузитьКолонку(ТаблицаТоваров.ВыгрузитьКолонку("Количество"),"КоличествоУпаковок");
			ТаблицаТоваров.ЗагрузитьКолонку(ТаблицаТоваров.ВыгрузитьКолонку("КоличествоВДокументе"),"КоличествоУпаковокВДокументе");
			ТаблицаТоваров.ЗагрузитьКолонку(ТаблицаТоваров.ВыгрузитьКолонку("КоличествоНеОтгружать"),"КоличествоУпаковокНеОтгружать");
			
			Запрос.УстановитьПараметр("ТаблицаТовары",ТаблицаТоваров);
			
		Иначе
			
			Запрос.УстановитьПараметр("ТаблицаТовары",Товары.Выгрузить());
			
		КонецЕсли;
		Запрос.УстановитьПараметр("ТаблицаСерии",Серии.Выгрузить());
		
		Результаты = Запрос.ВыполнитьПакет();
		
		Недостачи.Загрузить(Результаты[4].Выгрузить());
		Излишки.Загрузить(Результаты[5].Выгрузить());
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ОтразитьРасхожденияВУчете(СтруктураПараметров, РезультатПроверкиОрдераНаТовары) Экспорт
	
	Попытка
		ДокументОбъектОрдер = Ордер.ПолучитьОбъект();
		ДокументОбъектОрдер.Заблокировать();
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке().Описание);
		Возврат Ложь;
	КонецПопытки;
	ЕстьНедобор = (Недостачи.Количество() > 0 
					И Недостачи.НайтиСтроки(Новый Структура("КоличествоНедобор",0)).Количество() < Недостачи.Количество());
	Если СтруктураПараметров.ВариантУчетаНедобора = "УменьшитьОрдер"
	  ИЛИ НЕ ЕстьНедобор Тогда
		Если СтруктураПараметров.РежимИсправления Тогда
			СтатусДокумента = ДокументОбъектОрдер.Статус;
		Иначе
			Если ТипЗнч(Ордер) = Тип("ДокументСсылка.РасходныйОрдерНаТовары") Тогда
				СтатусДокумента = Перечисления.СтатусыРасходныхОрдеров.КОтгрузке;
			Иначе
				СтатусДокумента = Перечисления.СтатусыОрдеровНаПеремещение.КОтгрузке;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ТипЗнч(Ордер) = Тип("ДокументСсылка.РасходныйОрдерНаТовары") Тогда
			СтатусДокумента = Перечисления.СтатусыРасходныхОрдеров.КОтбору;
		Иначе
			СтатусДокумента = Перечисления.СтатусыОрдеровНаПеремещение.КОтбору;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаРасхождений();

	Запрос.УстановитьПараметр("ТаблицаТовары",Товары.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаСерии",Серии.Выгрузить());

	Результаты = Запрос.ВыполнитьПакет();
	
	Если СтруктураПараметров.ИспользуетсяАдресноеХранение Тогда
		
		Если Излишки.Количество() > 0 Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	БлокировкиСкладскихЯчеек.Ячейка КАК Ячейка,
			|	БлокировкиСкладскихЯчеек.ТипБлокировки КАК ТипБлокировки
			|ИЗ
			|	РегистрСведений.БлокировкиСкладскихЯчеек КАК БлокировкиСкладскихЯчеек
			|ГДЕ
			|	БлокировкиСкладскихЯчеек.Ячейка = &ЗонаОтгрузки
			|	И БлокировкиСкладскихЯчеек.ТипБлокировки <> ЗНАЧЕНИЕ(Перечисление.ТипыБлокировокСкладскихЯчеек.Отбор)
			|
			|СГРУППИРОВАТЬ ПО
			|	БлокировкиСкладскихЯчеек.Ячейка,
			|	БлокировкиСкладскихЯчеек.ТипБлокировки
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ячейка";
			
			Запрос.УстановитьПараметр("ЗонаОтгрузки", Ордер.ЗонаОтгрузки);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл
				ТекстСообщения = НСтр("ru='Невозможно разместить неотгружаемые товары. Зона отгрузки %Ячейка% заблокирована: тип блокировки ""%ТипБлокировки%""'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ячейка%", Выборка.Ячейка);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТипБлокировки%", Выборка.ТипБлокировки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Возврат Ложь
			КонецЦикла;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(РаспоряжениеНаИнвентаризацию)
		  И СтруктураПараметров.ИспользуетсяАдресноеХранение 
		  И СтруктураПараметров.ЕстьНедостачиИзлишки Тогда
			ТекстСообщения = НСтр("ru='Поле ""%РаспоряжениеНаИнвентаризацию%"" не заполнено.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%РаспоряжениеНаИнвентаризацию%","Распоряжение на инвентаризацию");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"РаспоряжениеНаИнвентаризацию","Объект");
			Возврат Ложь;
		КонецЕсли;
		
		ДокументОбъектОтражение = Документы.ОтражениеРезультатовПроверкиОрдераНаТовары.СоздатьДокумент();
		ДокументОбъектОтражение.Дата = ТекущаяДата();
		ДокументОбъектОтражение.Заполнить(Неопределено);
		ДокументОбъектОтражение.РаспоряжениеНаИнвентаризацию = РаспоряжениеНаИнвентаризацию;
		ДокументОбъектОтражение.Ордер						 = Ордер;
		ДокументОбъектОтражение.Склад						 = ДокументОбъектОрдер.Склад;
		ДокументОбъектОтражение.ЗонаОтгрузки				 = ДокументОбъектОрдер.ЗонаОтгрузки;
		
		Если ТипЗнч(Ордер) = Тип("ДокументСсылка.РасходныйОрдерНаТовары") Тогда
			ДокументОбъектОтражение.Помещение = ДокументОбъектОрдер.Помещение;
		Иначе
			ДокументОбъектОтражение.Помещение = ДокументОбъектОрдер.ПомещениеОтправитель;
		КонецЕсли;
		
		ВыборкаРасхождений = Результаты[5].Выбрать();
	
		Пока ВыборкаРасхождений.Следующий() Цикл
			
			Если ВыборкаРасхождений.КоличествоУпаковокНедостача > 0 Тогда
				НоваяСтрока = ДокументОбъектОтражение.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРасхождений);
				НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийОтраженияРезультатовПроверкиОрдераНаТовары.ОтразитьНедостачу;
				НоваяСтрока.Количество = ВыборкаРасхождений.КоличествоНедостача;
				НоваяСтрока.КоличествоУпаковок = ВыборкаРасхождений.КоличествоУпаковокНедостача;
			КонецЕсли;
				
			Если ВыборкаРасхождений.КоличествоУпаковокИзлишек > 0 Тогда
				НоваяСтрока = ДокументОбъектОтражение.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРасхождений);
				НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийОтраженияРезультатовПроверкиОрдераНаТовары.ОтразитьИзлишек;
				НоваяСтрока.Количество = ВыборкаРасхождений.КоличествоИзлишек;
				НоваяСтрока.КоличествоУпаковок = ВыборкаРасхождений.КоличествоУпаковокИзлишек;
			КонецЕсли;
			
			Если ВыборкаРасхождений.КоличествоУпаковокНеОтгружать > 0 Тогда
				НоваяСтрока = ДокументОбъектОтражение.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРасхождений);
				НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийОтраженияРезультатовПроверкиОрдераНаТовары.ОставитьВЗонеОтгрузки;
				НоваяСтрока.Количество = ВыборкаРасхождений.КоличествоНеОтгружать;
				НоваяСтрока.КоличествоУпаковок = ВыборкаРасхождений.КоличествоУпаковокНеОтгружать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если СтруктураПараметров.ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Если СтруктураПараметров.ВариантУчетаНедобора = "УменьшитьОрдер" И ЕстьНедобор Тогда
			ДокументОбъектОрдер.Серии.Загрузить(Результаты[8].Выгрузить());
		Иначе
			ДокументОбъектОрдер.Серии.Загрузить(Результаты[6].Выгрузить());
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьНедобор И СтруктураПараметров.ВариантУчетаНедобора = "УменьшитьОрдер" Тогда
		ДокументОбъектОрдер.Товары.Загрузить(Результаты[7].Выгрузить());
	КонецЕсли;
	ДокументОбъектОрдер.Статус = СтатусДокумента;
	ИмяОрдера = ДокументОбъектОрдер.Метаданные().Имя;
		
	НачатьТранзакцию();
	Попытка
		
		Если СтруктураПараметров.ИспользуетсяАдресноеХранение Тогда
			НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(
				ДокументОбъектОтражение,
				НоменклатураСервер.ПараметрыУказанияСерий(ДокументОбъектОтражение,
					Документы.ОтражениеРезультатовПроверкиОрдераНаТовары));
			ДокументОбъектОтражение.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъектОрдер,
			НоменклатураСервер.ПараметрыУказанияСерий(ДокументОбъектОрдер,Документы[ИмяОрдера]));
		Если ДокументОбъектОрдер.Товары.Количество() > 0 Тогда
			ДокументОбъектОрдер.Записать(РежимЗаписиДокумента.Проведение);
		Иначе
			ДокументОбъектОрдер.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		КонецЕсли;
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке().Описание);
		ОтменитьТранзакцию();
		Возврат Ложь;
	КонецПопытки;
	ЗафиксироватьТранзакцию();
	
	Если СтруктураПараметров.ИспользуетсяАдресноеХранение Тогда
		РезультатПроверкиОрдераНаТовары = ДокументОбъектОтражение.Ссылка;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция СвернутьТаблицуСерийДляПереноса(ПараметрыУказанияСерий)
	
	ПоляСвертки = "";
	
	Для Каждого СтрМас из ПараметрыУказанияСерий.ПоляСвязи Цикл
		
		ПоляСвертки = "," + СтрМас;
		
	КонецЦикла;
	
	ТаблицаСерий = Серии.Выгрузить();
	
	ТаблицаСерий.Свернуть("Номенклатура, Характеристика, Серия" + ПоляСвертки,
		"Количество, КоличествоУпаковок, КоличествоВДокументе, КоличествоНеОтгружать,
		|КоличествоУпаковокВДокументе, КоличествоУпаковокНеОтгружать");
	
	Индекс = ТаблицаСерий.Количество();
	
	Пока Индекс > 0 Цикл
		
		Индекс = Индекс - 1;
		Строка = ТаблицаСерий[Индекс];
		
		Если Строка.КоличествоВДокументе = 0 Тогда
			Строка.Количество         = Строка.Количество - Строка.КоличествоНеОтгружать;
			Строка.КоличествоУпаковок = Строка.КоличествоУпаковок - Строка.КоличествоУпаковокНеОтгружать;
		Иначе
			Строка.Количество         = Строка.КоличествоВДокументе;
			Строка.КоличествоУпаковок = Строка.КоличествоУпаковокВДокументе;
		КонецЕсли;
		
		Если Строка.Количество = 0 Тогда
			ТаблицаСерий.Удалить(Строка);
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаСерий.Колонки.Удалить("КоличествоНеОтгружать");
	ТаблицаСерий.Колонки.Удалить("КоличествоУпаковокНеОтгружать");
	
	Возврат ТаблицаСерий;
	
КонецФункции

Функция ТекстЗапросаРасхождений()
	
	Возврат "ВЫБРАТЬ
	        |	ТаблицаТовары.Номенклатура КАК Номенклатура,
	        |	ТаблицаТовары.Характеристика КАК Характеристика,
	        |	ТаблицаТовары.Упаковка КАК Упаковка,
	        |	ВЫБОР
	        |		КОГДА ТаблицаТовары.СтатусУказанияСерий = 0
	        |				ИЛИ ТаблицаТовары.СтатусУказанияСерий = 2
	        |			ТОГДА ЛОЖЬ
	        |		ИНАЧЕ ИСТИНА
	        |	КОНЕЦ КАК УчитыватьОстаткиСерий,
	        |	ТаблицаТовары.Количество КАК Количество,
	        |	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	        |	ТаблицаТовары.КоличествоНеОтгружать КАК КоличествоНеОтгружать,
	        |	ТаблицаТовары.КоличествоУпаковокНеОтгружать КАК КоличествоУпаковокНеОтгружать,
	        |	ТаблицаТовары.КоличествоВДокументе КАК КоличествоВДокументе,
	        |	ТаблицаТовары.КоличествоУпаковокВДокументе КАК КоличествоУпаковокВДокументе
	        |ПОМЕСТИТЬ ТаблицаТовары
	        |ИЗ
	        |	&ТаблицаТовары КАК ТаблицаТовары
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ТаблицаСерии.Номенклатура КАК Номенклатура,
	        |	ТаблицаСерии.Характеристика КАК Характеристика,
	        |	ТаблицаСерии.Серия КАК Серия,
	        |	ТаблицаСерии.Упаковка КАК Упаковка,
	        |	ТаблицаСерии.Количество КАК Количество,
	        |	ТаблицаСерии.КоличествоУпаковок КАК КоличествоУпаковок,
	        |	ТаблицаСерии.КоличествоНеОтгружать КАК КоличествоНеОтгружать,
	        |	ТаблицаСерии.КоличествоУпаковокНеОтгружать КАК КоличествоУпаковокНеОтгружать,
	        |	ТаблицаСерии.КоличествоВДокументе КАК КоличествоВДокументе,
	        |	ТаблицаСерии.КоличествоУпаковокВДокументе КАК КоличествоУпаковокВДокументе
	        |ПОМЕСТИТЬ ТаблицаСерии
	        |ИЗ
	        |	&ТаблицаСерии КАК ТаблицаСерии
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ТаблицаТовары.Номенклатура,
	        |	ТаблицаТовары.Характеристика,
	        |	ТаблицаТовары.Упаковка,
	        |	МАКСИМУМ(ТаблицаТовары.УчитыватьОстаткиСерий) КАК УчитыватьОстаткиСерий,
	        |	СУММА(ТаблицаТовары.Количество) КАК Количество,
	        |	СУММА(ТаблицаТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	        |	СУММА(ТаблицаТовары.КоличествоНеОтгружать) КАК КоличествоНеОтгружать,
	        |	СУММА(ТаблицаТовары.КоличествоУпаковокНеОтгружать) КАК КоличествоУпаковокНеОтгружать,
	        |	СУММА(ТаблицаТовары.КоличествоВДокументе) КАК КоличествоВДокументе,
	        |	СУММА(ТаблицаТовары.КоличествоУпаковокВДокументе) КАК КоличествоУпаковокВДокументе
	        |ПОМЕСТИТЬ ТаблицаТоварыДляЗапроса
	        |ИЗ
	        |	ТаблицаТовары КАК ТаблицаТовары
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ТаблицаТовары.Характеристика,
	        |	ТаблицаТовары.Номенклатура,
	        |	ТаблицаТовары.Упаковка
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ТаблицаСерии.Номенклатура,
	        |	ТаблицаСерии.Характеристика,
	        |	ТаблицаСерии.Серия,
	        |	ТаблицаСерии.Упаковка,
	        |	СУММА(ТаблицаСерии.Количество) КАК Количество,
	        |	СУММА(ТаблицаСерии.КоличествоУпаковок) КАК КоличествоУпаковок,
	        |	СУММА(ТаблицаСерии.КоличествоНеОтгружать) КАК КоличествоНеОтгружать,
	        |	СУММА(ТаблицаСерии.КоличествоУпаковокНеОтгружать) КАК КоличествоУпаковокНеОтгружать,
	        |	СУММА(ТаблицаСерии.КоличествоВДокументе) КАК КоличествоВДокументе,
	        |	СУММА(ТаблицаСерии.КоличествоУпаковокВДокументе) КАК КоличествоУпаковокВДокументе
	        |ПОМЕСТИТЬ ТаблицаСерииДляЗапроса
	        |ИЗ
	        |	ТаблицаСерии КАК ТаблицаСерии
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ТаблицаСерии.Номенклатура,
	        |	ТаблицаСерии.Характеристика,
	        |	ТаблицаСерии.Упаковка,
	        |	ТаблицаСерии.Серия
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ТаблицаТоварыДляЗапроса.Номенклатура,
	        |	ТаблицаТоварыДляЗапроса.Характеристика,
	        |	ТаблицаТоварыДляЗапроса.Упаковка,
	        |	ЕСТЬNULL(ТаблицаСерииДляЗапроса.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК Серия,
	        |	ЕСТЬNULL(ТаблицаСерииДляЗапроса.Количество, ТаблицаТоварыДляЗапроса.Количество) КАК Количество,
	        |	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковок, ТаблицаТоварыДляЗапроса.КоличествоУпаковок) КАК КоличествоУпаковок,
	        |	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоВДокументе, ТаблицаТоварыДляЗапроса.КоличествоВДокументе) КАК КоличествоВДокументе,
	        |	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковокВДокументе, ТаблицаТоварыДляЗапроса.КоличествоУпаковокВДокументе) КАК КоличествоУпаковокВДокументе,
	        |	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоНеОтгружать, ТаблицаТоварыДляЗапроса.КоличествоНеОтгружать) КАК КоличествоНеОтгружать,
	        |	ЕСТЬNULL(ТаблицаСерииДляЗапроса.КоличествоУпаковокНеОтгружать, ТаблицаТоварыДляЗапроса.КоличествоУпаковокНеОтгружать) КАК КоличествоУпаковокНеОтгружать
	        |ПОМЕСТИТЬ ТаблицаОбработки
	        |ИЗ
	        |	ТаблицаТоварыДляЗапроса КАК ТаблицаТоварыДляЗапроса
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСерииДляЗапроса КАК ТаблицаСерииДляЗапроса
	        |		ПО ТаблицаТоварыДляЗапроса.Номенклатура = ТаблицаСерииДляЗапроса.Номенклатура
	        |			И ТаблицаТоварыДляЗапроса.Характеристика = ТаблицаСерииДляЗапроса.Характеристика
	        |			И ТаблицаТоварыДляЗапроса.Упаковка = ТаблицаСерииДляЗапроса.Упаковка
	        |			И (ТаблицаТоварыДляЗапроса.УчитыватьОстаткиСерий)
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ТаблицаОбработки.Номенклатура КАК Номенклатура,
	        |	ТаблицаОбработки.Характеристика КАК Характеристика,
	        |	ТаблицаОбработки.Упаковка КАК Упаковка,
	        |	ТаблицаОбработки.Серия КАК Серия,
	        |	ВЫБОР
	        |		КОГДА ТаблицаОбработки.КоличествоВДокументе - ТаблицаОбработки.Количество > 0
	        |			ТОГДА ТаблицаОбработки.КоличествоВДокументе - ТаблицаОбработки.Количество
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК КоличествоНедостача,
	        |	ВЫБОР
	        |		КОГДА ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок > 0
	        |			ТОГДА ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК КоличествоУпаковокНедостача,
	        |	ВЫБОР
	        |		КОГДА ТаблицаОбработки.КоличествоВДокументе - ТаблицаОбработки.Количество < 0
	        |			ТОГДА -(ТаблицаОбработки.КоличествоВДокументе - ТаблицаОбработки.Количество)
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК КоличествоИзлишек,
	        |	ВЫБОР
	        |		КОГДА ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок < 0
	        |			ТОГДА -(ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок)
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК КоличествоУпаковокИзлишек,
	        |	ВЫБОР
	        |		КОГДА ТаблицаОбработки.КоличествоНеОтгружать - ВЫБОР
	        |				КОГДА ТаблицаОбработки.КоличествоВДокументе - ТаблицаОбработки.Количество < 0
	        |					ТОГДА -(ТаблицаОбработки.КоличествоВДокументе - ТаблицаОбработки.Количество)
	        |				ИНАЧЕ 0
	        |			КОНЕЦ > 0
	        |			ТОГДА ТаблицаОбработки.КоличествоНеОтгружать - ВЫБОР
	        |					КОГДА ТаблицаОбработки.КоличествоВДокументе - ТаблицаОбработки.Количество < 0
	        |						ТОГДА -(ТаблицаОбработки.КоличествоВДокументе - ТаблицаОбработки.Количество)
	        |					ИНАЧЕ 0
	        |				КОНЕЦ
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК КоличествоНеОтгружать,
	        |	ВЫБОР
	        |		КОГДА ТаблицаОбработки.КоличествоУпаковокНеОтгружать - ВЫБОР
	        |				КОГДА ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок < 0
	        |					ТОГДА -(ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок)
	        |				ИНАЧЕ 0
	        |			КОНЕЦ > 0
	        |			ТОГДА ТаблицаОбработки.КоличествоУпаковокНеОтгружать - ВЫБОР
	        |					КОГДА ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок < 0
	        |						ТОГДА -(ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок)
	        |					ИНАЧЕ 0
	        |				КОНЕЦ
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК КоличествоУпаковокНеОтгружать
	        |ИЗ
	        |	ТаблицаОбработки КАК ТаблицаОбработки
	        |ГДЕ
	        |	(ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок <> 0
	        |			ИЛИ ТаблицаОбработки.КоличествоУпаковокНеОтгружать - ВЫБОР
	        |				КОГДА ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок < 0
	        |					ТОГДА -(ТаблицаОбработки.КоличествоУпаковокВДокументе - ТаблицаОбработки.КоличествоУпаковок)
	        |				ИНАЧЕ 0
	        |			КОНЕЦ > 0)
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	Номенклатура,
	        |	Характеристика,
	        |	Упаковка,
	        |	Серия
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ТаблицаТоварыДляЗапроса.Номенклатура,
	        |	ТаблицаТоварыДляЗапроса.Характеристика,
	        |	ТаблицаТоварыДляЗапроса.Упаковка,
	        |	ТаблицаСерииДляЗапроса.Серия КАК Серия,
	        |	ВЫБОР
	        |		КОГДА ТаблицаТоварыДляЗапроса.УчитыватьОстаткиСерий
	        |			ТОГДА ТаблицаСерииДляЗапроса.КоличествоВДокументе
	        |		ИНАЧЕ ТаблицаСерииДляЗапроса.Количество - ТаблицаСерииДляЗапроса.КоличествоНеОтгружать
	        |	КОНЕЦ КАК Количество,
	        |	ВЫБОР
	        |		КОГДА ТаблицаТоварыДляЗапроса.УчитыватьОстаткиСерий
	        |			ТОГДА ТаблицаСерииДляЗапроса.КоличествоУпаковокВДокументе
	        |		ИНАЧЕ ТаблицаСерииДляЗапроса.КоличествоУпаковок - ТаблицаСерииДляЗапроса.КоличествоУпаковокНеОтгружать
	        |	КОНЕЦ КАК КоличествоУпаковок
	        |ИЗ
	        |	ТаблицаТоварыДляЗапроса КАК ТаблицаТоварыДляЗапроса
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСерииДляЗапроса КАК ТаблицаСерииДляЗапроса
	        |		ПО ТаблицаТоварыДляЗапроса.Номенклатура = ТаблицаСерииДляЗапроса.Номенклатура
	        |			И ТаблицаТоварыДляЗапроса.Характеристика = ТаблицаСерииДляЗапроса.Характеристика
	        |			И ТаблицаТоварыДляЗапроса.Упаковка = ТаблицаСерииДляЗапроса.Упаковка
	        |ГДЕ
	        |	ВЫБОР
	        |			КОГДА ТаблицаТоварыДляЗапроса.УчитыватьОстаткиСерий
	        |				ТОГДА ТаблицаСерииДляЗапроса.КоличествоУпаковокВДокументе
	        |			ИНАЧЕ ТаблицаСерииДляЗапроса.КоличествоУпаковок - ТаблицаСерииДляЗапроса.КоличествоУпаковокНеОтгружать
	        |		КОНЕЦ > 0
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ТаблицаОбработки.Номенклатура КАК Номенклатура,
	        |	ТаблицаОбработки.Характеристика КАК Характеристика,
	        |	ТаблицаОбработки.Упаковка КАК Упаковка,
	        |	СУММА(ТаблицаОбработки.Количество - ТаблицаОбработки.КоличествоНеОтгружать) КАК Количество,
	        |	СУММА(ТаблицаОбработки.КоличествоУпаковок - ТаблицаОбработки.КоличествоУпаковокНеОтгружать) КАК КоличествоУпаковок
	        |ИЗ
	        |	ТаблицаОбработки КАК ТаблицаОбработки
	        |ГДЕ
	        |	ТаблицаОбработки.КоличествоУпаковок - ТаблицаОбработки.КоличествоУпаковокНеОтгружать > 0
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ТаблицаОбработки.Номенклатура,
	        |	ТаблицаОбработки.Характеристика,
	        |	ТаблицаОбработки.Упаковка
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ТаблицаТоварыДляЗапроса.Номенклатура,
	        |	ТаблицаТоварыДляЗапроса.Характеристика,
	        |	ТаблицаТоварыДляЗапроса.Упаковка,
	        |	ТаблицаСерииДляЗапроса.Серия КАК Серия,
	        |	ТаблицаСерииДляЗапроса.Количество - ТаблицаСерииДляЗапроса.КоличествоНеОтгружать КАК Количество,
	        |	ТаблицаСерииДляЗапроса.КоличествоУпаковок - ТаблицаСерииДляЗапроса.КоличествоУпаковокНеОтгружать КАК КоличествоУпаковок
	        |ИЗ
	        |	ТаблицаТоварыДляЗапроса КАК ТаблицаТоварыДляЗапроса
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСерииДляЗапроса КАК ТаблицаСерииДляЗапроса
	        |		ПО ТаблицаТоварыДляЗапроса.Номенклатура = ТаблицаСерииДляЗапроса.Номенклатура
	        |			И ТаблицаТоварыДляЗапроса.Характеристика = ТаблицаСерииДляЗапроса.Характеристика
	        |			И ТаблицаТоварыДляЗапроса.Упаковка = ТаблицаСерииДляЗапроса.Упаковка
	        |ГДЕ
	        |	ТаблицаСерииДляЗапроса.КоличествоУпаковок - ТаблицаСерииДляЗапроса.КоличествоУпаковокНеОтгружать > 0";
	
		КонецФункции

#КонецЕсли