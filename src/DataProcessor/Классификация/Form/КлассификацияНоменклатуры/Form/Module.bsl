////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
	
		Возврат;
	
	КонецЕсли;
	
	ПолноеИмя = РеквизитФормыВЗначение("Объект").Метаданные().ПолноеИмя();
	
	// Параметры ABC/XYZ классификации
	ИспользоватьКлассификациюПоВыручке = Константы.ИспользоватьABCXYZКлассификациюНоменклатурыПоВыручке.Получить();
	ИспользоватьКлассификациюПоВаловойПрибыли = Константы.ИспользоватьABCXYZКлассификациюНоменклатурыПоВаловойПрибыли.Получить();
	ИспользоватьКлассификациюПоКоличествуПродаж = Константы.ИспользоватьABCXYZКлассификациюНоменклатурыПоКоличествуПродаж.Получить();
	
	// ABC классификация
	ПериодABCКлассификации = Константы.ПериодABCКлассификацииНоменклатуры.Получить();
	КоличествоПериодовABCКлассификации = Константы.КоличествоПериодовABCКлассификацииНоменклатуры.Получить();
	УчитыватьПравилаВнутреннегоТовародвиженияПриABCКлассификацииНоменклатуры = Константы.УчитыватьПравилаВнутреннегоТовародвиженияПриABCКлассификацииНоменклатуры.Получить();
	
	// XYZ классификация
	ПериодXYZКлассификации = Константы.ПериодXYZКлассификацииНоменклатуры.Получить();
	КоличествоПериодовXYZКлассификации = Константы.КоличествоПериодовXYZКлассификацииНоменклатуры.Получить();
	ПодпериодXYZКлассификации = Константы.ПодпериодXYZКлассификацииНоменклатуры.Получить();
	УчитыватьПравилаВнутреннегоТовародвиженияПриXYZКлассификацииНоменклатуры = Константы.УчитыватьПравилаВнутреннегоТовародвиженияПриXYZКлассификацииНоменклатуры.Получить();
	
	// Расчет товарных ограничений
	ПериодРасчетаТоварныхОграничений = Константы.ПериодРасчетаТоварныхОграничений.Получить();
	КоличествоПериодовРасчетаТоварныхОграничений = Константы.КоличествоПериодовРасчетаТоварныхОграничений.Получить();
	ВыполнятьРасчетТоварныхОграниченийПо = Константы.ВыполнятьРасчетТоварныхОграниченийПоКоличествуПродаж.Получить();
	УчитыватьПравилаВнутреннегоТовародвиженияПриРасчетеТоварныхОграничений = Константы.УчитыватьПравилаВнутреннегоТовародвиженияПриРасчетеТоварныхОграничений.Получить();
	
	// Цвета оформления надписей
	ЦветПоясняющийТекст = ЦветаСтиля.ПоясняющийТекст;
	ЦветПоясняющийОшибкуТекст = ЦветаСтиля.ПоясняющийОшибкуТекст;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.СтраницыВозможности.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	ОбновитьОтображениеФормы();
	ОбновитьОписания();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ДекорацияПолныеЧастоИспользуемыеВозможностиНажатие(Элемент)
	
	ПолныеВозможности = НЕ ПолныеВозможности;
	ОбновитьОтображениеФормы();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыполнитьABCXYZКлассификациюЧастоИспользуемыеВозможности(Команда)
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполняется ABC/XYZ классификация...'"), БиблиотекаКартинок.Информация32);
	
	ВыполнитьABCXYZКлассификациюНаСервере();
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполнена ABC/XYZ классификация!'"), БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьABCКлассификациюПолныеВозможности(Команда)
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполняется ABC классификация...'"), БиблиотекаКартинок.Информация32);
	
	ВыполнитьABCКлассификациюНаСервере();
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполнена ABC классификация!'"), БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьXYZКлассификациюПолныеВозможности(Команда)
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполняется XYZ классификация...'"), БиблиотекаКартинок.Информация32);
	
	ВыполнитьXYZКлассификациюНаСервере();
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполнена XYZ классификация!'"), БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРасчетТоварныхОграниченийПолныеВозможности(Команда)
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполняется расчет товарных ограничений...'"), БиблиотекаКартинок.Информация32);
	
	ВыполнитьРасчетТоварныхОграниченийНаСервере();
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполнен расчет товарных ограничений!'"), БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРасчетТоварныхОграниченийЧастоИспользуемыеВозможности(Команда)
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполняется расчет товарных ограничений...'"), БиблиотекаКартинок.Информация32);
	
	ВыполнитьРасчетТоварныхОграниченийНаСервере();
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполнен расчет товарных ограничений!'"), БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаABCXYZКлассификацияПолныеВозможности(Команда)
	
	КодВозврата = ОткрытьФормуМодально(ПолноеИмя + ".Форма.НастройкаПараметровКлассификацииНоменклатуры");
	
	Если ТипЗнч(КодВозврата) = Тип("Структура") Тогда
		
		ИспользоватьКлассификациюПоВыручке = КодВозврата.ИспользоватьКлассификациюПоВыручке;
		ИспользоватьКлассификациюПоВаловойПрибыли = КодВозврата.ИспользоватьКлассификациюПоВаловойПрибыли;
		ИспользоватьКлассификациюПоКоличествуПродаж = КодВозврата.ИспользоватьКлассификациюПоКоличествуПродаж;
		ПериодABCКлассификации = КодВозврата.ПериодABCКлассификации;
		КоличествоПериодовABCКлассификации = КодВозврата.КоличествоПериодовABCКлассификации;
		ПериодXYZКлассификации = КодВозврата.ПериодXYZКлассификации;
		КоличествоПериодовXYZКлассификации = КодВозврата.КоличествоПериодовXYZКлассификации;
		ПодпериодXYZКлассификации = КодВозврата.ПодпериодXYZКлассификации;
		УчитыватьПравилаВнутреннегоТовародвиженияПриABCКлассификацииНоменклатуры = КодВозврата.УчитыватьПравилаВнутреннегоТовародвиженияПриABCКлассификацииНоменклатуры;
		УчитыватьПравилаВнутреннегоТовародвиженияПриXYZКлассификацииНоменклатуры = КодВозврата.УчитыватьПравилаВнутреннегоТовародвиженияПриXYZКлассификацииНоменклатуры;
		
		ОбновитьОтображениеФормы();
		ОбновитьОписания();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаТоварныеОграниченияПолныеВозможности(Команда)
	
	КодВозврата = ОткрытьФормуМодально(ПолноеИмя + ".Форма.НастройкаПараметровРасчетаТоварныхОграничений");
	
	Если ТипЗнч(КодВозврата) = Тип("Структура") Тогда
		
		ПериодРасчетаТоварныхОграничений = КодВозврата.ПериодРасчетаТоварныхОграничений;
		КоличествоПериодовРасчетаТоварныхОграничений = КодВозврата.КоличествоПериодовРасчетаТоварныхОграничений;
		ВыполнятьРасчетТоварныхОграниченийПоКоличествуПродаж = КодВозврата.ВыполнятьРасчетТоварныхОграниченийПо;
		УчитыватьПравилаВнутреннегоТовародвиженияПриРасчетеТоварныхОграничений = КодВозврата.УчитыватьПравилаВнутреннегоТовародвиженияПриРасчетеТоварныхОграничений;
		
		ОбновитьОписания();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьABCКлассификациюПолныеВозможности(Команда)
	
	КодВозврата = Вопрос(НСтр("ru = 'Вся информация об ABC классификации номенклатуры будет очищена. Продолжить?'"), РежимДиалогаВопрос.ОКОтмена);
	
	Если КодВозврата = КодВозвратаДиалога.ОК Тогда
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполняется очистка ABC классификации...'"), БиблиотекаКартинок.Информация32);

		ОчиститьABCКлассификациюНаСервере();
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполнена очистка ABC классификации!'"), БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьXYZКлассификациюПолныеВозможности(Команда)
	
	КодВозврата = Вопрос(НСтр("ru = 'Вся информация об XYZ классификации номенклатуры будет очищена. Продолжить?'"), РежимДиалогаВопрос.ОКОтмена);
	
	Если КодВозврата = КодВозвратаДиалога.ОК Тогда
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполняется очистка XYZ классификации...'"), БиблиотекаКартинок.Информация32);

		ОчиститьXYZКлассификациюНаСервере();
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполнена очистка XYZ классификации!'"), БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТоварныеОграниченияПолныеВозможности(Команда)
	
	КодВозврата = Вопрос(НСтр("ru = 'Вся информация о товарных ограничениях будет очищена. Продолжить?'"), РежимДиалогаВопрос.ОКОтмена);
	
	Если КодВозврата = КодВозвратаДиалога.ОК Тогда
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполняется очистка товарных ограничений...'"), БиблиотекаКартинок.Информация32);

		ОчиститьТоварныеОграниченияНаСервере();
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Классификация номенклатуры'"),, НСтр("ru = 'Выполнена очистка товарных ограничений!'"), БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Процедура ОбновитьОписания()
	
	ABCОписаниеЧастоИспользуемыеВозможности =
		НСтр("ru = 'ABC классификация по данным за период: '") +
		ОписаниеНастройки(ПериодABCКлассификации, КоличествоПериодовABCКлассификации);
	XYZОписаниеЧастоИспользуемыеВозможности =
		НСтр("ru = 'XYZ классификация по данным за период: '") +
		ОписаниеНастройки(ПериодXYZКлассификации, КоличествоПериодовXYZКлассификации, ПодпериодXYZКлассификации);
	ABCОписаниеПолныеВозможности =
		НСтр("ru = 'ABC классификация за все периоды по данным за период: '") +
		ОписаниеНастройки(ПериодABCКлассификации, КоличествоПериодовABCКлассификации);
	XYZОписаниеПолныеВозможности =
		НСтр("ru = 'XYZ классификация за все периоды по данным за период: '") +
		ОписаниеНастройки(ПериодXYZКлассификации, КоличествоПериодовXYZКлассификации, ПодпериодXYZКлассификации);
	ТоварныеОграниченияОписание =
		НСтр("ru = 'Расчет рекомендуемых товарных ограничений по данным за период: '") +
		ОписаниеНастройки(ПериодРасчетаТоварныхОграничений, КоличествоПериодовРасчетаТоварныхОграничений);
	
	// Часто используемые возможности
	Если ИспользоватьКлассификациюПоВыручке
		ИЛИ ИспользоватьКлассификациюПоВаловойПрибыли
		ИЛИ ИспользоватьКлассификациюПоКоличествуПродаж Тогда
	
		Элементы.ДекорацияABCXYZКлассификацияОписаниеЧастоИспользуемыеВозможности.Заголовок =
			ABCОписаниеЧастоИспользуемыеВозможности + " " + XYZОписаниеЧастоИспользуемыеВозможности;
			
		Элементы.ДекорацияABCXYZКлассификацияОписаниеЧастоИспользуемыеВозможности.ЦветТекста = ЦветПоясняющийТекст;
		
	Иначе
		
		Элементы.ДекорацияABCXYZКлассификацияОписаниеЧастоИспользуемыеВозможности.Заголовок =
			НСтр("ru = 'Перейдите к полным возможностям и в настройке установите параметры, по которым необходимо выполнять классификацию.'");
			
		Элементы.ДекорацияABCXYZКлассификацияОписаниеЧастоИспользуемыеВозможности.ЦветТекста = ЦветПоясняющийОшибкуТекст;
		
	КонецЕсли;
	
	Элементы.ДекорацияТоварныеОграниченияОписаниеЧастоИспользуемыеВозможности.Заголовок = ТоварныеОграниченияОписание;
	
	// Полные возможности
	Если ИспользоватьКлассификациюПоВыручке
		ИЛИ ИспользоватьКлассификациюПоВаловойПрибыли
		ИЛИ ИспользоватьКлассификациюПоКоличествуПродаж Тогда
		
		Элементы.ДекорацияABCКлассификацияОписаниеПолныеВозможности.Заголовок = ABCОписаниеПолныеВозможности;
		Элементы.ДекорацияXYZКлассификацияОписаниеПолныеВозможности.Заголовок = XYZОписаниеПолныеВозможности;
		
		Элементы.ДекорацияABCКлассификацияОписаниеПолныеВозможности.ЦветТекста = ЦветПоясняющийТекст;
		Элементы.ДекорацияXYZКлассификацияОписаниеПолныеВозможности.ЦветТекста = ЦветПоясняющийТекст;
	
	Иначе
		
		Элементы.ДекорацияABCКлассификацияОписаниеПолныеВозможности.Заголовок = НСтр("ru = 'Перейдите к настройке и установите параметры, по которым необходимо выполнять классификацию.'");
		Элементы.ДекорацияXYZКлассификацияОписаниеПолныеВозможности.Заголовок = НСтр("ru = 'Перейдите к настройке и установите параметры, по которым необходимо выполнять классификацию.'");
		
		Элементы.ДекорацияABCКлассификацияОписаниеПолныеВозможности.ЦветТекста = ЦветПоясняющийОшибкуТекст;
		Элементы.ДекорацияXYZКлассификацияОписаниеПолныеВозможности.ЦветТекста = ЦветПоясняющийОшибкуТекст;
	
	КонецЕсли;
	
	Элементы.ДекорацияОчиститьABCКлассификацияОписаниеПолныеВозможности.Заголовок = НСтр("ru = 'Очистить ABC классификацию за все периоды.'");
	Элементы.ДекорацияОчиститьXYZКлассификацияОписаниеПолныеВозможности.Заголовок = НСтр("ru = 'Очистить XYZ классификацию за все периоды.'");
	Элементы.ДекорацияРассчитатьТоварныеОграниченияОписаниеПолныеВозможности.Заголовок = ТоварныеОграниченияОписание;
	Элементы.ДекорацияОчиститьТоварныеОграниченияОписаниеПолныеВозможности.Заголовок = НСтр("ru = 'Очистить рекомендуемые и используемые товарные ограничения.'");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеФормы()
	
	Если ПолныеВозможности Тогда
		
		// Переключение к полным возможностям
		Элементы.СтраницыВозможности.ТекущаяСтраница = Элементы.СтраницаПолныеВозможности;
		
		// Изменение заголовка формы и заголовка декорации
		Заголовок = НСтр("ru = 'Классификация номенклатуры - полные возможности'");
		Элементы.ДекорацияПолныеЧастоИспользуемыеВозможности.Заголовок = НСтр("ru = 'Часто используемые возможности'");
		
		// Доступность выполнения ABC классификации
		Элементы.ВыполнитьABCКлассификациюПолныеВозможности.Доступность = ИспользоватьКлассификациюПоВыручке
																			ИЛИ ИспользоватьКлассификациюПоВаловойПрибыли
																			ИЛИ ИспользоватьКлассификациюПоКоличествуПродаж;
																			
																			
		// Доступность выполнения XYZ классификации
		Элементы.ВыполнитьXYZКлассификациюПолныеВозможности.Доступность = ИспользоватьКлассификациюПоВыручке
																			ИЛИ ИспользоватьКлассификациюПоВаловойПрибыли
																			ИЛИ ИспользоватьКлассификациюПоКоличествуПродаж;
		
	Иначе
		
		 // Переключение к часто используемым возможностям
		Элементы.СтраницыВозможности.ТекущаяСтраница = Элементы.СтраницаЧастоИспользуемыеВозможности;
		
		// Изменение заголовка формы и заголовка декорации
		Заголовок = НСтр("ru = 'Классификация номенклатуры - часто используемые возможности'");
		Элементы.ДекорацияПолныеЧастоИспользуемыеВозможности.Заголовок = НСтр("ru = 'Полные возможности'");
		
		// Доступность выполнения ABC/XYZ классификации
		Элементы.ВыполнитьABCXYZКлассификациюЧастоИспользуемыеВозможности.Доступность = ИспользоватьКлассификациюПоВыручке
																						ИЛИ ИспользоватьКлассификациюПоВаловойПрибыли
																						ИЛИ ИспользоватьКлассификациюПоКоличествуПродаж;
		
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеНастройки(Период, КоличествоПериодов, Подпериод = Неопределено)
	
	ПредставлениеНастройки = "";
	
	Если НЕ ЗначениеЗаполнено(КоличествоПериодов) ИЛИ НЕ ЗначениеЗаполнено(Период) Тогда
		
		Возврат ПредставлениеНастройки;
		
	КонецЕсли;
	
	ПараметрыПредметаИсчисления = "";
	
	Если Период = ПредопределенноеЗначение("Перечисление.Периодичность.День") Тогда
		
		ПараметрыПредметаИсчисления = НСтр("ru = 'предыдущий день, предыдущих дня, предыдущих дней, м,,,,, 0'");
		
	ИначеЕсли Период = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
		
		ПараметрыПредметаИсчисления = НСтр("ru = 'предыдущая неделя, предыдущие недели, предыдущих недель, ж,,,,, 0'");
		
	ИначеЕсли Период = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
		
		ПараметрыПредметаИсчисления = НСтр("ru = 'предыдущая декада, предыдущие декады, предыдущих декад, ж,,,,, 0'");
		
	ИначеЕсли Период = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		
		ПараметрыПредметаИсчисления = НСтр("ru = 'предыдущий месяц, предыдущих месяца, предыдущих месяцев, м,,,,, 0'");
		
	ИначеЕсли Период = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		
		ПараметрыПредметаИсчисления = НСтр("ru = 'предыдущий квартал, предыдущих квартала, предыдущих кварталов, м,,,,, 0'");
		
	ИначеЕсли Период = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		
		ПараметрыПредметаИсчисления = НСтр("ru = 'предыдущее полугодие, предыдущих полугодия, предыдущих полугодий, с,,,,, 0'");
		
	ИначеЕсли Период = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		
		ПараметрыПредметаИсчисления = НСтр("ru = 'предыдущий год, предыдущих года, предыдущих лет, м,,,,, 0'");
		
	Иначе
		
		ПараметрыПредметаИсчисления = "";
		
	КонецЕсли;
	
	ПредставлениеНастройки = НРег(ЧислоПрописью(КоличествоПериодов,, ПараметрыПредметаИсчисления));
	
	Если ЗначениеЗаполнено(Подпериод) Тогда
		
		Если Подпериод = ПредопределенноеЗначение("Перечисление.Периодичность.День") Тогда
			
			ПредставлениеНастройки = ПредставлениеНастройки + НСтр("ru = ' (по дням).'");
			
		ИначеЕсли Подпериод = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
			
			ПредставлениеНастройки = ПредставлениеНастройки + НСтр("ru = ' (по неделям).'");
			
		ИначеЕсли Подпериод = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
			
			ПредставлениеНастройки = ПредставлениеНастройки + НСтр("ru = ' (по декадам).'");
			
		ИначеЕсли Подпериод = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
			
			ПредставлениеНастройки = ПредставлениеНастройки + НСтр("ru = ' (по месяцам).'");
			
		ИначеЕсли Подпериод = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
			
			ПредставлениеНастройки = ПредставлениеНастройки + НСтр("ru = ' (по кварталам).'");
			
		ИначеЕсли Подпериод = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
			
			ПредставлениеНастройки = ПредставлениеНастройки + НСтр("ru = ' (по полугодиям).'");
			
		ИначеЕсли Подпериод = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
			
			ПредставлениеНастройки = ПредставлениеНастройки + НСтр("ru = ' (по годам).'");
			
		КонецЕсли;
		
	Иначе
		
		ПредставлениеНастройки = ПредставлениеНастройки + ".";
		
	КонецЕсли;
	
	Возврат ПредставлениеНастройки;
	
КонецФункции

&НаСервере
Процедура ВыполнитьABCXYZКлассификациюНаСервере()
	
	Справочники.Номенклатура.ВыполнитьABCКлассификацию();
	Справочники.Номенклатура.ВыполнитьXYZКлассификацию();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьABCКлассификациюНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВыручкаИСебестоимостьПродаж.Период КАК Период
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ДатаКлассификации = ОбщегоНазначенияУТКлиентСервер.РасширенныйПериод(Выборка.Период, ПериодABCКлассификации, 0).ДатаОкончания + 1;
    МаксимальнаяДатаКлассификации = ОбщегоНазначенияУТКлиентСервер.РасширенныйПериод(ТекущаяДата(), ПериодABCКлассификации, -1).ДатаОкончания + 1;
	
	Пока ДатаКлассификации <= МаксимальнаяДатаКлассификации Цикл
		
		Справочники.Номенклатура.ВыполнитьABCКлассификацию(ДатаКлассификации);
		ДатаКлассификации = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(ДатаКлассификации, ПериодABCКлассификации, 1) + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьXYZКлассификациюНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВыручкаИСебестоимостьПродаж.Период КАК Период
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ДатаКлассификации = ОбщегоНазначенияУТКлиентСервер.РасширенныйПериод(Выборка.Период, ПериодXYZКлассификации, 0).ДатаОкончания + 1;
    МаксимальнаяДатаКлассификации = ОбщегоНазначенияУТКлиентСервер.РасширенныйПериод(ТекущаяДата(), ПериодXYZКлассификации, -1).ДатаОкончания + 1;
	
	Пока ДатаКлассификации <= МаксимальнаяДатаКлассификации Цикл
		
		Справочники.Номенклатура.ВыполнитьXYZКлассификацию(ДатаКлассификации);
		ДатаКлассификации = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(ДатаКлассификации, ПериодXYZКлассификации, 1) + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьРасчетТоварныхОграниченийНаСервере()
	
	Справочники.Номенклатура.ВыполнитьРасчетТоварныхОграничений();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьABCКлассификациюНаСервере()
	
	РегистрыСведений.ABCXYZКлассификацияНоменклатуры.ОчиститьABCКлассификацию();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьXYZКлассификациюНаСервере()
	
	РегистрыСведений.ABCXYZКлассификацияНоменклатуры.ОчиститьXYZКлассификацию();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТоварныеОграниченияНаСервере()
	
	РегистрыСведений.ТоварныеОграничения.ОчиститьТоварныеОграничения();
	
КонецПроцедуры
