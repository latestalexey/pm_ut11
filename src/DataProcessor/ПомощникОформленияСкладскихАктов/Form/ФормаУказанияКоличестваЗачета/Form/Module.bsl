&НаКлиенте
Процедура ОК(Команда)
	ОчищатьРаспределениеСписание = Ложь;
	ОчищатьРаспределениеОприходование = Ложь;
	ОчищатьРаспределениеПересортица = Ложь;
	
	Если КЗачетуВсего > КСписаниюВсего Тогда
		ТекстСообщения = НСтр("ru='Нельзя зачесть" + ИмяОперации + " больше, чем количество к списанию.'");
		Предупреждение(ТекстСообщения);
		Возврат;
	ИначеЕсли КЗачетуВсего > КОприходованиюВсего Тогда
		ТекстСообщения = НСтр("ru='Нельзя зачесть" + ИмяОперации + " больше, чем количество к оприходованию.'");
		Предупреждение(ТекстСообщения);
		Возврат;
	ИначеЕсли КЗачетуВсего > КСписаниюНеРаспределено 
		Или КЗачетуВсего > КОприходованиюНеРаспределено 
		Или КЗачетуВсего < КЗачетуРаспределено Тогда
		
		Если  КЗачетуВсего > КСписаниюНеРаспределено
			Или КЗачетуВсего > КОприходованиюНеРаспределено Тогда 
			ТекстСообщенияНачало = НСтр("ru='Количество к зачету превышает нераспределенное количество'");
		Иначе
			ТекстСообщенияНачало = "";
		КонецЕсли;
		
		ТекстСообщенияСередина = "";
		
		Если КЗачетуВсего > КСписаниюНеРаспределено Тогда
        	ТекстСообщенияСередина = НСтр("ru=' к списанию'");
			ОчищатьРаспределениеСписание = Истина;
		КонецЕсли;
		
		Если КЗачетуВсего > КОприходованиюНеРаспределено Тогда
        	ТекстСообщенияСередина = ТекстСообщенияСередина + ?(ПустаяСтрока(ТекстСообщенияСередина),НСтр("ru=' к оприходованию'"),
									НСтр("ru=' и к оприходованию'"));
			ОчищатьРаспределениеОприходование = Истина;
		КонецЕсли;
		
		Если КЗачетуВсего < КЗачетуРаспределено Тогда
			Если ПустаяСтрока(ТекстСообщенияНачало) Тогда
				ТекстСообщенияСередина = НСтр("ru='Количество к зачету меньше, чем распределенное количество к зачету'");	
			Иначе
				ТекстСообщенияСередина = ТекстСообщенияСередина + Символы.ВК
										+ НСтр("ru=', а также меньше, чем распределенное количество к зачету'");
			КонецЕсли;
			ОчищатьРаспределениеОприходование = Истина;
		КонецЕсли;
		
		ТекстСообщенияКонец = НСтр("ru=', поэтому конфликтующие распределения будут очищены. Продолжить?'");
		
		ТекстСообщения = ТекстСообщенияНачало + ТекстСообщенияСередина + ТекстСообщенияКонец;
		
		Ответ = Вопрос(ТекстСообщения, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
		
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("КЗачету",КЗачетуВсего);
	СтруктураВозврата.Вставить("ОчищатьРаспределениеСписание",ОчищатьРаспределениеСписание);
	СтруктураВозврата.Вставить("ОчищатьРаспределениеОприходование",ОчищатьРаспределениеОприходование);
	СтруктураВозврата.Вставить("ОчищатьРаспределениеОперации",ОчищатьРаспределениеПересортица);
	
	Закрыть(СтруктураВозврата); 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;	
	КЗачетуВсего 				 = Параметры.КЗачетуВсего;
	КЗачетуРаспределено 		 = Параметры.КЗачетуРаспределено;
	КСписаниюВсего 				 = Параметры.КСписаниюВсего;
	КСписаниюНеРаспределено 	 = Параметры.КСписаниюНеРаспределено;
	КОприходованиюВсего 		 = Параметры.КОприходованиюВсего;
	КОприходованиюНеРаспределено = Параметры.КОприходованиюНеРаспределено;
	ИмяОперации 				 = ?(Параметры.ВидОперации = "Пересортица", " по пересортице", " по порче");
	Заголовок 					 = Заголовок + ИмяОперации;
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры
