////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
	
		Возврат;
	
	КонецЕсли;

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
	НаборЗаписейСклад.Отбор, 
	"Склад", 
	Параметры.Склад, 
	ВидСравненияКомпоновкиДанных.Равно, 
	"Склад", 
	Истина);

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
	НаборЗаписейНоменклатура.Отбор, 
	"Склад", 
	Параметры.Склад, 
	ВидСравненияКомпоновкиДанных.Равно, 
	"Склад", 
	Истина);

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
	НаборЗаписейНоменклатура.Отбор, 
	"Номенклатура", 
	Параметры.Номенклатура, 
	ВидСравненияКомпоновкиДанных.Равно, 
	"Номенклатура", 
	Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
	НаборЗаписейХарактеристика.Отбор, 
	"Склад", 
	Параметры.Склад, 
	ВидСравненияКомпоновкиДанных.Равно, 
	"Склад", 
	Истина);

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
	НаборЗаписейХарактеристика.Отбор, 
	"Номенклатура", 
	Параметры.Номенклатура, 
	ВидСравненияКомпоновкиДанных.Равно, 
	"Номенклатура", 
	Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
	НаборЗаписейХарактеристика.Отбор, 
	"Характеристика", 
	Параметры.Характеристика, 
	ВидСравненияКомпоновкиДанных.Равно, 
	"Характеристика", 
	Истина);
	
	НаборЗаписейСклад.Параметры.УстановитьЗначениеПараметра("Номенклатура",          Параметры.Номенклатура);
	НаборЗаписейСклад.Параметры.УстановитьЗначениеПараметра("Характеристика",        Параметры.Характеристика);
	НаборЗаписейНоменклатура.Параметры.УстановитьЗначениеПараметра("Характеристика", Параметры.Характеристика);
	
	Элементы.ГруппаСклад.Заголовок =
		НСтр("ru = ""Порядок применения способов обеспечения на складе: """) + Строка(Параметры.Склад);
	
	Элементы.ГруппаНоменклатура.Заголовок =
		НСтр("ru = ""Порядок применения способов обеспечения номенклатуры: """) + Строка(Параметры.Номенклатура);
	
	Если ЗначениеЗаполнено(Параметры.Характеристика) Тогда
		
		Элементы.ГруппаХарактеристика.Заголовок =
			НСтр("ru = ""Порядок применения способов обеспечения характеристики номенклатуры: """)
			+ Строка(Параметры.Характеристика);
		Элементы.ГруппаХарактеристика.Видимость = Истина;
		
	КонецЕсли;
	
	ПолучитьДействующиеСпособы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Результат = Новый Структура();
	
	Результат.Вставить("ИзменениеОсновногоСпособаОбеспечения", Новый Массив());
	
	СпособДляСкладаСтарый         = СпособДляСклада;
	СпособДляНоменклатурыСтарый   = СпособДляНоменклатуры;
	СпособДляХарактеристикиСтарый = СпособДляХарактеристики;
	
	ПолучитьДействующиеСпособы();
	
	Если СпособДляСкладаСтарый         <> СпособДляСклада Тогда
		Результат.ИзменениеОсновногоСпособаОбеспечения.Добавить(0);
	КонецЕсли;
	Если СпособДляНоменклатурыСтарый   <> СпособДляНоменклатуры Тогда
		Результат.ИзменениеОсновногоСпособаОбеспечения.Добавить(1);
	КонецЕсли;
	Если СпособДляХарактеристикиСтарый <> СпособДляХарактеристики Тогда
		Результат.ИзменениеОсновногоСпособаОбеспечения.Добавить(2);
	КонецЕсли;
	
	Результат.Вставить("ИзменениеРеквизитовСпособаОбеспечения", ИзмененныеСпособы.ВыгрузитьЗначения());
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_СпособОбеспеченияПотребностей" Тогда
		
		ИзмененныеСпособы.Добавить(Источник);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <ИМЯ ТАБЛИЦЫ ФОРМЫ>

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПереместитьВверхСклад(Команда)
	
	КлючЗаписи = Элементы.НаборЗаписейСклад.ТекущаяСтрока;
	Если КлючЗаписи <> Неопределено Тогда
		
		ПереместитьВверхНаСервере(КлючЗаписи);
		
	КонецЕсли;
	Элементы.НаборЗаписейСклад.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВнизСклад(Команда)
	
	КлючЗаписи = Элементы.НаборЗаписейСклад.ТекущаяСтрока;
	Если КлючЗаписи <> Неопределено Тогда
		
		ПереместитьВнизНаСервере(КлючЗаписи);
		
	КонецЕсли;
	Элементы.НаборЗаписейСклад.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВверхНоменклатура(Команда)
	
	КлючЗаписи = Элементы.НаборЗаписейНоменклатура.ТекущаяСтрока;
	Если КлючЗаписи <> Неопределено Тогда
		
		ПереместитьВверхНаСервере(КлючЗаписи);
		
	КонецЕсли;
	Элементы.НаборЗаписейНоменклатура.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВнизНоменклатура(Команда)
	
	КлючЗаписи = Элементы.НаборЗаписейНоменклатура.ТекущаяСтрока;
	Если КлючЗаписи <> Неопределено Тогда
		
		ПереместитьВнизНаСервере(КлючЗаписи);
		
	КонецЕсли;
	Элементы.НаборЗаписейНоменклатура.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВверхХарактеристика(Команда)
	
	КлючЗаписи = Элементы.НаборЗаписейХарактеристика.ТекущаяСтрока;
	Если КлючЗаписи <> Неопределено Тогда
		
		ПереместитьВверхНаСервере(КлючЗаписи);
		
	КонецЕсли;
	Элементы.НаборЗаписейХарактеристика.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВнизХарактеристика(Команда)
	
	КлючЗаписи = Элементы.НаборЗаписейХарактеристика.ТекущаяСтрока;
	Если КлючЗаписи <> Неопределено Тогда
		
		ПереместитьВнизНаСервере(КлючЗаписи);
		
	КонецЕсли;
	Элементы.НаборЗаписейХарактеристика.Обновить();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере 
Процедура ПолучитьДействующиеСпособы()

	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеСпособы.ИсточникСпособа           КАК ИсточникСпособа,
	|	ОсновныеСпособы.СпособОбеспечения         КАК СпособОбеспечения
	|ИЗ
	|	(ВЫБРАТЬ
	|		0                                          КАК ИсточникСпособа,
	|		СпособыСклад.СпособОбеспеченияПотребностей КАК СпособОбеспечения
	|	ИЗ
	|		РегистрСведений.ВариантыОбеспеченияТоварами КАК СпособыСклад
	|	ГДЕ
	|		СпособыСклад.РеквизитДопУпорядочивания = 1
	|		И СпособыСклад.Склад          = &Склад
	|		И СпособыСклад.Номенклатура   = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		И СпособыСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		1                                                 КАК ИсточникСпособа,
	|		СпособыНоменклатура.СпособОбеспеченияПотребностей КАК СпособОбеспечения
	|	ИЗ
	|		РегистрСведений.ВариантыОбеспеченияТоварами КАК СпособыНоменклатура
	|	ГДЕ
	|		СпособыНоменклатура.РеквизитДопУпорядочивания = 1
	|		И СпособыНоменклатура.Склад          = &Склад
	|		И СпособыНоменклатура.Номенклатура   = &Номенклатура
	|		И СпособыНоменклатура.Номенклатура   <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		И СпособыНоменклатура.Характеристика =  ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		2                                                    КАК ИсточникСпособа,
	|		СпособыХарактеристика.СпособОбеспеченияПотребностей  КАК СпособОбеспечения
	|	ИЗ
	|		РегистрСведений.ВариантыОбеспеченияТоварами КАК СпособыХарактеристика
	|	ГДЕ
	|		СпособыХарактеристика.РеквизитДопУпорядочивания = 1
	|		И СпособыХарактеристика.Склад          =  &Склад
	|		И СпособыХарактеристика.Номенклатура   =  &Номенклатура
	|		И СпособыХарактеристика.Характеристика =  &Характеристика
	|		И СпособыХарактеристика.Номенклатура   <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		И СпособыХарактеристика.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|) КАК ОсновныеСпособы";
	
	Запрос.УстановитьПараметр("Склад",          Параметры.Склад);
	Запрос.УстановитьПараметр("Номенклатура",   Параметры.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Параметры.Характеристика);
	РезультатЗапроса = Запрос.Выполнить();
	
	СпособДляСклада         = Неопределено;
	СпособДляНоменклатуры   = Неопределено;
	СпособДляХарактеристики = Неопределено;
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ИсточникСпособа      = 0 Тогда
				СпособДляСклада         = Выборка.СпособОбеспечения;
			ИначеЕсли Выборка.ИсточникСпособа = 1 Тогда
				СпособДляНоменклатуры   = Выборка.СпособОбеспечения;
			ИначеЕсли Выборка.ИсточникСпособа = 2 Тогда
				СпособДляХарактеристики = Выборка.СпособОбеспечения;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПереместитьВверхНаСервере(КлючЗаписи)

 	МенеджерЗаписи = РегистрыСведений.ВариантыОбеспеченияТоварами.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Склад            = КлючЗаписи.Склад;
	МенеджерЗаписи.Номенклатура     = КлючЗаписи.Номенклатура;
	МенеджерЗаписи.Характеристика   = КлючЗаписи.Характеристика;
	МенеджерЗаписи.СпособОбеспеченияПотребностей = КлючЗаписи.СпособОбеспеченияПотребностей;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.РеквизитДопУпорядочивания > 0 Тогда
		
		МенеджерЗаписи.РеквизитДопУпорядочивания = МенеджерЗаписи.РеквизитДопУпорядочивания - 1;
		МенеджерЗаписи.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПереместитьВнизНаСервере(КлючЗаписи)
	
	МенеджерЗаписи = РегистрыСведений.ВариантыОбеспеченияТоварами.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Склад            = КлючЗаписи.Склад;
	МенеджерЗаписи.Номенклатура     = КлючЗаписи.Номенклатура;
	МенеджерЗаписи.Характеристика   = КлючЗаписи.Характеристика;
	МенеджерЗаписи.СпособОбеспеченияПотребностей = КлючЗаписи.СпособОбеспеченияПотребностей;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.РеквизитДопУпорядочивания = МенеджерЗаписи.РеквизитДопУпорядочивания + 1;
	МенеджерЗаписи.Записать();

КонецПроцедуры

