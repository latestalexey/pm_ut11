#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	Ценообразование.ПроверитьКорректностьЗаполненияДокументаУстановкиЦенНоменклатурыПоставщика(ЭтотОбъект, Отказ);
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

// Создает документ РегистрацияЦенНоменклатурыПоставщика и заполняет данными обработки
//
// Параметры:
//	РегистрироватьИзмененныеЦены - Булево - если Истина - в табличную часть Товары копируются только измененные цены
//
// Возвращаемое значение:
//	ДокументСсылка.РегистрацияЦенНоменклатурыПоставщика - если документ создан успешно
//	Неопределено - если не удалось создать документ
//
&НаСервере
Функция СоздатьДокументРегистрацииЦенНоменклатурыПоставщика(РегистрироватьИзмененныеЦены) Экспорт
	
	ДокументОбъект = Документы.РегистрацияЦенНоменклатурыПоставщика.СоздатьДокумент();
	
	НомерВПределахДня = Документы.РегистрацияЦенНоменклатурыПоставщика.РассчитатьНомерВПределахДня(ДатаНачалаДействияЦен, ДокументОбъект.Ссылка);
	
	ДокументОбъект.Дата              = Документы.РегистрацияЦенНоменклатурыПоставщика.РассчитатьДатуДокумента(ДатаНачалаДействияЦен, НомерВПределахДня);
	ДокументОбъект.Партнер           = Партнер;
	ДокументОбъект.Соглашение        = Соглашение;
	ДокументОбъект.Комментарий       = Комментарий;
	ДокументОбъект.ДокументОснование = ДокументОснование;
	ДокументОбъект.Ответственный     = Пользователи.ТекущийПользователь();
	
	ЕстьТовары = Ложь;
	
	Если РегистрироватьИзмененныеЦены = 0 Тогда
		Для Каждого ТекСтрока Из Товары Цикл
			НоваяСтрока = ДокументОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			НоваяСтрока.Цена = ТекСтрока.НоваяЦена;
			ЕстьТовары = Истина;
		КонецЦикла;
	Иначе
		Для Каждого ТекСтрока Из Товары Цикл
			Если ТекСтрока.Цена <> ТекСтрока.НоваяЦена Тогда
				НоваяСтрока = ДокументОбъект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
				НоваяСтрока.Цена = ТекСтрока.НоваяЦена;
				ЕстьТовары = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЕстьТовары Тогда
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Возврат ДокументОбъект.Ссылка;
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат Неопределено;
		КонецПопытки;
	Иначе
		
		Если РегистрироватьИзмененныеЦены = 1 Тогда
			
			ТекстОшибки = НСтр("ru='Отутствуют измененные цены'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				,
				,
				,
			);
			
		КонецЕсли;
		
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции




#КонецЕсли