////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
	
		Возврат;
	
	КонецЕсли;
	
	ОбновитьСостояниеНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОбновитьСостояние(Команда)
	
	ОбновитьСостояниеНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ОбновитьСостояниеНаСервере()
	
	Товары.Очистить();
	
	ТаблицаТовары = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	&Заказ КАК Заказ,
	|	Товары.НомерСтрокиПоПорядку КАК НомерСтрокиПоПорядку,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.УпаковкаКоэффициент КАК УпаковкаКоэффициент,
	|	Товары.КоличествоУпаковок * Товары.УпаковкаКоэффициент КАК Количество,
	|	Товары.ДатаОтгрузки КАК ДатаОтгрузки,
	|	Товары.Склад КАК Склад,
	|	Товары.ПринятКОбеспечению КАК ПринятКОбеспечению,
	|	Товары.СрокПоставки КАК СрокПоставки,
	|	Товары.УказыватьСерии КАК УказыватьСерии,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Заказ КАК Заказ,
	|	Товары.НомерСтрокиПоПорядку КАК НомерСтрокиПоПорядку,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.УпаковкаКоэффициент КАК УпаковкаКоэффициент,
	|	Товары.Количество КАК Количество,
	|	Товары.ДатаОтгрузки КАК ДатаОтгрузки,
	|	Товары.Склад КАК Склад,
	|	Товары.ПринятКОбеспечению КАК ПринятКОбеспечению,
	|	Товары.СрокПоставки КАК СрокПоставки,
	|	Товары.УказыватьСерии КАК УказыватьСерии,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ТаблицаЗаказа
	|ИЗ
	|	Товары КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ГрафикДвиженияТоваров.Регистратор,
	|	0,
	|	ГрафикДвиженияТоваров.Номенклатура,
	|	ГрафикДвиженияТоваров.Характеристика,
	|	Товары.УпаковкаКоэффициент,
	|	-ГрафикДвиженияТоваров.КоличествоКонечныйОстаток,
	|	ГрафикДвиженияТоваров.ДатаСобытия,
	|	ГрафикДвиженияТоваров.Склад,
	|	ИСТИНА,
	|	0,
	|	Товары.УказыватьСерии,
	|	Товары.СтатусУказанияСерий
	|ИЗ
	|	РегистрНакопления.ГрафикДвиженияТоваров.ОстаткиИОбороты(
	|			,
	|			,
	|			Регистратор,
	|			,
	|			(Склад, Номенклатура, Характеристика) В
	|				(ВЫБРАТЬ
	|					Товары.Склад КАК Склад,
	|					Товары.Номенклатура КАК Номенклатура,
	|					Товары.Характеристика КАК Характеристика
	|				ИЗ
	|					Товары КАК Товары
	|				ГДЕ
	|					Товары.НомерСтрокиПоПорядку = &НомерСтрокиПоПорядку)) КАК ГрафикДвиженияТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Товары КАК Товары
	|		ПО (Товары.НомерСтрокиПоПорядку = &НомерСтрокиПоПорядку)
	|ГДЕ
	|	ГрафикДвиженияТоваров.Регистратор <> &Заказ
	|	И ГрафикДвиженияТоваров.КоличествоКонечныйОстаток < 0
	|	И ГрафикДвиженияТоваров.КоличествоРасход > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗаказа.Заказ КАК Заказ,
	|	ТаблицаЗаказа.НомерСтрокиПоПорядку КАК НомерСтрокиПоПорядку,
	|	МАКСИМУМ(ТаблицаЗаказа.Склад) КАК Склад,
	|	МАКСИМУМ(ТаблицаЗаказа.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(ТаблицаЗаказа.Характеристика) КАК Характеристика,
	|	МАКСИМУМ(ТаблицаЗаказа.УпаковкаКоэффициент) КАК УпаковкаКоэффициент,
	|	МАКСИМУМ(ТаблицаЗаказа.ДатаОтгрузки) КАК ДатаОтгрузки,
	|	МАКСИМУМ(ТаблицаЗаказа.Количество) КАК Количество,
	|	СУММА(ЕСТЬNULL(ГрафикОтгрузкиТекущийЗаказ.Количество, 0)) КАК КоличествоПредыдущиеСтрокиЗаказа,
	|	МАКСИМУМ(ТаблицаЗаказа.ПринятКОбеспечению) КАК ПринятКОбеспечению,
	|	МАКСИМУМ(ТаблицаЗаказа.СрокПоставки) КАК СрокПоставки,
	|	МАКСИМУМ(ТаблицаЗаказа.УказыватьСерии) КАК УказыватьСерии,
	|	МАКСИМУМ(ТаблицаЗаказа.СтатусУказанияСерий) КАК СтатусУказанияСерий,
	|	МАКСИМУМ(ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля))) КАК ВариантКонтроля,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля)) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	|				ТОГДА ВЫБОР
	|						КОГДА ТаблицаЗаказа.СрокПоставки > 0
	|							ТОГДА ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, ТаблицаЗаказа.СрокПоставки + 1)
	|						ИНАЧЕ НЕОПРЕДЕЛЕНО
	|					КОНЕЦ
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ) КАК ГраницаГрафикаДоступностиСрокПоставки,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля)) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	|				ТОГДА ВЫБОР
	|						КОГДА НЕ НастройкаХарактеристика.ВариантКонтроля ЕСТЬ NULL 
	|							ТОГДА ВЫБОР
	|									КОГДА НастройкаХарактеристика.ГраницаГрафикаДоступности >= &НачалоТекущегоДня
	|										ТОГДА ДОБАВИТЬКДАТЕ(НастройкаХарактеристика.ГраницаГрафикаДоступности, ДЕНЬ, 1)
	|									КОГДА НастройкаХарактеристика.СрокПоставки > 0
	|										ТОГДА ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, НастройкаХарактеристика.СрокПоставки + 1)
	|									ИНАЧЕ ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, 1)
	|								КОНЕЦ
	|						КОГДА НЕ НастройкаНоменклатура.ВариантКонтроля ЕСТЬ NULL 
	|							ТОГДА ВЫБОР
	|									КОГДА НастройкаНоменклатура.ГраницаГрафикаДоступности >= &НачалоТекущегоДня
	|										ТОГДА ДОБАВИТЬКДАТЕ(НастройкаНоменклатура.ГраницаГрафикаДоступности, ДЕНЬ, 1)
	|									КОГДА НастройкаНоменклатура.СрокПоставки > 0
	|										ТОГДА ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, НастройкаНоменклатура.СрокПоставки + 1)
	|									ИНАЧЕ ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, 1)
	|								КОНЕЦ
	|						ИНАЧЕ ВЫБОР
	|								КОГДА НастройкаСклад.ГраницаГрафикаДоступности >= &НачалоТекущегоДня
	|									ТОГДА ДОБАВИТЬКДАТЕ(НастройкаСклад.ГраницаГрафикаДоступности, ДЕНЬ, 1)
	|								КОГДА НастройкаСклад.СрокПоставки > 0
	|									ТОГДА ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, НастройкаСклад.СрокПоставки + 1)
	|								ИНАЧЕ ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, 1)
	|							КОНЕЦ
	|					КОНЕЦ
	|			ИНАЧЕ ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, 1)
	|		КОНЕЦ) КАК ГраницаГрафикаДоступности
	|ПОМЕСТИТЬ ГрафикОтгрузкиПоЗаказуБезПредыдущихЗаказов
	|ИЗ
	|	ТаблицаЗаказа КАК ТаблицаЗаказа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗаказа КАК ГрафикОтгрузкиТекущийЗаказ
	|		ПО ТаблицаЗаказа.Заказ = ГрафикОтгрузкиТекущийЗаказ.Заказ
	|			И ТаблицаЗаказа.Склад = ГрафикОтгрузкиТекущийЗаказ.Склад
	|			И ТаблицаЗаказа.Номенклатура = ГрафикОтгрузкиТекущийЗаказ.Номенклатура
	|			И ТаблицаЗаказа.Характеристика = ГрафикОтгрузкиТекущийЗаказ.Характеристика
	|			И (ВЫБОР
	|				КОГДА ТаблицаЗаказа.СтатусУказанияСерий = 10
	|					ТОГДА ГрафикОтгрузкиТекущийЗаказ.СтатусУказанияСерий = 10
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|			И (ТаблицаЗаказа.ПринятКОбеспечению
	|					И ГрафикОтгрузкиТекущийЗаказ.ПринятКОбеспечению
	|					И (ТаблицаЗаказа.ДатаОтгрузки > ГрафикОтгрузкиТекущийЗаказ.ДатаОтгрузки
	|						ИЛИ ТаблицаЗаказа.ДатаОтгрузки = ГрафикОтгрузкиТекущийЗаказ.ДатаОтгрузки
	|							И ТаблицаЗаказа.НомерСтрокиПоПорядку > ГрафикОтгрузкиТекущийЗаказ.НомерСтрокиПоПорядку)
	|				ИЛИ НЕ ТаблицаЗаказа.ПринятКОбеспечению
	|					И ГрафикОтгрузкиТекущийЗаказ.ПринятКОбеспечению
	|				ИЛИ НЕ ТаблицаЗаказа.ПринятКОбеспечению
	|					И НЕ ГрафикОтгрузкиТекущийЗаказ.ПринятКОбеспечению
	|					И ТаблицаЗаказа.ДатаОтгрузки >= ГрафикОтгрузкиТекущийЗаказ.ДатаОтгрузки
	|					И ТаблицаЗаказа.НомерСтрокиПоПорядку > ГрафикОтгрузкиТекущийЗаказ.НомерСтрокиПоПорядку)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаХарактеристика
	|		ПО ТаблицаЗаказа.Склад = НастройкаХарактеристика.Склад
	|			И ТаблицаЗаказа.Номенклатура = НастройкаХарактеристика.Номенклатура
	|			И ТаблицаЗаказа.Характеристика = НастройкаХарактеристика.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаНоменклатура
	|		ПО ТаблицаЗаказа.Склад = НастройкаНоменклатура.Склад
	|			И ТаблицаЗаказа.Номенклатура = НастройкаНоменклатура.Номенклатура
	|			И (НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И (НастройкаХарактеристика.Склад ЕСТЬ NULL )
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаСклад
	|		ПО ТаблицаЗаказа.Склад = НастройкаСклад.Склад
	|			И (НастройкаСклад.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|			И (НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И (НастройкаХарактеристика.Склад ЕСТЬ NULL )
	|			И (НастройкаНоменклатура.Склад ЕСТЬ NULL )
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказа.Заказ,
	|	ТаблицаЗаказа.НомерСтрокиПоПорядку
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗаказа.Заказ КАК Заказ,
	|	ТаблицаЗаказа.НомерСтрокиПоПорядку КАК НомерСтрокиПоПорядку,
	|	МАКСИМУМ(ТаблицаЗаказа.Склад) КАК Склад,
	|	МАКСИМУМ(ТаблицаЗаказа.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(ТаблицаЗаказа.Характеристика) КАК Характеристика,
	|	МАКСИМУМ(ТаблицаЗаказа.УпаковкаКоэффициент) КАК УпаковкаКоэффициент,
	|	МАКСИМУМ(ТаблицаЗаказа.ДатаОтгрузки) КАК ДатаОтгрузки,
	|	МАКСИМУМ(ТаблицаЗаказа.Количество) КАК Количество,
	|	МАКСИМУМ(ТаблицаЗаказа.КоличествоПредыдущиеСтрокиЗаказа) КАК КоличествоПредыдущиеСтрокиЗаказа,
	|	СУММА(ЕСТЬNULL(ГрафикОтгрузкиПредыдущиеЗаказы.Количество, 0)) КАК КоличествоПредыдущиеЗаказы,
	|	МАКСИМУМ(ТаблицаЗаказа.ПринятКОбеспечению) КАК ПринятКОбеспечению,
	|	МАКСИМУМ(ТаблицаЗаказа.СрокПоставки) КАК СрокПоставки,
	|	МАКСИМУМ(ТаблицаЗаказа.УказыватьСерии) КАК УказыватьСерии,
	|	МАКСИМУМ(ТаблицаЗаказа.СтатусУказанияСерий) КАК СтатусУказанияСерий,
	|	МАКСИМУМ(ТаблицаЗаказа.ВариантКонтроля) КАК ВариантКонтроля,
	|	МАКСИМУМ(ТаблицаЗаказа.ГраницаГрафикаДоступностиСрокПоставки) КАК ГраницаГрафикаДоступностиСрокПоставки,
	|	МАКСИМУМ(ТаблицаЗаказа.ГраницаГрафикаДоступности) КАК ГраницаГрафикаДоступности
	|ПОМЕСТИТЬ ГрафикОтгрузкиПоЗаказу
	|ИЗ
	|	ГрафикОтгрузкиПоЗаказуБезПредыдущихЗаказов КАК ТаблицаЗаказа
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ГрафикДвиженияТоваров КАК ГрафикОтгрузкиПредыдущиеЗаказы
	|		ПО (ГрафикОтгрузкиПредыдущиеЗаказы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|			И ТаблицаЗаказа.Склад = ГрафикОтгрузкиПредыдущиеЗаказы.Склад
	|			И ТаблицаЗаказа.Номенклатура = ГрафикОтгрузкиПредыдущиеЗаказы.Номенклатура
	|			И ТаблицаЗаказа.Характеристика = ГрафикОтгрузкиПредыдущиеЗаказы.Характеристика
	|			И (ТаблицаЗаказа.СтатусУказанияСерий <> 10)
	|			И (ГрафикОтгрузкиПредыдущиеЗаказы.Регистратор <> ТаблицаЗаказа.Заказ)
	|			И (ТаблицаЗаказа.ДатаОтгрузки > ГрафикОтгрузкиПредыдущиеЗаказы.ДатаСобытия
	|				ИЛИ ТаблицаЗаказа.ДатаОтгрузки = ГрафикОтгрузкиПредыдущиеЗаказы.ДатаСобытия
	|					И ТаблицаЗаказа.Заказ.Дата > ГрафикОтгрузкиПредыдущиеЗаказы.Регистратор.Дата
	|				ИЛИ ТаблицаЗаказа.ДатаОтгрузки = ГрафикОтгрузкиПредыдущиеЗаказы.ДатаСобытия
	|					И ТаблицаЗаказа.Заказ.Дата = ГрафикОтгрузкиПредыдущиеЗаказы.Регистратор.Дата
	|					И ТаблицаЗаказа.Заказ > ГрафикОтгрузкиПредыдущиеЗаказы.Регистратор
	|				ИЛИ НЕ ТаблицаЗаказа.ПринятКОбеспечению)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказа.Заказ,
	|	ТаблицаЗаказа.НомерСтрокиПоПорядку
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикПоступления.Склад КАК Склад,
	|	ГрафикПоступления.Номенклатура КАК Номенклатура,
	|	ГрафикПоступления.Характеристика КАК Характеристика,
	|	ГрафикПоступления.ДатаСобытия КАК ДатаСобытия,
	|	ГрафикПоступления.Количество КАК Количество
	|ПОМЕСТИТЬ ВТГрафикПоступления
	|ИЗ
	|	РегистрНакопления.ГрафикДвиженияТоваров КАК ГрафикПоступления
	|ГДЕ
	|	(ГрафикПоступления.Склад, ГрафикПоступления.Номенклатура, ГрафикПоступления.Характеристика) В
	|			(ВЫБРАТЬ
	|				ГрафикОтгрузкиПоЗаказу.Склад КАК Склад,
	|				ГрафикОтгрузкиПоЗаказу.Номенклатура КАК Номенклатура,
	|				ГрафикОтгрузкиПоЗаказу.Характеристика КАК Характеристика
	|			ИЗ
	|				ГрафикОтгрузкиПоЗаказу)
	|	И ГрафикПоступления.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикПоступления.Склад КАК Склад,
	|	ГрафикПоступления.Номенклатура КАК Номенклатура,
	|	ГрафикПоступления.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ГрафикПоступления.ДатаСобытия < &НачалоТекущегоДня
	|			ТОГДА &НачалоТекущегоДня
	|		ИНАЧЕ ГрафикПоступления.ДатаСобытия
	|	КОНЕЦ КАК ДатаСобытия,
	|	СУММА(ГрафикПоступленияПредыдущиеДни.Количество) КАК КоличествоВсего,
	|	СУММА(ВЫБОР
	|			КОГДА ГрафикПоступления.ДатаСобытия >= ГрафикПоступленияПредыдущиеДни.ДатаСобытия
	|				ТОГДА ГрафикПоступленияПредыдущиеДни.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоДатаСобытия,
	|	СУММА(ВЫБОР
	|			КОГДА ГрафикПоступления.ДатаСобытия > ГрафикПоступленияПредыдущиеДни.ДатаСобытия
	|				ТОГДА ГрафикПоступленияПредыдущиеДни.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоПредыдущиеДни
	|ПОМЕСТИТЬ ГрафикПоступления
	|ИЗ
	|	ВТГрафикПоступления КАК ГрафикПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГрафикПоступления КАК ГрафикПоступленияПредыдущиеДни
	|		ПО ГрафикПоступления.Склад = ГрафикПоступленияПредыдущиеДни.Склад
	|			И ГрафикПоступления.Номенклатура = ГрафикПоступленияПредыдущиеДни.Номенклатура
	|			И ГрафикПоступления.Характеристика = ГрафикПоступленияПредыдущиеДни.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикПоступления.Склад,
	|	ГрафикПоступления.Номенклатура,
	|	ГрафикПоступления.Характеристика,
	|	ГрафикПоступления.ДатаСобытия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОтгрузкиПоЗаказу.Заказ КАК Заказ,
	|	ГрафикОтгрузкиПоЗаказу.НомерСтрокиПоПорядку КАК НомерСтрокиПоПорядку,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.Заказ.Дата) КАК Дата,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.Заказ.Номер) КАК Номер,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.Заказ.Статус) КАК Статус,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.Склад) КАК Склад,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.Заказ.Менеджер) КАК Менеджер,
	|	ТИПЗНАЧЕНИЯ(ГрафикОтгрузкиПоЗаказу.Заказ) КАК ТипДокумента,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.Характеристика) КАК Характеристика,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.УпаковкаКоэффициент) КАК УпаковкаКоэффициент,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.ДатаОтгрузки) КАК ДатаОтгрузки,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.Количество) КАК Количество,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.СтатусУказанияСерий) = 10
	|			ТОГДА МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.Количество)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.ПринятКОбеспечению) = ЛОЖЬ
	|						И МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.ВариантКонтроля) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	|					ТОГДА ВЫБОР
	|							КОГДА МАКСИМУМ(ЕСТЬNULL(СвободныеОстатки.ВНаличииОстаток, 0)) - МАКСИМУМ(ЕСТЬNULL(График.Количество, 0)) > МАКСИМУМ(ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеСтрокиЗаказа, 0))
	|								ТОГДА МАКСИМУМ(ЕСТЬNULL(СвободныеОстатки.ВНаличииОстаток, 0)) - МАКСИМУМ(ЕСТЬNULL(График.Количество, 0)) - МАКСИМУМ(ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеСтрокиЗаказа, 0))
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА МАКСИМУМ(ЕСТЬNULL(СвободныеОстатки.ВНаличииОстаток, 0)) > МАКСИМУМ(ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеЗаказы, 0))
	|							ТОГДА МАКСИМУМ(ЕСТЬNULL(СвободныеОстатки.ВНаличииОстаток, 0)) - МАКСИМУМ(ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеЗаказы, 0))
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК ОбеспеченоНаличием,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(ГрафикПоступления.КоличествоДатаСобытия, 0)) > 0
	|			ТОГДА ВЫБОР
	|					КОГДА МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.ПринятКОбеспечению) = ЛОЖЬ
	|							И МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.ВариантКонтроля) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	|						ТОГДА МАКСИМУМ(ЕСТЬNULL(ГрафикПоступления.КоличествоДатаСобытия, 0)) + МАКСИМУМ(ЕСТЬNULL(График.Количество, 0)) - МАКСИМУМ(ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеЗаказы, 0)) - МАКСИМУМ(ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеСтрокиЗаказа, 0))
	|					ИНАЧЕ МАКСИМУМ(ЕСТЬNULL(ГрафикПоступления.КоличествоДатаСобытия, 0)) + МАКСИМУМ(ЕСТЬNULL(График.Количество, 0)) - МАКСИМУМ(ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеЗаказы, 0))
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОбеспеченоПоступлением,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ВЫРАЗИТЬ(ГрафикОтгрузкиПоЗаказу.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры) = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоУслуга,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.ПринятКОбеспечению) КАК ПринятКОбеспечению,
	|	ВЫБОР
	|		КОГДА ГрафикОтгрузкиПоЗаказу.Заказ = &Заказ
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТекущийЗаказ,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.УказыватьСерии) КАК УказыватьСерии,
	|	МАКСИМУМ(ГрафикОтгрузкиПоЗаказу.СтатусУказанияСерий) КАК СтатусУказанияСерий
	|ИЗ
	|	ГрафикОтгрузкиПоЗаказу КАК ГрафикОтгрузкиПоЗаказу
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки(
	|				,
	|				(Склад, Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ
	|						Товары.Склад КАК Склад,
	|						Товары.Номенклатура КАК Номенклатура,
	|						Товары.Характеристика КАК Характеристика
	|					ИЗ
	|						Товары КАК Товары
	|					ГДЕ
	|						Товары.НомерСтрокиПоПорядку = &НомерСтрокиПоПорядку)) КАК СвободныеОстатки
	|		ПО ГрафикОтгрузкиПоЗаказу.Склад = СвободныеОстатки.Склад
	|			И ГрафикОтгрузкиПоЗаказу.Номенклатура = СвободныеОстатки.Номенклатура
	|			И ГрафикОтгрузкиПоЗаказу.Характеристика = СвободныеОстатки.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаХарактеристика
	|		ПО (СвободныеОстатки.Склад = НастройкаХарактеристика.Склад)
	|			И (СвободныеОстатки.Номенклатура = НастройкаХарактеристика.Номенклатура)
	|			И (СвободныеОстатки.Характеристика = НастройкаХарактеристика.Характеристика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаНоменклатура
	|		ПО (СвободныеОстатки.Склад = НастройкаНоменклатура.Склад)
	|			И (СвободныеОстатки.Номенклатура = НастройкаНоменклатура.Номенклатура)
	|			И (НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И (НастройкаХарактеристика.Склад ЕСТЬ NULL )
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаСклад
	|		ПО (СвободныеОстатки.Склад = НастройкаСклад.Склад)
	|			И (НастройкаСклад.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|			И (НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И (НастройкаХарактеристика.Склад ЕСТЬ NULL )
	|			И (НастройкаНоменклатура.Склад ЕСТЬ NULL )
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			График.Склад КАК Склад,
	|			График.Номенклатура КАК Номенклатура,
	|			График.Характеристика КАК Характеристика,
	|			ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля)) КАК ВариантКонтроля,
	|			-МИНИМУМ(График.КоличествоКонечныйОстаток) КАК Количество
	|		ИЗ
	|			РегистрНакопления.ГрафикДвиженияТоваров.ОстаткиИОбороты(
	|					КОНЕЦПЕРИОДА(&НачалоТекущегоДня, ДЕНЬ),
	|					,
	|					День,
	|					ДвиженияИГраницыПериода,
	|					(Склад, Номенклатура, Характеристика) В
	|						(ВЫБРАТЬ
	|							ГрафикОтгрузкиПоЗаказу.Склад КАК Склад,
	|							ГрафикОтгрузкиПоЗаказу.Номенклатура КАК Номенклатура,
	|							ГрафикОтгрузкиПоЗаказу.Характеристика КАК Характеристика
	|						ИЗ
	|							ГрафикОтгрузкиПоЗаказу)) КАК График
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаХарактеристика
	|				ПО График.Склад = НастройкаХарактеристика.Склад
	|					И График.Номенклатура = НастройкаХарактеристика.Номенклатура
	|					И График.Характеристика = НастройкаХарактеристика.Характеристика
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаНоменклатура
	|				ПО График.Склад = НастройкаНоменклатура.Склад
	|					И График.Номенклатура = НастройкаНоменклатура.Номенклатура
	|					И (НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|					И (НастройкаХарактеристика.Склад ЕСТЬ NULL )
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаСклад
	|				ПО График.Склад = НастройкаСклад.Склад
	|					И (НастройкаСклад.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|					И (НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|					И (НастройкаХарактеристика.Склад ЕСТЬ NULL )
	|					И (НастройкаНоменклатура.Склад ЕСТЬ NULL )
	|		ГДЕ
	|			ВЫБОР
	|					КОГДА ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля)) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	|						ТОГДА ВЫБОР
	|								КОГДА НЕ НастройкаХарактеристика.ВариантКонтроля ЕСТЬ NULL 
	|									ТОГДА ВЫБОР
	|											КОГДА НастройкаХарактеристика.ГраницаГрафикаДоступности >= &НачалоТекущегоДня
	|												ТОГДА График.Период <= НастройкаХарактеристика.ГраницаГрафикаДоступности
	|											КОГДА НастройкаХарактеристика.СрокПоставки > 0
	|												ТОГДА График.Период <= ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, НастройкаХарактеристика.СрокПоставки - 1)
	|											ИНАЧЕ ЛОЖЬ
	|										КОНЕЦ
	|								КОГДА НЕ НастройкаНоменклатура.ВариантКонтроля ЕСТЬ NULL 
	|									ТОГДА ВЫБОР
	|											КОГДА НастройкаНоменклатура.ГраницаГрафикаДоступности >= &НачалоТекущегоДня
	|												ТОГДА График.Период <= НастройкаНоменклатура.ГраницаГрафикаДоступности
	|											КОГДА НастройкаНоменклатура.СрокПоставки > 0
	|												ТОГДА График.Период <= ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, НастройкаНоменклатура.СрокПоставки - 1)
	|											ИНАЧЕ ЛОЖЬ
	|										КОНЕЦ
	|								ИНАЧЕ ВЫБОР
	|										КОГДА НастройкаСклад.ГраницаГрафикаДоступности >= &НачалоТекущегоДня
	|											ТОГДА График.Период <= НастройкаСклад.ГраницаГрафикаДоступности
	|										КОГДА НастройкаСклад.СрокПоставки > 0
	|											ТОГДА График.Период <= ДОБАВИТЬКДАТЕ(&НачалоТекущегоДня, ДЕНЬ, НастройкаСклад.СрокПоставки - 1)
	|										ИНАЧЕ ЛОЖЬ
	|									КОНЕЦ
	|							КОНЕЦ
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|			И График.КоличествоКонечныйОстаток < 0
	|		
	|		СГРУППИРОВАТЬ ПО
	|			График.Номенклатура,
	|			График.Характеристика,
	|			График.Склад,
	|			ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля))) КАК График
	|		ПО (СвободныеОстатки.Склад = График.Склад)
	|			И (СвободныеОстатки.Номенклатура = График.Номенклатура)
	|			И (СвободныеОстатки.Характеристика = График.Характеристика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикПоступления КАК ГрафикПоступления
	|		ПО ГрафикОтгрузкиПоЗаказу.Склад = ГрафикПоступления.Склад
	|			И ГрафикОтгрузкиПоЗаказу.Номенклатура = ГрафикПоступления.Номенклатура
	|			И ГрафикОтгрузкиПоЗаказу.Характеристика = ГрафикПоступления.Характеристика
	|			И (ГрафикОтгрузкиПоЗаказу.ДатаОтгрузки >= ГрафикПоступления.ДатаСобытия
	|				ИЛИ НЕ ГрафикОтгрузкиПоЗаказу.ПринятКОбеспечению)
	|			И (ВЫБОР
	|				КОГДА ГрафикОтгрузкиПоЗаказу.ПринятКОбеспечению = ЛОЖЬ
	|						И ГрафикОтгрузкиПоЗаказу.ВариантКонтроля = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	|					ТОГДА ГрафикОтгрузкиПоЗаказу.Количество <= ЕСТЬNULL(ГрафикПоступления.КоличествоДатаСобытия, 0) + ЕСТЬNULL(График.Количество, 0) - ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеЗаказы, 0) - ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеСтрокиЗаказа, 0)
	|								И ГрафикОтгрузкиПоЗаказу.Количество > ЕСТЬNULL(ГрафикПоступления.КоличествоПредыдущиеДни, 0) + ЕСТЬNULL(График.Количество, 0) - ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеЗаказы, 0) - ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеСтрокиЗаказа, 0)
	|							ИЛИ ГрафикОтгрузкиПоЗаказу.Количество > ЕСТЬNULL(ГрафикПоступления.КоличествоДатаСобытия, 0) + ЕСТЬNULL(График.Количество, 0) - ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеЗаказы, 0) - ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеСтрокиЗаказа, 0)
	|								И ГрафикПоступления.КоличествоДатаСобытия = ГрафикПоступления.КоличествоВсего
	|				ИНАЧЕ ГрафикОтгрузкиПоЗаказу.Количество <= ЕСТЬNULL(ГрафикПоступления.КоличествоДатаСобытия, 0) + ЕСТЬNULL(График.Количество, 0) - ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеЗаказы, 0)
	|							И ГрафикОтгрузкиПоЗаказу.Количество > ЕСТЬNULL(ГрафикПоступления.КоличествоПредыдущиеДни, 0) + ЕСТЬNULL(График.Количество, 0) - ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеЗаказы, 0)
	|						ИЛИ ГрафикОтгрузкиПоЗаказу.Количество > ЕСТЬNULL(ГрафикПоступления.КоличествоДатаСобытия, 0) + ЕСТЬNULL(График.Количество, 0) - ЕСТЬNULL(ГрафикОтгрузкиПоЗаказу.КоличествоПредыдущиеЗаказы, 0)
	|							И ГрафикПоступления.КоличествоДатаСобытия = ГрафикПоступления.КоличествоВсего
	|			КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОтгрузкиПоЗаказу.Заказ,
	|	ГрафикОтгрузкиПоЗаказу.НомерСтрокиПоПорядку
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОтгрузки,
	|	Заказ,
	|	НомерСтрокиПоПорядку");
	
	НачалоТекущегоДня = НачалоДня(ТекущаяДатаСеанса());
	
	Запрос.УстановитьПараметр("Заказ", Параметры.Заказ);
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	Запрос.УстановитьПараметр("НомерСтрокиПоПорядку", Параметры.НомерСтрокиПоПорядку);
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоТекущегоДня);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрокаТовары = Товары.Добавить();
		НоваяСтрокаТовары.Заказ = Выборка.Заказ;
		НоваяСтрокаТовары.ДатаОтгрузки = Выборка.ДатаОтгрузки;
		НоваяСтрокаТовары.Менеджер = Выборка.Менеджер;
		НоваяСтрокаТовары.Дата = Выборка.Дата;
		НоваяСтрокаТовары.Номер = Выборка.Номер;
		НоваяСтрокаТовары.ТипДокумента = Выборка.ТипДокумента;
		НоваяСтрокаТовары.Количество = Выборка.Количество;
		НоваяСтрокаТовары.ТекущийЗаказ = Выборка.ТекущийЗаказ;
		
		Если Выборка.ЭтоУслуга Тогда
			
			НоваяСтрокаТовары.ОбеспеченоНаличием = Выборка.Количество;
			
		Иначе
			
			Если Выборка.ТекущийЗаказ Тогда
				
				НоваяСтрокаТовары.ОбеспеченоНаличием = Мин(Выборка.Количество, Выборка.ОбеспеченоНаличием) / Выборка.УпаковкаКоэффициент;
				НоваяСтрокаТовары.ОбеспеченоПоступлением = Мин(Выборка.Количество / Выборка.УпаковкаКоэффициент - НоваяСтрокаТовары.ОбеспеченоНаличием, Выборка.ОбеспеченоПоступлением / Выборка.УпаковкаКоэффициент);
				
			Иначе
				
				НоваяСтрокаТовары.ОбеспеченоПоступлением = Мин(Выборка.Количество / Выборка.УпаковкаКоэффициент, Выборка.ОбеспеченоПоступлением / Выборка.УпаковкаКоэффициент);
				НоваяСтрокаТовары.ОбеспеченоНаличием = Мин(Выборка.Количество - НоваяСтрокаТовары.ОбеспеченоПоступлением, Выборка.ОбеспеченоНаличием) / Выборка.УпаковкаКоэффициент;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
