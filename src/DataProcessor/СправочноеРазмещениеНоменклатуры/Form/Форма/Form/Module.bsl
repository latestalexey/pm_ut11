////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияПоУмолчанию();
	
	Объект.ДокументПриемки = Параметры.ДокументПриемки;
	
	Если ЗначениеЗаполнено(Объект.ДокументПриемки) Тогда
	
		ЗаполнитьПоДокументуПриемкиСервер();
		
	Иначе
		
		ТекстСообщения = НСтр("ru='Предусмотрено открытие формы только из документа приемки товаров на склад'");
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	ПроверитьМодифицированность(Отказ);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ОСНОВНЫЕ ЯЧЕЙКИ

&НаКлиенте
Процедура ОсновныеЯчейкиПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.ОсновныеЯчейки.ТекущиеДанные;
	
	
	Если ТекущиеДанные <> Неопределено Тогда
		Отбор = Новый ФиксированнаяСтруктура("Номенклатура, Склад",ТекущиеДанные.Номенклатура, ТекущиеДанные.Склад);
		Элементы.ДополнительныеЯчейки.ОтборСтрок = Отбор;
	Иначе
		Отбор = Новый ФиксированнаяСтруктура("Номенклатура, Склад",Неопределено, Неопределено);
		Элементы.ДополнительныеЯчейки.ОтборСтрок = Отбор;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеЯчейкиПриИзменении(Элемент)
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеЯчейкиОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ПараметрыПеретаскивания.Действие <> ДействиеПеретаскивания.Отмена Тогда
		ТекущиеДанные = Элементы.ОсновныеЯчейки.ТекущиеДанные;
		ОбработатьНазначениеДополнительнойЯчейки(ТекущиеДанные.Номенклатура, ТекущиеДанные.Склад, ТекущиеДанные.Ячейка);
		Отбор = Новый ФиксированнаяСтруктура("Номенклатура, Склад",ТекущиеДанные.Номенклатура,ТекущиеДанные.Склад);
		Элементы.ДополнительныеЯчейки.ОтборСтрок = Отбор;
		Элементы.ДополнительныеЯчейки.Обновить();
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеЯчейкиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	Если Элемент = ЭтаФорма.ТекущийЭлемент Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	Иначе
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Перемещение;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеЯчейкиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеЯчейкиЯчейкаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ОсновныеЯчейки.ТекущиеДанные;
	ОбработатьНазначениеОсновнойЯчейки(ТекущиеДанные.Номенклатура, ТекущиеДанные.Склад, ТекущиеДанные.Ячейка, Ложь)
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ ДОПОЛНИТЕЛЬНЫЕ ЯЧЕЙКИ

&НаКлиенте
Процедура ДополнительныеЯчейкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	СтандартнаяОбработка = Ложь;
	Если Элементы.ДополнительныеЯчейки.ОтборСтрок = Неопределено
		Или Не ЗначениеЗаполнено(Элементы.ДополнительныеЯчейки.ОтборСтрок.Номенклатура) Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЯчейкиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.ДополнительныеЯчейки.ТекущиеДанные;
		ТекущиеДанные.Номенклатура = Элементы.ДополнительныеЯчейки.ОтборСтрок.Номенклатура;
		ТекущиеДанные.Склад        = Элементы.ДополнительныеЯчейки.ОтборСтрок.Склад;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЯчейкиПриИзменении(Элемент)
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЯчейкиЯчейкаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ДополнительныеЯчейки.ТекущиеДанные;
	ОбработатьНазначениеДополнительнойЯчейки(ТекущиеДанные.Номенклатура,ТекущиеДанные.Склад,ТекущиеДанные.Ячейка)
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЯчейкиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	Если Элемент = ЭтаФорма.ТекущийЭлемент Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	Иначе
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Перемещение;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЯчейкиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЯчейкиОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	Если ПараметрыПеретаскивания.Действие <> ДействиеПеретаскивания.Отмена Тогда
		ТекущиеДанные = Элементы.ДополнительныеЯчейки.ТекущиеДанные;
		ОбработатьНазначениеОсновнойЯчейки(ТекущиеДанные.Номенклатура,ТекущиеДанные.Склад,ТекущиеДанные.Ячейка, Истина);
		Элементы.ОсновныеЯчейки.Обновить();
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СделатьЯчейкуОсновной(Команда)
	ТекущиеДанные = Элементы.ДополнительныеЯчейки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОбработатьНазначениеОсновнойЯчейки(ТекущиеДанные.Номенклатура,ТекущиеДанные.Склад, ТекущиеДанные.Ячейка, Истина);
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВРегистр(Команда)
	ЭтаФорма.Модифицированность = ЗаписатьВРегистрСервер();
КонецПроцедуры

&НаКлиенте
Процедура СделатьЯчейкуДополнительной(Команда)
	ТекущиеДанные = Элементы.ОсновныеЯчейки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОбработатьНазначениеДополнительнойЯчейки(ТекущиеДанные.Номенклатура,ТекущиеДанные.Склад, ТекущиеДанные.Ячейка);
		Отбор = Новый ФиксированнаяСтруктура("Номенклатура,Склад",ТекущиеДанные.Номенклатура,ТекущиеДанные.Склад);
		Элементы.ДополнительныеЯчейки.ОтборСтрок = Отбор;
		Элементы.ДополнительныеЯчейки.Обновить();
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЗаданияНаОтборРазмещениеТоваров(Команда)
	Отказ = Ложь;
	ПроверитьМодифицированность(Отказ);
	Если НЕ Отказ Тогда
		Массив = Новый Массив;
		Массив.Добавить(Объект.ДокументПриемки);
		
		ПараметрыПечати = Новый Структура;
		
		Если ТипЗнч(Объект.ДокументПриемки) = Тип("ДокументСсылка.ПересчетТоваров") Тогда
			ПараметрыПечати.Вставить("ИмяФормы", "ЗаданиеНаПересчет");
		Иначе
			ПараметрыПечати.Вставить("ИмяФормы", "ЗаданиеНаРазмещение");
		КонецЕсли;			
			ПараметрыПечати.Вставить("БезДопКолонки");
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Обработка.ПечатьЗаданияНаОтборРазмещениеТоваров","ЗаданиеНаОтборРазмещениеТовара",Массив,ЭтаФорма,ПараметрыПечати);
		
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Заполнение и инициализация

&НаСервере
Процедура ЗаполнитьЗначенияПоУмолчанию()
	
	Объект.Склад = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Объект.Склад);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ЗаполнитьПоДокументуПриемкиСервер()
	Объект.ОсновныеЯчейки.Очистить();
	Объект.ДополнительныеЯчейки.Очистить();
	
	Если Не ЗначениеЗаполнено(Объект.ДокументПриемки) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОбъекта = ТипЗнч(Объект.ДокументПриемки);
	
	ОрдерностьПроверена     = Ложь;
	УказаниеЯчекекПроверено = Ложь;
	ПоказыватьКолонкуСклад  = Ложь;
	
	Если ТипОбъекта = Тип("ДокументСсылка.ПриходныйОрдерНаТовары") Тогда
		
		РеквизитыДокументаПриемки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДокументПриемки, "Склад,Помещение");
		Объект.Склад     = РеквизитыДокументаПриемки.Склад;
		Объект.Помещение = РеквизитыДокументаПриемки.Помещение;
		ОрдерностьПроверена = Истина;
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ОрдерНаПеремещениеТоваров") Тогда
		
		РеквизитыДокументаПриемки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДокументПриемки, "Склад,ПомещениеПолучатель");
		Объект.Склад     = РеквизитыДокументаПриемки.Склад;
		Объект.Помещение = РеквизитыДокументаПриемки.ПомещениеПолучатель;
		ОрдерностьПроверена = Истина;
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ОрдерНаОтражениеРезультатовПересчетовТоваров") Тогда
		
		РеквизитыДокументаПриемки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДокументПриемки, "Склад,Помещение");
		Объект.Склад     = РеквизитыДокументаПриемки.Склад;
		Объект.Помещение = РеквизитыДокументаПриемки.Помещение;
		ОрдерностьПроверена = Истина;
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ОрдерНаОтражениеИзлишковТоваров") Тогда
		
		РеквизитыДокументаПриемки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДокументПриемки, "Склад,Помещение");
		Объект.Склад     = РеквизитыДокументаПриемки.Склад;
		Объект.Помещение = РеквизитыДокументаПриемки.Помещение;
		ОрдерностьПроверена = Истина;
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ПересчетТоваров") Тогда
		
		РеквизитыДокументаПриемки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДокументПриемки, "Склад,Помещение");
		Объект.Склад     = РеквизитыДокументаПриемки.Склад;
		Объект.Помещение = РеквизитыДокументаПриемки.Помещение;
		ОрдерностьПроверена = Истина;
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров") Тогда
		
		Объект.Склад     = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументПриемки, "Склад");
		
		СтруктураПараметров = Новый Структура("Склад",Объект.Склад);
		ПредставлениеСклада = СкладыСервер.ПолучитьПредставлениеСклада(Объект.Склад);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач", СтруктураПараметров) Тогда
			
			ТекстСообщения = НСтр("ru='На складе ""%Склад%"" используется ордерная схема при отражении излишков, поэтому обработку необходимо использовать для ордера на отражение излишков товаров.'");	
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Склад%",ПредставлениеСклада);
			
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
		ОрдерностьПроверена = Истина;
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ВводОстатков") Тогда
		
		РеквизитыВводаОстатков = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДокументПриемки, "Склад,Помещение");
		Объект.Склад     = РеквизитыВводаОстатков.Склад;
		Объект.Помещение = РеквизитыВводаОстатков.Помещение;
		ОрдерностьПроверена = Истина;
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.СборкаТоваров") Тогда
		
		Объект.Склад     = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументПриемки, "Склад");
		ОрдерностьПроверена = Ложь;
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		Объект.Склад     = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументПриемки, "Склад");
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МАКСИМУМ(ПоступлениеТоваровУслугТовары.Склад.ИспользоватьАдресноеХранениеСправочно) КАК СкладИспользоватьАдресноеХранениеСправочно,
		|	МАКСИМУМ(ПоступлениеТоваровУслугТовары.Склад.ИспользоватьОрдернуюСхемуПриПоступлении) КАК СкладИспользоватьОрдернуюСхемуПриПоступлении,
		|	ПоступлениеТоваровУслугТовары.Склад
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|ГДЕ
		|	ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровУслугТовары.Склад
		|
		|ИМЕЮЩИЕ
		|	МАКСИМУМ(ПоступлениеТоваровУслугТовары.Склад.ИспользоватьАдресноеХранениеСправочно) = ИСТИНА
		|		И МАКСИМУМ(ПоступлениеТоваровУслугТовары.Склад.ИспользоватьОрдернуюСхемуПриПоступлении) = ЛОЖЬ";
		Запрос.УстановитьПараметр("Ссылка", Объект.ДокументПриемки);
		
		Если Запрос.Выполнить().Пустой() Тогда
			ТекстСообщения = НСтр("ru='В документе нет складов, которые не используют ордерную схему при поступлении, но используют справочное деление на складские ячейки.'");	
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;	
		
		ОрдерностьПроверена = Истина;
		УказаниеЯчекекПроверено = Истина;
		ПоказыватьКолонкуСклад = Истина;
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ПрочееОприходованиеТоваров") Тогда
		
		Объект.Склад     = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументПриемки, "Склад");
		ОрдерностьПроверена = Ложь;
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		
		Объект.Склад     = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументПриемки, "СкладПолучатель");
		ОрдерностьПроверена = Ложь;
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		
		Объект.Склад     = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументПриемки, "Склад");
		ОрдерностьПроверена = Ложь;
		
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("Склад, Помещение",Объект.Склад,Объект.Помещение);
	ПредставлениеСклада = СкладыСервер.ПолучитьПредставлениеСклада(Объект.Склад,Объект.Помещение);
	
	Если Не ОрдерностьПроверена
		И ПолучитьФункциональнуюОпцию("ИспользоватьОрдернуюСхемуПриПоступлении", СтруктураПараметров) Тогда
			
			ТекстСообщения = НСтр("ru='На складе ""%Склад%"" используется ордерная схема при поступлении, поэтому обработку необходимо использовать для приходного ордера.'");	
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Склад%",ПредставлениеСклада);
			
			ВызватьИсключение ТекстСообщения;
	КонецЕсли;		
	
	Если Не УказаниеЯчекекПроверено Тогда
		
		Если Не СкладыСервер.ИспользоватьАдресноеХранениеСправочно(Объект.Склад, Объект.Помещение) Тогда
			ТекстСообщения = НСтр("ru='Для склада ""%Склад%"" не используется справочное деление на складские ячейки.'");	
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Склад%",ПредставлениеСклада);
			ВызватьИсключение ТекстСообщения;
			
		КонецЕсли;
		
	КонецЕсли;
 
	Если ТипОбъекта = Тип("ДокументСсылка.СборкаТоваров")
		И Объект.ДокументПриемки.ТипОперации = Перечисления.ТипыОперацийЗаказаНаСборку.СборкаИзКомплектующих Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	1 КАК НомерСтрокиДокумента,
		|	СборкаТоваров.Номенклатура КАК Номенклатура,
		|	&Склад КАК Склад,
		|	РазмещениеНоменклатурыПоСкладскимЯчейкам.Ячейка КАК Ячейка,
		|	ЕСТЬNULL(РазмещениеНоменклатурыПоСкладскимЯчейкам.ОсновнаяЯчейка, ИСТИНА) КАК ОсновнаяЯчейка,
		|	СборкаТоваров.Характеристика КАК Характеристика
		|ИЗ
		|	Документ.СборкаТоваров КАК СборкаТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам КАК РазмещениеНоменклатурыПоСкладскимЯчейкам
		|		ПО СборкаТоваров.Номенклатура = РазмещениеНоменклатурыПоСкладскимЯчейкам.Номенклатура
		|			И (РазмещениеНоменклатурыПоСкладскимЯчейкам.Склад = &Склад)
		|			И (РазмещениеНоменклатурыПоСкладскимЯчейкам.Помещение = &Помещение)
		|ГДЕ
		|	СборкаТоваров.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОсновнаяЯчейка УБЫВ";
		Запрос.УстановитьПараметр("Склад", Объект.Склад);
		Запрос.УстановитьПараметр("Помещение", Объект.Помещение);
		Запрос.УстановитьПараметр("Ссылка", Объект.ДокументПриемки);
	ИначеЕсли  ТипОбъекта = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")  Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументТовары.НомерСтроки КАК НомерСтрокиДокумента,
		|	ДокументТовары.Склад КАК Склад,
		|	ДокументТовары.Номенклатура КАК Номенклатура,
		|	ДокументТовары.Характеристика КАК Характеристика,
		|	РазмещениеНоменклатурыПоСкладскимЯчейкам.Ячейка КАК Ячейка,
		|	ЕСТЬNULL(РазмещениеНоменклатурыПоСкладскимЯчейкам.ОсновнаяЯчейка, ИСТИНА) КАК ОсновнаяЯчейка
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ДокументТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам КАК РазмещениеНоменклатурыПоСкладскимЯчейкам
		|		ПО ДокументТовары.Номенклатура = РазмещениеНоменклатурыПоСкладскимЯчейкам.Номенклатура
		|			И (РазмещениеНоменклатурыПоСкладскимЯчейкам.Склад = ДокументТовары.Склад)
		|ГДЕ
		|	ДокументТовары.Ссылка = &Ссылка
		|	И ДокументТовары.Номенклатура.ТипНоменклатуры В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|	И ДокументТовары.Склад.ИспользоватьАдресноеХранениеСправочно
		|	И (НЕ ДокументТовары.Склад.ИспользоватьОрдернуюСхемуПриПоступлении)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументТовары.НомерСтроки,
		|	ОсновнаяЯчейка УБЫВ";
		Запрос.УстановитьПараметр("Ссылка", Объект.ДокументПриемки);
	Иначе	
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументТовары.НомерСтроки КАК НомерСтрокиДокумента,
		|	&Склад КАК Склад,
		|	ДокументТовары.Номенклатура КАК Номенклатура,
		|	ДокументТовары.Характеристика КАК Характеристика,
		|	РазмещениеНоменклатурыПоСкладскимЯчейкам.Ячейка КАК Ячейка,
		|	ЕСТЬNULL(РазмещениеНоменклатурыПоСкладскимЯчейкам.ОсновнаяЯчейка, ИСТИНА) КАК ОсновнаяЯчейка
		|ИЗ
		|	" + Объект.ДокументПриемки.Метаданные().ПолноеИмя() + ".Товары КАК ДокументТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам КАК РазмещениеНоменклатурыПоСкладскимЯчейкам
		|		ПО ДокументТовары.Номенклатура = РазмещениеНоменклатурыПоСкладскимЯчейкам.Номенклатура
		|			И (РазмещениеНоменклатурыПоСкладскимЯчейкам.Склад = &Склад)
		|			И (РазмещениеНоменклатурыПоСкладскимЯчейкам.Помещение = &Помещение)
		|ГДЕ
		|	ДокументТовары.Ссылка = &Ссылка
		|	И ДокументТовары.Номенклатура.ТипНоменклатуры В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументТовары.НомерСтроки,
		|	ОсновнаяЯчейка УБЫВ";
		
		Запрос.УстановитьПараметр("Склад", Объект.Склад);
		Запрос.УстановитьПараметр("Помещение", Объект.Помещение);
		Запрос.УстановитьПараметр("Ссылка", Объект.ДокументПриемки);
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбработанныйНомерСтроки = 0;
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ОсновнаяЯчейка
			Или ОбработанныйНомерСтроки <> Выборка.НомерСтрокиДокумента Тогда
			// Если основная ячейка, или основная ячейка не назначена
			НоваяСтрока = Объект.ОсновныеЯчейки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Если Не Выборка.ОсновнаяЯчейка Тогда
				НоваяСтрока.Ячейка = Справочники.СкладскиеЯчейки.ПустаяСсылка();
				СтруктураПоиска = Новый Структура;
				СтруктураПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
				СтруктураПоиска.Вставить("Ячейка", Выборка.Ячейка);
				СтруктураПоиска.Вставить("Склад", Выборка.Склад);
			
				Если Объект.ДополнительныеЯчейки.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
					ЗаполнитьЗначенияСвойств(Объект.ДополнительныеЯчейки.Добавить(), Выборка);
				КонецЕсли;
			КонецЕсли;
			ОбработанныйНомерСтроки = Выборка.НомерСтрокиДокумента;
		Иначе
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
			СтруктураПоиска.Вставить("Ячейка", Выборка.Ячейка);
			СтруктураПоиска.Вставить("Склад", Выборка.Склад);
			
			Если Объект.ДополнительныеЯчейки.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(Объект.ДополнительныеЯчейки.Добавить(), Выборка);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Склад, "ЭтоГруппа") Тогда
	    Элементы.Помещение.Видимость           = Ложь;
		Элементы.ОсновныеЯчейкиСклад.Видимость = Истина;
	Иначе
		Элементы.Помещение.Видимость           = СкладыСервер.ИспользоватьСкладскиеПомещения(Объект.Склад);
		Элементы.ОсновныеЯчейкиСклад.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьВРегистрСервер()
	//Минимизируем количество наборов регистра, которые надо записывать
	//за счет того, что все ячейки по одной номенклатуре будем писать разом
	
	ТаблицаЯчеек = Объект.ОсновныеЯчейки.Выгрузить();
	ТаблицаЯчеек.Колонки.Добавить("ОсновнаяЯчейка", Новый ОписаниеТипов("Булево"));
	ТаблицаЯчеек.ЗаполнитьЗначения(Истина,"ОсновнаяЯчейка"); 
	
	Для каждого СтрТабл из Объект.ДополнительныеЯчейки Цикл
		
		НоваяСтрока = ТаблицаЯчеек.Добавить(); 
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрТабл);
		НоваяСтрока.ОсновнаяЯчейка = Ложь;
		
	КонецЦикла;
	
	ТаблицаЯчеек.Свернуть("Номенклатура,Склад,Ячейка, ОсновнаяЯчейка");
	ТаблицаЯчеек.Сортировать("Номенклатура");	
	
	ЕстьОшибки = Ложь;
	
	ТекНоменклатура = Неопределено;
	
	Набор = РегистрыСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам.СоздатьНаборЗаписей();
	
	Для Каждого СтрТабл из ТаблицаЯчеек Цикл
		
		Если ТекНоменклатура <> СтрТабл.Номенклатура Тогда
			
			Если ЗначениеЗаполнено(Набор.Отбор.Номенклатура.Значение) Тогда
				Попытка
					Набор.Записать(Истина);
				Исключение
					ЕстьОшибки = Истина;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке().Описание);
				КонецПопытки;
			КонецЕсли;
			
			Набор = РегистрыСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам.СоздатьНаборЗаписей();
			Набор.Отбор.Склад.Установить(СтрТабл.Склад);
			Набор.Отбор.Помещение.Установить(Объект.Помещение);
			Набор.Отбор.Номенклатура.Установить(СтрТабл.Номенклатура);
			
			ТекНоменклатура = СтрТабл.Номенклатура;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрТабл.Ячейка) Тогда
			
			НоваяСтрока = Набор.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрТабл);
			НоваяСтрока.Склад = СтрТабл.Склад;
			НоваяСтрока.Помещение = Объект.Помещение;
			
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Набор.Отбор.Номенклатура.Значение) Тогда
		Попытка
			Набор.Записать(Истина);
		Исключение
			ЕстьОшибки = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке().Описание);
		КонецПопытки;
	КонецЕсли;
	
	
	Возврат ЕстьОшибки; 
КонецФункции

&НаКлиенте
Процедура ОбработатьНазначениеОсновнойЯчейки(Номенклатура, Склад,Ячейка, ДобавлятьВДополнительные)
	
	Если ЗначениеЗаполнено(Ячейка) Тогда
		
		ДопЯчейки = Объект.ДополнительныеЯчейки.НайтиСтроки(Новый Структура("Номенклатура,Склад, Ячейка",Номенклатура, Склад, Ячейка));
		
		Для Каждого СтрМас из ДопЯчейки Цикл
			Объект.ДополнительныеЯчейки.Удалить(СтрМас);
		КонецЦикла;
		
		ОснЯчейки =  Объект.ОсновныеЯчейки.НайтиСтроки(Новый Структура("Номенклатура, Склад",Номенклатура, Склад));
		
		Для Каждого СтрМас из ОснЯчейки Цикл
			Если ДобавлятьВДополнительные
				И ЗначениеЗаполнено(СтрМас.Ячейка) Тогда
				СтруктураПоиска = Новый Структура;
				СтруктураПоиска.Вставить("Номенклатура", СтрМас.Номенклатура);
				СтруктураПоиска.Вставить("Склад", СтрМас.Склад);
				СтруктураПоиска.Вставить("Ячейка", СтрМас.Ячейка);
				
				Если Объект.ДополнительныеЯчейки.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
					ЗаполнитьЗначенияСвойств(Объект.ДополнительныеЯчейки.Добавить(), СтрМас);
				КонецЕсли;
			КонецЕсли;
			СтрМас.Ячейка = Ячейка;	
		КонецЦикла;
		Отбор = Новый ФиксированнаяСтруктура("Номенклатура, Склад",Номенклатура, Склад);
		Элементы.ДополнительныеЯчейки.ОтборСтрок = Отбор;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНазначениеДополнительнойЯчейки(Номенклатура,Склад, Ячейка)
	
	Если ЗначениеЗаполнено(Ячейка) Тогда
		
		ДопЯчейки = Объект.ДополнительныеЯчейки.НайтиСтроки(Новый Структура("Номенклатура,Склад, Ячейка",Номенклатура, Склад, Ячейка));
		
		Если ДопЯчейки.Количество() > 1 Тогда
			
			ПерваяСтрока = Истина;
			
			Для Каждого СтрМас из ДопЯчейки Цикл
				Если ПерваяСтрока Тогда
					ПерваяСтрока = Ложь;
				Иначе
					Объект.ДополнительныеЯчейки.Удалить(СтрМас);
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ДопЯчейки.Количество() = 0 Тогда
			
			НоваяСтрока = Объект.ДополнительныеЯчейки.Добавить();
			НоваяСтрока.Номенклатура = Номенклатура;
			НоваяСтрока.Ячейка		 = Ячейка;
			НоваяСтрока.Склад		 = Склад;
			
		КонецЕсли;
		
		ОснЯчейки =  Объект.ОсновныеЯчейки.НайтиСтроки(Новый Структура("Номенклатура,Склад, Ячейка",Номенклатура, Склад, Ячейка));
		
		Для Каждого СтрМас из ОснЯчейки Цикл
			СтрМас.Ячейка = Неопределено;	
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьМодифицированность(Отказ)
	Если ЭтаФорма.Модифицированность Тогда
		ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
		Ответ = Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Да);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтаФорма.Модифицированность = ЗаписатьВРегистрСервер();
			
			Отказ = ЭтаФорма.Модифицированность;
			
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

