&НаСервере
Функция ПроверитьПроведенностьИСтатусПробитДокументов(знач Документы) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(Документ.Ссылка) КАК КоличествоЧеков,
		|	0 КАК КоличествоОтчетов
		|ПОМЕСТИТЬ Документы
		|ИЗ
		|	Документ.ЧекККМВозврат КАК Документ
		|ГДЕ
		|	Документ.Ссылка В(&МассивДокументов)
		|	И НЕ Документ.Проведен
		|	И НЕ Документ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	КОЛИЧЕСТВО(Документ.Ссылка)
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах КАК Документ
		|ГДЕ
		|	Документ.Ссылка В(&МассивДокументов)
		|	И НЕ Документ.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(Документы.КоличествоЧеков)   КАК КоличествоЧеков,
		|	СУММА(Документы.КоличествоОтчетов) КАК КоличествоОтчетов
		|ИЗ
		|	Документы КАК Документы");
		
	Запрос.УстановитьПараметр("МассивДокументов", Документы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Выбрать();
	ВозвращаемоеЗначение = Новый Структура("КоличествоЧеков, КоличествоОтчетов", 0, 0);
	Если Выборка.Следующий() Тогда
		ВозвращаемоеЗначение.КоличествоЧеков = Выборка.КоличествоЧеков;
		ВозвращаемоеЗначение.КоличествоОтчетов = Выборка.КоличествоОтчетов;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

&НаКлиенте
Функция ПроверитьЧекиПробиты(ДокументыМассив, ФормаИсточник = Неопределено) Экспорт
	
	ОчиститьСообщения();
	СтруктураОшибок = ПроверитьПроведенностьИСтатусПробитДокументов(ДокументыМассив);
	
	Если СтруктураОшибок.КоличествоЧеков > 0 Тогда
		
		Если СтруктураОшибок.КоличествоЧеков = 1 Тогда
			ТекстПредупреждения = НСтр("ru = 'Для того чтобы распечатать КМ3, необходимо предварительно пробить чек ККМ на возврат.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Для того чтобы распечатать КМ3, необходимо предварительно пробить чеки ККМ на возврат.'");
		КонецЕсли;
		
		Предупреждение(ТекстПредупреждения, 30);
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Если СтруктураОшибок.КоличествоОтчетов > 0 Тогда
		
		Если СтруктураОшибок.КоличествоОтчетов = 1 Тогда
			ТекстПредупреждения = НСтр("ru = 'Для того чтобы распечатать КМ3, необходимо предварительно провести отчет о розничных продажах.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Для того чтобы распечатать КМ3, необходимо предварительно провести отчеты о розничных продажах.'");
		КонецЕсли;
		
		Предупреждение(ТекстПредупреждения, 30);
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПроверитьЧекиПробиты(ПараметрКоманды, ПараметрыВыполненияКоманды.Источник) Тогда
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Обработка.ПечатьКМ3",
			"КМ3",
			ПараметрКоманды,
			ПараметрыВыполненияКоманды.Источник,
			Неопределено
		);
	КонецЕсли;
	
КонецПроцедуры
