
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьНачальныеЗначенияРеквизитов();
	
	УстановитьВидимостьДоступностьЗависимыхЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ТребуетсяОбновлениеИнтерфейса Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ИспользоватьОбменЭДПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЭлектронныеЦифровыеПодписиПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОбменЭДМеждуОрганизациямиПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьНемедленнуюОтправкуЭДПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОткрытьТиповыеСоглашенияОбЭД(Команда)
	
	ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаСпискаТиповыхСоглашений", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСоглашенияОбИспользованииЭД(Команда)
	
	ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаСписка", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиОбменаЭлектроннымиДокументами(Команда)
	
	ОткрытьФорму("ОбщаяФорма.НастройкаКриптографии", , ЭтаФорма);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Обновление интерфейса

&НаКлиенте 
Процедура ПодключитьОбработчикОжиданияОбновленияИнтерфейса()
	
	ТребуетсяОбновлениеИнтерфейса = Истина;
	
	#Если НЕ ВебКлиент Тогда
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбновленияИнтерфейса", 1, Истина);
	#КонецЕсли 
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбработчикОжиданияОбновленияИнтерфейса()
	
	ОбновитьИнтерфейс();
	
	ТребуетсяОбновлениеИнтерфейса = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Изменение реквизитов

&НаСервере
Процедура ИнициализироватьНачальныеЗначенияРеквизитов()
	
	// Инициализация набора констант
	НаборКонстантОбъект = ДанныеФормыВЗначение(НаборКонстант, Тип("КонстантыНабор"));
	НаборКонстантОбъект.Прочитать();
	ЗначениеВДанныеФормы(НаборКонстантОбъект, НаборКонстант);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьЗначениеКонстанты(ИмяКонстанты)
	
	УстановитьЗначениеКонстантыПоИмени(ИмяКонстанты);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	ВыполнитьМетодыПослеУстановкиЗначенияКонстанты(ИмяКонстанты);
	
КонецПроцедуры

&НаСервере 
Процедура ВыполнитьМетодыПослеУстановкиЗначенияКонстанты(ИмяКонстанты)
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьЗначениеКонстантыПоИмени(ИмяКонстанты)
	
	Если Константы[ИмяКонстанты].Получить() <> НаборКонстант[ИмяКонстанты] Тогда
		Константы[ИмяКонстанты].Установить(НаборКонстант[ИмяКонстанты]);
	КонецЕсли;
	
	УстановитьВидимостьДоступностьЗависимыхЭлементовФормы(ИмяКонстанты);
	
	УстановитьЗначенияЗависимыхКонстант(ИмяКонстанты);
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьЗначенияЗависимыхКонстант(ИмяРодительскойКонстанты)
	
	Если ИмяРодительскойКонстанты = "ИспользоватьОбменЭД" ИЛИ ИмяРодительскойКонстанты = "ИспользоватьЭлектронныеЦифровыеПодписи" Тогда
		
		Если НЕ НаборКонстант.ИспользоватьОбменЭД ИЛИ НЕ НаборКонстант.ИспользоватьЭлектронныеЦифровыеПодписи Тогда
			НаборКонстант.ИспользоватьОбменЭДМеждуОрганизациями = Ложь;
			УстановитьЗначениеКонстантыПоИмени("ИспользоватьОбменЭДМеждуОрганизациями");
			НаборКонстант.ИспользоватьОбменЭДСБанками = Ложь;
			УстановитьЗначениеКонстантыПоИмени("ИспользоватьОбменЭДСБанками");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЗависимыхЭлементовФормы(ИмяКонстанты = Неопределено)
	
	// Настройки, выполняемые однократно, только при создании формы
	Если ИмяКонстанты = "ИспользоватьЭлектронныеЦифровыеПодписи" ИЛИ ИмяКонстанты = Неопределено Тогда
		Элементы.ГруппаКомментарийОткрытьНастройкиОбменаЭлектроннымиДокументами.Видимость = НЕ НаборКонстант.ИспользоватьЭлектронныеЦифровыеПодписи;
	КонецЕсли;
	
	// Настройки, выполняемые при создании формы и при изменении соответствующего элемента формы
	Если ИмяКонстанты = "ИспользоватьОбменЭД" ИЛИ ИмяКонстанты = "ИспользоватьЭлектронныеЦифровыеПодписи"
		ИЛИ ИмяКонстанты = Неопределено Тогда
		
		Элементы.ОткрытьТиповыеСоглашенияОбИспользованииЭД.Доступность = НаборКонстант.ИспользоватьОбменЭД;
		Элементы.ОткрытьСоглашенияОбИспользованииЭД.Доступность = НаборКонстант.ИспользоватьОбменЭД;
		Элементы.ИспользоватьОтложеннуюОтправкуЭлектронныхДокументов.Доступность = НаборКонстант.ИспользоватьОбменЭД;
		Элементы.ОткрытьНастройкиОбменаЭлектроннымиДокументами.Доступность = НаборКонстант.ИспользоватьОбменЭД
																			 И НаборКонстант.ИспользоватьЭлектронныеЦифровыеПодписи;
		Элементы.ИспользоватьОбменЭДМеждуОрганизациями.Доступность = НаборКонстант.ИспользоватьОбменЭД 
																	 И НаборКонстант.ИспользоватьЭлектронныеЦифровыеПодписи;
	
	КонецЕсли;
	
КонецПроцедуры

