
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьНачальныеЗначенияРеквизитов();
	
	УстановитьВидимостьДоступностьЗависимыхЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ТребуетсяОбновлениеИнтерфейса Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ИспользоватьПочтовыйКлиентПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьПрочиеВзаимодействияПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправлятьПисьмаВФорматеHTMLПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьПризнакРассмотреноПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЗаметкиПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьНапоминанияПользователяПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьБизнесПроцессыИЗадачиПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДатуНачалаЗадачПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДатуИВремяВСрокахЗадачПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменятьЗаданияЗаднимЧисломПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОткрытьНастройкиСистемнойУчетнойЗаписиЭП(Команда)
	
	ОткрытьФорму(
		"Справочник.УчетныеЗаписиЭлектроннойПочты.ФормаОбъекта", 
		Новый Структура(
			"Ключ",
			ПредопределенноеЗначение("Справочник.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты")),
		ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьУчетныеЗаписиЭлектроннойПочты(Команда)
	
	ОткрытьФорму("Справочник.УчетныеЗаписиЭлектроннойПочты.ФормаСписка", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРолиИИсполнителиБизнесПроцессов(Команда)
	
	ОткрытьФорму("РегистрСведений.ИсполнителиЗадач.Форма.АдресацияПоОбъекту",,ЭтаФорма);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Функция ПолучитьМаксимальныйРазмерПередаваемогоФайла()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МаксимальныйРазмерФайлаПередачи = Константы.МаксимальныйРазмерФайлаДляПередачиВ1СДокументооборот.Получить();
	
	Если МаксимальныйРазмерФайлаПередачи = Неопределено Или МаксимальныйРазмерФайлаПередачи = 0 Тогда
		МаксимальныйРазмерФайлаПередачи = 10*1024*1024; // 10 мб
		Константы.МаксимальныйРазмерФайлаДляПередачиВ1СДокументооборот.Установить(МаксимальныйРазмерФайлаПередачи);
	КонецЕсли;
	
	Возврат МаксимальныйРазмерФайлаПередачи;
	
КонецФункции

&НаСервере
Функция ТекстКомментарияИспользоватьБизнесПроцессы()
	
	ТекстКомментарияИспользоватьБизнесПроцессы = НСтр("ru = 'Невозможно отключение бизнес-процессов и задач, потому что включено:'");
	
	ДополнениеККомментарию = "";
	
	Если НаборКонстант.ИспользоватьСогласованиеЦенНоменклатуры Тогда
		
		ДополнениеККомментарию = Символы.ПС + Символы.Таб + "- " + НСтр("ru = 'Согласование цен в разделе ""Маркетинг""'");
		
	КонецЕсли;
	
	Если НаборКонстант.ИспользоватьСогласованиеЗаказовКлиентов Тогда
		
		ДополнениеККоментариюДокументыПродажи = НСтр("ru = 'Согласование заказов клиентов в разделе ""Продажи""'");
		
		ДополнениеККомментарию = ДополнениеККомментарию + ?(Не ПустаяСтрока(ДополнениеККомментарию), ";", "") + Символы.ПС + Символы.Таб + "- " + ДополнениеККоментариюДокументыПродажи;
		
	КонецЕсли;
	
	Если НаборКонстант.ИспользоватьСогласованиеЗаявокНаВозвратТоваровОтКлиентов Тогда
		
		ДополнениеККоментариюДокументыПродажи = НСтр("ru = 'Согласование заявок на возврат в разделе ""Продажи""'");
		
		ДополнениеККомментарию = ДополнениеККомментарию + ?(Не ПустаяСтрока(ДополнениеККомментарию), ";", "") + Символы.ПС + Символы.Таб + "- " + ДополнениеККоментариюДокументыПродажи;
		
	КонецЕсли;
	
	Если НаборКонстант.ИспользоватьСогласованиеКоммерческихПредложений Тогда
		
		ДополнениеККоментариюДокументыПродажи = НСтр("ru = 'Согласование коммерческих предложений в разделе ""Продажи""'");
		
		ДополнениеККомментарию = ДополнениеККомментарию + ?(Не ПустаяСтрока(ДополнениеККомментарию), ";", "") + Символы.ПС + Символы.Таб + "- " + ДополнениеККоментариюДокументыПродажи;
		
	КонецЕсли;
	
	Если НаборКонстант.ИспользоватьСогласованиеСоглашенийСКлиентами Тогда
		
		ДополнениеККоментариюДокументыПродажи = НСтр("ru = 'Согласование соглашений с клиентами в разделе ""Продажи""'");
		
		ДополнениеККомментарию = ДополнениеККомментарию + ?(Не ПустаяСтрока(ДополнениеККомментарию), ";", "") + Символы.ПС + Символы.Таб + "- " + ДополнениеККоментариюДокументыПродажи;
		
	КонецЕсли;
	
	Если НаборКонстант.ИспользоватьСогласованиеЗаказовПоставщикам Тогда
		
		ДополнениеККоментариюДокументыЗакупки = НСтр("ru = 'Согласование документов закупок в разделе ""Запасы и закупки""'");
		
		ДополнениеККомментарию = ДополнениеККомментарию + ?(Не ПустаяСтрока(ДополнениеККомментарию), ";", "") + Символы.ПС + Символы.Таб + "- " + ДополнениеККоментариюДокументыЗакупки;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ДополнениеККомментарию) Тогда
		ТекстКомментарияИспользоватьБизнесПроцессы = ТекстКомментарияИспользоватьБизнесПроцессы + " " + ДополнениеККомментарию + ".";
	Иначе 
		ТекстКомментарияИспользоватьБизнесПроцессы = "";
	КонецЕсли;
	
	Возврат ТекстКомментарияИспользоватьБизнесПроцессы;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обновление интерфейса

&НаКлиенте 
Процедура ПодключитьОбработчикОжиданияОбновленияИнтерфейса()
	
	ТребуетсяОбновлениеИнтерфейса = Истина;
	
	#Если НЕ ВебКлиент Тогда
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбновленияИнтерфейса", 1, Истина);
	#КонецЕсли 
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбработчикОжиданияОбновленияИнтерфейса()
	
	ОбновитьИнтерфейс();
	
	ТребуетсяОбновлениеИнтерфейса = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Изменение реквизитов

&НаСервере
Процедура ИнициализироватьНачальныеЗначенияРеквизитов()
	
	// Инициализация набора констант
	НаборКонстантОбъект = ДанныеФормыВЗначение(НаборКонстант, Тип("КонстантыНабор"));
	НаборКонстантОбъект.Прочитать();
	ЗначениеВДанныеФормы(НаборКонстантОбъект, НаборКонстант);
	
	МаксимальныйРазмерФайлаДляПередачи  = ПолучитьМаксимальныйРазмерПередаваемогоФайла() / (1024*1024);
	ПользовательДокументооборот 		= ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ИнтеграцияС1СДокументооборот", "Пользователь");
	
КонецПроцедуры

&НаСервере 
Процедура СохранитьЗначениеКонстанты(ИмяКонстанты)
	
	УстановитьЗначениеКонстантыПоИмени(ИмяКонстанты);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	ВыполнитьМетодыПослеУстановкиЗначенияКонстанты(ИмяКонстанты);
	
КонецПроцедуры

&НаСервере 
Процедура ВыполнитьМетодыПослеУстановкиЗначенияКонстанты(ИмяКонстанты)
	
	ВариантыОтчетовУТПереопределяемый.НастроитьВариантыОтчетовПоФункциональнойОпции(ИмяКонстанты, НаборКонстант[ИмяКонстанты]);
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьЗначениеКонстантыПоИмени(ИмяКонстанты)
	
	Если Константы[ИмяКонстанты].Получить() <> НаборКонстант[ИмяКонстанты] Тогда
		Константы[ИмяКонстанты].Установить(НаборКонстант[ИмяКонстанты]);
	КонецЕсли;
	
	УстановитьВидимостьДоступностьЗависимыхЭлементовФормы(ИмяКонстанты);
	
	УстановитьЗначенияЗависимыхКонстант(ИмяКонстанты);
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьЗначенияЗависимыхКонстант(ИмяРодительскойКонстанты)
	
	Если ИмяРодительскойКонстанты = "ИспользоватьПочтовыйКлиент" Тогда
		
		Если Не НаборКонстант.ИспользоватьПочтовыйКлиент Тогда
			НаборКонстант.ИспользоватьПрочиеВзаимодействия = Ложь;
			НаборКонстант.ОтправлятьПисьмаВФорматеHTML = Ложь;
			НаборКонстант.ИспользоватьПризнакРассмотрено = Ложь;
			
			УстановитьЗначениеКонстантыПоИмени("ИспользоватьПрочиеВзаимодействия");
			УстановитьЗначениеКонстантыПоИмени("ОтправлятьПисьмаВФорматеHTML");
			УстановитьЗначениеКонстантыПоИмени("ИспользоватьПризнакРассмотрено");
		КонецЕсли;
		
	ИначеЕсли ИмяРодительскойКонстанты = "ИспользоватьБизнесПроцессыИЗадачи" Тогда
		
		Если Не НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи Тогда
			НаборКонстант.ИспользоватьДатуНачалаЗадач = Ложь;
			НаборКонстант.ИспользоватьДатуИВремяВСрокахЗадач = Ложь;
			НаборКонстант.ИзменятьЗаданияЗаднимЧислом = Ложь;
			
			УстановитьЗначениеКонстантыПоИмени("ИспользоватьДатуНачалаЗадач");
			УстановитьЗначениеКонстантыПоИмени("ИспользоватьДатуИВремяВСрокахЗадач");
			УстановитьЗначениеКонстантыПоИмени("ИзменятьЗаданияЗаднимЧислом");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЗависимыхЭлементовФормы(ИмяКонстанты = Неопределено)
	
	// Настройки, выполняемые однократно, только при создании формы
	Если ИмяКонстанты = Неопределено Тогда
		
	КонецЕсли;
	
	// Настройки, выполняемые при создании формы и при изменении соответствующего элемента формы
	Если ИмяКонстанты = "ИспользоватьБизнесПроцессыИЗадачи" Или ИмяКонстанты = Неопределено Тогда
		Элементы.ИспользоватьДатуНачалаЗадач.Доступность 		= НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи;
		Элементы.ИспользоватьДатуИВремяВСрокахЗадач.Доступность = НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи;
		Элементы.ИзменятьЗаданияЗаднимЧислом.Доступность 		= НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи;
	КонецЕсли;
		
	Если ИмяКонстанты = "ИспользоватьПочтовыйКлиент" Или ИмяКонстанты = Неопределено Тогда
		Элементы.ИспользоватьПрочиеВзаимодействия.Доступность = НаборКонстант.ИспользоватьПочтовыйКлиент;
		Элементы.ИспользоватьПризнакРассмотрено.Доступность   = НаборКонстант.ИспользоватьПочтовыйКлиент;
		Элементы.ОтправлятьПисьмаВФорматеHTML.Доступность 	  = НаборКонстант.ИспользоватьПочтовыйКлиент;
	КонецЕсли;
		
	Если ИмяКонстанты = "ИспользоватьБизнесПроцессыИЗадачи" Или ИмяКонстанты = Неопределено Тогда
		
		ДоступностьБизнесПроцессовИЗадач =
			НЕ НаборКонстант.ИспользоватьСогласованиеЗаказовКлиентов И
			НЕ НаборКонстант.ИспользоватьСогласованиеСоглашенийСКлиентами И
			НЕ НаборКонстант.ИспользоватьСогласованиеКоммерческихПредложений И
			НЕ НаборКонстант.ИспользоватьСогласованиеЗаявокНаВозвратТоваровОтКлиентов И
			НЕ НаборКонстант.ИспользоватьСогласованиеЦенНоменклатуры И
			НЕ НаборКонстант.ИспользоватьСогласованиеЗаказовПоставщикам;
		
		Элементы.ИспользоватьБизнесПроцессыИЗадачи.Доступность 	= ДоступностьБизнесПроцессовИЗадач;
		Элементы.ГруппаКомментарийБизнесПроцессы.Видимость 		= НЕ ДоступностьБизнесПроцессовИЗадач;
		
		Если НЕ ДоступностьБизнесПроцессовИЗадач Тогда
			Элементы.КомментарийИспользоватьБизнесПроцессы.Заголовок = ТекстКомментарияИспользоватьБизнесПроцессы();
		КонецЕсли;
		
		Если НЕ НаборКонстант.ИспользоватьСинхронизациюДанных И НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи Тогда
			Элементы.ГруппаКомментарийБизнесПроцессы.Видимость 		 = Истина;
			Элементы.КомментарийИспользоватьБизнесПроцессы.Заголовок = Элементы.КомментарийИспользоватьБизнесПроцессы.Заголовок 
																		+ ?(НЕ ПустаяСтрока(Элементы.КомментарийИспользоватьБизнесПроцессы.Заголовок), Символы.ПС + Символы.ПС, "")
																		+ НСтр("ru= 'Для использования внешних задач требуется включение опции ""Обмен данными"" в разделе ""Обмен данными"".'") ;
		КонецЕсли;
		
		Элементы.ОткрытьРолиИИсполнителиБизнесПроцессов.Доступность = НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи;
		Элементы.ИспользоватьДатуНачалаЗадач.Доступность 			= НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи;
		Элементы.ИспользоватьДатуИВремяВСрокахЗадач.Доступность 	= НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи;
		Элементы.ИзменятьЗаданияЗаднимЧислом.Доступность 			= НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи;
		
	КонецЕсли;
			
КонецПроцедуры
