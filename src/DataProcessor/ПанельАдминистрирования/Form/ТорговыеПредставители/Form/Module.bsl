
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьНачальныеЗначенияРеквизитов();
	
	УстановитьВидимостьДоступностьЗависимыхЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ТребуетсяОбновлениеИнтерфейса Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВедетсяРаботаЧерезТорговыхПредставителейПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеЗаданийТорговымПредставителямПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеЗаданийТорговымПредставителямОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДетализироватьЗаданияТорговымПредставителямПоНоменклатуреПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОткрытьМобильныеПриложения(Команда)
	
	ОткрытьФорму("Справочник.ВерсииМобильныхПриложений.ФормаСписка", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьМобильныеКомпьютеры(Команда)
	
	ОткрытьФорму("Справочник.МобильныеКомпьютеры.ФормаСписка", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиКомпонентыОбменаДаннымиСКПК(Команда)
	
	ОткрытьФорму("ОбщаяФорма.НастройкиКомпонентыОбменаДаннымиСКПК", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиТорговыхПредставителей(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("НеОтображатьЭтотУзел", Истина);
	
	ОткрытьФорму("ПланОбмена.МобильноеПриложениеТорговыйПредставитель.ФормаСписка", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Обновление интерфейса

&НаКлиенте 
Процедура ПодключитьОбработчикОжиданияОбновленияИнтерфейса()
	
	ТребуетсяОбновлениеИнтерфейса = Истина;
	
	#Если НЕ ВебКлиент Тогда
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбновленияИнтерфейса", 1, Истина);
	#КонецЕсли 
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбработчикОжиданияОбновленияИнтерфейса()
	
	ОбновитьИнтерфейс();
	
	ТребуетсяОбновлениеИнтерфейса = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Изменение реквизитов

&НаСервере
Процедура ИнициализироватьНачальныеЗначенияРеквизитов()
	
	// Инициализация набора констант
	НаборКонстантОбъект = ДанныеФормыВЗначение(НаборКонстант, Тип("КонстантыНабор"));
	НаборКонстантОбъект.Прочитать();
	ЗначениеВДанныеФормы(НаборКонстантОбъект, НаборКонстант);
	
КонецПроцедуры

&НаСервере 
Процедура СохранитьЗначениеКонстанты(ИмяКонстанты)
	
	УстановитьЗначениеКонстантыПоИмени(ИмяКонстанты);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	ВыполнитьМетодыПослеУстановкиЗначенияКонстанты(ИмяКонстанты);
	
КонецПроцедуры

&НаСервере 
Процедура ВыполнитьМетодыПослеУстановкиЗначенияКонстанты(ИмяКонстанты)
	
	ВариантыОтчетовУТПереопределяемый.НастроитьВариантыОтчетовПоФункциональнойОпции(ИмяКонстанты, НаборКонстант[ИмяКонстанты]);
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьЗначениеКонстантыПоИмени(ИмяКонстанты)
	
	Если Константы[ИмяКонстанты].Получить() <> НаборКонстант[ИмяКонстанты] Тогда
		Константы[ИмяКонстанты].Установить(НаборКонстант[ИмяКонстанты]);
	КонецЕсли;
	
	УстановитьВидимостьДоступностьЗависимыхЭлементовФормы(ИмяКонстанты);
	
	УстановитьЗначенияЗависимыхКонстант(ИмяКонстанты);
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьЗначенияЗависимыхКонстант(ИмяРодительскойКонстанты)
	
	Если ИмяРодительскойКонстанты = "ВедетсяРаботаЧерезТорговыхПредставителей" Тогда
		
		Если Не НаборКонстант.ВедетсяРаботаЧерезТорговыхПредставителей Тогда
			НаборКонстант.ИспользованиеЗаданийТорговымПредставителям = ПредопределенноеЗначение("Перечисление.ИспользованиеЗаданийТорговымПредставителям.НеИспользуются");
			УстановитьЗначениеКонстантыПоИмени("ИспользованиеЗаданийТорговымПредставителям");
		КонецЕсли;
		
	ИначеЕсли ИмяРодительскойКонстанты = "ИспользованиеЗаданийТорговымПредставителям" Тогда
		
		ЗаданияИспользуются = 
			(НаборКонстант.ИспользованиеЗаданийТорговымПредставителям = ПредопределенноеЗначение("Перечисление.ИспользованиеЗаданийТорговымПредставителям.ИспользуютсяДляУправленияТорговымиПредставителями"))
			Или (НаборКонстант.ИспользованиеЗаданийТорговымПредставителям = ПредопределенноеЗначение("Перечисление.ИспользованиеЗаданийТорговымПредставителям.ИспользуютсяТорговымиПредставителямиДляПланирования"));
		
		Если Не ЗаданияИспользуются Тогда
			НаборКонстант.ДетализироватьЗаданияТорговымПредставителямПоНоменклатуре = Ложь;
			НаборКонстант.НеДетализироватьЗаданияТорговымПредставителямПоНоменклатуре = Ложь;
		КонецЕсли;
		
		НаборКонстант.ИспользоватьЗаданияДляУправленияТорговымиПредставителями = (НаборКонстант.ИспользованиеЗаданийТорговымПредставителям = ПредопределенноеЗначение("Перечисление.ИспользованиеЗаданийТорговымПредставителям.ИспользуютсяДляУправленияТорговымиПредставителями"));
		НаборКонстант.ИспользоватьЗаданияТорговымПредставителямДляПланирования = (НаборКонстант.ИспользованиеЗаданийТорговымПредставителям = ПредопределенноеЗначение("Перечисление.ИспользованиеЗаданийТорговымПредставителям.ИспользуютсяТорговымиПредставителямиДляПланирования"));
		
		УстановитьЗначениеКонстантыПоИмени("ДетализироватьЗаданияТорговымПредставителямПоНоменклатуре");
		УстановитьЗначениеКонстантыПоИмени("НеДетализироватьЗаданияТорговымПредставителямПоНоменклатуре");
		УстановитьЗначениеКонстантыПоИмени("ИспользоватьЗаданияДляУправленияТорговымиПредставителями");
		УстановитьЗначениеКонстантыПоИмени("ИспользоватьЗаданияТорговымПредставителямДляПланирования");
		
	ИначеЕсли ИмяРодительскойКонстанты = "ДетализироватьЗаданияТорговымПредставителямПоНоменклатуре" Тогда
		
		НаборКонстант.НеДетализироватьЗаданияТорговымПредставителямПоНоменклатуре = (Не НаборКонстант.ДетализироватьЗаданияТорговымПредставителямПоНоменклатуре
			И НаборКонстант.ИспользованиеЗаданийТорговымПредставителям = ПредопределенноеЗначение("Перечисление.ИспользованиеЗаданийТорговымПредставителям.ИспользуютсяДляУправленияТорговымиПредставителями"));
		УстановитьЗначениеКонстантыПоИмени("НеДетализироватьЗаданияТорговымПредставителямПоНоменклатуре");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЗависимыхЭлементовФормы(ИмяКонстанты = Неопределено)
	
	// Настройки, выполняемые однократно, только при создании формы
	Если ИмяКонстанты = Неопределено Тогда
		Элементы.ВедетсяРаботаЧерезТорговыхПредставителей.Доступность 				 = НаборКонстант.ИспользоватьЗаказыКлиентов;
		Элементы.ГруппаКомментарийВедетсяРаботаЧерезТорговыхПредставителей.Видимость = НЕ НаборКонстант.ИспользоватьЗаказыКлиентов;
	КонецЕсли;
	
	// Настройки, выполняемые при создании формы и при изменении соответствующего элемента формы
	Если ИмяКонстанты = "ВедетсяРаботаЧерезТорговыхПредставителей" Или ИмяКонстанты = Неопределено Тогда
		Элементы.ОткрытьМобильныеКомпьютеры.Доступность 				 = НаборКонстант.ВедетсяРаботаЧерезТорговыхПредставителей;
		Элементы.ОткрытьМобильныеПриложения.Доступность 				 = НаборКонстант.ВедетсяРаботаЧерезТорговыхПредставителей;
		Элементы.ОткрытьНастройкиКомпонентыОбменаДаннымиСКПК.Доступность = НаборКонстант.ВедетсяРаботаЧерезТорговыхПредставителей;
		Элементы.ОткрытьНастройкиТорговыхПредставителей.Доступность 	 = НаборКонстант.ВедетсяРаботаЧерезТорговыхПредставителей;
		Элементы.ИспользованиеЗаданийТорговымПредставителям.Доступность	 = НаборКонстант.ВедетсяРаботаЧерезТорговыхПредставителей;
	КонецЕсли;
		
	Если ИмяКонстанты = "ИспользованиеЗаданийТорговымПредставителям" Или ИмяКонстанты = Неопределено Тогда
		ЗаданияИспользуются = 
			(НаборКонстант.ИспользованиеЗаданийТорговымПредставителям = ПредопределенноеЗначение("Перечисление.ИспользованиеЗаданийТорговымПредставителям.ИспользуютсяДляУправленияТорговымиПредставителями"))
			ИЛИ (НаборКонстант.ИспользованиеЗаданийТорговымПредставителям = ПредопределенноеЗначение("Перечисление.ИспользованиеЗаданийТорговымПредставителям.ИспользуютсяТорговымиПредставителямиДляПланирования"));
			
		Элементы.ДетализироватьЗаданияТорговымПредставителямПоНоменклатуре.Доступность = ЗаданияИспользуются;
	КонецЕсли;
	
КонецПроцедуры
