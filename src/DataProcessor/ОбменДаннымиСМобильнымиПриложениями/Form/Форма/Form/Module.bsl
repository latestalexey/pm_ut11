////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипСвязи = "TCP|IP";
	
	Если ТипПодключения = 0 Тогда
		ТипПодключения = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	 УстановитьПараметрыНастройкиОбмена(Настройки.Получить("УзелОбмена"));
	 
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриИзмененииТипаСвязи();
	ПриИзмененииТипаПодключения();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТипСвязиПриИзменении(Элемент)
	
	ПриИзмененииТипаСвязи();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипСвязиОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПодключенияПриИзменении(Элемент)
	
	ПриИзмененииТипаПодключения();
	Если ТипПодключения >1 Тогда
		АдресСервера = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УзелОбменаПриИзменении(Элемент)
	
	УстановитьПараметрыНастройкиОбмена(УзелОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяМобильнойБазыНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Отказ = Ложь;
	ПроверитьВозможностьРаботы(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТипСвязиЧислом = ТипСвязиЧислом(ТипСвязи);
	глКомпонентаОбменаСМобильнымиПриложениями.УстановитьПараметрыСвязи(КодМобильногоКомпьютера, ТипСвязиЧислом, АдресСервера, Порт, ИмяСервисаIRDA);
	
	Элемент.СписокВыбора.ЗагрузитьЗначения(МассивБазКлиента());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура НастройкиКомпоненты(Команда)
	
	ОткрытьФормуМодально("ОбщаяФорма.НастройкиКомпонентыОбменаДаннымиСКПК");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбменДанными(Команда)
	
	ВыполнитьОбменДаннымиСМобильнымПриложением();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаКлиенте
Процедура ПриИзмененииТипаСвязи()
	
	Если ТипСвязи = "TCP|IP" ИЛИ ТипСвязи = "BlueTooth" Тогда		
		Элементы.TCPIP.Видимость = Истина;
		Элементы.COMПорт.Видимость = Ложь;
		Элементы.ИнфракрасныйПорт.Видимость = Ложь;
		Элементы.Порт.Видимость = Истина;
	ИначеЕсли ТипСвязи = "COM-порт" Тогда
		Элементы.TCPIP.Видимость = Ложь;
		Элементы.COMПорт.Видимость = Истина;
		Элементы.ИнфракрасныйПорт.Видимость = Ложь;
	ИначеЕсли ТипСвязи = "Инфракрасный порт" Тогда
		Элементы.TCPIP.Видимость = Ложь;
		Элементы.COMПорт.Видимость = Ложь;
		Элементы.ИнфракрасныйПорт.Видимость = Истина;
	ИначеЕсли ТипСвязи = "ActiveSync" Тогда
		АдресСервера = "169.254.2.1";
		Элементы.TCPIP.Видимость = Истина;
		Элементы.Порт.Видимость = Истина;
		Элементы.COMПорт.Видимость = Ложь;
		Элементы.ИнфракрасныйПорт.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТипаПодключения()
	
	Если ТипПодключения = 1 Тогда
		АдресСервера = "127.0.0.1";
		Элементы.АдресСервера.Доступность = Ложь;
	Иначе
		Элементы.АдресСервера.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Процедура ВыполнитьОбменДаннымиСМобильнымПриложением()
	
	Отказ = Ложь;
	ПроверитьВозможностьРаботы(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбменаДанными = "";
	
	ТекстСостояния = НСтр("ru='Инициализация сеанса обмена'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	ТипСвязиЧислом = ТипСвязиЧислом(ТипСвязи);
	глКомпонентаОбменаСМобильнымиПриложениями.УстановитьПараметрыСвязи(КодМобильногоКомпьютера, ТипСвязиЧислом, АдресСервера, Порт, ИмяСервисаIRDA);
	
	Попытка
		глКомпонентаОбменаСМобильнымиПриложениями.НачатьСеансОбмена(КодМобильногоКомпьютера, ИмяМобильнойБазы, ИмяПользователя, 
		ПарольПользователя, ПередаватьДанныеКлиенту);
	Исключение
		Предупреждение(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
		
	глКомпонентаОбменаСМобильнымиПриложениями.УстановитьРежимНачальнойИнициализации(КодМобильногоКомпьютера, НачальнаяИнициализацияИБ);
		
	// Получение приложения, предназначенного для указанного клиента
	ПриложениеСтрокой = МобильныеПриложенияВызовСервера.ПолучитьПриложение(ИмяПользователя, КодМобильногоКомпьютера, ПараметрыОбменаДанными);
	
	// Установка приложения для текущего сеанса обмена
	Попытка
		глКомпонентаОбменаСМобильнымиПриложениями.УстановитьПриложение(КодМобильногоКомпьютера, ПриложениеСтрокой);
	Исключение
		Предупреждение(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;	
	
	ТекстСостояния = НСтр("ru='Получение данных от клиента'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	
	// Получение данных от клиента
	Попытка
		ДанныеКлиента = глКомпонентаОбменаСМобильнымиПриложениями.ПолучитьДанныеКлиента(КодМобильногоКомпьютера);
	Исключение
		Предупреждение(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
			
	ТекстСостояния = НСтр("ru='Запись полученных данных'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	//Запись полученных данных в базу
	Попытка
		МобильныеПриложенияВызовСервера.ЗаписатьДанные(ИмяПользователя, КодМобильногоКомпьютера, ДанныеКлиента, ПараметрыОбменаДанными);
	Исключение
		Предупреждение(ИнформацияОбОшибке().Описание);
		Возврат;
	КонецПопытки;
	
	Если ПередаватьДанныеКлиенту Тогда
		
		ТекстСостояния = НСтр("ru='Формирование пакета обмена для клиента'");
		Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
		
		ДанныеДляОбмена = МобильныеПриложенияВызовСервера.ПолучитьДанные(ИмяПользователя, КодМобильногоКомпьютера, НачальнаяИнициализацияИБ, ПараметрыОбменаДанными);
		
		ТекстСостояния = НСтр("ru='Передача данных клиенту'");
		Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
		
		Попытка
			глКомпонентаОбменаСМобильнымиПриложениями.ПередатьДанныеКлиенту(КодМобильногоКомпьютера, ДанныеДляОбмена);
		Исключение
			Предупреждение(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
		
	КонецЕсли;
	
	ТекстСостояния = НСтр("ru='Регистрация получения данных клиентом'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	Попытка
		МобильныеПриложенияВызовСервера.ЗарегистрироватьПолучениеДанных(ИмяПользователя, КодМобильногоКомпьютера, ПараметрыОбменаДанными);
	Исключение
		Предупреждение(ИнформацияОбОшибке().Описание);
		Возврат;
	КонецПопытки;
	
	ТекстСостояния = НСтр("ru='Завершение сеанса обмен'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	Попытка
		глКомпонентаОбменаСМобильнымиПриложениями.ЗавершитьСеансОбмена(КодМобильногоКомпьютера, ИмяМобильнойБазы, ИмяПользователя);
	Исключение
		Предупреждение(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	ТекстСообщения = НСтр("ru='Сеанс обмена завершен'");
	Предупреждение(ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Функция ТипСвязиЧислом(ТипСвязиСтрокой)
	
	Если ТипСвязиСтрокой = "TCP|IP" Тогда
		Возврат 1;
	ИначеЕсли ТипСвязиСтрокой = "BlueTooth" Тогда
		Возврат 2;
	ИначеЕсли ТипСвязиСтрокой = "Инфракрасный порт" Тогда
	    Возврат 3;
	ИначеЕсли ТипСвязиСтрокой = "COM-порт" Тогда
	    Возврат 4;
	ИначеЕсли ТипСвязиСтрокой = "ActiveSync" Тогда
	    Возврат 5;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции	

&НаКлиенте
Функция МассивБазКлиента()
	
	ТекстСостояния = НСтр("ru='Получение списка информационных баз клиента'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	МассивИменБазКлиента = Новый Массив();
	СписокБазКлиента = глКомпонентаОбменаСМобильнымиПриложениями.ПолучитьСписокИнформационныхБазКлиента(КодМобильногоКомпьютера, ИмяПользователя);
	КоличествоСтрок = СтрЧислоСтрок(СписокБазКлиента);
	Если КоличествоСтрок>0 Тогда
		Для НомерСтроки = 1 по КоличествоСтрок Цикл
			МассивИменБазКлиента.Добавить(СтрПолучитьСтроку(СписокБазКлиента, НомерСтроки));
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивИменБазКлиента;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьВозможностьРаботы(Отказ)
	
	Если УзелОбмена.Пустая() Тогда
		ТекстСообщения = НСтр("ru='Не указаны настройки пользователя'");
		Предупреждение(ТекстСообщения);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если глКомпонентаОбменаСМобильнымиПриложениями = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Не найден объект компоненты обмена данными'");
		Предупреждение(ТекстСообщения);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыНастройкиОбмена(УзелОбмена)
	
	ИмяПользователя = СокрЛП(УзелОбмена.ИмяПользователя);
	КодМобильногоКомпьютера = СокрЛП(УзелОбмена.МобильныйКомпьютер.СерийныйНомер);
	ПарольПользователя = СокрЛП(УзелОбмена.Пароль);
	
КонецПроцедуры