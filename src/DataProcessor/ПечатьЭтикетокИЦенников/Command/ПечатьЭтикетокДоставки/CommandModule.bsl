&НаСервере
Функция ПолучитьДанныеДляПечатиЭтикеток(ПараметрКоманды)
	
	Возврат Обработки.ПечатьЭтикетокИЦенников.ПолучитьДанныеДляПечатиЭтикетокДоставки(ПараметрКоманды[0]);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если УправлениеПечатьюКлиент.ПроверитьДокументыПроведены(ПараметрКоманды, ПараметрыВыполненияКоманды.Источник) Тогда
		
		СтруктураВозврата = ПолучитьДанныеДляПечатиЭтикеток(ПараметрКоманды);
		
		Если СтруктураВозврата.МассивСсылокСОшибками.Количество() > 0 Тогда
			ТекстСообщения = НСтр("ru = 'Не настроен общий шаблон этикетки доставки. Обратитесь к администратору.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Для Каждого СсылкаПеревозчик Из СтруктураВозврата.МассивПеревозчиковБезШаблоновЭтикетки Цикл
			Если ЗначениеЗаполнено(СсылкаПеревозчик) Тогда
				ТекстСообщения = НСтр("ru = 'Для перевозчика ""%Перевозчик%"" не заполнен шаблон этикетки доставки.'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Перевозчик%", СсылкаПеревозчик);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СсылкаРаспоряжение Из СтруктураВозврата.МассивСсылокСОшибками Цикл
			ТекстСообщения = НСтр("ru = 'Не удалось напечатать этикетку доставки для документа ""%Распоряжение%"".'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Распоряжение%", СсылкаРаспоряжение);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
		
		Если СтруктураВозврата.ЕстьЭтикеткиДляПечати Тогда
			
			МассивПараметров = Новый Массив;
			МассивПараметров.Добавить(
				Новый Структура(
					"АдресВХранилище, СтруктураМакетаШаблона",
					СтруктураВозврата.АдресВХранилище, Неопределено));
			
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
				"Обработка.ПечатьЭтикетокИЦенников",
				"ЭтикеткаДоставки",
				МассивПараметров,
				Неопределено,
				Неопределено
			);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры