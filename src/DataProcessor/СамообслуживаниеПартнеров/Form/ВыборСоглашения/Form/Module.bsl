
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Партнер") Тогда
		
		СоглашенияСписок.Параметры.УстановитьЗначениеПараметра("Партнер",Параметры.Партнер);
		СоглашенияСписок.Параметры.УстановитьЗначениеПараметра("ТекущаяДата",НачалоДня(ТекущаяДата()));
		
		Если Параметры.Свойство("Соглашение") Тогда
			Элементы.СоглашенияСписок.ТекущаяСтрока = Параметры.Соглашение;
		КонецЕсли;
		
	Иначе
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	ИспользоватьТиповыеСоглашенияСКлиентами			= ПолучитьФункциональнуюОпцию("ИспользоватьТиповыеСоглашенияСКлиентами");
	ИспользоватьИндивидуальныеСоглашенияСКлиентами	= ПолучитьФункциональнуюОпцию("ИспользоватьИндивидуальныеСоглашенияСКлиентами");
	ТолькоТиповые = ИспользоватьТиповыеСоглашенияСКлиентами И НЕ ИспользоватьИндивидуальныеСоглашенияСКлиентами;
	ТолькоИндивидуальные = НЕ ИспользоватьТиповыеСоглашенияСКлиентами И ИспользоватьИндивидуальныеСоглашенияСКлиентами;
	
	СоглашенияСписок.Параметры.УстановитьЗначениеПараметра("ТолькоТиповые",ТолькоТиповые);
	СоглашенияСписок.Параметры.УстановитьЗначениеПараметра("ТолькоИндивидуальные",ТолькоИндивидуальные);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ СОГЛАШЕНИЯ СПИСОК

&НаКлиенте
Процедура СоглашенияСписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура СоглашенияСписокПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОбработатьВыбор();
	
	ТекущиеДанные = Элементы.СоглашенияСписок.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Закрыть();
	Иначе
		Закрыть(Новый Структура("Соглашение,СоглашениеСтрока,Валюта,Склад,НалогообложениеНДС,ЦенаВключаетНДС,ГрафикОплаты,ФормаОплаты,Контрагент,Организация",
		ТекущиеДанные.Ссылка,
		ТекущиеДанные.Наименование,
		ТекущиеДанные.Валюта,
		ТекущиеДанные.Склад,
		ТекущиеДанные.НалогообложениеНДС,
		ТекущиеДанные.ЦенаВключаетНДС,
		ТекущиеДанные.ГрафикОплаты,
		ТекущиеДанные.ФормаОплаты,
		ТекущиеДанные.Контрагент,
		ТекущиеДанные.Организация));
	КонецЕсли;
	
КонецПроцедуры







