///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция ТекстЗапросаСостояниеОстатковТоваровОрганизаций() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.Организация КАК Организация,
	|	ТоварыОрганизаций.Склад КАК Склад,
	|	ТоварыОрганизаций.Номенклатура КАК Номенклатура,
	|	ТоварыОрганизаций.Характеристика КАК Характеристика,
	|	ТоварыОрганизаций.Период КАК Период
	|
	|ПОМЕСТИТЬ ВтТаблицаОтрицательныхОстатков
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, Месяц,,) КАК ТоварыОрганизаций
	|ГДЕ
	|	ТоварыОрганизаций.КоличествоКонечныйОстаток < 0
	|	И ТоварыОрганизаций.КоличествоНачальныйОстаток <> ТоварыОрганизаций.КоличествоКонечныйОстаток
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Склад,
	|	Номенклатура,
	|	Характеристика,
	|	Период
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.Склад КАК Склад,
	|	ТоварыОрганизаций.Номенклатура КАК Номенклатура,
	|	ТоварыОрганизаций.Характеристика КАК Характеристика,
	|	ТоварыОрганизаций.Период КАК Период
	|
	|ПОМЕСТИТЬ ВтТаблицаОтсутствующихТоваров
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, Месяц,,) КАК ТоварыОрганизаций
	|ГДЕ
	|	ТоварыОрганизаций.КоличествоКонечныйОстаток < 0
	|	И ТоварыОрганизаций.КоличествоНачальныйОстаток <> ТоварыОрганизаций.КоличествоКонечныйОстаток
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Склад,
	|	Номенклатура,
	|	Характеристика,
	|	Период
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|// ОТРИЦАТЕЛЬНЫЕ ОСТАТКИ ТОВАРОВ ОРГАНИЗАЦИЙ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтрицательныхОстатков.Период КАК Период
	|ИЗ
	|	ВтТаблицаОтрицательныхОстатков КАК ТаблицаОтрицательныхОстатков
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период ВОЗР
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|// РАЗВЕРНУТОЕ САЛЬДО ПО ТОВАРАМ ОРГАНИЗАЦИЙ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыОрганизаций.Период КАК Период
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТоварыОрганизаций.Организация КАК Организация,
	|		ТоварыОрганизаций.Склад КАК Склад,
	|		ТоварыОрганизаций.Номенклатура КАК Номенклатура,
	|		ТоварыОрганизаций.Характеристика КАК Характеристика,
	|		ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|		ТоварыОрганизаций.Период
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, Месяц,, 
	|			Организация В (&МассивОрганизаций)
	|		) КАК ТоварыОрганизаций
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ВтТаблицаОтрицательныхОстатков КАК ТаблицаОтрицательныхОстатков
	|		ПО
	|			ТоварыОрганизаций.Организация = ТаблицаОтрицательныхОстатков.Организация
	|			И ТоварыОрганизаций.Склад = ТаблицаОтрицательныхОстатков.Склад
	|			И ТоварыОрганизаций.Номенклатура = ТаблицаОтрицательныхОстатков.Номенклатура
	|			И ТоварыОрганизаций.Характеристика = ТаблицаОтрицательныхОстатков.Характеристика
	|			И ТоварыОрганизаций.Период = ТаблицаОтрицательныхОстатков.Период
	|	ГДЕ
	|		ТоварыОрганизаций.КоличествоКонечныйОстаток < 0
	|		И ТоварыОрганизаций.КоличествоНачальныйОстаток <> ТоварыОрганизаций.КоличествоКонечныйОстаток
	|		И ТаблицаОтрицательныхОстатков.Период ЕСТЬ NULL
	|		
	|	) КАК ТоварыОрганизаций
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период ВОЗР
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|// НЕДОСТАТОЧНО ТОВАРОВ ОРГАНИЗАЦИЙ НА СКЛАДАХ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтсутствующихТоваров.Период КАК Период
	|ИЗ
	|	ВтТаблицаОтсутствующихТоваров КАК ТаблицаОтсутствующихТоваров
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период ВОЗР
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|// МОЖНО ОФОРМИТЬ ПЕРЕДАЧИ ТОВАРОВ МЕЖДУ ОРГАНИЗАЦИЯМИ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыОрганизаций.Период КАК Период
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТаблицаОтрицательныхОстатков.Организация КАК Организация,
	|		ТаблицаОтрицательныхОстатков.Склад КАК Склад,
	|		ТаблицаОтрицательныхОстатков.Номенклатура КАК Номенклатура,
	|		ТаблицаОтрицательныхОстатков.Характеристика КАК Характеристика,
	|		ТаблицаОтрицательныхОстатков.Период КАК Период
	|	ИЗ
	|		ВтТаблицаОтрицательныхОстатков КАК ТаблицаОтрицательныхОстатков
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ВтТаблицаОтсутствующихТоваров КАК ТаблицаОтсутствующихТоваров
	|		ПО
	|			ТаблицаОтрицательныхОстатков.Склад = ТаблицаОтсутствующихТоваров.Склад
	|			И ТаблицаОтрицательныхОстатков.Номенклатура = ТаблицаОтсутствующихТоваров.Номенклатура
	|			И ТаблицаОтрицательныхОстатков.Характеристика = ТаблицаОтсутствующихТоваров.Характеристика
	|			И ТаблицаОтрицательныхОстатков.Период = ТаблицаОтсутствующихТоваров.Период
	|	ГДЕ
	|		ТаблицаОтсутствующихТоваров.Период ЕСТЬ NULL
	|
	|	) КАК ТоварыОрганизаций
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период ВОЗР
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции