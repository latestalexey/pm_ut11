&НаКлиенте
Перем ИмяРедактируемогоРеквизита;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Элементы.СтраницыДекорацийНавигации.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыШагов.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыКомандныхПанелей.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыСовпадениеПаролей.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыОшибкаЭлектронногоАдреса.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыЗаголовокНаименования.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	ИнициализироватьНачальныеНастройки();
	ИнициализироватьИнформациюОВалютах();
	ИнициализироватьКонтактнуюИнформацию();
	
	ОбновитьСтрокуРеквизитовОрганизации(ЭтаФорма);
	
	ДобавитьСтраницуПерехода(СтраницыПереходов, Элементы.СтраницаОбщаяИнформация, Ложь,,, "НаименованиеПользователя");
	ДобавитьСтраницуПерехода(СтраницыПереходов, Элементы.СтраницаВариантНастройки, , Ложь,, "ПервоеИспользованиеУТ");
	ДобавитьСтраницуПерехода(СтраницыПереходов, Элементы.СтраницаСведенияОрганизации,,,, "ИндивидуальныйПредприниматель");
	ДобавитьСтраницуПерехода(СтраницыПереходов, Элементы.СтраницаКонтакты,,,, "");
	ДобавитьСтраницуПерехода(СтраницыПереходов, Элементы.СтраницаСчет,,,, "НомерСчета");
	ДобавитьСтраницуПерехода(СтраницыПереходов, Элементы.СтраницаЦены,,,,"Декорация8");
	ДобавитьСтраницуПерехода(СтраницыПереходов, Элементы.СтраницаЗавершение, Ложь,,, "НадписьЗавершение");
	
	УстановитьЦветЗаголовкаПоляОшибки();
	УстановитьНазваниеКомандыРеквизитовОрганизации(ЭтаФорма);
	УстановитьНазваниеКомандыОтветственныхЛиц(ЭтаФорма);
	
	УстановитьДоступностьВалютыЦены(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьСписокВыбораВалютыЦены();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если Не ДанныеЗаписаны Тогда 
		Отказ = Не ОткрытьФормуМодально("Обработка.ПомощникНастройкиПараметровБазовойВерсии.Форма.ДиалогОтмены", , ЭтаФорма);
		Если Не Отказ Тогда
			ЗаписатьВалютыСервер();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(РезультатВыбора, ИсточникВыбора)
	Если Не (ИсточникВыбора.ИмяФормы = "Справочник.БанковскиеСчетаОрганизаций.Форма.РеквизитыБанка"
		Или ИсточникВыбора.ИмяФормы = "Справочник.КлассификаторБанковРФ.Форма.ФормаВыбора") Тогда
		Возврат;
	КонецЕсли;
	
	Если (ИсточникВыбора.ИмяФормы = "Справочник.БанковскиеСчетаОрганизаций.Форма.РеквизитыБанка") Тогда
		Если Не ПустаяСтрока(РезультатВыбора) Тогда
			Если РезультатВыбора.Реквизит = "БИКБанка" Тогда
				РучноеИзменениеРеквизитовБанка	= РезультатВыбора.РучноеИзменение;
				БИКБанка						= РезультатВыбора.ЗначенияПолей.БИК;
				НаименованиеБанка				= РезультатВыбора.ЗначенияПолей.Наименование;
				КоррСчетБанка					= РезультатВыбора.ЗначенияПолей.КоррСчет;
				ГородБанка						= РезультатВыбора.ЗначенияПолей.Город;
				Если РезультатВыбора.РучноеИзменение Тогда
					Банк				 = "";
				Иначе
					Банк				 = РезультатВыбора.Банк;
				КонецЕсли;
			ИначеЕсли РезультатВыбора.Реквизит = "БИКБанкаДляРасчетов" Тогда
				РучноеИзменениеРеквизитовБанкаДляРасчетов	= РезультатВыбора.РучноеИзменение;
				БИКБанкаДляРасчетов							= РезультатВыбора.ЗначенияПолей.БИК;
				НаименованиеБанкаДляРасчетов				= РезультатВыбора.ЗначенияПолей.Наименование;
				КоррСчетБанкаДляРасчетов					= РезультатВыбора.ЗначенияПолей.КоррСчет;
				ГородБанкаДляРасчетов						= РезультатВыбора.ЗначенияПолей.Город;
				Если РезультатВыбора.РучноеИзменение Тогда
					БанкДляРасчетов = "";
				Иначе
					БанкДляРасчетов = РезультатВыбора.Банк;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли (ИсточникВыбора.ИмяФормы = "Справочник.КлассификаторБанковРФ.Форма.ФормаВыбора") Тогда	
		Если ТипЗнч(РезультатВыбора) = Тип("СправочникСсылка.КлассификаторБанковРФ") Тогда
			Если ИмяРедактируемогоРеквизита = "БИКБанка" Тогда
				Банк = РезультатВыбора;
				ЗаполнитьРеквизитыБанкаПоБанку(РезультатВыбора, "Банк", Ложь);
			ИначеЕсли ИмяРедактируемогоРеквизита = "БИКБанкаДляРасчетов" Тогда
				БанкДляРасчетов = РезультатВыбора;
				ЗаполнитьРеквизитыБанкаПоБанку(РезультатВыбора, "БанкДляРасчетов", Ложь);
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	
	Если ИмяРедактируемогоРеквизита = "БИКБанка" Тогда
		РеквизитыБанка = Новый Структура("
			|Банк, 
			|БИКБанка, 
			|ВалютаРасчетногоСчета, 
			|ГородБанка, 
			|КоррСчетБанка, 
			|НаименованиеБанка, 
			|РучноеИзменениеРеквизитовБанка");
			
		ЗаполнитьЗначенияСвойств(РеквизитыБанка, ЭтаФорма);
		ОбновитьСтрокуРеквизитовБанка(ЭтаФорма, РеквизитыБанка);
	ИначеЕсли ИмяРедактируемогоРеквизита = "БИКБанкаДляРасчетов" Тогда
		РеквизитыБанка = Новый Структура("
			|БанкДляРасчетов, 
			|БИКБанкаДляРасчетов, 
			|ГородБанкаДляРасчетов, 
			|КоррСчетБанкаДляРасчетов, 
			|НаименованиеБанкаДляРасчетов, 
			|РучноеИзменениеРеквизитовБанкаДляРасчетов");
			
		ЗаполнитьЗначенияСвойств(РеквизитыБанка, ЭтаФорма);
		ОбновитьСтрокуРеквизитовБанка(ЭтаФорма, РеквизитыБанка, Истина);
	КонецЕсли;	
	ДоступностьРеквизитовИКомандБанкаРасчетов();
	
	Если Окно <> Неопределено Тогда
		Окно.Активизировать();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте 
Процедура Подключаемый_ВалютаПриИзменении(Элемент)
	ИдентификаторВалюты = СтрЗаменить(Элемент.Имя, "Валюта__", "");

	ИскомаяВалюта = ПолучитьСсылкуСправочникаПоИдентификатору("Валюты", ИдентификаторВалюты);
	
	ИскомаяВалютаВСписке = ВалютыРасчетов.НайтиПоЗначению(ИскомаяВалюта);
	ИскомаяВалютаВСписке.Пометка = НЕ ИскомаяВалютаВСписке.Пометка;
	
	УстановитьДоступностьВалютыЦены(ЭтаФорма);
	
	ОбновитьСписокВыбораВалютыЦены();
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПользователяПриИзменении(Элемент)
	ПользовательИнфБазыИмя =
		ПользователиСлужебныйКлиентСервер.ПолучитьКраткоеИмяПользователяИБ(НаименованиеПользователя);
		
	// Заполним реквизиты организации
	Наименование = НаименованиеПользователя;
	НаименованиеПолное = НаименованиеПользователя;
	НаименованиеСокращенное = СокращенноеФИО(НаименованиеПолное);
	
	Если Не ПустаяСтрока(НаименованиеПользователя) Тогда
		Элементы.НадписьФИО.ЦветТекста = ЦветЗаголовкаАвто;
	КонецЕсли;
	Если Не ПустаяСтрока(ПользовательИнфБазыИмя) Тогда
		Элементы.ПользовательИнфБазыИмя.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
	КонецЕсли;
	
	ПроверитьЗаполнениеКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ПользовательИнфБазыИмяПриИзменении(Элемент)
	ПроверитьЗаполнениеКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	ПриИзмененииПароля();
КонецПроцедуры

&НаКлиенте
Процедура ПодтверждениеПароляПриИзменении(Элемент)
	ПриИзмененииПароля();
КонецПроцедуры

&НаКлиенте
Процедура ИндивидуальныйПредпринимательПриИзменении(Элемент)
	Если ИндивидуальныйПредприниматель = 1 Тогда
		Элементы.СтраницыЗаголовокНаименования.ТекущаяСтраница = Элементы.СтраницаЗаголовокИП;
		ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель");
	Иначе
		Элементы.СтраницыЗаголовокНаименования.ТекущаяСтраница = Элементы.СтраницаЗаголовокЮЛ;
		ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)
	Наименование = НаименованиеПолное;
	
	Если ИндивидуальныйПредприниматель = 1 Тогда
		НаименованиеСокращенное = СокращенноеФИО(НаименованиеПолное);
	КонецЕсли;
		
	Если Не ПустаяСтрока(НаименованиеПолное) Тогда
		Элементы.ЗаголовокНаименованиеИП.ЦветТекста = ЦветЗаголовкаАвто;
		Элементы.ЗаголовокНаименованиеЮЛ.ЦветТекста = ЦветЗаголовкаАвто;
		СведенияОрганизацииЗаполняются = Истина;
	КонецЕсли;
	
	ПроверитьЗаполнениеКлиент();
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПриИзменении(Элемент)
	Если Не СистемаНалогообложения.Пустая() Тогда
		Элементы.СистемаНалогообложения.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
		СведенияОрганизацииЗаполняются = Истина;
	КонецЕсли;
	
	ПроверитьЗаполнениеКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРозничныеПродажиПриИзменении(Элемент)
	Элементы.РозничнаяТорговляОблагаетсяЕНВД.Доступность = ИспользоватьРозничныеПродажи;
	Если Не ИспользоватьРозничныеПродажи Тогда
		РозничнаяТорговляОблагаетсяЕНВД = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	
	УправлениеКонтактнойИнформациейКлиент.ПредставлениеПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры

&НаКлиенте 
Процедура Подключаемый_ЭлектроннаяПочтаПриИзменении(Элемент)
	ПроверитьАдресЭлектроннойПочты(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.ПредставлениеНачалоВыбора(ЭтаФорма, Элемент, Модифицированность, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерСчетаПриИзменении(Элемент)
	НомерСчетаПриИзмененииСервер();
	
	ДоступностьБанковОстатковБезналичных(НомерСчета);
КонецПроцедуры

&НаКлиенте
Процедура БИКБанкаПриИзменении(Элемент)
	ИмяРедактируемогоРеквизита = Элемент.Имя;
	РеквизитБанкаПриИзменении(Элемент.Имя);
	
	РеквизитыБанка = Новый Структура("
		|Банк, 
		|БИКБанка, 
		|ВалютаРасчетногоСчета, 
		|ГородБанка, 
		|КоррСчетБанка, 
		|НаименованиеБанка, 
		|РучноеИзменениеРеквизитовБанка");
	ЗаполнитьЗначенияСвойств(РеквизитыБанка, ЭтаФорма);
	
	ОбновитьСтрокуРеквизитовБанка(ЭтаФорма, РеквизитыБанка);
	ДоступностьРеквизитовИКомандБанкаРасчетов();
КонецПроцедуры

&НаКлиенте
Процедура БИКБанкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ИмяРедактируемогоРеквизита = Элемент.Имя;
	РеквизитБанкаПриВыборе(Элемент.Имя, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура БИКБанкаОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИмяРедактируемогоРеквизита = Элемент.Имя;
	РеквизитБанкаОткрытие(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура БИКБанкаДляРасчетовПриИзменении(Элемент)
	ИмяРедактируемогоРеквизита = Элемент.Имя;
	РеквизитБанкаПриИзменении(Элемент.Имя);
	
	РеквизитыБанка = Новый Структура("
		|БанкДляРасчетов, 
		|БИКБанкаДляРасчетов, 
		|ГородБанкаДляРасчетов, 
		|КоррСчетБанкаДляРасчетов, 
		|НаименованиеБанкаДляРасчетов, 
		|РучноеИзменениеРеквизитовБанкаДляРасчетов");
	ЗаполнитьЗначенияСвойств(РеквизитыБанка, ЭтаФорма);
	
	ОбновитьСтрокуРеквизитовБанка(ЭтаФорма, РеквизитыБанка, Истина);
	ДоступностьРеквизитовИКомандБанкаРасчетов();
КонецПроцедуры

&НаКлиенте
Процедура БИКБанкаДляРасчетовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ИмяРедактируемогоРеквизита = Элемент.Имя;
	РеквизитБанкаПриВыборе(Элемент.Имя, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура БИКБанкаДляРасчетовОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИмяРедактируемогоРеквизита = Элемент.Имя;
	РеквизитБанкаОткрытие(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяБанкДляРасчетовПриИзменении(Элемент)
	ДоступностьРеквизитовИКомандБанкаРасчетов();
КонецПроцедуры

&НаКлиенте
Процедура ВалютаЦеныПриИзменении(Элемент)
	Если Не ВалютаЦены.Пустая() Тогда
		Элементы.НадписьНомерСчета.ЦветТекста = ЦветЗаголовкаАвто;
	КонецЕсли;
	
	ПроверитьЗаполнениеКлиент();
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаданияЦеныВручнуюПриИзменении(Элемент)
	ДоступностьНаценки();
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаданияЦеныНаценкаПриИзменении(Элемент)
	ДоступностьНаценки();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЧНОЙ ЧАСТИ <ИМЯ ТАБЛИЧНОЙ ЧАСТИ>

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Далее(Команда)
	ОсуществитьПереходВперед();
КонецПроцедуры
 
&НаКлиенте
Процедура Назад(Команда)
	ОсуществитьПереходНазад();
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьНастройку(Команда)
	ЗавершениеНастройки = Истина;
	
	ЗаписатьИзмененияВИБ();
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Настройка программы завершена'"), , НСтр("ru = 'Минимально необходимая настройка завершена.'"), БиблиотекаКартинок.Информация32);
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДругиеВалюты(Команда)
	ДобавлениеДополнительныхВалют = Истина;
	
	ОткрытьФормуМодально("Справочник.Валюты.Форма.ПодборВалютИзКлассификатора",,ЭтаФорма);
	
	ИнициализироватьИнформациюОВалютах();
	
	ОбновитьСписокВыбораВалютыЦены();
	
	УстановитьДоступностьВалютыЦены(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПервоеИспользованиеУТ(Команда)
	
	ОсуществитьПереходВперед();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПомощникПереходаСТорговляИСклад77(Команда)
	
	ОткрытьФорму("Обработка.ПомощникПереходаСТорговляИСклад77.Форма");
	
	ДанныеЗаписаны = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗагрузкаДанныхИзУТ103(Команда)
	
	ОткрытьФорму("Обработка.ЗагрузкаДанныхИзУТ103.Форма");
	
	ДанныеЗаписаны = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОрганизацию(Команда)
	ДобавитьОрганизациюСервер();
	
	СведенияОрганизацииЗаполняются = Ложь;
	ОсуществитьПереходКСтранице("СтраницаСведенияОрганизации");
	
	ДоступностьБанковОстатковБезналичных(НомерСчета);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРеквизитыБанка(Команда)
	РеквизитыБанка = Новый Структура("
		|Банк, 
		|БИКБанка, 
		|ВалютаРасчетногоСчета, 
		|ГородБанка, 
		|КоррСчетБанка, 
		|НаименованиеБанка, 
		|РучноеИзменениеРеквизитовБанка");
	ЗаполнитьЗначенияСвойств(РеквизитыБанка, ЭтаФорма);
	
	ИзмененныеРеквизиты = ОткрытьФормуМодально("Обработка.ПомощникНастройкиПараметровБазовойВерсии.Форма.РеквизитыБанка", РеквизитыБанка, ЭтаФорма);
	Если Не ИзмененныеРеквизиты = Неопределено Тогда
		// Заполним значения реквизитов
		Для Каждого ИзмененныйРеквизит Из ИзмененныеРеквизиты Цикл 
			ЭтаФорма[ИзмененныйРеквизит.Ключ] = ИзмененныйРеквизит.Значение;
		КонецЦикла;
		ОбновитьСтрокуРеквизитовБанка(ЭтаФорма, ИзмененныеРеквизиты);
		ДоступностьРеквизитовИКомандБанкаРасчетов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРеквизитыБанкаДляРасчетов(Команда)
	РеквизитыБанка = Новый Структура("
		|БанкДляРасчетов, 
		|БИКБанкаДляРасчетов, 
		|ГородБанкаДляРасчетов, 
		|КоррСчетБанкаДляРасчетов, 
		|НаименованиеБанкаДляРасчетов, 
		|РучноеИзменениеРеквизитовБанкаДляРасчетов");
	ЗаполнитьЗначенияСвойств(РеквизитыБанка, ЭтаФорма);
	
	РеквизитыБанкаДляРасчетов = Новый Структура; 
	
	Для Каждого РеквизитБанка Из РеквизитыБанка Цикл 
		НовыйКлюч = СтрЗаменить(РеквизитБанка.Ключ, "ДляРасчетов", "");
		РеквизитыБанкаДляРасчетов.Вставить(НовыйКлюч, РеквизитБанка.Значение);
	КонецЦикла;
	РеквизитыБанкаДляРасчетов.Вставить("РеквизитыБанкаДляРасчетов", Истина);
	
	ИзмененныеРеквизиты = ОткрытьФормуМодально("Обработка.ПомощникНастройкиПараметровБазовойВерсии.Форма.РеквизитыБанка", РеквизитыБанкаДляРасчетов, ЭтаФорма);
	Если Не ИзмененныеРеквизиты = Неопределено Тогда
		РеквизитыБанкаДляРасчетов.Очистить();
		
		// Заполним значения реквизитов
		Для Каждого ИзмененныйРеквизит Из ИзмененныеРеквизиты Цикл 
			ИмяЭлемента = ИзмененныйРеквизит.Ключ + "ДляРасчетов";
			
			ЭтаФорма[ИмяЭлемента] = ИзмененныйРеквизит.Значение;
			
			РеквизитыБанкаДляРасчетов.Вставить(ИмяЭлемента, ИзмененныйРеквизит.Значение);
		КонецЦикла;
		ОбновитьСтрокуРеквизитовБанка(ЭтаФорма, РеквизитыБанкаДляРасчетов, Истина);
		ДоступностьРеквизитовИКомандБанкаРасчетов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРеквизитыОрганизации(Команда)
	РеквизитыОрганизации = Новый Структура("
		|ИндивидуальныйПредприниматель, 
		|ИНН, 
		|КодНалоговогоОргана, 
		|КодПоОКАТО, 
		|КодПоОКПО, 
		|КПП, 
		|ОГРН, 
		|Префикс");
	ЗаполнитьЗначенияСвойств(РеквизитыОрганизации, ЭтаФорма);
	
	ИзмененныеРеквизиты = ОткрытьФормуМодально("Обработка.ПомощникНастройкиПараметровБазовойВерсии.Форма.РеквизитыОрганизации", РеквизитыОрганизации, ЭтаФорма);
	Если Не ИзмененныеРеквизиты = Неопределено Тогда
		// Заполним значения реквизитов
		Для Каждого ИзмененныйРеквизит Из ИзмененныеРеквизиты Цикл 
			ЭтаФорма[ИзмененныйРеквизит.Ключ] = ИзмененныйРеквизит.Значение;
		КонецЦикла;
		ОбновитьСтрокуРеквизитовОрганизации(ЭтаФорма, ИзмененныеРеквизиты);
	КонецЕсли;
	
	УстановитьНазваниеКомандыРеквизитовОрганизации(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтветственныеЛица(Команда)
	ОтветственныеЛица = Новый Структура("
		|РуководительФИО, 
		|ТекущаяДолжностьРуководителя, 
		|БухгалтерФИО");
	ЗаполнитьЗначенияСвойств(ОтветственныеЛица, ЭтаФорма);
	
	ИзмененныеРеквизиты = ОткрытьФормуМодально("Обработка.ПомощникНастройкиПараметровБазовойВерсии.Форма.ОтветственныеЛица", ОтветственныеЛица, ЭтаФорма);
	Если Не ИзмененныеРеквизиты = Неопределено Тогда
		// Заполним значения реквизитов
		Для Каждого ИзмененныйРеквизит Из ИзмененныеРеквизиты Цикл 
			ЭтаФорма[ИзмененныйРеквизит.Ключ] = ИзмененныйРеквизит.Значение;
		КонецЦикла;
		ОбновитьСтрокуОтветственныхЛиц(ЭтаФорма, ИзмененныеРеквизиты);
	КонецЕсли;
	
	УстановитьНазваниеКомандыОтветственныхЛиц(ЭтаФорма);
КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаКлиенте 
Процедура ПриИзмененииПароля()
	Если Пароль <> ПодтверждениеПароля Тогда
		Элементы.СтраницыСовпадениеПаролей.ТекущаяСтраница = Элементы.СтраницаПаролиНеСовпадают;
	ИначеЕсли ПустаяСтрока(Пароль) Тогда
		Элементы.СтраницыСовпадениеПаролей.ТекущаяСтраница = Элементы.СтраницаПаролиНеВведены;
	Иначе
		Элементы.СтраницыСовпадениеПаролей.ТекущаяСтраница = Элементы.СтраницаПаролиСовпадают;
	КонецЕсли;
	
	ПроверитьЗаполнениеКлиент();
КонецПроцедуры

&НаКлиенте 
Процедура ДоступностьНаценки()
	Если СпособЗаданияЦены = ПредопределенноеЗначение("Перечисление.СпособыЗаданияЦен.Вручную") Тогда
		Элементы.Наценка.Доступность = Ложь;
	Иначе
		Элементы.Наценка.Доступность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте 
Процедура ДоступностьБанковОстатковБезналичных(НомерСчета)
	НомерСчетаЗаполнен = Не ПустаяСтрока(НомерСчета);
	
	Элементы.ГруппаТекущийОстатокБезналичныхДенежныхСредств.ТолькоПросмотр = Не НомерСчетаЗаполнен;
	
	Если Не НомерСчетаЗаполнен Тогда
		ИспользуетсяБанкДляРасчетов = Ложь;
		Элементы.БИКБанкаДляРасчетов.ТолькоПросмотр = Не ИспользуетсяБанкДляРасчетов;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте 
Процедура ДоступностьРеквизитовИКомандБанкаРасчетов()
	ДоступностьБанкаДляРасчетов = Не РучноеИзменениеРеквизитовБанка И Не Банк.Пустая()
		ИЛИ РучноеИзменениеРеквизитовБанка И Не ПустаяСтрока(БИКБанка);
		
	Элементы.ГруппаИспользуетсяБанкДляРасчетов.ТолькоПросмотр = Не ДоступностьБанкаДляРасчетов;
	
	Элементы.ГруппаРеквизитыБанкаДляРасчетовИКоманды.ТолькоПросмотр = Не ИспользуетсяБанкДляРасчетов;
	Элементы.ИзменитьРеквизитыБанкаДляРасчетов.Доступность = ИспользуетсяБанкДляРасчетов;
КонецПроцедуры

&НаСервере 
Процедура ДобавитьОрганизациюСервер()
	// Сохраним данные организации во временное хранилище
	СохранитьДанныеОрганизации();
	
	// Подготовим элементы формы к вводу новой организации
	ОчиститьРеквизитыОрганизации();
	
	РеквизитыБанка = Новый Структура("
		|Банк, 
		|БИКБанка, 
		|ВалютаРасчетногоСчета, 
		|ГородБанка, 
		|КоррСчетБанка, 
		|НаименованиеБанка, 
		|РучноеИзменениеРеквизитовБанка");
	ЗаполнитьЗначенияСвойств(РеквизитыБанка, ЭтаФорма);
	ОбновитьСтрокуРеквизитовБанка(ЭтаФорма, РеквизитыБанка);
	
	РеквизитыБанка = Новый Структура("
		|БанкДляРасчетов, 
		|БИКБанкаДляРасчетов, 
		|ГородБанкаДляРасчетов, 
		|КоррСчетБанкаДляРасчетов, 
		|НаименованиеБанкаДляРасчетов, 
		|РучноеИзменениеРеквизитовБанкаДляРасчетов");
	ЗаполнитьЗначенияСвойств(РеквизитыБанка, ЭтаФорма);
	ОбновитьСтрокуРеквизитовБанка(ЭтаФорма, РеквизитыБанка, Истина);
	
	// Дополним карту переходов
	Если Не ЗавершениеНастройки Тогда
		ДобавитьПереходыОрганизации();
	КонецЕсли;
КонецПроцедуры

&НаСервере 
Процедура ДобавитьПереходыОрганизации()
	ИндексПервогоПерехода = 3 * (НомерТекущейОрганизации + 1) - 1;
	
	ДобавитьСтраницуПерехода(СтраницыПереходов, Элементы.СтраницаСведенияОрганизации, , , ИндексПервогоПерехода);
	ДобавитьСтраницуПерехода(СтраницыПереходов, Элементы.СтраницаКонтакты, , , ИндексПервогоПерехода + 1);
	ДобавитьСтраницуПерехода(СтраницыПереходов, Элементы.СтраницаСчет, , , ИндексПервогоПерехода + 2);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ОбновитьСтрокуРеквизитовОрганизации(Форма, РеквизитыОрганизации = Неопределено)
	СтрокаРеквизитовОрганизации = "";
	
	Если Не РеквизитыОрганизации = Неопределено
		И ПустаяСтрока(РеквизитыОрганизации.ИНН + 
			?(РеквизитыОрганизации.ИндивидуальныйПредприниматель = 0, РеквизитыОрганизации.КПП, "") +
			РеквизитыОрганизации.КодПоОКАТО + 
			РеквизитыОрганизации.КодПоОКПО + 
			РеквизитыОрганизации.ОГРН +
			РеквизитыОрганизации.Префикс) Тогда
		
		РеквизитыОрганизации = Неопределено;
	КонецЕсли;
	
	Если Не РеквизитыОрганизации = Неопределено Тогда
		СтрокаРеквизитовОрганизации = СтрокаРеквизитовОрганизации + 
			?(ПустаяСтрока(РеквизитыОрганизации.ИНН), "", НСтр("ru= 'ИНН:'") + Символы.НПП + РеквизитыОрганизации.ИНН); 
		Если РеквизитыОрганизации.ИндивидуальныйПредприниматель = 0 Тогда
			СтрокаРеквизитовОрганизации = СтрокаРеквизитовОрганизации + 
				?(ПустаяСтрока(РеквизитыОрганизации.КПП), "", 
					?(ПустаяСтрока(СтрокаРеквизитовОрганизации), "", ";" + Символы.НПП) + 
					НСтр("ru= 'КПП:'") + Символы.НПП + РеквизитыОрганизации.КПП); 
		КонецЕсли;
		СтрокаРеквизитовОрганизации = СтрокаРеквизитовОрганизации + 
			?(ПустаяСтрока(РеквизитыОрганизации.КодПоОКАТО), "", 
				?(ПустаяСтрока(СтрокаРеквизитовОрганизации), "", ";" + Символы.НПП) + 
				НСтр("ru= 'Код по ОКАТО:'") + Символы.НПП + РеквизитыОрганизации.КодПоОКАТО); 
		СтрокаРеквизитовОрганизации = СтрокаРеквизитовОрганизации + 
			?(ПустаяСтрока(РеквизитыОрганизации.КодПоОКПО), "", 
				?(ПустаяСтрока(СтрокаРеквизитовОрганизации), "", ";" + Символы.НПП) + 
				НСтр("ru= 'Код по ОКПО:'") + Символы.НПП + РеквизитыОрганизации.КодПоОКПО); 
		СтрокаРеквизитовОрганизации = СтрокаРеквизитовОрганизации + 
			?(ПустаяСтрока(РеквизитыОрганизации.ОГРН), "", 
				?(ПустаяСтрока(СтрокаРеквизитовОрганизации), "", ";" + Символы.НПП) + 
				НСтр("ru= 'ОГРН:'") + Символы.НПП + РеквизитыОрганизации.ОГРН); 
		СтрокаРеквизитовОрганизации = СтрокаРеквизитовОрганизации + 
			?(ПустаяСтрока(РеквизитыОрганизации.Префикс), "", 
				?(ПустаяСтрока(СтрокаРеквизитовОрганизации), "", ";" + Символы.НПП) + 
				НСтр("ru= 'Префикс:'") + Символы.НПП + РеквизитыОрганизации.Префикс); 
	КонецЕсли;
	
	Форма.СтрокаРеквизитовОрганизации = СтрокаРеквизитовОрганизации;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ОбновитьСтрокуОтветственныхЛиц(Форма, ОтветственныеЛица = Неопределено)
	СтрокаОтветственныхЛиц = "";
	
	Если Не ОтветственныеЛица = Неопределено
		И ПустаяСтрока(ОтветственныеЛица.РуководительФИО + 
			ОтветственныеЛица.ТекущаяДолжностьРуководителя + 
			ОтветственныеЛица.БухгалтерФИО) Тогда
		
		ОтветственныеЛица = Неопределено;
	КонецЕсли;
	
	Если Не ОтветственныеЛица = Неопределено Тогда
		СтрокаОтветственныхЛиц = СтрокаОтветственныхЛиц + НСтр("ru= 'Руководитель:'") + Символы.НПП + 
									?(ПустаяСтрока(ОтветственныеЛица.РуководительФИО), "", ОтветственныеЛица.РуководительФИО); 
		СтрокаОтветственныхЛиц = СтрокаОтветственныхЛиц + ";" + Символы.НПП + НСтр("ru= 'Должность:'") + Символы.НПП + 
									?(ПустаяСтрока(ОтветственныеЛица.РуководительФИО), НСтр("ru= '<Не указана>'"), ОтветственныеЛица.ТекущаяДолжностьРуководителя); 
		СтрокаОтветственныхЛиц = СтрокаОтветственныхЛиц + ";" + Символы.НПП + НСтр("ru= 'Бухгалтер:'") + Символы.НПП + 
									?(ПустаяСтрока(ОтветственныеЛица.РуководительФИО), "", ОтветственныеЛица.БухгалтерФИО); 
	КонецЕсли;
	
	Форма.СтрокаОтветственныхЛиц = СтрокаОтветственныхЛиц;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ОбновитьСтрокуРеквизитовБанка(Форма, РеквизитыБанка = Неопределено, БанкДляРасчетов = Ложь)
	СтрокаРеквизитовБанка = "";
	
	Суффикс = ?(БанкДляРасчетов, "ДляРасчетов", "");
	
	Если Не РеквизитыБанка = Неопределено
		И ПустаяСтрока(РеквизитыБанка["БИКБанка" + Суффикс] + 
			РеквизитыБанка["ГородБанка" + Суффикс] + 
			РеквизитыБанка["КоррСчетБанка" + Суффикс] + 
			РеквизитыБанка["НаименованиеБанка" + Суффикс]) Тогда
		
		РеквизитыБанка = Неопределено;
	КонецЕсли;
	
	Если Не РеквизитыБанка = Неопределено Тогда
		СтрокаРеквизитовБанка = СтрокаРеквизитовБанка + 
			?(ПустаяСтрока(РеквизитыБанка["ГородБанка" + Суффикс]), "", 
			РеквизитыБанка["ГородБанка" + Суффикс]); 
		СтрокаРеквизитовБанка = СтрокаРеквизитовБанка + 
			?(ПустаяСтрока(РеквизитыБанка["НаименованиеБанка" + Суффикс]), "", 
				?(ПустаяСтрока(СтрокаРеквизитовБанка), "", "," + Символы.НПП) + 
				РеквизитыБанка["НаименованиеБанка" + Суффикс]); 
		СтрокаРеквизитовБанка = СтрокаРеквизитовБанка + 
			?(ПустаяСтрока(РеквизитыБанка["БИКБанка" + Суффикс]), "", 
				?(ПустаяСтрока(СтрокаРеквизитовБанка), "", "," + Символы.НПП) + 
				НСтр("ru= 'БИК'") + Символы.НПП + РеквизитыБанка["БИКБанка" + Суффикс]); 
		СтрокаРеквизитовБанка = СтрокаРеквизитовБанка + 
			?(ПустаяСтрока(РеквизитыБанка["КоррСчетБанка" + Суффикс]), "", 
				?(ПустаяСтрока(СтрокаРеквизитовБанка), "", "," + Символы.НПП) + 
				НСтр("ru= 'к/с'") + Символы.НПП + РеквизитыБанка["КоррСчетБанка" + Суффикс]); 
	КонецЕсли;
	
	Если Не БанкДляРасчетов Тогда
		Форма.СтрокаРеквизитовБанка = СтрокаРеквизитовБанка;
	Иначе 
		Форма.СтрокаРеквизитовБанкаДляРасчетов = СтрокаРеквизитовБанка;
	КонецЕсли;
КонецПроцедуры

&НаСервере 
Процедура НомерСчетаПриИзмененииСервер()
	Если Не ПустаяСтрока(НомерСчета) Тогда
		Валюта = ДенежныеСредстваСервер.ПолучитьВалютуПоНомеруСчета(НомерСчета);
		Если ЗначениеЗаполнено(Валюта) Тогда
			ВалютаРасчетногоСчета = Валюта;
			Элементы.НадписьВалютаОстаткаБезналичных.Заголовок = Валюта.Наименование;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РеквизитБанкаПриИзменении(ИмяЭлемента)

	Если (ИмяЭлемента = "БИКБанка") Тогда
		Если Не РучноеИзменениеРеквизитовБанка Тогда
			Если Не ЗаполнитьРеквизитыБанкаПоБИК(БИКБанка, "Банк", Истина) Тогда
				СписокВариантовОтветовНаВопрос = Новый СписокЗначений;
				СписокВариантовОтветовНаВопрос.Добавить("ВыбратьИзСписка", "Выбрать из списка");
				СписокВариантовОтветовНаВопрос.Добавить("ПродолжитьВвод",  "Продолжить ввод");
				СписокВариантовОтветовНаВопрос.Добавить("ОтменитьВвод",	   "Отменить ввод");
				
				ТекстВопроса = НСтр("ru = 'Банк с БИК  %Значение% не найден в классификаторе банков.'");
				ТекстВопроса = СтрЗаменить(ТекстВопроса,"%Значение%",БИКБанка);
				
				Ответ = Вопрос(ТекстВопроса, СписокВариантовОтветовНаВопрос, 0,,НСтр("ru = 'Выбор банка из классификатора'"));
				
				Если Ответ = "ОтменитьВвод" Тогда
					БИКБанка = "";
				ИначеЕсли Ответ = "ПродолжитьВвод" Тогда
					РучноеИзменениеРеквизитовБанка = Истина;
					БИКБанка = БИКБанка;
				ИначеЕсли Ответ = "ВыбратьИзСписка" Тогда
					СтруктураПараметров = Новый Структура;
					СтруктураПараметров.Вставить("Реквизит", "БИКБанка");
                	ОткрытьФорму("Справочник.КлассификаторБанковРФ.Форма.ФормаВыбора", СтруктураПараметров, ЭтаФорма);
				КонецЕсли;
            КонецЕсли;	
		КонецЕсли;	
	ИначеЕсли (ИмяЭлемента = "БИКБанкаДляРасчетов") Тогда		
		Если Не РучноеИзменениеРеквизитовБанкаДляРасчетов Тогда
			Если Не ЗаполнитьРеквизитыБанкаПоБИК(БИКБанкаДляРасчетов, "БанкДляРасчетов", Истина) Тогда
				
				СписокВариантовОтветовНаВопрос = Новый СписокЗначений;
				СписокВариантовОтветовНаВопрос.Добавить("ВыбратьИзСписка", "Выбрать из списка");
				СписокВариантовОтветовНаВопрос.Добавить("ПродолжитьВвод",  "Продолжить ввод");
				СписокВариантовОтветовНаВопрос.Добавить("ОтменитьВвод",	   "Отменить ввод");
				
				ТекстВопроса = НСтр("ru = 'Банк с БИК  %Значение% не найден в классификаторе банков.'");
				ТекстВопроса = СтрЗаменить(ТекстВопроса,"%Значение%",БИКБанкаДляРасчетов);
				
				Ответ = Вопрос(ТекстВопроса, СписокВариантовОтветовНаВопрос, 0,,НСтр("ru = 'Выбор банка-корреспондента из классификатора'"));
			
				Если Ответ = "ОтменитьВвод" Тогда
					БИКБанкаДляРасчетов = "";
				ИначеЕсли Ответ = "ПродолжитьВвод" Тогда
					РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина;
					БИКБанкаДляРасчетов = БИКБанкаДляРасчетов;
				ИначеЕсли Ответ = "ВыбратьИзСписка" Тогда
					СтруктураПараметров = Новый Структура;
					СтруктураПараметров.Вставить("Реквизит", "БИКБанкаДляРасчетов");
                	ОткрытьФорму("Справочник.КлассификаторБанковРФ.Форма.ФормаВыбора", СтруктураПараметров, ЭтаФорма);
				КонецЕсли;
 			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитБанкаПриВыборе(ИмяЭлемента, Форма)
	Если ИмяЭлемента = "БИКБанка" Тогда
		Если Не РучноеИзменениеРеквизитовБанка Тогда
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("Реквизит", ИмяЭлемента);
       		ОткрытьФорму("Справочник.КлассификаторБанковРФ.Форма.ФормаВыбора", СтруктураПараметров, Форма);
		КонецЕсли;
	ИначеЕсли ИмяЭлемента = "БИКБанкаДляРасчетов" Тогда
		Если Не РучноеИзменениеРеквизитовБанкаДляРасчетов Тогда
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("Реквизит", ИмяЭлемента);
       		ОткрытьФорму("Справочник.КлассификаторБанковРФ.Форма.ФормаВыбора", СтруктураПараметров, Форма);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитБанкаОткрытие(ИмяЭлемента)
	
	СтруктураПараметров = Новый Структура;
    СтруктураПараметров.Вставить("Реквизит", ИмяЭлемента);
    ЗначенияПараметров = Новый Структура;
	
	Если ИмяЭлемента = "БИКБанка" Тогда
		
		СтруктураПараметров.Вставить("РучноеИзменение", РучноеИзменениеРеквизитовБанка);
		
		Если РучноеИзменениеРеквизитовБанка Тогда
			ЗначенияПараметров.Вставить("БИК", БИКБанка);
			ЗначенияПараметров.Вставить("Наименование", НаименованиеБанка);
			ЗначенияПараметров.Вставить("КоррСчет", КоррСчетБанка);
			ЗначенияПараметров.Вставить("Город", ГородБанка);
		Иначе
		    СтруктураПараметров.Вставить("Банк", Банк);
		КонецЕсли;
						
	ИначеЕсли ИмяЭлемента = "БИКБанкаДляРасчетов" Тогда
		
		СтруктураПараметров.Вставить("РучноеИзменение", РучноеИзменениеРеквизитовБанкаДляРасчетов);
				
		Если РучноеИзменениеРеквизитовБанкаДляРасчетов Тогда
        	ЗначенияПараметров.Вставить("БИК", БИКБанкаДляРасчетов);
			ЗначенияПараметров.Вставить("Наименование", НаименованиеБанкаДляРасчетов);
			ЗначенияПараметров.Вставить("КоррСчет", КоррСчетБанкаДляРасчетов);
			ЗначенияПараметров.Вставить("Город", ГородБанкаДляРасчетов);
		Иначе
			СтруктураПараметров.Вставить("Банк", БанкДляРасчетов);
		КонецЕсли;	
		
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ЗначенияПолей", ЗначенияПараметров);
	ОткрытьФорму("Справочник.БанковскиеСчетаОрганизаций.Форма.РеквизитыБанка",СтруктураПараметров, ЭтаФорма);
			    	
КонецПроцедуры

&НаКлиенте 
Процедура ОбновитьСписокВыбораВалютыЦены()
	Элементы.ВалютаЦены.СписокВыбора.Очистить();
	Для Каждого ВалютаРасчетов Из ВалютыРасчетов Цикл 
		Если Не ВалютаРасчетов.Пометка Тогда
			Продолжить;
		КонецЕсли;
		Элементы.ВалютаЦены.СписокВыбора.Добавить(ВалютаРасчетов.Значение);
	КонецЦикла;
	Если Элементы.ВалютаЦены.СписокВыбора.Количество() = 1 Тогда
		ВалютаЦены = ВалютыРасчетов[0].Значение;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Инициализация данных

&НаСервере 
Процедура ИнициализироватьНачальныеНастройки()
	ИндивидуальныйПредприниматель = 1;
	НомерТекущейОрганизации = 1;
	ТекущийИндексПерехода = 0;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ОписаниеПользователя = Неопределено;
	Если Не ТекущийПользователь.Пустая() Тогда
		Пользователи.ПрочитатьПользователяИБ(ТекущийПользователь.ИдентификаторПользователяИБ,
			ОписаниеПользователя);
		НаименованиеПользователя = ОписаниеПользователя.ПолноеИмя;
		ПользовательИнфБазыИмя = ОписаниеПользователя.Имя;
		
		// Перенос Ф.И.О. в реквизит организации
		Наименование = НаименованиеПользователя;
		НаименованиеПолное = НаименованиеПользователя;
		НаименованиеСокращенное = СокращенноеФИО(НаименованиеПолное);
	КонецЕсли;
	
	ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель");
	СпособЗаданияЦены = ПредопределенноеЗначение("Перечисление.СпособыЗаданияЦен.Вручную");
КонецПроцедуры

&НаСервере 
Процедура ИнициализироватьИнформациюОВалютах()
	ПрефиксЭлементаФормы = "Валюта__";
	ОбработчикСобытияЭлементаФормы = "Подключаемый_ВалютаПриИзменении";
	
	ОписаниеТипаБулево = Новый ОписаниеТипов("Булево");
	
	// Получим созданные в системе валюты
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоВалютам();
	Запрос.УстановитьПараметр("ВалютыРасчетов", ВалютыРасчетов);
	РезультатЗапроса = Запрос.Выполнить();
	
	// Обновим список элементов
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ТаблицаЭлементов = РезультатЗапроса.Выгрузить();
		
		ОбязательнаяВалюта = Ложь;
		
		// Сформируем массив создаваемых реквизитов формы
		МассивРеквизитов = Новый Массив;
		Для Каждого СтрокаЭлементов Из ТаблицаЭлементов Цикл
			УникальныйИдентификаторЭлемента = УникальныйИдентификаторНормализованный(СтрокаЭлементов.Ссылка);
			ИмяРеквизита = ПрефиксЭлементаФормы + УникальныйИдентификаторЭлемента;
			ЗаголовокЭлементаФормы = СтрокаЭлементов.НаименованиеПолное + Символы.НПП + "(" + СтрокаЭлементов.Наименование + ")";
			
			Если ВалютыРасчетов.НайтиПоЗначению(СтрокаЭлементов.Ссылка) = Неопределено Тогда
				МассивРеквизитов.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипаБулево, , ЗаголовокЭлементаФормы));
			КонецЕсли;
		КонецЦикла;
		
		ИзменитьРеквизиты(МассивРеквизитов);
		
		// Установим значения созданных реквизитов формы 
		Для Каждого СтрокаЭлементов Из ТаблицаЭлементов Цикл
			ПометкаВалюты = СтрокаЭлементов.ДобавлятьВФорму;
			УникальныйИдентификаторЭлемента = УникальныйИдентификаторНормализованный(СтрокаЭлементов.Ссылка);
			ИмяРеквизита = ПрефиксЭлементаФормы + УникальныйИдентификаторЭлемента;
			ЗаголовокЭлементаФормы = СтрокаЭлементов.НаименованиеПолное + Символы.НПП + "(" + СтрокаЭлементов.Наименование + ")";
			
			Если СтрокаЭлементов.Код = "643" Тогда
				ОбязательнаяВалюта = Истина;
			Иначе
				ОбязательнаяВалюта = Ложь;
			КонецЕсли;
		
			Если ПометкаВалюты Тогда
				ПометкаВалюты = ОбязательнаяВалюта Или ДобавлениеДополнительныхВалют;
				
				// Определим позицию по порядку
				ИндексСтроки = ТаблицаЭлементов.Индекс(СтрокаЭлементов);
				
				Если (ИндексСтроки + 1) < ТаблицаЭлементов.Количество() Тогда
					СледующаяВалюта = ТаблицаЭлементов.Получить(ИндексСтроки + 1);
					Если Не СледующаяВалюта.ДобавлятьВФорму Тогда
						УникальныйИдентификаторСледующегоЭлемента = УникальныйИдентификаторНормализованный(СледующаяВалюта.Ссылка);
						ИмяСледующегоРеквизита = ПрефиксЭлементаФормы + УникальныйИдентификаторСледующегоЭлемента;
					Иначе
						ИмяСледующегоРеквизита = "";
					КонецЕсли;
				КонецЕсли;
				
				ЭтаФорма[ИмяРеквизита] = ПометкаВалюты;
				
				// Создадим элемент формы
				Если ПустаяСтрока(ИмяСледующегоРеквизита) Тогда
					НадписьЭлементаВалюты = Элементы.Вставить(ИмяРеквизита, Тип("ПолеФормы"), Элементы.ГруппаВалютыРасчетовДинамическая);
				Иначе
					НадписьЭлементаВалюты = Элементы.Вставить(ИмяРеквизита, Тип("ПолеФормы"), Элементы.ГруппаВалютыРасчетовДинамическая, Элементы[ИмяСледующегоРеквизита]);
				КонецЕсли;
				
				НадписьЭлементаВалюты.ПутьКДанным = ИмяРеквизита;
				НадписьЭлементаВалюты.Вид = ВидПоляФормы.ПолеФлажка;
				НадписьЭлементаВалюты.Заголовок = ЗаголовокЭлементаФормы;
				НадписьЭлементаВалюты.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
				НадписьЭлементаВалюты.ШрифтЗаголовка = Новый Шрифт(ШрифтыСтиля.ШрифтТекста, , 18);
                НадписьЭлементаВалюты.ТолькоПросмотр = ОбязательнаяВалюта;
				
				НадписьЭлементаВалюты.УстановитьДействие("ПриИзменении", ОбработчикСобытияЭлементаФормы);
			КонецЕсли;
			
			ИскомыйЭлемент = ВалютыРасчетов.НайтиПоЗначению(СтрокаЭлементов.Ссылка);
			Если ИскомыйЭлемент = Неопределено Тогда
				ВалютыРасчетов.Добавить(СтрокаЭлементов.Ссылка, , ПометкаВалюты);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Навигация

&НаКлиенте 
Функция ПроверитьЗаполнениеКлиент()
	
	РезультатПроверки = Новый Структура("ЕстьОшибки, ОписаниеОшибки");
	
	ЕстьОшибки = Ложь;
	ОписаниеОшибки = "";
	
	ИмяСтраницыПерехода = СтраницыПереходов.Получить(ТекущийИндексПерехода).СтраницаПерехода;
	Если ИмяСтраницыПерехода = "СтраницаОбщаяИнформация" Тогда
		Если ПустаяСтрока(НаименованиеПользователя) Тогда
			ЕстьОшибки = Истина;
			ОписаниеОшибки = НСтр("ru= 'Не заполнен пользователь.'"); 
			Элементы.НадписьФИО.ЦветТекста = ЦветЗаголовкаПоляОшибки;
		Иначе
			Элементы.НадписьФИО.ЦветТекста = ЦветЗаголовкаАвто;
		КонецЕсли;
		Если ПустаяСтрока(ПользовательИнфБазыИмя) Тогда
			ЕстьОшибки = Истина;
			ОписаниеОшибки = ОписаниеОшибки + 
				?(ПустаяСтрока(ОписаниеОшибки),"", Символы.ПС) + 
				НСтр("ru= 'Не заполнено имя для входа.'"); 
			Элементы.ПользовательИнфБазыИмя.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
		Иначе
			Элементы.ПользовательИнфБазыИмя.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
		КонецЕсли;
		Если Не Пароль = ПодтверждениеПароля Тогда
			ЕстьОшибки = Истина;
			ОписаниеОшибки = ОписаниеОшибки + 
				?(ПустаяСтрока(ОписаниеОшибки),"", Символы.ПС) + 
				НСтр("ru= 'Введенные пароли не совпадают.'"); 
			Элементы.Пароль.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
			Элементы.ПодтверждениеПароля.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
		Иначе
			Элементы.Пароль.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
			Элементы.ПодтверждениеПароля.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
		КонецЕсли;
	ИначеЕсли ИмяСтраницыПерехода = "СтраницаСведенияОрганизации" Тогда
		Если ПустаяСтрока(НаименованиеПолное) Тогда
			ЕстьОшибки = Истина;
			ОписаниеОшибки = НСтр("ru= 'Не заполнено наименование организации.'"); 
			Элементы.ЗаголовокНаименованиеИП.ЦветТекста = ЦветЗаголовкаПоляОшибки;
			Элементы.ЗаголовокНаименованиеЮЛ.ЦветТекста = ЦветЗаголовкаПоляОшибки;
		Иначе
			Элементы.ЗаголовокНаименованиеИП.ЦветТекста = ЦветЗаголовкаАвто;
			Элементы.ЗаголовокНаименованиеЮЛ.ЦветТекста = ЦветЗаголовкаАвто;
		КонецЕсли;
		Если СистемаНалогообложения.Пустая() Тогда
			ЕстьОшибки = Истина;
			ОписаниеОшибки = ОписаниеОшибки + 
				?(ПустаяСтрока(ОписаниеОшибки),"", Символы.ПС) + 
				НСтр("ru= 'Не указана система налогообложения.'"); 
			Элементы.СистемаНалогообложения.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
		Иначе
			Элементы.СистемаНалогообложения.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
		КонецЕсли;
	ИначеЕсли ИмяСтраницыПерехода = "СтраницаЦены" Тогда
		Если ВалютаЦены.Пустая() Тогда
			ЕстьОшибки = Истина;
			ОписаниеОшибки = НСтр("ru= 'Не указана валюта цены.'"); 
			Элементы.ВалютаЦены.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
		Иначе
			Элементы.ВалютаЦены.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
		КонецЕсли;
	КонецЕсли;
	
	РезультатПроверки.Вставить("ЕстьОшибки", ЕстьОшибки);
	РезультатПроверки.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	
	Возврат РезультатПроверки;
КонецФункции

&НаКлиентеНаСервереБезКонтекста 
Процедура ДобавитьСтраницуПерехода(Переходы, Страница, ПромежуточныйПереход = Истина, ОтображатьКомандыПереходов = Истина, ИндексПерехода = Неопределено, ИмяЭлементаПоУмолчанию = "")
	Если ИндексПерехода = Неопределено Тогда
		НовыйПереход = Переходы.Добавить();
	Иначе
		НовыйПереход = Переходы.Вставить(ИндексПерехода);
	КонецЕсли;
	НовыйПереход.ПромежуточныйПереход = ПромежуточныйПереход;
	НовыйПереход.ОтображатьКомандыПереходов = ОтображатьКомандыПереходов;
	НовыйПереход.СтраницаПерехода = Страница.Имя;
	НовыйПереход.ИмяЭлементаПоУмолчанию = ИмяЭлементаПоУмолчанию;
КонецПроцедуры

&НаКлиенте 
Процедура ОсуществитьПереходВперед()
	РезультатПроверки = ПроверитьЗаполнениеКлиент();
	
	Если РезультатПроверки.ЕстьОшибки Тогда
		ПараметрыФормы = Новый Структура("ОписаниеОшибки", РезультатПроверки.ОписаниеОшибки);
		ОткрытьФормуМодально("Обработка.ПомощникНастройкиПараметровБазовойВерсии.Форма.ДиалогОшибки", ПараметрыФормы, ЭтаФорма);
		Возврат;
	КонецЕсли;
	
	ТекущаяСтраница = Элементы.СтраницыШагов.ТекущаяСтраница;
	
	ИндексТекущейСтраницы = ТекущийИндексПерехода;
	
	Если ИндексТекущейСтраницы < СтраницыПереходов.Количество() - 1 Тогда
		Переход = СтраницыПереходов.Получить(ИндексТекущейСтраницы + 1);
		
		ИмяСтраницыПерехода = Переход.СтраницаПерехода;
		
		Элементы.СтраницыШагов.ТекущаяСтраница = Элементы[ИмяСтраницыПерехода];
		
		Элементы.СтраницыДекорацийНавигации.ТекущаяСтраница = Элементы[СтрЗаменить(ИмяСтраницыПерехода, "Страница","СтраницаДекорация")];
		
		Если Переход.ПромежуточныйПереход Тогда
			Если Переход.ОтображатьКомандыПереходов Тогда
				СтраницаКоманднойПанели = Элементы.СтраницаПромежуточныхШагов;
			Иначе
				СтраницаКоманднойПанели = Элементы.СтраницаПромежуточныхШаговБезКомандПереходов;
			КонецЕсли;
		ИначеЕсли ИндексТекущейСтраницы + 1 = СтраницыПереходов.Количество() - 1 Тогда
			СтраницаКоманднойПанели = Элементы.СтраницаПоследнегоШага;
		КонецЕсли;
		Элементы.СтраницыКомандныхПанелей.ТекущаяСтраница = СтраницаКоманднойПанели;
		УстановитьКнопкуПоУмолчанию(СтраницаКоманднойПанели);
		
		ТекущийИндексПерехода = ТекущийИндексПерехода + 1;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Переход.ИмяЭлементаПоУмолчанию) Тогда
		ТекущийЭлемент = Элементы[Переход.ИмяЭлементаПоУмолчанию];
	КонецЕсли;
КонецПроцедуры

&НаКлиенте 
Процедура ОсуществитьПереходНазад()
	ТекущаяСтраница = Элементы.СтраницыШагов.ТекущаяСтраница;
	
	ИндексТекущейСтраницы = ТекущийИндексПерехода;
	
	Если ИндексТекущейСтраницы > 0 Тогда
		Переход = СтраницыПереходов.Получить(ИндексТекущейСтраницы - 1);
		
		ИмяСтраницыПерехода = Переход.СтраницаПерехода;
		
		Если Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаСведенияОрганизации
			И Элементы[ИмяСтраницыПерехода] = Элементы.СтраницаСчет Тогда
			Если СведенияОрганизацииЗаполняются Тогда
				Отказ = Не ОткрытьФормуМодально("Обработка.ПомощникНастройкиПараметровБазовойВерсии.Форма.ДиалогВозврата", , ЭтаФорма);
				Если Отказ Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			УдалитьПереходыОрганизации(НомерТекущейОрганизации);
			УдалитьДанныеОрганизации(НомерТекущейОрганизации);
			НомерТекущейОрганизации = НомерТекущейОрганизации - 1;
			ВосстановитьДанныеОрганизации(НомерТекущейОрганизации);
		КонецЕсли;
		
		Элементы.СтраницыШагов.ТекущаяСтраница = Элементы[ИмяСтраницыПерехода];
		Элементы.СтраницыДекорацийНавигации.ТекущаяСтраница = Элементы[СтрЗаменить(ИмяСтраницыПерехода, "Страница","СтраницаДекорация")];
		
		Если Переход.ПромежуточныйПереход Тогда 
			Если Переход.ОтображатьКомандыПереходов Тогда
				СтраницаКоманднойПанели = Элементы.СтраницаПромежуточныхШагов;
			Иначе
				СтраницаКоманднойПанели = Элементы.СтраницаПромежуточныхШаговБезКомандПереходов;
			КонецЕсли;
		ИначеЕсли ИндексТекущейСтраницы - 1 = 0 Тогда
			СтраницаКоманднойПанели = Элементы.СтраницаПервогоШага;
		КонецЕсли;
		Элементы.СтраницыКомандныхПанелей.ТекущаяСтраница = СтраницаКоманднойПанели;
		УстановитьКнопкуПоУмолчанию(СтраницаКоманднойПанели);
		
		ТекущийИндексПерехода = ТекущийИндексПерехода - 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте 
Процедура УстановитьКнопкуПоУмолчанию(СтраницаКоманднойПанели)
	Если Не Элементы.СтраницыКомандныхПанелей.ТекущаяСтраница = СтраницаКоманднойПанели Тогда
		Если СтраницаКоманднойПанели = Элементы.СтраницаПромежуточныхШагов Тогда
			Элементы.Далее1.КнопкаПоУмолчанию = Истина;
		ИначеЕсли СтраницаКоманднойПанели = Элементы.СтраницаПервогоШага Тогда
			Элементы.Далее.КнопкаПоУмолчанию = Истина;
		ИначеЕсли СтраницаКоманднойПанели = Элементы.СтраницаПоследнегоШага Тогда
			Элементы.ЗавершитьНастройку.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте 
Процедура ОсуществитьПереходКСтранице(ИмяСтраницы)
	НомерТекущейОрганизации = НомерТекущейОрганизации + 1;
	ТекущийИндексПерехода = ТекущийИндексПерехода + 1;
	
	Элементы.СтраницыШагов.ТекущаяСтраница = Элементы[ИмяСтраницы];
	Элементы.СтраницыДекорацийНавигации.ТекущаяСтраница = Элементы[СтрЗаменить(ИмяСтраницы, "Страница","СтраницаДекорация")];
КонецПроцедуры

&НаСервере 
Процедура УдалитьПереходыОрганизации(НомерОрганизации)
	ИндексПервогоПерехода = 3 * НомерОрганизации - 1;
	
	Для Счетчик = 1 По 3 Цикл 
		СтраницыПереходов.Удалить(ИндексПервогоПерехода);
	КонецЦикла;
КонецПроцедуры

&НаСервере 
Процедура УдалитьДанныеОрганизации(НомерОрганизации)
	Если АдресаДанныхОрганизаций.Количество() > 1 Тогда
		АдресаДанныхОрганизаций.Удалить(НомерОрганизации - 1);
	КонецЕсли;
КонецПроцедуры

&НаСервере 
Процедура ВосстановитьДанныеОрганизации(НомерОрганизации)
	АдресДанныхОрганизации = АдресаДанныхОрганизаций.Получить(НомерОрганизации - 1);
	ДанныеОрганизации = ПолучитьИзВременногоХранилища(АдресДанныхОрганизации);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеОрганизации.Организация);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Работы с реквизитами организации, расчетного счета и кассы

&НаСервере 
Функция РеквизитыОрганизации()
	РеквизитыОрганизации = Новый Массив;
	
	// Организация
	РеквизитыОрганизации.Добавить("БухгалтерФИО");
	РеквизитыОрганизации.Добавить("ИНН");
	РеквизитыОрганизации.Добавить("ИндивидуальныйПредприниматель");
	РеквизитыОрганизации.Добавить("ИспользоватьРозничныеПродажи");
	РеквизитыОрганизации.Добавить("КодНалоговогоОргана");
	РеквизитыОрганизации.Добавить("КодПоОКАТО");
	РеквизитыОрганизации.Добавить("КодПоОКПО");
	РеквизитыОрганизации.Добавить("КПП");
	РеквизитыОрганизации.Добавить("ОГРН");
	РеквизитыОрганизации.Добавить("Наименование");
	РеквизитыОрганизации.Добавить("НаименованиеПолное");
	РеквизитыОрганизации.Добавить("НаименованиеСокращенное");
	РеквизитыОрганизации.Добавить("Префикс");
	РеквизитыОрганизации.Добавить("РозничнаяТорговляОблагаетсяЕНВД");
	РеквизитыОрганизации.Добавить("РуководительФИО");
	РеквизитыОрганизации.Добавить("СистемаНалогообложения");
	РеквизитыОрганизации.Добавить("ТекущаяДолжностьРуководителя");
	РеквизитыОрганизации.Добавить("ЮрФизЛицо");
	
	// Расчетный счет
	РеквизитыОрганизации.Добавить("Банк");
	РеквизитыОрганизации.Добавить("БанкДляРасчетов");
	РеквизитыОрганизации.Добавить("БИКБанка");
	РеквизитыОрганизации.Добавить("БИКБанкаДляРасчетов");
	РеквизитыОрганизации.Добавить("ГородБанка");
	РеквизитыОрганизации.Добавить("ГородБанкаДляРасчетов");
	РеквизитыОрганизации.Добавить("ИспользуетсяБанкДляРасчетов");
	РеквизитыОрганизации.Добавить("КоррСчетБанка");
	РеквизитыОрганизации.Добавить("КоррСчетБанкаДляРасчетов");
	РеквизитыОрганизации.Добавить("НаименованиеБанка");
	РеквизитыОрганизации.Добавить("НаименованиеБанкаДляРасчетов");
	РеквизитыОрганизации.Добавить("НомерСчета");
	РеквизитыОрганизации.Добавить("РучноеИзменениеРеквизитовБанка");
	РеквизитыОрганизации.Добавить("РучноеИзменениеРеквизитовБанкаДляРасчетов");
	
	// Остатки денежных средств
	РеквизитыОрганизации.Добавить("ТекущийОстатокБезналичныхДенежныхСредств");
	РеквизитыОрганизации.Добавить("ТекущийОстатокНаличныхДенежныхСредств");
	
	// Прочие
	РеквизитыОрганизации.Добавить("СтрокаРеквизитовОрганизации");
	РеквизитыОрганизации.Добавить("СтрокаОтветственныхЛиц");
	РеквизитыОрганизации.Добавить("СтрокаРеквизитовБанка");
	РеквизитыОрганизации.Добавить("СтрокаРеквизитовБанкаДляРасчетов");
	
	Возврат РеквизитыОрганизации;
КонецФункции

&НаСервере 
Процедура СохранитьДанныеОрганизации()
	СохраняемыеРеквизиты = РеквизитыОрганизации();
	
	СтруктураРеквизитов = Новый Структура("
		|Организация, 
		|КонтактнаяИнформация, 
		|ОстаткиДенежныхСредств");
	
	ОписаниеОрганизации = Новый Структура;
	Для Каждого СохраняемыйРеквизит Из СохраняемыеРеквизиты Цикл
		ОписаниеОрганизации.Вставить(СохраняемыйРеквизит, ЭтаФорма[СохраняемыйРеквизит]);
	КонецЦикла;
	
	СтруктураРеквизитов.Вставить("Организация", ОписаниеОрганизации);
	
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтаФорма, Объект);
	ОбработкаОбъект = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.ПомощникНастройкиПараметровБазовойВерсии"));
	КонтактнаяИнформация = ОбработкаОбъект.КонтактнаяИнформация.Выгрузить();
	СтруктураРеквизитов.Вставить("КонтактнаяИнформация", КонтактнаяИнформация);
	
	СтруктураРеквизитов.Вставить("ОстаткиДенежныхСредств", 
		Новый Структура("Безналичные, Наличные", 
		ТекущийОстатокБезналичныхДенежныхСредств, ТекущийОстатокНаличныхДенежныхСредств));
		
	Адрес = ПоместитьВоВременноеХранилище(СтруктураРеквизитов, Новый УникальныйИдентификатор);
	АдресаДанныхОрганизаций.Добавить(Адрес);
КонецПроцедуры

&НаСервере 
Процедура ОчиститьРеквизитыОрганизации()
	ОчищаемыеРеквизиты = РеквизитыОрганизации();
	
	Для Каждого ОчищаемыйРеквизит Из ОчищаемыеРеквизиты Цикл 
		Если ОчищаемыйРеквизит = "ИндивидуальныйПредприниматель"
			ИЛИ ОчищаемыйРеквизит = "ЮрФизЛицо" Тогда
			Продолжить;
		КонецЕсли;
		ЭтаФорма[ОчищаемыйРеквизит] = Неопределено;
	КонецЦикла;
	
	Объект.КонтактнаяИнформация.Очистить();
	КИОписаниеДополнительныхРеквизитов = ЭтаФорма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов;
	Для Каждого ОписаниеКИ Из КИОписаниеДополнительныхРеквизитов Цикл 
		ЭтаФорма[ОписаниеКИ.ИмяРеквизита] = Неопределено;
	КонецЦикла;	
КонецПроцедуры

&НаСервере 
Функция СокращенноеФИО(Знач ФИО)
	МассивФИО = ПолучитьМассивФИО(ФИО);
	
	СокращенноеФИО = МассивФИО[0] + Символы.НПП + Лев(МассивФИО[1], 1) + "." + Лев(МассивФИО[2], 1) + ".";
	
	Возврат СокращенноеФИО;
КонецФункции

&НаСервере 
Функция ПолучитьМассивФИО(Знач ФИО)

	МассивФИО = Новый Массив;
	МассивФИО.Добавить("");
	МассивФИО.Добавить("");
	МассивФИО.Добавить("");
	
	МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ФИО, " ");
	Для Индекс = 0 По МассивПодстрок.ВГраница() Цикл
		Если Индекс < 3 Тогда
			МассивФИО[Индекс] = МассивПодстрок[Индекс];
		Иначе
			МассивФИО[2] = МассивФИО[2] + " " + МассивПодстрок[Индекс];
		КонецЕсли;
	КонецЦикла;

	Возврат МассивФИО;
	
КонецФункции

&НаСервере 
Функция СформироватьАвтоНаименованиеРасчетногоСчета(РасчетныйСчет)
	
	Если РасчетныйСчет.РучноеИзменениеРеквизитовБанка Тогда
		СтрокаНаименования = Прав(СокрЛП(РасчетныйСчет.НомерСчета), 4) 
		+ ?(ЗначениеЗаполнено(РасчетныйСчет.НаименованиеБанка), " в " + Строка(РасчетныйСчет.НаименованиеБанка), "")
		+ ", " + СокрЛП(РасчетныйСчет.Владелец);
		СтрокаНаименования = Лев(СтрокаНаименования, 150);
		
	Иначе	
		
		СтрокаНаименования = Прав(СокрЛП(РасчетныйСчет.НомерСчета), 4) 
		+ ?(ЗначениеЗаполнено(РасчетныйСчет.Банк), " в " + Строка(РасчетныйСчет.Банк), "")
		+ ", " + СокрЛП(РасчетныйСчет.Владелец);
		СтрокаНаименования = Лев(СтрокаНаименования, 150);
		
	КонецЕсли;
	
	Возврат СтрокаНаименования;

КонецФункции

 &НаСервере
Функция ЗаполнитьРеквизитыБанкаПоБИК(БИК = "", ТипБанка, ПеренестиЗначенияРеквизитов = Ложь)
	
	НашлиПоБИК	 = Ложь;
	ЗаписьОБанке = "";
	
	Если ТипБанка = "Банк" Тогда
		БИКБанка		  = "";
		КоррСчетБанка	  = "";
		НаименованиеБанка = "";
		ГородБанка		  = "";
		РаботаСБанками.ПолучитьДанныеКлассификатораРФ(БИК,,ЗаписьОБанке);
		Если НЕ ПустаяСтрока(ЗаписьОБанке) Тогда
			БИКБанка		  = ЗаписьОБанке.Код;
			КоррСчетБанка	  = ЗаписьОБанке.КоррСчет;
			НаименованиеБанка = ЗаписьОБанке.Наименование;
			ГородБанка		  = ЗаписьОБанке.Город;
			НашлиПоБИК		  = Истина;
			Банк				 = ЗаписьОБанке;
		КонецЕсли;		
	КонецЕсли;
	Возврат НашлиПоБИК;
КонецФункции

&НаСервере
Функция ЗаполнитьРеквизитыБанкаПоБанку(Банк = "", ТипБанка, ПеренестиЗначенияРеквизитов = Ложь)
	Если ТипЗнч(Банк) = Тип("СправочникСсылка.КлассификаторБанковРФ") Тогда
		Если ТипБанка = "Банк" Тогда
			БИКБанка		  = Банк.Код;
			КоррСчетБанка	  = Банк.КоррСчет;
			НаименованиеБанка = Банк.Наименование;
			ГородБанка		  = Банк.Город;
		ИначеЕсли ТипБанка = "БанкДляРасчетов" Тогда
			БИКБанкаДляРасчетов				= Банк.Код;
			КоррСчетБанкаДляРасчетов		= Банк.КоррСчет;
			НаименованиеБанкаДляРасчетов	= Банк.Наименование;
			ГородБанкаДляРасчетов			= Банк.Город;
		КонецЕсли;
	КонецЕсли;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Контактная информация

&НаСервере 
Процедура ИнициализироватьКонтактнуюИнформацию()
	// Обработчик механизма "Контактная информация"
	СтруктураКонтактнаяИнформация = Новый Структура("
		|Ссылка, КонтактнаяИнформация", 
		Справочники.Организации.ПустаяСсылка(), Объект.КонтактнаяИнформация);
	
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтаФорма, СтруктураКонтактнаяИнформация, "ГруппаВводДанных4");
	
	ВидимостьДоступностьСтильКонтактнойИнформации("ГруппаВводДанных4");
	
	ПодключитьПроверкуЭлектроннойПочты();
КонецПроцедуры

&НаСервере 
Процедура ВидимостьДоступностьСтильКонтактнойИнформации(ИмяГруппыКонтактнойИнформации)
	ЭлементыКонтактнойИнформации = Элементы[ИмяГруппыКонтактнойИнформации].ПодчиненныеЭлементы;
	КонтактнаяИнформацияОписание = ЭтаФорма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов;
	
	ШрифтПоляВвода = Новый Шрифт(, 18);
	ШрифтЗаголовка = Новый Шрифт(, 10);
	
	Для Каждого Элемент Из ЭлементыКонтактнойИнформации Цикл 
		Если Не Элемент.Родитель = Элементы[ИмяГруппыКонтактнойИнформации] 
			ИЛИ ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяЭлемента = Элемент.Имя;
		ОписаниеЭлемента = ОписаниеЭлементаКонтактнойИнформации(ИмяЭлемента, КонтактнаяИнформацияОписание);
		
		Если ОписаниеЭлемента.ТипЭлемента = Перечисления.ТипыКонтактнойИнформации.Другое
			ИЛИ Найти(ОписаниеЭлемента.Представление, НСтр("ru= 'Другая информация'")) > 0 
			ИЛИ Найти(ОписаниеЭлемента.Представление, НСтр("ru= 'Почтовый адрес'")) > 0 Тогда
			Элемент.Видимость = Ложь;
		Иначе
			Элемент.ШрифтЗаголовка	= ШрифтЗаголовка;
			Элемент.Шрифт			= ШрифтПоляВвода;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере 
Функция ОписаниеЭлементаКонтактнойИнформации(ИмяЭлемента, КонтактнаяИнформацияОписание)
	Описание = Новый Структура("
		|ТипЭлемента, Представление", 
		Перечисления.ТипыКонтактнойИнформации.ПустаяСсылка(), "");
	
	Отбор = Новый Структура("ИмяРеквизита", ИмяЭлемента);
	ИскомыеЭлементы = КонтактнаяИнформацияОписание.НайтиСтроки(Отбор);
	Если ИскомыеЭлементы.Количество() > 0 Тогда
		ИскомыйЭлемент = ИскомыеЭлементы[0];
		
		Описание.ТипЭлемента = ИскомыйЭлемент.Тип;
		Описание.Представление = ИскомыйЭлемент.Вид.Наименование;
	КонецЕсли;
	
	Возврат Описание;
КонецФункции

&НаСервере 
Процедура ПодключитьПроверкуЭлектроннойПочты()
	ОтборПоТипуКИ = Новый Структура("Тип", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты"));
	ПоляЭлектроннойПочты = ЭтаФорма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(ОтборПоТипуКИ);
	
	Для Каждого ПолеЭлектроннойПочты Из ПоляЭлектроннойПочты Цикл 
		ПолеВводаЭлектроннойПочты = Элементы[ПолеЭлектроннойПочты.ИмяРеквизита];
		ПолеВводаЭлектроннойПочты.УстановитьДействие("ПриИзменении", "Подключаемый_ЭлектроннаяПочтаПриИзменении");
		
		Элементы.Переместить(Элементы.СтраницыОшибкаЭлектронногоАдреса, ПолеВводаЭлектроннойПочты.Родитель);
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте 
Процедура ПроверитьАдресЭлектроннойПочты(Элемент)
	ЗначениеЭлектроннойПочты = ЭтаФорма[Элемент.Имя];
	
	РезультатыПроверки = ОбщегоНазначенияКлиентСервер.АдресаЭлектроннойПочтыИзСтроки(ЗначениеЭлектроннойПочты);
	ТекстОшибки = "";
	Для Каждого РезультатПроверки Из РезультатыПроверки Цикл 
		Если ПустаяСтрока(РезультатПроверки.Адрес) Тогда
			// Проверка не пройдена, сообщим пользователю
			ТекстОшибки = ТекстОшибки + ?(ПустаяСтрока(ТекстОшибки),"",Символы.ПС) + РезультатПроверки.Псевдоним + ":" + Символы.НПП + НРег(РезультатПроверки.ОписаниеОшибки) + ".";
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		ТекущийЭлемент = Элемент;
		Элементы.СтраницыОшибкаЭлектронногоАдреса.ТекущаяСтраница = Элементы.СтраницаЕстьОшибкаЭлектронногоАдреса;
		
		Элементы.Далее1.Доступность = Ложь;
	Иначе
		Элементы.СтраницыОшибкаЭлектронногоАдреса.ТекущаяСтраница = Элементы.СтраницаНетОшибкиЭлектронногоАдреса;
		
		Элементы.Далее1.Доступность = Истина;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Функция ПолучитьСсылкуСправочникаПоИдентификатору(ИмяОбъекта, Идентификатор)
	Перем ИскомаяСсылка;
	
	УникальныйИдентификаторПоказателя = Новый УникальныйИдентификатор(СтрЗаменить(Идентификатор, "_", "-"));
	ИскомаяСсылка = Справочники[ИмяОбъекта].ПолучитьСсылку(УникальныйИдентификаторПоказателя);
	
	Возврат ИскомаяСсылка;
КонецФункции

&НаСервере 
Функция УникальныйИдентификаторНормализованный(Ссылка)
	
	УникальныйИдентификаторПоСсылке = Строка(Ссылка.УникальныйИдентификатор());
	УникальныйИдентификаторНормализованный = СтрЗаменить(УникальныйИдентификаторПоСсылке, "-", "_");
	
	Возврат УникальныйИдентификаторНормализованный;
	
КонецФункции

&НаСервере 
Процедура ЗаписатьИзмененияВИБ()
	ЗаписатьПользователяСервер();
	ЗаписатьВалютыСервер();
	ЗаписатьДанныеОрганизацииСервер();
	ДанныеЗаписаны = Истина;
КонецПроцедуры

&НаСервере
Функция ОписаниеПользователяИБ()
	
	Результат = Пользователи.НовоеОписаниеПользователяИБ();
	Пользователи.СкопироватьСвойстваПользователяИБ(
		Результат,
		ЭтаФорма,
		,
		"УникальныйИдентификатор,
		|Роли",
		"ПользовательИнфБазы");
		
	Результат.Вставить("ПодтверждениеПароля", ПодтверждениеПароля);
	
	Если Истина Тогда
		Роли = Новый Массив;
		Роли.Добавить("ПолныеПрава");
		
		ИмяРолиАдминистратораСистемы = Пользователи.РольАдминистратораСистемы().Имя;
		Если Роли.Найти(ИмяРолиАдминистратораСистемы) = Неопределено Тогда
			Роли.Добавить(ИмяРолиАдминистратораСистемы);
		КонецЕсли;
		Результат.Вставить("Роли", Роли);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере 
Процедура ЗаписатьПользователяСервер()
	ОписаниеПользователяИБ = ОписаниеПользователяИБ();
	ОписаниеПользователяИБ.Вставить("Действие", "Записать");
	
	Если Не ТекущийПользователь.Пустая() Тогда
		Пользователь = ТекущийПользователь.ПолучитьОбъект();
	Иначе
		Пользователь = Справочники.Пользователи.СоздатьЭлемент();
	КонецЕсли;
	Пользователь.Наименование = НаименованиеПользователя;
	Пользователь.ДополнительныеСвойства.Вставить(
			"ОписаниеПользователяИБ", ОписаниеПользователяИБ);
			
	Пользователь.ДополнительныеСвойства.Вставить("ЗначениеКопирования", Справочники.Пользователи.ПустаяСсылка());
	Пользователь.ДополнительныеСвойства.Вставить("ГруппаНовогоПользователя", Справочники.ГруппыПользователей.ПустаяСсылка());
	
	Пользователь.ДополнительныеСвойства.ОписаниеПользователяИБ.Вставить("ДоступКИнформационнойБазеРазрешен", Истина);
	Пользователь.ДополнительныеСвойства.ОписаниеПользователяИБ.Вставить("АутентификацияСтандартная", Истина);
	Пользователь.ДополнительныеСвойства.ОписаниеПользователяИБ.Вставить("ПоказыватьВСпискеВыбора", Истина);
	Пользователь.ДополнительныеСвойства.ОписаниеПользователяИБ.Вставить("Пароль", Пароль);
	
	Пользователь.Записать();
	
	ТекущийПользователь = Пользователь.Ссылка;
КонецПроцедуры	

&НаСервере 
Функция ТекстЗапросаПоВалютам()
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	Валюты.Ссылка,
	|	Валюты.ДобавлятьВФорму,
	|	Валюты.Порядок КАК Порядок,
	|	Валюты.Ссылка.Наименование КАК Наименование,
	|	Валюты.Ссылка.НаименованиеПолное КАК НаименованиеПолное,
	|	Валюты.Ссылка.Код КАК Код
	|ИЗ
	|	(ВЫБРАТЬ
	|		Валюты.Ссылка КАК Ссылка,
	|		1 КАК Порядок,
	|		ИСТИНА КАК ДобавлятьВФорму
	|	ИЗ
	|		Справочник.Валюты КАК Валюты
	|	ГДЕ
	|		Валюты.Код = ""643""
	|		И НЕ Валюты.Ссылка В (&ВалютыРасчетов)
	|		И НЕ Валюты.ПометкаУдаления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Валюты.Ссылка,
	|		2,
	|		ИСТИНА
	|	ИЗ
	|		Справочник.Валюты КАК Валюты
	|	ГДЕ
	|		Валюты.Код <> ""643""
	|		И НЕ Валюты.Ссылка В (&ВалютыРасчетов)
	|		И НЕ Валюты.ПометкаУдаления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Валюты.Ссылка,
	|		ВЫБОР
	|			КОГДА Валюты.Код = ""643""
	|				ТОГДА 1
	|			ИНАЧЕ 2
	|		КОНЕЦ,
	|		ЛОЖЬ
	|	ИЗ
	|		Справочник.Валюты КАК Валюты
	|	ГДЕ
	|		Валюты.Ссылка В(&ВалютыРасчетов)
	|		И НЕ Валюты.ПометкаУдаления) КАК Валюты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Валюты.Ссылка.НаименованиеПолное";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере 
Процедура ЗаписатьВалютыСервер()
	КоличествоВалют = ВалютыРасчетов.Количество();
	
	Пока Не КоличествоВалют = 0 Цикл 
		Валюта = ВалютыРасчетов[КоличествоВалют - 1];
		
		Если Не Валюта.Пометка Тогда
			ВалютаОбъект = Валюта.Значение.ПолучитьОбъект();
			ВалютаОбъект.Удалить();
			ВалютыРасчетов.Удалить(Валюта);
		КонецЕсли;
		КоличествоВалют = КоличествоВалют - 1;
	КонецЦикла;
	
	Константы.ВалютаУправленческогоУчета.Установить(ВалютыРасчетов[0].Значение);
	Константы.ВалютаРегламентированногоУчета.Установить(ВалютыРасчетов[0].Значение);
	
	Если ВалютыРасчетов.Количество() > 1 Тогда
		Константы.ИспользоватьНесколькоВалют.Установить(Истина);
	Иначе
		Константы.ИспользоватьНесколькоВалют.Установить(Ложь);
	КонецЕсли;
КонецПроцедуры

&НаСервере 
Процедура ЗаписатьДанныеОрганизацииСервер()
	Если НомерТекущейОрганизации > АдресаДанныхОрганизаций.Количество() Тогда
		СохранитьДанныеОрганизации();
	КонецЕсли;
	
	// Создадим вид цены
	ВидЦены = Ценообразование.ВидЦеныПрайсЛист().ПолучитьОбъект();
	ВидЦены.ВалютаЦены = ВалютаЦены;
	ВидЦены.ЦенаВключаетНДС = ЦенаВключаетНДС;
	ВидЦены.СпособЗаданияЦены = СпособЗаданияЦены;
	Если СпособЗаданияЦены = ПредопределенноеЗначение("Перечисление.СпособыЗаданияЦен.НаценкаНаЦенуПоступления") Тогда
		ВидЦены.Наценка = Наценка;
	КонецЕсли;
	ВидЦены.Записать();
	
	ОптовыйСклад = Неопределено;
	ОптовоРозничныйСклад = Неопределено;
	Для Каждого АдресДанныхОрганизации Из АдресаДанныхОрганизаций Цикл 
		ДанныеОрганизации = ПолучитьИзВременногоХранилища(АдресДанныхОрганизации);
		
		Организация = Справочники.Организации.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(Организация, ДанныеОрганизации.Организация);
		Организация.КонтактнаяИнформация.Загрузить(ДанныеОрганизации.КонтактнаяИнформация);
		
		Организация.Записать();
		
		// Создадим физические лица
		Если Не ПустаяСтрока(ДанныеОрганизации.Организация.РуководительФИО) Тогда
			Руководитель = Справочники.ФизическиеЛица.СоздатьЭлемент();
			Руководитель.Наименование = ДанныеОрганизации.Организация.РуководительФИО;
			Руководитель.Записать();
			
			НовыйОтветственный = Справочники.ОтветственныеЛицаОрганизаций.СоздатьЭлемент();
			НовыйОтветственный.Владелец = Организация.Ссылка;
			НовыйОтветственный.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.Руководитель;
			НовыйОтветственный.ФизическоеЛицо = Руководитель.Ссылка;
			НовыйОтветственный.Должность = ДанныеОрганизации.Организация.ТекущаяДолжностьРуководителя;
			НовыйОтветственный.ДатаНачала = Дата(1980, 1, 1);
			НовыйОтветственный.Наименование = ДанныеОрганизации.Организация.РуководительФИО;
			НовыйОтветственный.Записать();
		КонецЕсли;
		Если Не ПустаяСтрока(ДанныеОрганизации.Организация.БухгалтерФИО) Тогда
			Если СокрЛП(ДанныеОрганизации.Организация.БухгалтерФИО) = СокрЛП(ДанныеОрганизации.Организация.РуководительФИО) Тогда
				Бухгалтер = Руководитель;
			Иначе
				Бухгалтер = Справочники.ФизическиеЛица.СоздатьЭлемент();
				Бухгалтер.Наименование = ДанныеОрганизации.Организация.БухгалтерФИО;
				Бухгалтер.Записать();
			КонецЕсли;
			
			НовыйОтветственный = Справочники.ОтветственныеЛицаОрганизаций.СоздатьЭлемент();
			НовыйОтветственный.Владелец = Организация.Ссылка;
			НовыйОтветственный.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер;
			НовыйОтветственный.ФизическоеЛицо = Бухгалтер.Ссылка;
			НовыйОтветственный.ДатаНачала = Дата(1980, 1, 1);
			НовыйОтветственный.Наименование = ДанныеОрганизации.Организация.БухгалтерФИО;
			НовыйОтветственный.Записать();
		КонецЕсли;
		
		// Создадим оптово-розничный/оптовый склад и кассу ККМ
		Если ДанныеОрганизации.Организация.ИспользоватьРозничныеПродажи Тогда
			Если ОптовоРозничныйСклад = Неопределено Тогда
				Если ОптовыйСклад = Неопределено Тогда
					ОптовоРозничныйСклад = Справочники.Склады.СоздатьЭлемент();
					ОптовоРозничныйСклад.Наименование = НСтр("ru='Розничный склад'");
					ОптовоРозничныйСклад.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин;
					ОптовоРозничныйСклад.РозничныйВидЦены = ВидЦены.Ссылка;
					
					ЗначенияЗаполнения = Новый Структура("
						|ВариантКонтроля, УровеньОбслуживания", 
						Перечисления.ВариантыКонтроля.ОстаткиСУчетомРезерва, Перечисления.УровниОбслуживания.УровеньОбслуживания84_0);
					ОптовоРозничныйСклад.Заполнить(ЗначенияЗаполнения);
				Иначе 
					ОптовоРозничныйСклад = ОптовыйСклад;
					ОптовыйСклад = Неопределено;
					ОптовоРозничныйСклад.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин;
					ОптовоРозничныйСклад.РозничныйВидЦены = ВидЦены.Ссылка;
				КонецЕсли;
				
				ОптовоРозничныйСклад.ДополнительныеСвойства.Вставить("ПропуститьОбновлениеФлагаКонтроляОперативныхОстатков");
				ОптовоРозничныйСклад.Записать();
			КонецЕсли;
		
			КассаККМ = Справочники.КассыККМ.СоздатьЭлемент();
			КассаККМ.Владелец = Организация.Ссылка;
			КассаККМ.ТипКассы = Перечисления.ТипыКассККМ.АвтономнаяККМ;
			КассаККМ.Склад = ОптовоРозничныйСклад.Ссылка;
			КассаККМ.Наименование = Строка(КассаККМ.ТипКассы) + "(" + КассаККМ.Склад + ")";
			КассаККМ.ВалютаДенежныхСредств = ВалютыРасчетов[0].Значение;
			КассаККМ.Записать();
			
			Константы.ИспользоватьРозничныеПродажи.Установить(Истина);
		Иначе
			Если ОптовоРозничныйСклад = Неопределено И ОптовыйСклад = Неопределено Тогда
				ОптовыйСклад = Справочники.Склады.СоздатьЭлемент();
				ОптовыйСклад.Наименование = НСтр("ru='Оптовый склад'");
				ОптовыйСклад.ТипСклада = Перечисления.ТипыСкладов.ОптовыйСклад;
				
				ЗначенияЗаполнения = Новый Структура("
					|ВариантКонтроля, УровеньОбслуживания", 
					Перечисления.ВариантыКонтроля.ОстаткиСУчетомРезерва, Перечисления.УровниОбслуживания.УровеньОбслуживания84_0);
				ОптовыйСклад.Заполнить(ЗначенияЗаполнения);
				
				ОптовыйСклад.ДополнительныеСвойства.Вставить("ПропуститьОбновлениеФлагаКонтроляОперативныхОстатков");
				ОптовыйСклад.Записать();
			КонецЕсли;
		КонецЕсли;
		
		Касса = Справочники.Кассы.СоздатьЭлемент();
		Касса.Владелец = Организация.Ссылка;
		Касса.ВалютаДенежныхСредств = ВалютыРасчетов[0].Значение;
		Касса.Наименование = Лев(СокрЛП(Касса.Владелец) + " (" + Строка(Касса.ВалютаДенежныхСредств) + ")", 100);
		Касса.Записать();
			
		// Добавим режим ЕНВД для организации
		Если ДанныеОрганизации.Организация.РозничнаяТорговляОблагаетсяЕНВД Тогда
			Режимы = РегистрыСведений.ПримененияЕНВД.СоздатьНаборЗаписей();
			Режимы.Отбор.Организация.Использование = Истина;
			Режимы.Отбор.Организация.Значение = Организация.Ссылка;
			
			Режим = Режимы.Добавить();
			Режим.Период = НачалоМесяца(ТекущаяДатаСеанса);
			Режим.Организация = Организация.Ссылка;
			Режим.Склад = ОптовоРозничныйСклад.Ссылка;
			Режим.РозничнаяТорговляОблагаетсяЕНВД = Истина;
			
			Режимы.Записать();
		КонецЕсли;
		
		// Добавим систему налогообложения для организации
		СистемыНалогообложения = РегистрыСведений.СистемыНалогообложенияОрганизаций.СоздатьНаборЗаписей();
		СистемыНалогообложения.Отбор.Организация.Установить(Организация.Ссылка);
		
		СистемаНалогообложенияОрганизации = СистемыНалогообложения.Добавить();
		СистемаНалогообложенияОрганизации.Период = НачалоМесяца(ТекущаяДатаСеанса);
		СистемаНалогообложенияОрганизации.Организация = Организация.Ссылка;
		СистемаНалогообложенияОрганизации.СистемаНалогообложения = ДанныеОрганизации.Организация.СистемаНалогообложения;
		СистемыНалогообложения.Записать();
		
		// Добавим методы оценки для организации
		МетодыОценки = РегистрыСведений.НастройкаМетодовОценкиСтоимостиТоваров.СоздатьНаборЗаписей();
		МетодыОценки.Отбор.Организация.Установить(Организация.Ссылка);
		
		МетодОценки = МетодыОценки.Добавить();
		МетодОценки.Период = НачалоГода(ТекущаяДатаСеанса);
		МетодОценки.Организация = Организация.Ссылка;
		МетодОценки.МетодОценки = Перечисления.МетодыОценкиСтоимостиТоваров.СредняяЗаМесяц;
		МетодОценки.ОбновлятьСтоимостьРегламентнымЗаданием = Истина;
		МетодыОценки.Записать();
		
		// Запишем расчетный счет
		Если Не ПустаяСтрока(ДанныеОрганизации.Организация.НомерСчета) Тогда
			РасчетныйСчет = Справочники.БанковскиеСчетаОрганизаций.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(РасчетныйСчет, ДанныеОрганизации.Организация);
			РасчетныйСчет.Владелец = Организация.Ссылка;
			Если ВалютаРасчетногоСчета.Пустая() Тогда
				РасчетныйСчет.ВалютаДенежныхСредств = ВалютыРасчетов[0].Значение;
			Иначе
				РасчетныйСчет.ВалютаДенежныхСредств = ВалютаРасчетногоСчета;
			КонецЕсли;
			РасчетныйСчет.Наименование = СформироватьАвтоНаименованиеРасчетногоСчета(РасчетныйСчет);
			РасчетныйСчет.Записать();
		КонецЕсли;
	
		// Запишем документы ввода начальных остатков по банковским счетам
		Если ДанныеОрганизации.Организация.ТекущийОстатокБезналичныхДенежныхСредств > 0 Тогда
			ДокументВводаОстатков = Документы.ВводОстатков.СоздатьДокумент();
			//! проверить по стандарту текущей даты 
			ДокументВводаОстатков.Дата = ТекущаяДатаСеанса;
			//! 
			ДокументВводаОстатков.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиНаБанковскихСчетах;
			ДокументВводаОстатков.Организация = Организация.Ссылка;
			Если ВалютаРасчетногоСчета.Пустая() Тогда
				ДокументВводаОстатков.Валюта = ВалютыРасчетов[0].Значение;
			Иначе
				ДокументВводаОстатков.Валюта = ВалютаРасчетногоСчета;
			КонецЕсли;
			ДокументВводаОстатков.Ответственный = ТекущийПользователь;
			ДокументВводаОстатков.Комментарий = НСтр("ru= 'Введен автоматически при настройке системы.'");
			СтрокаБанковскогоСчета = ДокументВводаОстатков.БанковскиеСчета.Добавить();
			СтрокаБанковскогоСчета.БанковскийСчет = РасчетныйСчет.Ссылка;
			СтрокаБанковскогоСчета.Сумма = ДанныеОрганизации.Организация.ТекущийОстатокБезналичныхДенежныхСредств;
			ДокументВводаОстатков.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		
		// Запишем документы ввода начальных остатков по кассам
		Если ДанныеОрганизации.Организация.ТекущийОстатокНаличныхДенежныхСредств > 0 Тогда
			ДокументВводаОстатков = Документы.ВводОстатков.СоздатьДокумент();
			//! проверить по стандарту текущей даты 
			ДокументВводаОстатков.Дата = ТекущаяДатаСеанса;
			//! 
			ДокументВводаОстатков.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВКассах;
			ДокументВводаОстатков.Организация = Организация.Ссылка;
			ДокументВводаОстатков.Валюта = ВалютыРасчетов[0].Значение;
			ДокументВводаОстатков.Ответственный = ТекущийПользователь;
			ДокументВводаОстатков.Комментарий = НСтр("ru= 'Введен автоматически при настройке системы.'");
			СтрокаКассы = ДокументВводаОстатков.Кассы.Добавить();
			СтрокаКассы.Касса= Касса.Ссылка;
			СтрокаКассы.Сумма = ДанныеОрганизации.Организация.ТекущийОстатокНаличныхДенежныхСредств;
			ДокументВводаОстатков.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
	КонецЦикла;
	
	Если АдресаДанныхОрганизаций.Количество() > 1 Тогда
		Константы.ИспользоватьНесколькоОрганизаций.Установить(Истина);
		Константы.ИспользоватьНесколькоКасс.Установить(Истина);
		Константы.ИспользоватьНесколькоРасчетныхСчетов.Установить(Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере 
Процедура УстановитьЦветЗаголовкаПоляОшибки()
	ЦветЗаголовкаПоляОшибки = ЦветаСтиля.ПоясняющийОшибкуТекст;
	ЦветЗаголовкаАвто = ЦветаСтиля.ЦветТекстаФормы;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура УстановитьНазваниеКомандыРеквизитовОрганизации(ЭтаФорма)
	Если ПустаяСтрока(ЭтаФорма.СтрокаРеквизитовОрганизации) Тогда
		ЭтаФорма.Элементы.ИзменитьРеквизитыОрганизации.Заголовок = НСтр("ru = 'Указать'");
	Иначе
		ЭтаФорма.Элементы.ИзменитьРеквизитыОрганизации.Заголовок = НСтр("ru = 'Изменить'");
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура УстановитьНазваниеКомандыОтветственныхЛиц(ЭтаФорма)
	Если ПустаяСтрока(ЭтаФорма.СтрокаОтветственныхЛиц) Тогда
		ЭтаФорма.Элементы.ИзменитьОтветственныеЛица.Заголовок = НСтр("ru = 'Указать'");
	Иначе
		ЭтаФорма.Элементы.ИзменитьОтветственныеЛица.Заголовок = НСтр("ru = 'Изменить'");
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Функция ОднаВалютаРасчетов(ВалютыРасчетов)
	ОтмеченныхВалют = 0;
	Для Каждого ВалютаРасчетов Из ВалютыРасчетов Цикл 
		Если ВалютаРасчетов.Пометка Тогда
			ОтмеченныхВалют = ОтмеченныхВалют + 1;
		КонецЕсли;
	КонецЦикла;
	
	ОднаВалютаРасчетов = (ОтмеченныхВалют = 1);
	
	Возврат ОднаВалютаРасчетов;
КонецФункции

&НаКлиентеНаСервереБезКонтекста 
Процедура УстановитьДоступностьВалютыЦены(ЭтаФорма)
	// Установим валюту цены, если валюта одна
	Если ОднаВалютаРасчетов(ЭтаФорма.ВалютыРасчетов) Тогда
		ЭтаФорма.Элементы.ВалютаЦены.ТолькоПросмотр = Истина;
		ЭтаФорма.Элементы.ВалютаЦены.РежимВыбораИзСписка = Ложь;
		ЭтаФорма.Элементы.ВалютаЦены.КнопкаВыбора = Ложь;
		ЭтаФорма.ВалютаЦены = ЭтаФорма.ВалютыРасчетов[0].Значение;
	Иначе
		ЭтаФорма.Элементы.ВалютаЦены.ТолькоПросмотр = Ложь;
		ЭтаФорма.Элементы.ВалютаЦены.РежимВыбораИзСписка = Истина;
		ЭтаФорма.Элементы.ВалютаЦены.КнопкаВыбора = Истина;
	КонецЕсли;
КонецПроцедуры

